OBJECT Table 5330 CRM Connection Setup
{
  OBJECT-PROPERTIES
  {
    Date=10/23/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.43402;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    CaptionML=[ENU=CRM Connection Setup;
               ESM=Configuraci¢n de conexi¢n CRM;
               FRC=Configuration de la connexion … CRM;
               ENC=CRM Connection Setup];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code20        ;CaptionML=[ENU=Primary Key;
                                                              ESM=Clave primaria;
                                                              FRC=Cl‚ primaire;
                                                              ENC=Primary Key] }
    { 2   ;   ;Server Address      ;Text250       ;CaptionML=[ENU=Dynamics CRM URL;
                                                              ESM=URL de Dynamics CRM;
                                                              FRC=URL Dynamics CRM;
                                                              ENC=Dynamics CRM URL] }
    { 3   ;   ;User Name           ;Text250       ;CaptionML=[ENU=User Name;
                                                              ESM=Nombre de usuario;
                                                              FRC=Nom de l'utilisateur;
                                                              ENC=User Name] }
    { 4   ;   ;User Password Key   ;GUID          ;CaptionML=[ENU=User Password Key;
                                                              ESM=Clave de contrase¤a de usuario;
                                                              FRC=Cl‚ mot de passe utilisateur;
                                                              ENC=User Password Key] }
    { 60  ;   ;Is Enabled          ;Boolean       ;CaptionML=[ENU=Is Enabled;
                                                              ESM=Est  habilitado;
                                                              FRC=Est activ‚;
                                                              ENC=Is Enabled] }
    { 61  ;   ;Is User Mapping Required;Boolean   ;InitValue=Yes;
                                                   CaptionML=[ENU=Dynamics NAV Users Must Map to Dynamics CRM Users;
                                                              ESM=Los usuarios de Dynamics NAV deben asignar a los usuarios de Dynamics CRM;
                                                              FRC=Correspondance obligatoire entre utilisateurs DynamicsÿNAV et DynamicsÿCRM;
                                                              ENC=Dynamics NAV Users Must Map to Dynamics CRM Users] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConnectionStringFormatTok@1000 : TextConst '@@@={Locked};ENU="Url=%1; UserName=%2; Password=%3";ESM="Url=%1; UserName=%2; Password=%3";FRC="Url=%1; UserName=%2; Password=%3";ENC="Url=%1; UserName=%2; Password=%3"';
      CantRegisterDisabledConnectionErr@1001 : TextConst 'ENU=A disabled connection cannot be registered.;ESM=Una conexi¢n deshabilitada no se puede registrar.;FRC=Il est impossible d''enregistrer une connexion d‚sactiv‚e.;ENC=A disabled connection cannot be registered.';
      ConnectionWithCallerIdStringFormatTok@1002 : TextConst '@@@={Locked};ENU="Url=%1; UserName=%2; Password=%3; CallerID=%4";ESM="Url=%1; UserName=%2; Password=%3; CallerID=%4";FRC="Url=%1; UserName=%2; Password=%3; CallerID=%4";ENC="Url=%1; UserName=%2; Password=%3; CallerID=%4"';
      UnableToRetrieveCrmVersionErr@1004 : TextConst 'ENU=Unable to retrieve Dynamics CRM version.;ESM=No se pudo recuperar la versi¢n de Dynamics CRM.;FRC=Impossible d''extraire la version DynamicsÿCRM.;ENC=Unable to retrieve Dynamics CRM version.';
      MissingUsernameTok@1003 : TextConst '@@@={Locked};ENU={USER};ESM={USER};FRC={USER};ENC={USER}';
      MissingPasswordTok@1005 : TextConst '@@@={Locked};ENU={PASSWORD};ESM={PASSWORD};FRC={PASSWORD};ENC={PASSWORD}';

    PROCEDURE HasPassword@8() : Boolean;
    BEGIN
      EXIT(GetPassword <> '');
    END;

    PROCEDURE SetPassword@1(PasswordText@1000 : Text);
    VAR
      ServicePassword@1002 : Record 1261;
    BEGIN
      IF ISNULLGUID("User Password Key") OR NOT ServicePassword.GET("User Password Key") THEN BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.INSERT(TRUE);
        "User Password Key" := ServicePassword.Key;
      END ELSE BEGIN
        ServicePassword.SavePassword(PasswordText);
        ServicePassword.MODIFY;
      END;
    END;

    PROCEDURE UpdateAllConnectionRegistrations@4();
    VAR
      CRMConnectionSetup@1000 : Record 5330;
    BEGIN
      UNREGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,GETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM));
      IF CRMConnectionSetup.FINDSET THEN
        REPEAT
          CRMConnectionSetup.UnregisterConnection;
          IF CRMConnectionSetup."Is Enabled" THEN
            CRMConnectionSetup.RegisterUserConnection;
        UNTIL CRMConnectionSetup.NEXT = 0;
    END;

    PROCEDURE RegisterConnection@17();
    BEGIN
      RegisterConnectionWithName("Primary Key");
    END;

    PROCEDURE RegisterConnectionWithName@12(ConnectionName@1000 : Text);
    BEGIN
      REGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,ConnectionName,ConstructConnectionString);
      IF ConnectionName = '' THEN
        SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM,ConnectionName);
    END;

    PROCEDURE UnregisterConnection@5() : Boolean;
    BEGIN
      EXIT(UnregisterConnectionWithName("Primary Key"));
    END;

    [TryFunction]
    PROCEDURE UnregisterConnectionWithName@20(ConnectionName@1000 : Text);
    BEGIN
      UNREGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,ConnectionName);
    END;

    PROCEDURE ConstructConnectionString@3() : Text;
    BEGIN
      IF "User Name" = '' THEN
        EXIT(STRSUBSTNO(ConnectionStringFormatTok,"Server Address",MissingUsernameTok,MissingPasswordTok));

      EXIT(STRSUBSTNO(ConnectionStringFormatTok,"Server Address","User Name",GetPassword));
    END;

    LOCAL PROCEDURE ConstructConnectionStringWithCalledID@16(CallerID@1000 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(ConnectionWithCallerIdStringFormatTok,"Server Address","User Name",GetPassword,CallerID));
    END;

    PROCEDURE RegisterUserConnection@6();
    VAR
      User@1002 : Record 2000000120;
      CallerID@1001 : GUID;
    BEGIN
      RegisterConnection;

      IF NOT User.GET(DATABASE.USERSECURITYID) THEN
        EXIT;

      IF User."Authentication Email" <> '' THEN BEGIN
        IF NOT TryGetSystemUserIdFromEmail(User."Authentication Email",CallerID) THEN
          CLEARLASTERROR;
      END;

      IF NOT ISNULLGUID(CallerID) THEN BEGIN
        UnregisterConnection;
        RegisterConnectionWithCallerID(CallerID);
      END ELSE BEGIN
        IF "Is User Mapping Required" THEN
          UnregisterConnection;
      END;
    END;

    LOCAL PROCEDURE RegisterConnectionWithCallerID@11(CallerID@1000 : Text);
    BEGIN
      IF NOT "Is Enabled" THEN
        ERROR(CantRegisterDisabledConnectionErr);

      REGISTERTABLECONNECTION(TABLECONNECTIONTYPE::CRM,"Primary Key",ConstructConnectionStringWithCalledID(CallerID));
      IF "Primary Key" = '' THEN
        SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM,"Primary Key");
    END;

    PROCEDURE GetIntegrationUserID@7() : GUID;
    VAR
      CRMConnectionSetup@1000 : Record 5330;
      CRMSystemuser@1001 : Record 5340;
    BEGIN
      CRMConnectionSetup.SETRANGE("Is Enabled",TRUE);
      CRMConnectionSetup.FINDFIRST;
      CRMSystemuser.SETRANGE(InternalEMailAddress,CRMConnectionSetup."User Name");
      IF CRMSystemuser.FINDFIRST THEN
        EXIT(CRMSystemuser.SystemUserId);
    END;

    LOCAL PROCEDURE GetPassword@2() : Text;
    VAR
      ServicePassword@1000 : Record 1261;
    BEGIN
      IF NOT ISNULLGUID("User Password Key") THEN
        IF ServicePassword.GET("User Password Key") THEN
          EXIT(ServicePassword.GetPassword);
    END;

    PROCEDURE TestConnection@9() Success : Boolean;
    VAR
      TestConnectionName@1000 : Text;
    BEGIN
      TestConnectionName := FORMAT(CREATEGUID);
      UnregisterConnectionWithName(TestConnectionName);
      RegisterConnectionWithName(TestConnectionName);
      SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::CRM,TestConnectionName,TRUE);
      IF TryReadSystemUsers THEN
        Success := TRUE;
      UnregisterConnectionWithName(TestConnectionName);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryReadSystemUsers@14();
    VAR
      CRMSystemuser@1000 : Record 5340;
    BEGIN
      IF CRMSystemuser.FINDFIRST THEN;
    END;

    LOCAL PROCEDURE CreateOrganizationService@10(VAR service@1000 : DotNet "'Microsoft.Xrm.Client, Version=7.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Client.Services.OrganizationService");
    VAR
      crmConnection@1001 : DotNet "'Microsoft.Xrm.Client, Version=7.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Client.CrmConnection";
    BEGIN
      service := service.OrganizationService(crmConnection.Parse(ConstructConnectionString));
    END;

    [TryFunction]
    PROCEDURE GetCrmVersion@15(VAR Version@1003 : Text);
    VAR
      service@1000 : DotNet "'Microsoft.Xrm.Client, Version=7.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Client.Services.OrganizationService";
      request@1001 : DotNet "'Microsoft.Xrm.Sdk, Version=7.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.OrganizationRequest";
      response@1002 : DotNet "'Microsoft.Xrm.Sdk, Version=7.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Xrm.Sdk.OrganizationResponse";
    BEGIN
      Version := '';
      IF STRPOS("Server Address",'@@') = 1 THEN
        EXIT;
      CreateOrganizationService(service);
      request := request.OrganizationRequest;
      request.RequestName := 'RetrieveVersion';
      response := service.Execute(request);
      IF NOT response.Results.TryGetValue('Version',Version) THEN
        ERROR(UnableToRetrieveCrmVersionErr);
    END;

    PROCEDURE IsCurrentUserMappedToCrmSystemUser@13() : Boolean;
    VAR
      User@1000 : Record 2000000120;
      CRMSystemUserId@1002 : GUID;
    BEGIN
      IF NOT User.GET(DATABASE.USERSECURITYID) THEN
        EXIT(FALSE);

      IF User."Authentication Email" = '' THEN
        EXIT(FALSE);

      EXIT(TryGetSystemUserIdFromEmail(User."Authentication Email",CRMSystemUserId));
    END;

    [TryFunction]
    LOCAL PROCEDURE TryGetSystemUserIdFromEmail@18(EmailAddress@1000 : Text;VAR SystemUserId@1001 : GUID);
    VAR
      CRMSystemuser@1002 : Record 5340;
    BEGIN
      CRMSystemuser.SETRANGE(InternalEMailAddress,EmailAddress);
      CRMSystemuser.FINDFIRST;
      SystemUserId := CRMSystemuser.SystemUserId;
    END;

    BEGIN
    END.
  }
}

