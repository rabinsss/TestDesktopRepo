OBJECT Table 7134 Item Budget Entry
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               TempDimSetEntry@1000 : TEMPORARY Record 480;
             BEGIN
               CheckIfBlocked;
               TESTFIELD(Date);

               GetGLSetup;
               IF ("Source No." = '') AND ("Item No." = '') THEN BEGIN
                 GetSalesSetup;
                 GetInventorySetup;

                 IF NOT (CheckGroupDimFilled(SalesSetup."Customer Group Dimension Code") OR
                         CheckGroupDimFilled(SalesSetup."Salesperson Dimension Code") OR
                         CheckGroupDimFilled(InventorySetup."Item Group Dimension Code"))
                 THEN
                   TESTFIELD("Item No.");
               END;

               TESTFIELD("Budget Name");
               LOCKTABLE;
               "User ID" := USERID;
               IF "Entry No." = 0 THEN
                 "Entry No." := GetNextEntryNo;

               GetGLSetup;
               DimMgt.GetDimensionSet(TempDimSetEntry,"Dimension Set ID");
               UpdateDimSet(TempDimSetEntry,GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               UpdateDimSet(TempDimSetEntry,GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               UpdateDimSet(TempDimSetEntry,ItemBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               UpdateDimSet(TempDimSetEntry,ItemBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               UpdateDimSet(TempDimSetEntry,ItemBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
               "Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
             END;

    OnModify=VAR
               TempDimSetEntry@1000 : TEMPORARY Record 480;
             BEGIN
               CheckIfBlocked;
               "User ID" := USERID;
               GetGLSetup;

               DimMgt.GetDimensionSet(TempDimSetEntry,"Dimension Set ID");
               IF "Global Dimension 1 Code" <> xRec."Global Dimension 1 Code" THEN
                 UpdateDimSet(TempDimSetEntry,GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
               IF "Global Dimension 2 Code" <> xRec."Global Dimension 2 Code" THEN
                 UpdateDimSet(TempDimSetEntry,GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
               IF "Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code" THEN
                 UpdateDimSet(TempDimSetEntry,ItemBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
               IF "Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code" THEN
                 UpdateDimSet(TempDimSetEntry,ItemBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
               IF "Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code" THEN
                 UpdateDimSet(TempDimSetEntry,ItemBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
               "Dimension Set ID" := DimMgt.GetDimensionSetID(TempDimSetEntry);
             END;

    OnDelete=BEGIN
               CheckIfBlocked;
             END;

    CaptionML=[ENU=Item Budget Entry;
               ESM=Movimiento del presupuesto de productos;
               FRC=êcriture de budget d'article;
               ENC=Item Budget Entry];
    LookupPageID=Page7133;
    DrillDownPageID=Page7133;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=Nß mov.;
                                                              FRC=N¯ d'Çcriture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Analysis Area       ;Option        ;CaptionML=[ENU=Analysis Area;
                                                              ESM=µrea an†lisis;
                                                              FRC=Zone d'analyse;
                                                              ENC=Analysis Area];
                                                   OptionCaptionML=[ENU=Sales,Purchase;
                                                                    ESM=Ventas,Compra;
                                                                    FRC=Vente,Achat;
                                                                    ENC=Sales,Purchase];
                                                   OptionString=Sales,Purchase;
                                                   NotBlank=Yes }
    { 3   ;   ;Budget Name         ;Code10        ;TableRelation="Item Budget Name".Name WHERE (Analysis Area=FIELD(Analysis Area));
                                                   CaptionML=[ENU=Budget Name;
                                                              ESM=Nombre ppto.;
                                                              FRC=Nom du budget;
                                                              ENC=Budget Name] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ESM=Fecha;
                                                              FRC=Date;
                                                              ENC=Date];
                                                   ClosingDates=Yes }
    { 5   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESM=Nß producto;
                                                              FRC=N¯ d'article;
                                                              ENC=Item No.] }
    { 6   ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF "Source Type" <> xRec."Source Type" THEN
                                                                  VALIDATE("Source No.",'');
                                                              END;

                                                   CaptionML=[ENU=Source Type;
                                                              ESM=Tipo procedencia mov.;
                                                              FRC=Type d'origine;
                                                              ENC=Source Type];
                                                   OptionCaptionML=[ENU=" ,Customer,Vendor,Item";
                                                                    ESM=" ,Cliente,Proveedor,Producto";
                                                                    FRC=" ,Client,Fournisseur,Article";
                                                                    ENC=" ,Customer,Vendor,Item"];
                                                   OptionString=[ ,Customer,Vendor,Item] }
    { 7   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type=CONST(Item)) Item;
                                                   CaptionML=[ENU=Source No.;
                                                              ESM=C¢d. procedencia mov.;
                                                              FRC=N¯ d'origine;
                                                              ENC=Source No.] }
    { 8   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 9   ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESM=Cantidad;
                                                              FRC=QuantitÇ;
                                                              ENC=Quantity];
                                                   DecimalPlaces=0:5 }
    { 10  ;   ;Cost Amount         ;Decimal       ;CaptionML=[ENU=Cost Amount;
                                                              ESM=Importe costo;
                                                              FRC=Coñt indiquÇ;
                                                              ENC=Cost Amount];
                                                   AutoFormatType=1 }
    { 11  ;   ;Sales Amount        ;Decimal       ;CaptionML=[ENU=Sales Amount;
                                                              ESM=Importe ventas;
                                                              FRC=Montant de ventes;
                                                              ENC=Sales Amount];
                                                   AutoFormatType=1 }
    { 13  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESM=Id. de usuario;
                                                              FRC=Code utilisateur;
                                                              ENC=User ID];
                                                   Editable=No }
    { 14  ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ESM=C¢d. almacÇn;
                                                              FRC=Code d'emplacement;
                                                              ENC=Location Code] }
    { 15  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                IF "Global Dimension 1 Code" = '' THEN
                                                                  EXIT;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 1 Code","Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 1 Code;
                                                              ESM=C¢d. dimensi¢n global 1;
                                                              FRC=Code de dimension principal 1;
                                                              ENC=Global Dimension 1 Code];
                                                   CaptionClass='1,1,1' }
    { 16  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                IF "Global Dimension 2 Code" = '' THEN
                                                                  EXIT;
                                                                GetGLSetup;
                                                                ValidateDimValue(GLSetup."Global Dimension 2 Code","Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[ENU=Global Dimension 2 Code;
                                                              ESM=C¢d. dimensi¢n global 2;
                                                              FRC=Code de dimension principal 2;
                                                              ENC=Global Dimension 2 Code];
                                                   CaptionClass='1,1,2' }
    { 17  ;   ;Budget Dimension 1 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 1 Code" = '' THEN
                                                                  EXIT;
                                                                IF ItemBudgetName.Name <> "Budget Name" THEN
                                                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                                                ValidateDimValue(ItemBudgetName."Budget Dimension 1 Code","Budget Dimension 1 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 1 Code" := OnLookupDimCode(2,"Budget Dimension 1 Code");
                                                            END;

                                                   AccessByPermission=TableData 348=R;
                                                   CaptionML=[ENU=Budget Dimension 1 Code;
                                                              ESM=C¢d. dim. presupuesto 1;
                                                              FRC=Code de dimension budget 1;
                                                              ENC=Budget Dimension 1 Code];
                                                   CaptionClass=GetCaptionClass(1) }
    { 18  ;   ;Budget Dimension 2 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 2 Code" = '' THEN
                                                                  EXIT;
                                                                IF ItemBudgetName.Name <> "Budget Name" THEN
                                                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                                                ValidateDimValue(ItemBudgetName."Budget Dimension 2 Code","Budget Dimension 2 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 2 Code" := OnLookupDimCode(3,"Budget Dimension 2 Code");
                                                            END;

                                                   AccessByPermission=TableData 348=R;
                                                   CaptionML=[ENU=Budget Dimension 2 Code;
                                                              ESM=C¢d. dim. presupuesto 2;
                                                              FRC=Code de dimension budget 2;
                                                              ENC=Budget Dimension 2 Code];
                                                   CaptionClass=GetCaptionClass(2) }
    { 19  ;   ;Budget Dimension 3 Code;Code20     ;OnValidate=BEGIN
                                                                IF "Budget Dimension 3 Code" = '' THEN
                                                                  EXIT;
                                                                IF ItemBudgetName.Name <> "Budget Name" THEN
                                                                  ItemBudgetName.GET("Analysis Area","Budget Name");
                                                                ValidateDimValue(ItemBudgetName."Budget Dimension 3 Code","Budget Dimension 3 Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              "Budget Dimension 3 Code" := OnLookupDimCode(4,"Budget Dimension 3 Code");
                                                            END;

                                                   AccessByPermission=TableData 350=R;
                                                   CaptionML=[ENU=Budget Dimension 3 Code;
                                                              ESM=C¢d. dim. presupuesto 3;
                                                              FRC=Code de dimension budget 3;
                                                              ENC=Budget Dimension 3 Code];
                                                   CaptionClass=GetCaptionClass(3) }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDimensions;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              ESM=Id. grupo dimensiones;
                                                              FRC=Code ensemble de dimensions;
                                                              ENC=Dimension Set ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Analysis Area,Budget Name,Item No.,Source Type,Source No.,Date,Location Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code;
                                                   SumIndexFields=Cost Amount,Sales Amount,Quantity }
    {    ;Analysis Area,Budget Name,Source Type,Source No.,Item No.,Date,Location Code,Global Dimension 1 Code,Global Dimension 2 Code,Budget Dimension 1 Code,Budget Dimension 2 Code,Budget Dimension 3 Code;
                                                   SumIndexFields=Cost Amount,Sales Amount,Quantity }
    {    ;Analysis Area,Budget Name,Item No.,Date  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=The dimension value %1 has not been set up for dimension %2.;ESM=El valor dimensi¢n %1 no ha sido config. para la dimensi¢n %2.;FRC=La valeur de dimension %1 n''a pas ÇtÇ paramÇtrÇe pour la dimension %2.;ENC=The dimension value %1 has not been set up for dimension %2.';
      Text001@1001 : TextConst 'ENU=1,5,,Budget Dimension 1 Code;ESM=1,5,,C¢digo Dimensi¢n Presupuesto 1;FRC=1,5,,Code de dimension budget 1;ENC=1,5,,Budget Dimension 1 Code';
      Text002@1002 : TextConst 'ENU=1,5,,Budget Dimension 2 Code;ESM=1,5,,C¢digo Dimensi¢n Presupuesto 2;FRC=1,5,,Code de dimension budget 2;ENC=1,5,,Budget Dimension 2 Code';
      Text003@1003 : TextConst 'ENU=1,5,,Budget Dimension 3 Code;ESM=1,5,,C¢digo Dimensi¢n Presupuesto 3;FRC=1,5,,Code de dimension budget 3;ENC=1,5,,Budget Dimension 3 Code';
      ItemBudgetName@1005 : Record 7132;
      GLSetup@1006 : Record 98;
      InventorySetup@1008 : Record 313;
      SalesSetup@1011 : Record 311;
      DimVal@1009 : Record 349;
      DimMgt@1004 : Codeunit 408;
      GLSetupRetrieved@1007 : Boolean;
      InventorySetupRetrieved@1010 : Boolean;
      SalesSetupRetrieved@1012 : Boolean;

    LOCAL PROCEDURE CheckIfBlocked@1();
    BEGIN
      IF "Budget Name" = ItemBudgetName.Name THEN
        EXIT;
      IF ItemBudgetName.Name <> "Budget Name" THEN
        ItemBudgetName.GET("Analysis Area","Budget Name");
      ItemBudgetName.TESTFIELD(Blocked,FALSE);
    END;

    LOCAL PROCEDURE ValidateDimValue@5(DimCode@1000 : Code[20];VAR DimValueCode@1001 : Code[20]) : Boolean;
    VAR
      DimValue@1002 : Record 349;
    BEGIN
      DimValue."Dimension Code" := DimCode;
      DimValue.Code := DimValueCode;
      DimValue.FIND('=><');
      IF DimValueCode <> COPYSTR(DimValue.Code,1,STRLEN(DimValueCode)) THEN
        ERROR(Text000,DimValueCode,DimCode);
      DimValueCode := DimValue.Code;
    END;

    LOCAL PROCEDURE GetGLSetup@2();
    BEGIN
      IF NOT GLSetupRetrieved THEN BEGIN
        GLSetup.GET;
        GLSetupRetrieved := TRUE;
      END;
    END;

    LOCAL PROCEDURE GetInventorySetup@8();
    BEGIN
      IF NOT InventorySetupRetrieved THEN BEGIN
        InventorySetup.GET;
        InventorySetupRetrieved := TRUE;
      END;
    END;

    LOCAL PROCEDURE GetSalesSetup@9();
    BEGIN
      IF NOT SalesSetupRetrieved THEN BEGIN
        SalesSetup.GET;
        SalesSetupRetrieved := TRUE;
      END;
    END;

    LOCAL PROCEDURE OnLookupDimCode@3(DimOption@1000 : 'Global Dimension 1,Global Dimension 2,Budget Dimension 1,Budget Dimension 2,Budget Dimension 3,Budget Dimension 4';DefaultValue@1001 : Code[20]) : Code[20];
    VAR
      DimValue@1002 : Record 349;
      DimValueList@1003 : Page 560;
    BEGIN
      IF DimOption IN [DimOption::"Global Dimension 1",DimOption::"Global Dimension 2"] THEN
        GetGLSetup
      ELSE
        IF ItemBudgetName.Name <> "Budget Name" THEN
          ItemBudgetName.GET("Analysis Area","Budget Name");
      CASE DimOption OF
        DimOption::"Global Dimension 1":
          DimValue."Dimension Code" := GLSetup."Global Dimension 1 Code";
        DimOption::"Global Dimension 2":
          DimValue."Dimension Code" := GLSetup."Global Dimension 2 Code";
        DimOption::"Budget Dimension 1":
          DimValue."Dimension Code" := ItemBudgetName."Budget Dimension 1 Code";
        DimOption::"Budget Dimension 2":
          DimValue."Dimension Code" := ItemBudgetName."Budget Dimension 2 Code";
        DimOption::"Budget Dimension 3":
          DimValue."Dimension Code" := ItemBudgetName."Budget Dimension 3 Code";
      END;
      DimValue.SETRANGE("Dimension Code",DimValue."Dimension Code");
      IF DimValue.GET(DimValue."Dimension Code",DefaultValue) THEN;
      DimValueList.SETTABLEVIEW(DimValue);
      DimValueList.SETRECORD(DimValue);
      DimValueList.LOOKUPMODE := TRUE;
      IF DimValueList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValueList.GETRECORD(DimValue);
        EXIT(DimValue.Code);
      END;
      EXIT(DefaultValue);
    END;

    LOCAL PROCEDURE GetNextEntryNo@4() : Integer;
    VAR
      ItemBudgetEntry@1000 : Record 7134;
    BEGIN
      ItemBudgetEntry.SETCURRENTKEY("Entry No.");
      IF ItemBudgetEntry.FINDLAST THEN
        EXIT(ItemBudgetEntry."Entry No." + 1);

      EXIT(1);
    END;

    LOCAL PROCEDURE GetCaptionClass@7(BudgetDimType@1000 : Integer) : Text[250];
    BEGIN
      IF (ItemBudgetName."Analysis Area" <> "Analysis Area") OR
         (ItemBudgetName.Name <> "Budget Name")
      THEN
        IF NOT ItemBudgetName.GET("Analysis Area","Budget Name") THEN
          EXIT('');
      CASE BudgetDimType OF
        1:
          BEGIN
            IF ItemBudgetName."Budget Dimension 1 Code" <> '' THEN
              EXIT('1,5,' + ItemBudgetName."Budget Dimension 1 Code");
            EXIT(Text001);
          END;
        2:
          BEGIN
            IF ItemBudgetName."Budget Dimension 2 Code" <> '' THEN
              EXIT('1,5,' + ItemBudgetName."Budget Dimension 2 Code");
            EXIT(Text002);
          END;
        3:
          BEGIN
            IF ItemBudgetName."Budget Dimension 3 Code" <> '' THEN
              EXIT('1,5,' + ItemBudgetName."Budget Dimension 3 Code");
            EXIT(Text003);
          END;
      END;
    END;

    LOCAL PROCEDURE CheckGroupDimFilled@10(GroupDimCode@1000 : Code[20]) : Boolean;
    BEGIN
      IF GroupDimCode <> '' THEN
        CASE GroupDimCode OF
          GLSetup."Global Dimension 1 Code":
            EXIT("Global Dimension 1 Code" <> '');
          GLSetup."Global Dimension 2 Code":
            EXIT("Global Dimension 2 Code" <> '');
          ItemBudgetName."Budget Dimension 1 Code":
            EXIT("Budget Dimension 1 Code" <> '');
          ItemBudgetName."Budget Dimension 2 Code":
            EXIT("Budget Dimension 2 Code" <> '');
          ItemBudgetName."Budget Dimension 3 Code":
            EXIT("Budget Dimension 3 Code" <> '');
        END;
    END;

    PROCEDURE GetCaption@11() : Text[1024];
    VAR
      GLSetup@1008 : Record 98;
      ItemBudgetName@1009 : Record 7132;
      Cust@1000 : Record 18;
      Vend@1004 : Record 23;
      Item@1005 : Record 27;
      Dimension@1007 : Record 348;
      DimValue@1006 : Record 349;
      SourceTableCaption@1001 : Text[250];
      SourceFilter@1002 : Text[250];
      Description@1003 : Text[250];
    BEGIN
      CASE TRUE OF
        GETFILTER("Source No.") <> '':
          CASE "Source Type" OF
            "Source Type"::Customer:
              BEGIN
                SourceTableCaption := Cust.TABLECAPTION;
                SourceFilter := GETFILTER("Source No.");
                IF MAXSTRLEN(Cust."No.") >= STRLEN(SourceFilter) THEN
                  IF Cust.GET(SourceFilter) THEN
                    Description := Cust.Name;
              END;
            "Source Type"::Vendor:
              BEGIN
                SourceTableCaption := Vend.TABLECAPTION;
                SourceFilter := GETFILTER("Source No.");
                IF MAXSTRLEN(Vend."No.") >= STRLEN(SourceFilter) THEN
                  IF Vend.GET(SourceFilter) THEN
                    Description := Vend.Name;
              END;
          END;
        GETFILTER("Item No.") <> '':
          BEGIN
            SourceTableCaption := Item.TABLECAPTION;
            SourceFilter := GETFILTER("Item No.");
            IF MAXSTRLEN(Item."No.") >= STRLEN(SourceFilter) THEN
              IF Item.GET(SourceFilter) THEN
                Description := Item.Description;
          END;
        GETFILTER("Global Dimension 1 Code") <> '':
          BEGIN
            GLSetup.GET;
            Dimension.Code := GLSetup."Global Dimension 1 Code";
            SourceFilter := GETFILTER("Global Dimension 1 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(GLSetup."Global Dimension 1 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Global Dimension 2 Code") <> '':
          BEGIN
            GLSetup.GET;
            Dimension.Code := GLSetup."Global Dimension 2 Code";
            SourceFilter := GETFILTER("Global Dimension 2 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(GLSetup."Global Dimension 2 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Budget Dimension 1 Code") <> '':
          IF ItemBudgetName.GET("Analysis Area","Budget Name") THEN BEGIN
            Dimension.Code := ItemBudgetName."Budget Dimension 1 Code";
            SourceFilter := GETFILTER("Budget Dimension 1 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(ItemBudgetName."Budget Dimension 1 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Budget Dimension 2 Code") <> '':
          IF ItemBudgetName.GET("Analysis Area","Budget Name") THEN BEGIN
            Dimension.Code := ItemBudgetName."Budget Dimension 2 Code";
            SourceFilter := GETFILTER("Budget Dimension 2 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(ItemBudgetName."Budget Dimension 2 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
        GETFILTER("Budget Dimension 3 Code") <> '':
          IF ItemBudgetName.GET("Analysis Area","Budget Name") THEN BEGIN
            Dimension.Code := ItemBudgetName."Budget Dimension 3 Code";
            SourceFilter := GETFILTER("Budget Dimension 3 Code");
            SourceTableCaption := Dimension.GetMLName(GLOBALLANGUAGE);
            IF MAXSTRLEN(DimValue.Code) >= STRLEN(SourceFilter) THEN
              IF DimValue.GET(ItemBudgetName."Budget Dimension 3 Code",SourceFilter) THEN
                Description := DimValue.Name;
          END;
      END;

      EXIT(
        DELCHR(
          STRSUBSTNO('%1 %2 %3 %4',SourceTableCaption,SourceFilter,Description,"Budget Name"),
          '>'));
    END;

    PROCEDURE ShowDimensions@12();
    VAR
      DimSetEntry@1001 : Record 480;
      OldDimSetID@1000 : Integer;
    BEGIN
      OldDimSetID := "Dimension Set ID";
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet("Dimension Set ID",STRSUBSTNO('%1 %2 %3',"Budget Name","Item No.",Date));

      IF OldDimSetID = "Dimension Set ID" THEN
        EXIT;

      GetGLSetup;
      ItemBudgetName.GET("Analysis Area","Budget Name");

      "Global Dimension 1 Code" := '';
      "Global Dimension 2 Code" := '';
      "Budget Dimension 1 Code" := '';
      "Budget Dimension 2 Code" := '';
      "Budget Dimension 3 Code" := '';

      IF DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 1 Code") THEN
        "Global Dimension 1 Code" := DimSetEntry."Dimension Value Code";
      IF DimSetEntry.GET("Dimension Set ID",GLSetup."Global Dimension 2 Code") THEN
        "Global Dimension 2 Code" := DimSetEntry."Dimension Value Code";
      IF DimSetEntry.GET("Dimension Set ID",ItemBudgetName."Budget Dimension 1 Code") THEN
        "Budget Dimension 1 Code" := DimSetEntry."Dimension Value Code";
      IF DimSetEntry.GET("Dimension Set ID",ItemBudgetName."Budget Dimension 2 Code") THEN
        "Budget Dimension 2 Code" := DimSetEntry."Dimension Value Code";
      IF DimSetEntry.GET("Dimension Set ID",ItemBudgetName."Budget Dimension 3 Code") THEN
        "Budget Dimension 3 Code" := DimSetEntry."Dimension Value Code";
    END;

    LOCAL PROCEDURE UpdateDimSet@13(VAR TempDimSetEntry@1003 : TEMPORARY Record 480;DimCode@1000 : Code[20];DimValueCode@1001 : Code[20]);
    BEGIN
      IF DimCode = '' THEN
        EXIT;
      IF TempDimSetEntry.GET("Dimension Set ID",DimCode) THEN
        TempDimSetEntry.DELETE;
      IF DimValueCode <> '' THEN BEGIN
        DimVal.GET(DimCode,DimValueCode);
        TempDimSetEntry.INIT;
        TempDimSetEntry."Dimension Set ID" := "Dimension Set ID";
        TempDimSetEntry."Dimension Code" := DimCode;
        TempDimSetEntry."Dimension Value Code" := DimValueCode;
        TempDimSetEntry."Dimension Value ID" := DimVal."Dimension Value ID";
        TempDimSetEntry.INSERT;
      END;
    END;

    BEGIN
    END.
  }
}

