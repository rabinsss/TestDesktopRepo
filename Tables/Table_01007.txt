OBJECT Table 1007 Job WIP Warning
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Job WIP Warning;
               ESM=Aviso WIP proyecto;
               FRC=Avertissement TEC projet;
               ENC=Job WIP Warning];
    LookupPageID=Page1026;
    DrillDownPageID=Page1026;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=N§ mov.;
                                                              FRC=Nø ‚criture;
                                                              ENC=Entry No.];
                                                   Editable=No }
    { 2   ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   CaptionML=[ENU=Job No.;
                                                              ESM=N§ proyecto;
                                                              FRC=Nø projet;
                                                              ENC=Job No.] }
    { 3   ;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No.";
                                                   CaptionML=[ENU=Job Task No.;
                                                              ESM=N§ tarea proyecto;
                                                              FRC=Nø tƒche de projet;
                                                              ENC=Job Task No.] }
    { 4   ;   ;Job WIP Total Entry No.;Integer    ;TableRelation="Job WIP Total";
                                                   CaptionML=[ENU=Job WIP Total Entry No.;
                                                              ESM=N§ mov. total WIP proyecto;
                                                              FRC=Nø s‚quence total TEC projet;
                                                              ENC=Job WIP Total Entry No.];
                                                   Editable=No }
    { 5   ;   ;Warning Message     ;Text250       ;CaptionML=[ENU=Warning Message;
                                                              ESM=Mensaje aviso;
                                                              FRC=Message d'avertissement;
                                                              ENC=Warning Message];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Job No.,Job Task No.                     }
    {    ;Job WIP Total Entry No.                  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=%1 is 0.;ESM=%1 es 0.;FRC=%1 est 0.;ENC=%1 is 0.';
      Text002@1001 : TextConst 'ENU=Cost completion is greater than 100%.;ESM=Costo de compleci¢n superior a 100%.;FRC=L''achŠvement des co–ts est sup‚rieur … 100 %.;ENC=Cost completion is greater than 100%.';
      Text003@1002 : TextConst 'ENU=%1 is negative.;ESM=%1 es negativo.;FRC=%1 est n‚gatif.;ENC=%1 is negative.';

    PROCEDURE CreateEntries@1(JobWIPTotal@1000 : Record 1021);
    VAR
      Job@1001 : Record 167;
    BEGIN
      Job.GET(JobWIPTotal."Job No.");
      IF NOT Job.Complete THEN BEGIN
        IF JobWIPTotal."Contract (Total Price)" = 0 THEN
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text001,JobWIPTotal.FIELDCAPTION("Contract (Total Price)")));

        IF JobWIPTotal."Schedule (Total Cost)" = 0 THEN
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text001,JobWIPTotal.FIELDCAPTION("Schedule (Total Cost)")));

        IF JobWIPTotal."Schedule (Total Price)" = 0 THEN
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text001,JobWIPTotal.FIELDCAPTION("Schedule (Total Price)")));

        IF JobWIPTotal."Usage (Total Cost)" > JobWIPTotal."Schedule (Total Cost)" THEN
          InsertWarning(JobWIPTotal,Text002);

        IF JobWIPTotal."Calc. Recog. Sales Amount" < 0 THEN
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text003,JobWIPTotal.FIELDCAPTION("Calc. Recog. Sales Amount")));

        IF JobWIPTotal."Calc. Recog. Costs Amount" < 0 THEN
          InsertWarning(JobWIPTotal,STRSUBSTNO(Text003,JobWIPTotal.FIELDCAPTION("Calc. Recog. Costs Amount")));
      END;
    END;

    PROCEDURE DeleteEntries@3(JobWIPTotal@1000 : Record 1021);
    BEGIN
      SETRANGE("Job WIP Total Entry No.",JobWIPTotal."Entry No.");
      IF NOT ISEMPTY THEN
        DELETEALL(TRUE);
    END;

    LOCAL PROCEDURE InsertWarning@2(JobWIPTotal@1000 : Record 1021;Message@1001 : Text[250]);
    BEGIN
      RESET;
      IF FINDLAST THEN
        "Entry No." += 1
      ELSE
        "Entry No." := 1;
      "Job WIP Total Entry No." := JobWIPTotal."Entry No.";
      "Job No." := JobWIPTotal."Job No.";
      "Job Task No." := JobWIPTotal."Job Task No.";
      "Warning Message" := Message;
      INSERT;
    END;

    BEGIN
    END.
  }
}

