OBJECT Table 80 Gen. Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NAVNA9.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Page ID");
             END;

    OnDelete=BEGIN
               GenJnlAlloc.SETRANGE("Journal Template Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name",Name);
               GenJnlLine.DELETEALL(TRUE);
               GenJnlBatch.SETRANGE("Journal Template Name",Name);
               GenJnlBatch.DELETEALL;
               DepositHeader.SETCURRENTKEY("Journal Template Name","Journal Batch Name");
               DepositHeader.SETRANGE("Journal Template Name",Name);
               DepositHeader.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Gen. Journal Template;
               ESM=Libro del diario general;
               FRC=ModŠle journal g‚n‚ral;
               ENC=Gen. Journal Template];
    LookupPageID=Page250;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ESM=Nombre;
                                                              FRC=Nom;
                                                              ENC=Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Test Report ID;
                                                              ESM=N§ informe prueba;
                                                              FRC=Code de rapport de test;
                                                              ENC=Test Report ID] }
    { 6   ;   ;Page ID             ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Page));
                                                   OnValidate=BEGIN
                                                                IF "Page ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[ENU=Page ID;
                                                              ESM=Id. p gina;
                                                              FRC=Code page;
                                                              ENC=Page ID] }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Report));
                                                   CaptionML=[ENU=Posting Report ID;
                                                              ESM=N§ informe para registro;
                                                              FRC=Code de rapport sur le report;
                                                              ENC=Posting Report ID] }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=[ENU=Force Posting Report;
                                                              ESM=Forzar informe para registro;
                                                              FRC=Forcer rapport sur le report;
                                                              ENC=Force Posting Report] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"General Journal - Test";
                                                                "Posting Report ID" := REPORT::"G/L Register";
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::General:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."General Journal";
                                                                      "Page ID" := PAGE::"General Journal";
                                                                    END;
                                                                  Type::Sales:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Sales Journal";
                                                                      "Page ID" := PAGE::"Sales Journal";
                                                                    END;
                                                                  Type::Purchases:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Purchase Journal";
                                                                      "Page ID" := PAGE::"Purchase Journal";
                                                                    END;
                                                                  Type::"Cash Receipts":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Cash Receipt Journal";
                                                                      "Page ID" := PAGE::"Cash Receipt Journal";
                                                                    END;
                                                                  Type::Payments:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Payment Journal";
                                                                      "Page ID" := PAGE::"Payment Journal";
                                                                      "Test Report ID" := REPORT::"Payment Journal - Test";
                                                                    END;
                                                                  Type::Assets:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Fixed Asset G/L Journal";
                                                                      "Page ID" := PAGE::"Fixed Asset G/L Journal";
                                                                    END;
                                                                  Type::Intercompany:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."IC General Journal";
                                                                      "Page ID" := PAGE::"IC General Journal";
                                                                    END;
                                                                  Type::Jobs:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Job G/L Journal";
                                                                      "Page ID" := PAGE::"Job G/L Journal";
                                                                    END;
                                                                  Type::"Payroll Accrual":
                                                                    BEGIN
                                                                    END;
                                                                  Type::Deposits:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup.Deposits;
                                                                      "Page ID" := PAGE::Deposit;
                                                                      "Test Report ID" := REPORT::"Deposit Test Report";
                                                                      "Posting Report ID" := REPORT::Deposit;
                                                                      "Bal. Account Type" := "Bal. Account Type"::"Bank Account";
                                                                    END;
                                                                  Type::"Sales Tax":
                                                                    BEGIN
                                                                      "Page ID" := PAGE::"Sales Tax Journal";
                                                                    END;
                                                                END;

                                                                IF Recurring THEN
                                                                  "Page ID" := PAGE::"Recurring General Journal";
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,Payroll Accrual,Deposits,Sales Tax;
                                                                    ESM=General,Ventas,Compras,Cobros,Pagos,Activos,Entre compa¤¡as,Proyectos,Devengo de n¢mina,Dep¢sitos,Impuesto de ventas;
                                                                    FRC=G‚n‚ral,Ventes,Achats,Re‡us comptant,Paiements,Actifs,Intercompagnie,Projets,R‚gularisation paies,D‚p“ts,Taxe de vente;
                                                                    ENC=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,Payroll Accrual,Deposits,Sales Tax];
                                                   OptionString=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs,Payroll Accrual,Deposits,Sales Tax }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.SETRANGE("Journal Template Name",Name);
                                                                GenJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              ESM=C¢d. origen;
                                                              FRC=Code d'origine;
                                                              ENC=Source Code] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ESM=C¢d. auditor¡a;
                                                              FRC=Code motif;
                                                              ENC=Reason Code] }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                IF Recurring THEN
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=[ENU=Recurring;
                                                              ESM=Peri¢dico;
                                                              FRC=R‚current;
                                                              ENC=Recurring] }
    { 15  ;   ;Test Report Caption ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Test Report ID)));
                                                   CaptionML=[ENU=Test Report Caption;
                                                              ESM=T¡tulo informe prueba;
                                                              FRC=Libell‚ du rapport de test;
                                                              ENC=Test Report Caption];
                                                   Editable=No }
    { 16  ;   ;Page Caption        ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Page),
                                                                                                                Object ID=FIELD(Page ID)));
                                                   CaptionML=[ENU=Page Caption;
                                                              ESM=T¡tulo p gina;
                                                              FRC=L‚gende de la page;
                                                              ENC=Page Caption];
                                                   Editable=No }
    { 17  ;   ;Posting Report Caption;Text250     ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Posting Report ID)));
                                                   CaptionML=[ENU=Posting Report Caption;
                                                              ESM=T¡tulo del informe de registro;
                                                              FRC=Libell‚ du rapport de report;
                                                              ENC=Posting Report Caption];
                                                   Editable=No }
    { 18  ;   ;Force Doc. Balance  ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Force Doc. Balance;
                                                              ESM=Forzar saldo por n§ documento;
                                                              FRC=Forcer ‚quilibre doc.;
                                                              ENC=Force Doc. Balance] }
    { 19  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                IF Type = Type::Deposits THEN
                                                                  IF "Bal. Account Type" <> "Bal. Account Type"::"Bank Account" THEN BEGIN
                                                                    "Bal. Account Type" := "Bal. Account Type"::"Bank Account";
                                                                    ERROR(USText000,FIELDCAPTION(Type),Type,FIELDCAPTION("Bal. Account Type"),"Bal. Account Type");
                                                                  END;
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              ESM=Tipo contrapartida;
                                                              FRC=Type compte contr“le;
                                                              ENC=Bal. Account Type];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                                    ESM=Cuenta,Cliente,Proveedor,Banco,Activo;
                                                                    FRC=Compte du grand livre,Client,Fournisseur,Compte bancaire,Immobilisation;
                                                                    ENC=G/L Account,Customer,Vendor,Bank Account,Fixed Asset];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 20  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              ESM=Cta. contrapartida;
                                                              FRC=Nø compte contr“le;
                                                              ENC=Bal. Account No.] }
    { 21  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series] }
    { 22  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ESM=N§ serie registro;
                                                              FRC=S‚ries de nø report;
                                                              ENC=Posting No. Series] }
    { 23  ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Copy VAT Setup to Jnl. Lines" <> xRec."Copy VAT Setup to Jnl. Lines" THEN BEGIN
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Copy VAT Setup to Jnl. Lines","Copy VAT Setup to Jnl. Lines");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Copy Tax Setup to Jnl. Lines;
                                                              ESM=Copiar conf. IVA a l¡n. dia.;
                                                              FRC=Cop. para. TVA sur lgnes jour.;
                                                              ENC=Copy VAT Setup to Jnl. Lines] }
    { 24  ;   ;Allow VAT Difference;Boolean       ;OnValidate=BEGIN
                                                                IF "Allow VAT Difference" <> xRec."Allow VAT Difference" THEN BEGIN
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Allow VAT Difference","Allow VAT Difference");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allow Tax Difference;
                                                              ESM=Permitir diferen. IVA;
                                                              FRC=Autoriser diff‚rence TVA;
                                                              ENC=Allow VAT Difference] }
    { 25  ;   ;Cust. Receipt Report ID;Integer    ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Report));
                                                   AccessByPermission=TableData 18=R;
                                                   CaptionML=[ENU=Cust. Receipt Report ID;
                                                              ESM=N§ informe recep. clie.;
                                                              FRC=Coderapport r‚ception client;
                                                              ENC=Cust. Receipt Report ID] }
    { 26  ;   ;Cust. Receipt Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Cust. Receipt Report ID)));
                                                   AccessByPermission=TableData 18=R;
                                                   CaptionML=[ENU=Cust. Receipt Report Caption;
                                                              ESM=Cliente T¡tulo informe recepci¢n;
                                                              FRC=Libell‚ du rapport de r‚ception du client;
                                                              ENC=Cust. Receipt Report Caption];
                                                   Editable=No }
    { 27  ;   ;Vendor Receipt Report ID;Integer   ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Report));
                                                   AccessByPermission=TableData 23=R;
                                                   CaptionML=[ENU=Vendor Receipt Report ID;
                                                              ESM=N§ informe recep. prov.;
                                                              FRC=Coderapport r‚cep. fournisseur;
                                                              ENC=Vendor Receipt Report ID] }
    { 28  ;   ;Vendor Receipt Report Caption;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Vendor Receipt Report ID)));
                                                   AccessByPermission=TableData 23=R;
                                                   CaptionML=[ENU=Vendor Receipt Report Caption;
                                                              ESM=T¡tulo informe recep. prov.;
                                                              FRC=Libell‚ du rapport de r‚ception du fournisseur;
                                                              ENC=Vendor Receipt Report Caption];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ESM=S¢lo se debe completar el campo %1 en los diarios peri¢dicos.;FRC=Seul le champ %1 peut ˆtre rempli sur les journaux r‚currents.;ENC=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1;ESM=No puede ser %1.;FRC=ne doit pas ˆtre %1;ENC=must not be %1';
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1004 : Record 81;
      GenJnlAlloc@1005 : Record 221;
      SourceCodeSetup@1006 : Record 242;
      DepositHeader@1020000 : Record 10140;
      USText000@1020001 : TextConst 'ENU=If the %1 is %2, then the %3 must be %4.;ESM=Si %1 es %2, %3 es %4.;FRC=Si le %1 est %2, le %3 doit alors ˆtre %4.;ENC=If the %1 is %2, then the %3 must be %4.';

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

