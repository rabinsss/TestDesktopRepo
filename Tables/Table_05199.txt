OBJECT Table 5199 Attendee
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Todo2@1005 : Record 5080;
             BEGIN
               ValidateAttendee(Rec,Attendee);
               Todo2.GET("To-do No.");
               Todo2.CreateSubTodo(Rec,Todo2);
             END;

    OnModify=VAR
               Todo@1000 : Record 5080;
             BEGIN
               ValidateAttendee(Rec,Attendee);
               IF xRec."Attendance Type" = "Attendance Type"::"To-do Organizer" THEN BEGIN
                 IF "Attendance Type" <> xRec."Attendance Type" THEN
                   ERROR(Text006,FIELDCAPTION("Attendance Type"));
                 IF "Attendee No." <> xRec."Attendee No." THEN
                   ERROR(Text008);
               END ELSE
                 IF "Attendee No." <> xRec."Attendee No." THEN BEGIN
                   Todo.DeleteAttendeeTodo(xRec);
                   Todo.GET("To-do No.");
                   Todo.CreateSubTodo(Rec,Todo);
                 END ELSE
                   IF (xRec."Invitation Response Type" <> "Invitation Response Type") OR
                      (xRec."Invitation Sent" <> "Invitation Sent")
                   THEN
                     EXIT;
             END;

    OnDelete=BEGIN
               IF "Attendance Type" = "Attendance Type"::"To-do Organizer" THEN
                 ERROR(Text005);
               Todo.DeleteAttendeeTodo(Rec);

               Todo.GET("To-do No.");
             END;

    CaptionML=[ENU=Attendee;
               ESM=Asistente;
               FRC=Participant;
               ENC=Attendee];
  }
  FIELDS
  {
    { 1   ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[ENU=To-do No.;
                                                              ESM=N§ tarea;
                                                              FRC=Nø tƒche;
                                                              ENC=To-do No.] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=N§ l¡nea;
                                                              FRC=Nø ligne;
                                                              ENC=Line No.] }
    { 3   ;   ;Attendance Type     ;Option        ;OnValidate=VAR
                                                                Cont@1001 : Record 5050;
                                                                Salesperson@1000 : Record 13;
                                                              BEGIN
                                                                IF "Attendance Type" = "Attendance Type"::"To-do Organizer" THEN
                                                                  "Send Invitation" := TRUE
                                                                ELSE BEGIN
                                                                  IF "Attendee Type" = "Attendee Type"::Contact THEN BEGIN
                                                                    IF Cont.GET("Attendee No.") THEN
                                                                      "Send Invitation" := Cont."E-Mail" <> '';
                                                                  END ELSE
                                                                    IF Salesperson.GET("Attendee No.") THEN
                                                                      "Send Invitation" := Salesperson."E-Mail" <> '';
                                                                END
                                                              END;

                                                   CaptionML=[ENU=Attendance Type;
                                                              ESM=Tipo asistencia;
                                                              FRC=Type de participation;
                                                              ENC=Attendance Type];
                                                   OptionCaptionML=[ENU=Required,Optional,To-do Organizer;
                                                                    ESM=Requerido,Opcional,Organizador tareas;
                                                                    FRC=Requis,Facultatif,Organisateur de tƒches;
                                                                    ENC=Required,Optional,To-do Organizer];
                                                   OptionString=Required,Optional,To-do Organizer }
    { 4   ;   ;Attendee Type       ;Option        ;OnValidate=BEGIN
                                                                IF "Attendee Type" <> xRec."Attendee Type" THEN BEGIN
                                                                  "Attendee No." := '';
                                                                  "Attendee Name" := '';
                                                                  "Send Invitation" := FALSE;
                                                                  IF "Attendance Type" = "Attendance Type"::"To-do Organizer" THEN
                                                                    "Send Invitation" := TRUE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Attendee Type;
                                                              ESM=Tipo asistente;
                                                              FRC=Type de participant;
                                                              ENC=Attendee Type];
                                                   OptionCaptionML=[ENU=Contact,Salesperson;
                                                                    ESM=Contacto,Vendedor;
                                                                    FRC=Contact,Repr‚sentant;
                                                                    ENC=Contact,Salesperson];
                                                   OptionString=Contact,Salesperson }
    { 5   ;   ;Attendee No.        ;Code20        ;TableRelation=IF (Attendee Type=CONST(Contact)) Contact WHERE (No.=FIELD(Attendee No.))
                                                                 ELSE IF (Attendee Type=CONST(Salesperson)) Salesperson/Purchaser WHERE (Code=FIELD(Attendee No.));
                                                   OnValidate=VAR
                                                                Cont@1000 : Record 5050;
                                                                Salesperson@1001 : Record 13;
                                                              BEGIN
                                                                TESTFIELD("Attendee No.");
                                                                IF "Attendee Type" = "Attendee Type"::Contact THEN BEGIN
                                                                  Cont.GET("Attendee No.");
                                                                  "Attendee Name" := Cont.Name;
                                                                  IF CurrFieldNo <> 0 THEN
                                                                    "Send Invitation" := Cont."E-Mail" <> '';
                                                                END ELSE BEGIN
                                                                  Salesperson.GET("Attendee No.");
                                                                  "Attendee Name" := Salesperson.Name;
                                                                  IF CurrFieldNo <> 0 THEN
                                                                    IF "Attendance Type" <> "Attendance Type"::"To-do Organizer" THEN
                                                                      "Send Invitation" := Salesperson."E-Mail" <> '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Attendee No.;
                                                              ESM=N§ asistente;
                                                              FRC=Nø de participant;
                                                              ENC=Attendee No.] }
    { 6   ;   ;Attendee Name       ;Text50        ;CaptionML=[ENU=Attendee Name;
                                                              ESM=Nombre asistente;
                                                              FRC=Nom de participant;
                                                              ENC=Attendee Name];
                                                   Editable=No }
    { 7   ;   ;Send Invitation     ;Boolean       ;OnValidate=VAR
                                                                Cont@1000 : Record 5050;
                                                                Salesperson@1001 : Record 13;
                                                                ToDo@1002 : Record 5080;
                                                              BEGIN
                                                                IF NOT "Send Invitation" AND
                                                                   ("Attendance Type" = "Attendance Type"::"To-do Organizer")
                                                                THEN
                                                                  ERROR(Text007,FIELDCAPTION("Send Invitation"));

                                                                ToDo.INIT;
                                                                IF ToDo.GET("To-do No.") THEN;
                                                                IF "Send Invitation" AND (ToDo.Type <> ToDo.Type::"Phone Call") THEN
                                                                  IF "Attendee Type" = "Attendee Type"::Salesperson THEN BEGIN
                                                                    IF Salesperson.GET("Attendee No.") AND (Salesperson."E-Mail" = '') THEN
                                                                      ERROR(Text004,FIELDCAPTION("Send Invitation"),Salesperson.Name);
                                                                  END ELSE
                                                                    IF Cont.GET("Attendee No.") AND (Cont."E-Mail" = '') THEN
                                                                      ERROR(Text004,FIELDCAPTION("Send Invitation"),Cont.Name);
                                                              END;

                                                   CaptionML=[ENU=Send Invitation;
                                                              ESM=Enviar invitaci¢n;
                                                              FRC=Envoyer une invitation;
                                                              ENC=Send Invitation] }
    { 8   ;   ;Invitation Response Type;Option    ;CaptionML=[ENU=Invitation Response Type;
                                                              ESM=Tipo respuesta invitaci¢n;
                                                              FRC=Type de r‚ponse … l'invitation;
                                                              ENC=Invitation Response Type];
                                                   OptionCaptionML=[ENU=None,Accepted,Declined,Tentative;
                                                                    ESM=Ninguno,Aceptado,Rechazado,Provisional;
                                                                    FRC=Aucune,Accept‚e,Refus‚e,Tentative;
                                                                    ENC=None,Accepted,Declined,Tentative];
                                                   OptionString=None,Accepted,Declined,Tentative }
    { 9   ;   ;Invitation Sent     ;Boolean       ;CaptionML=[ENU=Invitation Sent;
                                                              ESM=Invitaci¢n enviada;
                                                              FRC=Invitation envoy‚e;
                                                              ENC=Invitation Sent] }
  }
  KEYS
  {
    {    ;To-do No.,Line No.                      ;Clustered=Yes }
    {    ;To-do No.,Attendee Type,Attendee No.     }
    {    ;To-do No.,Attendance Type                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1002 : TextConst 'ENU=A to-do organizer must always be a salesperson.;ESM=Un organizador de tarea debe siempre ser un vendedor.;FRC=Un organisateur de tƒches doit toujours ˆtre un repr‚sentant.;ENC=A to-do organizer must always be a salesperson.';
      Text002@1001 : TextConst 'ENU=You cannot have more than one to-do organizer.;ESM=No puede tener m s de un organizador de tareas.;FRC=Vous ne pouvez pas avoir plus d''un organisateur de tƒche.;ENC=You cannot have more than one to-do organizer.';
      Text003@1000 : TextConst 'ENU=This attendee already exists.;ESM=Este asistente ya existe.;FRC=Ce participant existe d‚j….;ENC=This attendee already exists.';
      Attendee@1003 : Record 5199;
      Todo@1004 : Record 5080;
      Text004@1005 : TextConst 'ENU=You cannot select the %1 for %2 because he/she does not have an e-mail address.;ESM=No puede seleccionar el %1 para %2 por que no tiene direcci¢n de correo electr¢nico.;FRC=Vous ne pouvez pas s‚lectionner le %1 pour %2 puisqu''il/elle n''a pas d''adresse de courriel.;ENC=You cannot select the %1 for %2 because he/she does not have an e-mail address.';
      Text005@1006 : TextConst 'ENU=You cannot delete a to-do organizer.;ESM=No puede borrar el organizador de tareas.;FRC=Vous ne pouvez pas supprimer un organisateur de tƒches.;ENC=You cannot delete a to-do organizer.';
      Text006@1007 : TextConst 'ENU=You cannot change an %1 for a to-do organizer.;ESM=No puede cambiar un %1 por un organizador de tareas.;FRC=Vous ne pouvez pas modifier un %1 pour un organisateur de tƒches.;ENC=You cannot change an %1 for a to-do organizer.';
      Text007@1008 : TextConst 'ENU=The %1 option is not available for a to-do organizer.;ESM=La opci¢n%1 no est  disponible para un organiz. de tareas.;FRC=L''option %1 n''est pas disponible pour un organisateur de tƒches.;ENC=The %1 option is not available for a to-do organizer.';
      Text008@1010 : TextConst 'ENU=You cannot change the to-do organizer.;ESM=No puede cambiar el organizador de tareas.;FRC=Vous ne pouvez pas modifier l''organisateur de tƒches.;ENC=You cannot change the to-do organizer.';
      Text011@1009 : TextConst 'ENU=You cannot set %1 as organizer because he/she does not have e-mail address.;ESM=No puede asignar a %1 como organizador porque no tiene una direcci¢n de correo electr¢nico.;FRC=Vous ne pouvez pas d‚finir %1 comme organisateur, car aucune adresse de courriel ne lui a ‚t‚ attribu‚e.;ENC=You cannot set %1 as organizer because he/she does not have e-mail address.';

    PROCEDURE ValidateAttendee@4(AttendeeRec@1000 : Record 5199;VAR Attendee@1001 : Record 5199);
    BEGIN
      AttendeeRec.TESTFIELD("Attendee No.");
      ValidateOrganizer(AttendeeRec."Attendee No.",AttendeeRec."Attendance Type",AttendeeRec."Attendee Type",AttendeeRec."To-do No.");

      IF AttendeeRec."Attendance Type" = "Attendance Type"::"To-do Organizer" THEN BEGIN
        IF AttendeeRec."Attendee Type" = "Attendee Type"::Contact THEN
          ERROR(Text001);

        Attendee.SETRANGE("To-do No.",AttendeeRec."To-do No.");
        Attendee.SETRANGE("Attendance Type","Attendance Type"::"To-do Organizer");
        IF Attendee.FIND('-') THEN
          IF Attendee."Line No." <> AttendeeRec."Line No." THEN BEGIN
            Attendee.RESET;
            ERROR(Text002);
          END;
        Attendee.RESET;
      END;

      Attendee.SETRANGE("To-do No.",AttendeeRec."To-do No.");
      Attendee.SETFILTER("Attendee No.",AttendeeRec."Attendee No.");
      IF Attendee.FIND('-') THEN
        IF Attendee."Line No." <> AttendeeRec."Line No." THEN BEGIN
          Attendee.RESET;
          ERROR(Text003);
        END;
      Attendee.RESET;
    END;

    PROCEDURE CreateAttendee@5(VAR Attendee@1005 : Record 5199;TodoNo@1006 : Code[20];LineNo@1003 : Integer;AttendanceType@1002 : Integer;AttendeeType@1001 : Integer;AttendeeNo@1000 : Code[20];SendInvitation@1004 : Boolean);
    BEGIN
      ValidateOrganizer(AttendeeNo,AttendanceType,AttendeeType,TodoNo);

      Attendee.INIT;
      Attendee."To-do No." := TodoNo;
      Attendee."Line No." := LineNo;
      Attendee."Attendance Type" := AttendanceType;
      Attendee.VALIDATE("Attendee Type",AttendeeType);
      Attendee.VALIDATE("Attendee No.",AttendeeNo);
      IF Attendee."Attendance Type" <> Attendee."Attendance Type"::"To-do Organizer" THEN
        Attendee.VALIDATE("Send Invitation",SendInvitation)
      ELSE
        Attendee.VALIDATE("Send Invitation",TRUE);
      IF NOT Attendee.GET(Attendee."To-do No.",Attendee."Line No.") THEN
        Attendee.INSERT;
    END;

    LOCAL PROCEDURE ValidateOrganizer@1(AttendeeNo@1001 : Code[20];AttendanceType@1002 : Integer;AttendeeType@1003 : Integer;TodoNo@1004 : Code[20]);
    VAR
      SalesPurchPerson@1000 : Record 13;
      ToDo@1005 : Record 5080;
    BEGIN
      IF AttendanceType <> Attendee."Attendance Type"::"To-do Organizer" THEN
        EXIT;

      IF AttendeeType = "Attendee Type"::Contact THEN
        ERROR(Text001);

      SalesPurchPerson.GET(AttendeeNo);
      ToDo.INIT;
      IF ToDo.GET(TodoNo) THEN;
      IF (SalesPurchPerson."E-Mail" = '') AND (ToDo.Type <> ToDo.Type::"Phone Call") THEN
        ERROR(Text011,SalesPurchPerson.Name);
    END;

    BEGIN
    END.
  }
}

