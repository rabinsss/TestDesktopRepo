OBJECT Table 172 Standard Customer Sales Code
{
  OBJECT-PROPERTIES
  {
    Date=01/29/16;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.44974,NAVNA9.00.00.44974;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Standard Customer Sales Code;
               ESM=C¢digo ventas cliente est ndar;
               FRC=Code de vente client standard;
               ENC=Standard Customer Sales Code];
  }
  FIELDS
  {
    { 1   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[ENU=Customer No.;
                                                              ESM=N§ cliente;
                                                              FRC=Nø client;
                                                              ENC=Customer No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code10        ;TableRelation="Standard Sales Code";
                                                   OnValidate=VAR
                                                                StdSalesCode@1000 : Record 170;
                                                              BEGIN
                                                                IF Code = '' THEN
                                                                  EXIT;
                                                                StdSalesCode.GET(Code);
                                                                Description := StdSalesCode.Description;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;Valid From Date     ;Date          ;CaptionML=[ENU=Valid From Date;
                                                              ESM=Fecha inicial v lida;
                                                              FRC=Date d‚but validit‚;
                                                              ENC=Valid From Date] }
    { 5   ;   ;Valid To date       ;Date          ;CaptionML=[ENU=Valid To date;
                                                              ESM=Fecha final v lida;
                                                              FRC=Date fin validit‚;
                                                              ENC=Valid To date] }
    { 6   ;   ;Payment Method Code ;Code10        ;TableRelation="Payment Method";
                                                   CaptionML=[ENU=Payment Method Code;
                                                              ESM=C¢d. forma pago;
                                                              FRC=Code mode de paiement;
                                                              ENC=Payment Method Code] }
    { 7   ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   CaptionML=[ENU=Payment Terms Code;
                                                              ESM=C¢d. t‚rminos pago;
                                                              FRC=Code modalit‚s de paiement;
                                                              ENC=Payment Terms Code] }
    { 8   ;   ;Direct Debit Mandate ID;Code35     ;TableRelation="SEPA Direct Debit Mandate" WHERE (Customer No.=FIELD(Customer No.),
                                                                                                    Blocked=CONST(No),
                                                                                                    Closed=CONST(No));
                                                   CaptionML=[ENU=Direct Debit Mandate ID;
                                                              ESM=Id. de orden de domiciliaci¢n de adeudo directo;
                                                              FRC=ID mandat de pr‚lŠvement;
                                                              ENC=Direct Debit Mandate ID] }
    { 9   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESM=Bloqueado;
                                                              FRC=Bloqu‚;
                                                              ENC=Blocked] }
  }
  KEYS
  {
    {    ;Customer No.,Code                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateSalesInvoice@6(OrderDate@1000 : Date;PostingDate@1002 : Date);
    VAR
      SalesHeader@1001 : Record 36;
    BEGIN
      TESTFIELD(Blocked,FALSE);
      SalesHeader.INIT;
      SalesHeader."No." := '';
      SalesHeader."Document Type" := SalesHeader."Document Type"::Invoice;
      SalesHeader.INSERT(TRUE);
      SalesHeader.VALIDATE("Sell-to Customer No.","Customer No.");
      SalesHeader.VALIDATE("Order Date",OrderDate);
      SalesHeader.VALIDATE("Posting Date",PostingDate);
      SalesHeader.VALIDATE("Document Date",OrderDate);
      IF "Payment Method Code" <> '' THEN
        SalesHeader.VALIDATE("Payment Method Code","Payment Method Code");
      IF "Payment Terms Code" <> '' THEN
        SalesHeader.VALIDATE("Payment Terms Code","Payment Terms Code");
      IF "Direct Debit Mandate ID" <> '' THEN
        SalesHeader.VALIDATE("Direct Debit Mandate ID","Direct Debit Mandate ID");
      SalesHeader.MODIFY;
      ApplyStdCodesToSalesLines(SalesHeader,Rec);
    END;

    PROCEDURE InsertSalesLines@1(VAR SalesHeader@1000 : Record 36);
    VAR
      StdCustSalesCode@1001 : Record 172;
      StdCustSalesCodes@1002 : Page 173;
    BEGIN
      SalesHeader.TESTFIELD("No.");
      SalesHeader.TESTFIELD("Sell-to Customer No.");

      StdCustSalesCode.FILTERGROUP := 2;
      StdCustSalesCode.SETRANGE("Customer No.",SalesHeader."Sell-to Customer No.");
      StdCustSalesCode.FILTERGROUP := 0;

      StdCustSalesCodes.SETTABLEVIEW(StdCustSalesCode);
      StdCustSalesCodes.LOOKUPMODE(TRUE);
      IF StdCustSalesCodes.RUNMODAL = ACTION::LookupOK THEN BEGIN
        StdCustSalesCodes.GetSelected(StdCustSalesCode);
        IF StdCustSalesCode.FINDSET THEN
          REPEAT
            ApplyStdCodesToSalesLines(SalesHeader,StdCustSalesCode);
          UNTIL StdCustSalesCode.NEXT = 0;
      END;
    END;

    PROCEDURE ApplyStdCodesToSalesLines@2(VAR SalesHeader@1000 : Record 36;StdCustSalesCode@1004 : Record 172);
    VAR
      Currency@1006 : Record 4;
      StdSalesLine@1003 : Record 171;
      StdSalesCode@1002 : Record 170;
      SalesLine@1005 : Record 37;
      Factor@1001 : Integer;
    BEGIN
      IF SalesHeader."Currency Code" = '' THEN
        Currency.InitRoundingPrecision
      ELSE
        Currency.GET(SalesHeader."Currency Code");

      StdCustSalesCode.TESTFIELD(Code);
      StdCustSalesCode.TESTFIELD("Customer No.",SalesHeader."Sell-to Customer No.");
      StdSalesCode.GET(StdCustSalesCode.Code);
      StdSalesCode.TESTFIELD("Currency Code",SalesHeader."Currency Code");
      StdSalesLine.SETRANGE("Standard Sales Code",StdCustSalesCode.Code);
      SalesLine."Document Type" := SalesHeader."Document Type";
      SalesLine."Document No." := SalesHeader."No.";
      SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
      SalesLine.SETRANGE("Document No.",SalesHeader."No.");
      IF SalesHeader."Prices Including VAT" THEN
        Factor := 1
      ELSE
        Factor := 0;
      SalesLine.LOCKTABLE;
      StdSalesLine.LOCKTABLE;
      IF StdSalesLine.FIND('-') THEN BEGIN
        REPEAT
          SalesLine.INIT;
          SalesLine."Line No." := 0;
          SalesLine.VALIDATE(Type,StdSalesLine.Type);
          IF StdSalesLine.Type = StdSalesLine.Type::" " THEN BEGIN
            SalesLine.VALIDATE("No.",StdSalesLine."No.");
            SalesLine.Description := StdSalesLine.Description
          END ELSE
            IF NOT StdSalesLine.EmptyLine THEN BEGIN
              StdSalesLine.TESTFIELD("No.");
              SalesLine.VALIDATE("No.",StdSalesLine."No.");
              IF StdSalesLine."Variant Code" <> '' THEN
                SalesLine.VALIDATE("Variant Code",StdSalesLine."Variant Code");
              SalesLine.VALIDATE(Quantity,StdSalesLine.Quantity);
              IF StdSalesLine."Unit of Measure Code" <> '' THEN
                SalesLine.VALIDATE("Unit of Measure Code",StdSalesLine."Unit of Measure Code");
              SalesLine.Description := StdSalesLine.Description;
              IF (StdSalesLine.Type = StdSalesLine.Type::"G/L Account") OR
                 (StdSalesLine.Type = StdSalesLine.Type::"Charge (Item)")
              THEN
                SalesLine.VALIDATE(
                  "Unit Price",
                  ROUND(StdSalesLine."Amount Excl. VAT" *
                    (SalesLine."VAT %" / 100 * Factor + 1),Currency."Unit-Amount Rounding Precision"));
            END;

          SalesLine."Shortcut Dimension 1 Code" := StdSalesLine."Shortcut Dimension 1 Code";
          SalesLine."Shortcut Dimension 2 Code" := StdSalesLine."Shortcut Dimension 2 Code";

          CombineDimensions(SalesLine,StdSalesLine);

          IF StdSalesLine.InsertLine THEN BEGIN
            SalesLine."Line No." := GetNextLineNo(SalesLine);
            SalesLine.INSERT(TRUE);
            SalesLine.AutoAsmToOrder;
            InsertExtendedText(SalesLine);
          END;
        UNTIL StdSalesLine.NEXT = 0;
        SalesLine.CalcSalesTaxLines(SalesHeader,SalesLine);
      END;
    END;

    LOCAL PROCEDURE CombineDimensions@5(VAR SalesLine@1001 : Record 37;StdSalesLine@1002 : Record 171);
    VAR
      DimensionManagement@1000 : Codeunit 408;
      DimensionSetIDArr@1005 : ARRAY [10] OF Integer;
    BEGIN
      DimensionSetIDArr[1] := SalesLine."Dimension Set ID";
      DimensionSetIDArr[2] := StdSalesLine."Dimension Set ID";

      SalesLine."Dimension Set ID" :=
        DimensionManagement.GetCombinedDimensionSetID(
          DimensionSetIDArr,SalesLine."Shortcut Dimension 1 Code",SalesLine."Shortcut Dimension 2 Code");
    END;

    LOCAL PROCEDURE InsertExtendedText@3(SalesLine@1000 : Record 37);
    VAR
      TransferExtendedText@1001 : Codeunit 378;
    BEGIN
      IF TransferExtendedText.SalesCheckIfAnyExtText(SalesLine,FALSE) THEN
        TransferExtendedText.InsertSalesExtText(SalesLine);
    END;

    LOCAL PROCEDURE GetNextLineNo@4(SalesLine@1001 : Record 37) : Integer;
    BEGIN
      SalesLine.SETRANGE("Document Type",SalesLine."Document Type");
      SalesLine.SETRANGE("Document No.",SalesLine."Document No.");
      IF SalesLine.FINDLAST THEN
        EXIT(SalesLine."Line No." + 10000);

      EXIT(10000);
    END;

    BEGIN
    END.
  }
}

