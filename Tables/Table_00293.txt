OBJECT Table 293 Reminder Level
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Reminder Terms Code,No.;
    OnDelete=BEGIN
               AdditionalFeeSetup.SETRANGE("Reminder Terms Code","Reminder Terms Code");
               AdditionalFeeSetup.SETRANGE("Reminder Level No.","No.");
               AdditionalFeeSetup.DELETEALL(TRUE);

               ReminderText.SETRANGE("Reminder Terms Code","Reminder Terms Code");
               ReminderText.SETRANGE("Reminder Level","No.");
               ReminderText.DELETEALL;

               CurrencyForReminderLevel.SETRANGE("Reminder Terms Code","Reminder Terms Code");
               CurrencyForReminderLevel.SETRANGE("No.","No.");
               CurrencyForReminderLevel.DELETEALL;
             END;

    OnRename=BEGIN
               AdditionalFeeSetup.SETRANGE("Reminder Terms Code",xRec."Reminder Terms Code");
               AdditionalFeeSetup.SETRANGE("Reminder Level No.",xRec."No.");
               WHILE AdditionalFeeSetup.FINDFIRST DO
                 AdditionalFeeSetup.RENAME("Reminder Terms Code",
                   "No.",
                   AdditionalFeeSetup."Charge Per Line",
                   AdditionalFeeSetup."Currency Code",
                   AdditionalFeeSetup."Threshold Remaining Amount");

               ReminderText.SETRANGE("Reminder Terms Code",xRec."Reminder Terms Code");
               ReminderText.SETRANGE("Reminder Level",xRec."No.");
               WHILE ReminderText.FINDFIRST DO
                 ReminderText.RENAME("Reminder Terms Code","No.",ReminderText.Position,ReminderText."Line No.");

               CurrencyForReminderLevel.SETRANGE("Reminder Terms Code",xRec."Reminder Terms Code");
               CurrencyForReminderLevel.SETRANGE("No.",xRec."No.");
               WHILE CurrencyForReminderLevel.FINDFIRST DO
                 CurrencyForReminderLevel.RENAME("Reminder Terms Code","No.",
                   CurrencyForReminderLevel."Currency Code");
             END;

    CaptionML=[ENU=Reminder Level;
               ESM=Nivel recordatorio;
               FRC=Niveau rappel;
               ENC=Reminder Level];
    LookupPageID=Page432;
    DrillDownPageID=Page432;
  }
  FIELDS
  {
    { 1   ;   ;Reminder Terms Code ;Code10        ;TableRelation="Reminder Terms";
                                                   CaptionML=[ENU=Reminder Terms Code;
                                                              ESM=C¢d. recordatorio;
                                                              FRC=Code modalit‚ de rappel;
                                                              ENC=Reminder Terms Code];
                                                   NotBlank=Yes }
    { 2   ;   ;No.                 ;Integer       ;CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.];
                                                   MinValue=1;
                                                   NotBlank=Yes }
    { 3   ;   ;Grace Period        ;DateFormula   ;CaptionML=[ENU=Grace Period;
                                                              ESM=Periodo gracia;
                                                              FRC=D‚lai de grƒce;
                                                              ENC=Grace Period] }
    { 4   ;   ;Additional Fee (LCY);Decimal       ;CaptionML=[ENU=Additional Fee ($);
                                                              ESM=Recargo fijo ($);
                                                              FRC=Frais suppl. ($);
                                                              ENC=Additional Fee ($)];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 5   ;   ;Calculate Interest  ;Boolean       ;CaptionML=[ENU=Calculate Interest;
                                                              ESM=C lculo inter‚s;
                                                              FRC=Calculer int‚rˆts;
                                                              ENC=Calculate Interest] }
    { 6   ;   ;Due Date Calculation;DateFormula   ;CaptionML=[ENU=Due Date Calculation;
                                                              ESM=C lculo de fecha de vencimiento;
                                                              FRC=Calcul date d'‚ch‚ance;
                                                              ENC=Due Date Calculation] }
    { 7   ;   ;Add. Fee per Line Amount (LCY);Decimal;
                                                   CaptionML=[ENU=Add. Fee per Line Amount ($);
                                                              ESM=Recargo fijo por importe l¡nea ($);
                                                              FRC=Montant des frais suppl‚mentaires par ligne en $;
                                                              ENC=Add. Fee per Line Amount ($)];
                                                   MinValue=0 }
    { 8   ;   ;Add. Fee per Line Description;Text100;
                                                   CaptionML=[ENU=Add. Fee per Line Description;
                                                              ESM=Descripci¢n recargo fijo por l¡nea;
                                                              FRC=Description des frais suppl‚mentaires par ligne;
                                                              ENC=Add. Fee per Line Description] }
    { 9   ;   ;Add. Fee Calculation Type;Option   ;CaptionML=[ENU=Add. Fee Calculation Type;
                                                              ESM=Tipo c lculo recargo fijo;
                                                              FRC=Type calcul frais suppl‚mentaires;
                                                              ENC=Add. Fee Calculation Type];
                                                   OptionCaptionML=[ENU=Fixed,Single Dynamic,Accumulated Dynamic;
                                                                    ESM=Fijo,énico din mico,Acumulado din mico;
                                                                    FRC=Fixe,Dynamique unique,Dynamique cumul‚;
                                                                    ENC=Fixed,Single Dynamic,Accumulated Dynamic];
                                                   OptionString=Fixed,Single Dynamic,Accumulated Dynamic }
  }
  KEYS
  {
    {    ;Reminder Terms Code,No.                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ReminderLevel@1000 : Record 293;
      ReminderText@1001 : Record 294;
      CurrencyForReminderLevel@1002 : Record 329;
      AdditionalFeeSetup@1003 : Record 1050;

    PROCEDURE CalculateAdditionalFixedFee@1002(CurrencyCode@1000 : Code[10];ChargePerLine@1001 : Boolean;PostingDate@1004 : Date) : Decimal;
    VAR
      CurrExchRate@1003 : Record 330;
      FeeAmount@1002 : Decimal;
    BEGIN
      IF CurrencyCode = '' THEN BEGIN
        IF ChargePerLine THEN
          EXIT("Add. Fee per Line Amount (LCY)");

        EXIT("Additional Fee (LCY)");
      END;

      CurrencyForReminderLevel.SETRANGE("Reminder Terms Code","Reminder Terms Code");
      CurrencyForReminderLevel.SETRANGE("No.","No.");
      CurrencyForReminderLevel.SETRANGE("Currency Code",CurrencyCode);
      IF CurrencyForReminderLevel.FINDFIRST THEN BEGIN
        IF ChargePerLine THEN
          EXIT(CurrencyForReminderLevel."Add. Fee per Line");

        EXIT(CurrencyForReminderLevel."Additional Fee");
      END;
      IF ChargePerLine THEN
        FeeAmount := "Add. Fee per Line Amount (LCY)"
      ELSE
        FeeAmount := "Additional Fee (LCY)";
      EXIT(CurrExchRate.ExchangeAmtLCYToFCY(
          PostingDate,CurrencyCode,
          FeeAmount,
          CurrExchRate.ExchangeRate(PostingDate,CurrencyCode)));
    END;

    PROCEDURE NewRecord@1();
    BEGIN
      ReminderLevel.SETRANGE("Reminder Terms Code","Reminder Terms Code");
      IF ReminderLevel.FINDLAST THEN
        "No." := ReminderLevel."No.";
      "No." += 1;
    END;

    PROCEDURE GetAdditionalFee@1000(RemainingAmount@1003 : Decimal;CurrencyCode@1004 : Code[10];ChargePerLine@1005 : Boolean;PostingDate@1007 : Date) : Decimal;
    VAR
      ReminderTerms@1000 : Record 292;
      AdditionalFeeSetup@1002 : Record 1050;
    BEGIN
      IF NOT ReminderTerms.GET("Reminder Terms Code") THEN
        EXIT(0);

      CASE "Add. Fee Calculation Type" OF
        "Add. Fee Calculation Type"::Fixed:
          EXIT(CalculateAdditionalFixedFee(CurrencyCode,ChargePerLine,PostingDate));
        "Add. Fee Calculation Type"::"Accumulated Dynamic":
          EXIT(AdditionalFeeSetup.GetAdditionalFeeFromSetup(Rec,RemainingAmount,
              CurrencyCode,ChargePerLine,"Add. Fee Calculation Type"::"Accumulated Dynamic",PostingDate));
        "Add. Fee Calculation Type"::"Single Dynamic":
          EXIT(AdditionalFeeSetup.GetAdditionalFeeFromSetup(Rec,RemainingAmount,
              CurrencyCode,ChargePerLine,"Add. Fee Calculation Type"::"Single Dynamic",PostingDate));
      END;
    END;

    BEGIN
    END.
  }
}

