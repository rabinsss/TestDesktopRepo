OBJECT Table 9600 XML Schema
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=VAR
               XMLSchemaElement@1000 : Record 9610;
               XMLSchemaRestriction@1001 : Record 9611;
               ChildXMLSchema@1002 : Record 9600;
               ReferencedXMLSchema@1003 : Record 9612;
               TopSchemaCode@1005 : Code[20];
             BEGIN
               IF Indentation > 0 THEN
                 TopSchemaCode := GetTopSchemaCode(Rec)
               ELSE
                 TopSchemaCode := Code;

               ReferencedXMLSchema.SETFILTER(Code,STRSUBSTNO('%1:*|%1',TopSchemaCode));
               ReferencedXMLSchema.DELETEALL(TRUE);

               XMLSchemaElement.SETFILTER("XML Schema Code",STRSUBSTNO('%1:*|%1',TopSchemaCode));
               XMLSchemaElement.DELETEALL(TRUE);

               XMLSchemaRestriction.SETFILTER("XML Schema Code",STRSUBSTNO('%1:*|%1',TopSchemaCode));
               XMLSchemaRestriction.DELETEALL(TRUE);

               ChildXMLSchema.SETFILTER(Code,STRSUBSTNO('(%1:*|%1)&(<>%2)',TopSchemaCode,Code));
               ChildXMLSchema.DELETEALL(FALSE);
             END;

    CaptionML=[ENU=XML Schema;
               ESM=Esquema XML;
               FRC=Sch‚ma XML;
               ENC=XML Schema];
    LookupPageID=Page9600;
    DrillDownPageID=Page9600;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code] }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 3   ;   ;Target Namespace    ;Text250       ;CaptionML=[ENU=Target Namespace;
                                                              ESM=Espacio de nombres de destino;
                                                              FRC=Espace de noms cible;
                                                              ENC=Target Namespace] }
    { 4   ;   ;XSD                 ;BLOB          ;CaptionML=[ENU=XSD;
                                                              ESM=XSD;
                                                              FRC=XSD;
                                                              ENC=XSD] }
    { 6   ;   ;Indentation         ;Integer       ;CaptionML=[ENU=Indentation;
                                                              ESM=Indentar;
                                                              FRC=Indentation;
                                                              ENC=Indentation] }
    { 7   ;   ;Path                ;Text250       ;CaptionML=[ENU=Path;
                                                              ESM=Ruta acceso;
                                                              FRC=Chemin;
                                                              ENC=Path] }
    { 10  ;   ;Target Namespace Aliases;Text250   ;CaptionML=[ENU=Target Namespace Aliases;
                                                              ESM=Alias espacio de nombres de destino;
                                                              FRC=Alias espace de noms cible;
                                                              ENC=Target Namespace Aliases] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ReplaceQst@1000 : TextConst 'ENU=Do you want to replace the existing definition?;ESM=¨Confirma que desea sustituir la definici¢n existente?;FRC=Souhaitez-vous remplacer la d‚finition existante ?;ENC=Do you want to replace the existing definition?';

    PROCEDURE LoadSchema@4();
    VAR
      TempBlob@1004 : Record 99008535;
      XMLSchema@1006 : Record 9600;
      FileMgt@1003 : Codeunit 419;
      XSDParser@1005 : Codeunit 9610;
      XMLExists@1002 : Boolean;
      FileName@1001 : Text;
      i@1000 : Integer;
    BEGIN
      CALCFIELDS(XSD);
      XMLExists := XSD.HASVALUE;

      FileName := FileMgt.BLOBImport(TempBlob,'*.xsd');
      IF FileName = '' THEN
        EXIT;

      IF XMLExists THEN BEGIN
        IF NOT CONFIRM(ReplaceQst,FALSE) THEN
          EXIT;

        TESTFIELD(Code);
        XMLSchema := Rec;
        DELETE(TRUE);
        XMLSchema.INSERT;
        Rec := XMLSchema;
      END;

      XSD := TempBlob.Blob;
      IF STRPOS(FileName,'\') <> 0 THEN BEGIN
        i := STRLEN(FileName);
        WHILE (i > 0) AND (FileName[i] <> '\') DO
          i := i - 1;
      END;

      Description := COPYSTR(FileMgt.GetFileName(FileName),1,MAXSTRLEN(Description));
      Path := COPYSTR(FileName,1,MAXSTRLEN(Path));
      XSDParser.LoadSchema(Rec);
      MODIFY;
    END;

    PROCEDURE ExportSchema@6(ShowFileDialog@1002 : Boolean) : Text;
    VAR
      TempBlob@1001 : Record 99008535;
      FileMgt@1000 : Codeunit 419;
    BEGIN
      CALCFIELDS(XSD);
      IF XSD.HASVALUE THEN BEGIN
        TempBlob.Blob := XSD;
        EXIT(FileMgt.BLOBExport(TempBlob,'*.xsd',ShowFileDialog));
      END;
    END;

    PROCEDURE GetSchemaContext@1() : Text;
    VAR
      XMLSchemaElement@1000 : Record 9610;
      XMLSchemaElement2@1001 : Record 9610;
      Context@1002 : Text;
    BEGIN
      XMLSchemaElement.SETRANGE("XML Schema Code",Code);
      XMLSchemaElement.SETCURRENTKEY(Indentation);

      Context := '';
      IF XMLSchemaElement.FINDSET THEN
        REPEAT
          XMLSchemaElement2.COPYFILTERS(XMLSchemaElement);
          XMLSchemaElement2.SETRANGE(Indentation,XMLSchemaElement.Indentation);
          IF XMLSchemaElement2.COUNT > 1 THEN
            EXIT(Context);
          Context := XMLSchemaElement.GetFullPath;
        UNTIL XMLSchemaElement.NEXT = 0;
    END;

    PROCEDURE GetTopSchemaCode@32(XMLSchema@1000 : Record 9600) : Code[20];
    VAR
      TopElementCode@1001 : Text;
    BEGIN
      TopElementCode := XMLSchema.Code;
      IF STRPOS(XMLSchema.Code,':') > 1 THEN
        TopElementCode := COPYSTR(XMLSchema.Code,1,STRPOS(XMLSchema.Code,':') - 1);

      EXIT(TopElementCode);
    END;

    BEGIN
    END.
  }
}

