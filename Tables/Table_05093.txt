OBJECT Table 5093 Opportunity Entry
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               Opp@1000 : Record 5092;
             BEGIN
               Opp.GET("Opportunity No.");
               CASE "Action Taken" OF
                 "Action Taken"::" ",
                 "Action Taken"::Next,
                 "Action Taken"::Previous,
                 "Action Taken"::Updated,
                 "Action Taken"::Jumped:
                   IF Opp.Status <> Opp.Status::"In Progress" THEN BEGIN
                     Opp.Status := Opp.Status::"In Progress";
                     Opp.MODIFY;
                   END;
                 "Action Taken"::Won:
                   BEGIN
                     TestCust;
                     IF Opp.Status <> Opp.Status::Won THEN BEGIN
                       Opp.Status := Opp.Status::Won;
                       Opp.Closed := TRUE;
                       Opp."Date Closed" := "Date of Change";
                       "Date Closed" := "Date of Change";
                       "Estimated Close Date" := "Date of Change";
                       Opp.MODIFY;
                     END;
                   END;
                 "Action Taken"::Lost:
                   IF Opp.Status <> Opp.Status::Lost THEN BEGIN
                     Opp.Status := Opp.Status::Lost;
                     Opp.Closed := TRUE;
                     Opp."Date Closed" := "Date of Change";
                     "Date Closed" := "Date of Change";
                     "Estimated Close Date" := "Date of Change";
                     Opp.MODIFY;
                   END;
               END;
             END;

    CaptionML=[ENU=Opportunity Entry;
               ESM=Mov. oportunidad;
               FRC=êcriture opportunitÇ;
               ENC=Opportunity Entry];
    LookupPageID=Page5130;
    DrillDownPageID=Page5130;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=Nß mov.;
                                                              FRC=N¯ Çcriture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Opportunity No.     ;Code20        ;TableRelation=Opportunity;
                                                   OnValidate=VAR
                                                                OppEntry@1000 : Record 5093;
                                                              BEGIN
                                                                OppEntry.SETCURRENTKEY(Active,"Opportunity No.");
                                                                OppEntry.SETRANGE(Active,TRUE);
                                                                OppEntry.SETRANGE("Opportunity No.","Opportunity No.");
                                                                IF OppEntry.FINDFIRST THEN BEGIN
                                                                  "Estimated Value (LCY)" := OppEntry."Estimated Value (LCY)";
                                                                  "Chances of Success %" := OppEntry."Chances of Success %";
                                                                  "Date of Change" := OppEntry."Date of Change";
                                                                  IF OppEntry."Date of Change" > WORKDATE THEN
                                                                    "Date of Change" := OppEntry."Date of Change"
                                                                  ELSE
                                                                    "Date of Change" := WORKDATE;
                                                                  "Estimated Close Date" := OppEntry."Estimated Close Date";
                                                                  "Previous Sales Cycle Stage" := OppEntry."Sales Cycle Stage";
                                                                  "Action Taken" := OppEntry."Action Taken";
                                                                END ELSE
                                                                  "Date of Change" := WORKDATE;
                                                              END;

                                                   CaptionML=[ENU=Opportunity No.;
                                                              ESM=Nß oportunidad;
                                                              FRC=N¯ opportunitÇ;
                                                              ENC=Opportunity No.] }
    { 3   ;   ;Sales Cycle Code    ;Code10        ;TableRelation="Sales Cycle";
                                                   CaptionML=[ENU=Sales Cycle Code;
                                                              ESM=C¢d. ciclo ventas;
                                                              FRC=Code cycle de vente;
                                                              ENC=Sales Cycle Code] }
    { 4   ;   ;Sales Cycle Stage   ;Integer       ;TableRelation="Sales Cycle Stage".Stage WHERE (Sales Cycle Code=FIELD(Sales Cycle Code));
                                                   CaptionML=[ENU=Sales Cycle Stage;
                                                              ESM=Etapa ciclo ventas;
                                                              FRC=Phase du cycle de vente;
                                                              ENC=Sales Cycle Stage];
                                                   MinValue=1 }
    { 5   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   CaptionML=[ENU=Contact No.;
                                                              ESM=Nß contacto;
                                                              FRC=N¯ contact;
                                                              ENC=Contact No.] }
    { 6   ;   ;Contact Company No. ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=[ENU=Contact Company No.;
                                                              ESM=Nß empresa contacto;
                                                              FRC=N¯ compagnie contact;
                                                              ENC=Contact Company No.] }
    { 7   ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[ENU=Salesperson Code;
                                                              ESM=C¢d. vendedor;
                                                              FRC=Code reprÇsentant;
                                                              ENC=Salesperson Code] }
    { 8   ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=[ENU=Campaign No.;
                                                              ESM=Nß campa§a;
                                                              FRC=N¯ promotion;
                                                              ENC=Campaign No.] }
    { 9   ;   ;Date of Change      ;Date          ;CaptionML=[ENU=Date of Change;
                                                              ESM=Fecha del cambio;
                                                              FRC=Date modification;
                                                              ENC=Date of Change] }
    { 10  ;   ;Active              ;Boolean       ;CaptionML=[ENU=Active;
                                                              ESM=Activo;
                                                              FRC=Actif;
                                                              ENC=Active] }
    { 11  ;   ;Date Closed         ;Date          ;CaptionML=[ENU=Date Closed;
                                                              ESM=Fecha cerrada;
                                                              FRC=Date fermeture;
                                                              ENC=Date Closed] }
    { 12  ;   ;Days Open           ;Decimal       ;CaptionML=[ENU=Days Open;
                                                              ESM=D°as abierto;
                                                              FRC=Jours ouverture;
                                                              ENC=Days Open];
                                                   DecimalPlaces=0:0;
                                                   MinValue=0 }
    { 13  ;   ;Action Taken        ;Option        ;OnValidate=BEGIN
                                                                IF "Action Taken" <> xRec."Action Taken" THEN
                                                                  CLEAR("Close Opportunity Code");
                                                              END;

                                                   CaptionML=[ENU=Action Taken;
                                                              ESM=Acci¢n tomada;
                                                              FRC=Action choisie;
                                                              ENC=Action Taken];
                                                   OptionCaptionML=[ENU=" ,Next,Previous,Updated,Jumped,Won,Lost";
                                                                    ESM=" ,Siguiente,Anterior,Actualizado,Saltado,Ganado,Perdido";
                                                                    FRC=" ,Suivante,PrÇcÇdente,Mise Ö jour,Omise,GagnÇe,Perdue";
                                                                    ENC=" ,Next,Previous,Updated,Jumped,Won,Lost"];
                                                   OptionString=[ ,Next,Previous,Updated,Jumped,Won,Lost] }
    { 14  ;   ;Estimated Value (LCY);Decimal      ;CaptionML=[ENU=Estimated Value ($);
                                                              ESM=Valor estimado ($);
                                                              FRC=Valeur estimÇe ($);
                                                              ENC=Estimated Value ($)];
                                                   AutoFormatType=1 }
    { 15  ;   ;Calcd. Current Value (LCY);Decimal ;CaptionML=[ENU=Calcd. Current Value ($);
                                                              ESM=Valor actual calculado ($);
                                                              FRC=Valeur calculÇe ($);
                                                              ENC=Calcd. Current Value ($)];
                                                   AutoFormatType=1 }
    { 16  ;   ;Completed %         ;Decimal       ;CaptionML=[ENU=Completed %;
                                                              ESM=% Completado;
                                                              FRC=% terminÇ;
                                                              ENC=Completed %];
                                                   DecimalPlaces=0:0 }
    { 17  ;   ;Chances of Success %;Decimal       ;CaptionML=[ENU=Chances of Success %;
                                                              ESM=% Posibilidades de Çxito;
                                                              FRC=% chance de succäs;
                                                              ENC=Chances of Success %];
                                                   DecimalPlaces=0:0;
                                                   MaxValue=100 }
    { 18  ;   ;Probability %       ;Decimal       ;CaptionML=[ENU=Probability %;
                                                              ESM=% Probabilidad;
                                                              FRC=% probabilitÇ;
                                                              ENC=Probability %];
                                                   DecimalPlaces=0:0 }
    { 19  ;   ;Close Opportunity Code;Code10      ;TableRelation=IF (Action Taken=CONST(Won)) "Close Opportunity Code" WHERE (Type=CONST(Won))
                                                                 ELSE IF (Action Taken=CONST(Lost)) "Close Opportunity Code" WHERE (Type=CONST(Lost));
                                                   CaptionML=[ENU=Close Opportunity Code;
                                                              ESM=C¢d. cierre oportunidad;
                                                              FRC=Code fin opportunitÇ;
                                                              ENC=Close Opportunity Code] }
    { 20  ;   ;Previous Sales Cycle Stage;Integer ;TableRelation="Sales Cycle Stage".Stage WHERE (Sales Cycle Code=FIELD(Sales Cycle Code));
                                                   CaptionML=[ENU=Previous Sales Cycle Stage;
                                                              ESM=Etapa ciclo ventas anterior;
                                                              FRC=Phase de cycle de vente prÇcÇdente;
                                                              ENC=Previous Sales Cycle Stage] }
    { 21  ;   ;Estimated Close Date;Date          ;CaptionML=[ENU=Estimated Close Date;
                                                              ESM=Fecha cierre estimada;
                                                              FRC=Date fin prÇvue;
                                                              ENC=Estimated Close Date] }
    { 9501;   ;Wizard Step         ;Option        ;CaptionML=[ENU=Wizard Step;
                                                              ESM=Paso asistente;
                                                              FRC=êtape de l'assistant;
                                                              ENC=Wizard Step];
                                                   OptionCaptionML=[ENU=" ,1,2,3,4,5,6";
                                                                    ESM=" ,1,2,3,4,5,6";
                                                                    FRC=" ,1,2,3,4,5,6";
                                                                    ENC=" ,1,2,3,4,5,6"];
                                                   OptionString=[ ,1,2,3,4,5,6];
                                                   Editable=No }
    { 9502;   ;Cancel Old To Do    ;Boolean       ;CaptionML=[ENU=Cancel Old To Do;
                                                              ESM=Cancelar tareas antiguas;
                                                              FRC=Annuler ancienne tÉche;
                                                              ENC=Cancel Old To Do] }
    { 9503;   ;Action Type         ;Option        ;CaptionML=[ENU=Action Type;
                                                              ESM=Tipo acci¢n;
                                                              FRC=Type action;
                                                              ENC=Action Type];
                                                   OptionCaptionML=[ENU=" ,First,Next,Previous,Skip,Update,Jump";
                                                                    ESM=" ,Primera,Siguiente,Anterior,Omitida,Actualizada,Saltada";
                                                                    FRC=" ,Premier,Suivant,PrÇcÇdent,Omettre,Mettre Ö jour,Sauter";
                                                                    ENC=" ,First,Next,Previous,Skip,Update,Jump"];
                                                   OptionString=[ ,First,Next,Previous,Skip,Update,Jump] }
    { 9504;   ;Sales Cycle Stage Description;Text50;
                                                   CaptionML=[ENU=Sales Cycle Stage Description;
                                                              ESM=Descripci¢n etapa ciclo ventas;
                                                              FRC=Description de phase de cycle de vente;
                                                              ENC=Sales Cycle Stage Description] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Opportunity No.                          }
    {    ;Contact Company No.,Contact No.,Active  ;SumIndexFields=Estimated Value (LCY),Calcd. Current Value (LCY) }
    {    ;Campaign No.,Active                     ;SumIndexFields=Estimated Value (LCY),Calcd. Current Value (LCY) }
    {    ;Active,Sales Cycle Code,Sales Cycle Stage,Estimated Close Date;
                                                   SumIndexFields=Estimated Value (LCY),Calcd. Current Value (LCY),Days Open }
    {    ;Active,Opportunity No.                  ;SumIndexFields=Estimated Value (LCY),Calcd. Current Value (LCY),Days Open }
    {    ;Active,Salesperson Code,Date of Change  ;SumIndexFields=Estimated Value (LCY),Calcd. Current Value (LCY) }
    {    ;Active,Salesperson Code,Estimated Close Date,Action Taken,Sales Cycle Code,Sales Cycle Stage,Completed %,Probability %,Contact No.,Contact Company No.,Campaign No.,Chances of Success %,Estimated Value (LCY),Calcd. Current Value (LCY),Close Opportunity Code;
                                                   SumIndexFields=Estimated Value (LCY),Calcd. Current Value (LCY);
                                                   MaintainSIFTIndex=No }
    {    ;Close Opportunity Code                   }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Opportunity No.,Active         }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You can not create a Customer from contact %1 before you assign a Contact Company No. to the contact.;ESM=No puede crear un cliente desde el contacto %1 antes de asignar un n.ß de empresa de contacto al contacto.;FRC=Vous ne pouvez pas crÇer de client Ö partir du contact %1 avant d''avoir affectÇ un numÇro compagnie contact au contact.;ENC=You can not create a Customer from contact %1 before you assign a Contact Company No. to the contact.';
      Text001@1002 : TextConst 'ENU=You must select either Won or Lost.;ESM=Debe seleccionar Ganada o Perdida.;FRC=Vous devez sÇlectionner GagnÇe ou Perdue.;ENC=You must select either Won or Lost.';
      CloseOpportunityCode@1003 : Record 5094;
      ToDo@1016 : Record 5080;
      TempSalesCycleStageFirst@1010 : TEMPORARY Record 5091;
      TempSalesCycleStageNext@1009 : TEMPORARY Record 5091;
      TempSalesCycleStagePrevious@1008 : TEMPORARY Record 5091;
      TempSalesCycleStageSkip@1007 : TEMPORARY Record 5091;
      TempSalesCycleStageUpdate@1006 : TEMPORARY Record 5091;
      TempSalesCycleStageJump@1005 : TEMPORARY Record 5091;
      OppEntry@1011 : Record 5093;
      SalesCycleStage@1012 : Record 5091;
      PreviousDateOfChange@1013 : Date;
      EntryExists@1014 : Boolean;
      Text005@1015 : TextConst 'ENU=You cannot go to this stage before you have assigned a sales quote.;ESM=No puede ir a esta etapa antes de asignar una cotizaci¢n de venta.;FRC=Vous ne pouvez pas passer Ö cette phase avant d''avoir attribuÇ un devis.;ENC=You cannot go to this stage before you have assigned a sales quote.';
      Text006@1020 : TextConst 'ENU=There are no stages in sales cycle %1.;ESM=No existen etapas en el ciclo ventas %1.;FRC=Les phases du cycle de vente %1 n''ont pas ÇtÇ dÇfinies.;ENC=There are no stages in sales cycle %1.';
      Text007@1019 : TextConst 'ENU=The Date of Change has to be after last Date of change.;ESM=La fecha de cambio debe ser posterior a la £ltima fecha de cambio.;FRC=La date de modification doit àtre postÇrieure Ö la derniäre date de modification.;ENC=The Date of Change has to be after last Date of change.';
      Text008@1018 : TextConst 'ENU=%1 must be greater than 0.;ESM=%1 debe ser mayor que 0.;FRC=%1 doit àtre supÇrieur Ö 0.;ENC=%1 must be greater than 0.';
      Text009@1017 : TextConst 'ENU=The Estimated closing date has to be later than this change.;ESM=La fecha estimada de cierre debe ser posterior a la fecha de esta modificaci¢n.;FRC=La date de modification doit àtre antÇrieure Ö la date de fin estimÇe.;ENC=The Estimated closing date has to be later than this change.';
      Text011@1023 : TextConst 'ENU=You must select either Won or Lost.;ESM=Debe seleccionar Ganada o Perdida.;FRC=Vous devez sÇlectionner GagnÇe ou Perdue.;ENC=You must select either Won or Lost.';
      Text012@1022 : TextConst 'ENU=Sales ($) must be greater than 0.;ESM=Ventas ($) debe ser mayor que 0.;FRC=Ventes ($) doit àtre supÇrieur Ö 0.;ENC=Sales ($) must be greater than 0.';
      Text013@1021 : TextConst 'ENU=You must fill in the %1 field.;ESM=Debe rellenar el campo %1.;FRC=Vous devez remplir le champ %1.;ENC=You must fill in the %1 field.';

    PROCEDURE InsertEntry@1(VAR OppEntry@1000 : Record 5093;CancelOldToDo@1001 : Boolean;CreateNewToDo@1002 : Boolean);
    VAR
      OppEntry2@1003 : Record 5093;
      EntryNo@1004 : Integer;
    BEGIN
      OppEntry2.RESET;
      IF OppEntry2.FINDLAST THEN
        EntryNo := OppEntry2."Entry No."
      ELSE
        EntryNo := 0;
      OppEntry2.SETCURRENTKEY(Active,"Opportunity No.");
      OppEntry2.SETRANGE(Active,TRUE);
      OppEntry2.SETRANGE("Opportunity No.",OppEntry."Opportunity No.");
      IF OppEntry2.FINDFIRST THEN BEGIN
        OppEntry2.Active := FALSE;
        OppEntry2."Days Open" := OppEntry."Date of Change" - OppEntry2."Date of Change";
        OppEntry2.MODIFY;
      END;

      OppEntry2 := OppEntry;
      OppEntry2."Entry No." := EntryNo + 1;
      OppEntry2.Active := TRUE;
      OppEntry2.CreateToDo(CancelOldToDo,CreateNewToDo);
      OppEntry2.INSERT(TRUE);
      OppEntry := OppEntry2;
    END;

    PROCEDURE UpdateEstimates@2();
    VAR
      SalesCycle@1000 : Record 5090;
      SalesCycleStage@1001 : Record 5091;
      Opp@1002 : Record 5092;
      SalesHeader@1003 : Record 36;
    BEGIN
      IF SalesCycleStage.GET("Sales Cycle Code","Sales Cycle Stage") THEN BEGIN
        SalesCycle.GET("Sales Cycle Code");
        CASE SalesCycle."Probability Calculation" OF
          SalesCycle."Probability Calculation"::Multiply:
            "Probability %" := "Chances of Success %" * SalesCycleStage."Completed %" / 100;
          SalesCycle."Probability Calculation"::Add:
            "Probability %" := ("Chances of Success %" + SalesCycleStage."Completed %") / 2;
          SalesCycle."Probability Calculation"::"Chances of Success %":
            "Probability %" := "Chances of Success %";
          SalesCycle."Probability Calculation"::"Completed %":
            "Probability %" := SalesCycleStage."Completed %";
        END;
        "Completed %" := SalesCycleStage."Completed %";
        "Calcd. Current Value (LCY)" := "Estimated Value (LCY)" * "Probability %" / 100;
      END;

      CASE "Action Taken" OF
        "Action Taken"::Won:
          BEGIN
            Opp.GET("Opportunity No.");
            IF SalesHeader.GET(SalesHeader."Document Type"::Quote,Opp."Sales Document No.") THEN
              "Calcd. Current Value (LCY)" := GetSalesDocValue(SalesHeader);

            "Completed %" := 100;
            "Chances of Success %" := 100;
            "Probability %" := 100;
          END;
        "Action Taken"::Lost:
          BEGIN
            "Calcd. Current Value (LCY)" := 0;
            "Completed %" := 100;
            "Chances of Success %" := 0;
            "Probability %" := 0;
          END;
      END;
      MODIFY;
    END;

    PROCEDURE CreateToDo@3(CancelOldToDo@1000 : Boolean;CreateNewToDo@1001 : Boolean);
    VAR
      SalesCycleStage@1002 : Record 5091;
      ToDo@1003 : Record 5080;
      Opp@1004 : Record 5092;
      RMCommentLine@1005 : TEMPORARY Record 5061;
      AttendeeTemp@1008 : TEMPORARY Record 5199;
      TodoInteractionLanguageTemp@1007 : TEMPORARY Record 5196;
      AttachmentTemp@1006 : TEMPORARY Record 5062;
    BEGIN
      IF CancelOldToDo THEN
        ToDo.CancelOpenTodos("Opportunity No.");

      IF CreateNewToDo THEN
        IF SalesCycleStage.GET("Sales Cycle Code","Sales Cycle Stage") THEN
          IF SalesCycleStage."Activity Code" <> '' THEN BEGIN
            Opp.GET("Opportunity No.");
            ToDo."No." := '';
            ToDo."Campaign No." := "Campaign No.";
            ToDo."Segment No." := Opp."Segment No.";
            ToDo."Salesperson Code" := "Salesperson Code";
            ToDo.VALIDATE("Contact No.","Contact No.");
            ToDo."Opportunity No." := "Opportunity No.";
            ToDo."Opportunity Entry No." := "Entry No.";
            ToDo.Date := "Date of Change";
            ToDo.Duration := 1440 * 1000 * 60;
            ToDo.InsertTodo(
              ToDo,RMCommentLine,AttendeeTemp,
              TodoInteractionLanguageTemp,AttachmentTemp,
              SalesCycleStage."Activity Code",FALSE);
          END;
    END;

    PROCEDURE GetSalesDocValue@4(SalesHeader@1000 : Record 36) : Decimal;
    VAR
      TotalSalesLine@1001 : Record 37;
      TotalSalesLineLCY@1002 : Record 37;
      SalesPost@1003 : Codeunit 80;
      VATAmount@1004 : Decimal;
      VATAmountText@1005 : Text[30];
      ProfitLCY@1006 : Decimal;
      ProfitPct@1007 : Decimal;
      TotalAdjCostLCY@1009 : Decimal;
    BEGIN
      SalesPost.SumSalesLines(
        SalesHeader,0,TotalSalesLine,TotalSalesLineLCY,
        VATAmount,VATAmountText,ProfitLCY,ProfitPct,TotalAdjCostLCY);
      EXIT(TotalSalesLineLCY.Amount);
    END;

    LOCAL PROCEDURE TestCust@5();
    VAR
      Cont@1000 : Record 5050;
      ContBusRel@1001 : Record 5054;
    BEGIN
      Cont.GET("Contact No.");

      IF Cont.Type = Cont.Type::Person THEN
        IF NOT Cont.GET(Cont."Company No.") THEN
          ERROR(Text000,Cont."No.");

      ContBusRel.SETRANGE("Contact No.",Cont."No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);

      IF NOT ContBusRel.FINDFIRST THEN
        Cont.CreateCustomer('');
    END;

    PROCEDURE InitOpportunityEntry@6(Opp@1000 : Record 5092);
    BEGIN
      VALIDATE("Opportunity No.",Opp."No.");
      "Sales Cycle Code" := Opp."Sales Cycle Code";
      "Contact No." := Opp."Contact No.";
      "Contact Company No." := Opp."Contact Company No.";
      "Salesperson Code" := Opp."Salesperson Code";
      "Campaign No." := Opp."Campaign No.";
    END;

    PROCEDURE CloseOppFromOpp@30(VAR Opp@1000 : Record 5092);
    BEGIN
      Opp.TESTFIELD(Closed,FALSE);
      DELETEALL;
      INIT;
      VALIDATE("Opportunity No.",Opp."No.");
      "Sales Cycle Code" := Opp."Sales Cycle Code";
      "Contact No." := Opp."Contact No.";
      "Contact Company No." := Opp."Contact Company No.";
      "Salesperson Code" := Opp."Salesperson Code";
      "Campaign No." := Opp."Campaign No.";

      StartWizard;
    END;

    LOCAL PROCEDURE StartWizard@15();
    BEGIN
      "Wizard Step" := "Wizard Step"::"1";
      INSERT;
      PAGE.RUNMODAL(PAGE::"Close Opportunity",Rec);
    END;

    PROCEDURE PerformNextWizardStatus@9();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"2":
          BEGIN
            UpdateEstimates;
            CloseOpportunityCode.RESET;
            CASE "Action Taken" OF
              "Action Taken"::Won:
                BEGIN
                  CloseOpportunityCode.SETRANGE(Type,CloseOpportunityCode.Type::Won);
                  IF CloseOpportunityCode.COUNT = 1 THEN BEGIN
                    CloseOpportunityCode.FINDFIRST;
                    "Close Opportunity Code" := CloseOpportunityCode.Code;
                  END;
                END;
              "Action Taken"::Lost:
                BEGIN
                  CloseOpportunityCode.SETRANGE(Type,CloseOpportunityCode.Type::Lost);
                  IF CloseOpportunityCode.COUNT = 1 THEN BEGIN
                    CloseOpportunityCode.FINDFIRST;
                    "Close Opportunity Code" := CloseOpportunityCode.Code;
                  END;
                END;
            END;
          END;
      END;

      "Wizard Step" := "Wizard Step" + 1;
    END;

    PROCEDURE PerformPrevWizardStatus@19();
    BEGIN
      "Wizard Step" := "Wizard Step" - 1;
    END;

    PROCEDURE CheckStatus@16();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          IF NOT ("Action Taken" IN ["Action Taken"::Won,"Action Taken"::Lost]) THEN
            ERROR(Text011);
        "Wizard Step"::"2":
          BEGIN
            IF "Close Opportunity Code" = '' THEN
              ErrorMessage(FIELDCAPTION("Close Opportunity Code"));
            IF "Date of Change" = 0D THEN
              ErrorMessage(FIELDCAPTION("Date of Change"));
            IF "Action Taken" = "Action Taken"::Won THEN
              IF "Calcd. Current Value (LCY)" <= 0 THEN
                ERROR(Text012);
          END;
      END;
    END;

    PROCEDURE FinishWizard@18();
    VAR
      OppEntry@1000 : Record 5093;
    BEGIN
      UpdateEstimates;
      OppEntry := Rec;
      InsertEntry(OppEntry,"Cancel Old To Do",FALSE);
      DELETE;
    END;

    LOCAL PROCEDURE ErrorMessage@10(FieldName@1000 : Text[1024]);
    BEGIN
      ERROR(Text013,FieldName);
    END;

    PROCEDURE UpdateOppFromOpp@31(VAR Opp@1000 : Record 5092);
    BEGIN
      Opp.TESTFIELD(Closed,FALSE);
      DELETEALL;
      INIT;
      VALIDATE("Opportunity No.",Opp."No.");
      "Sales Cycle Code" := Opp."Sales Cycle Code";
      "Contact No." := Opp."Contact No.";
      "Contact Company No." := Opp."Contact Company No.";
      "Salesperson Code" := Opp."Salesperson Code";
      "Campaign No." := Opp."Campaign No.";

      StartWizard2;
    END;

    LOCAL PROCEDURE StartWizard2@20();
    BEGIN
      "Wizard Step" := "Wizard Step"::"1";
      CreateStageList;
      INSERT;
      PAGE.RUNMODAL(PAGE::"Update Opportunity",Rec);
    END;

    PROCEDURE PerformNextWizardStatus2@12();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          BEGIN
            CASE "Action Type" OF
              "Action Type"::First:
                BEGIN
                  TempSalesCycleStageFirst.FINDFIRST;
                  "Sales Cycle Stage" := TempSalesCycleStageFirst.Stage;
                  "Sales Cycle Stage Description" := TempSalesCycleStageFirst.Description;
                  "Action Taken" := "Action Taken"::" ";
                  "Cancel Old To Do" := FALSE;
                END;
              "Action Type"::Next:
                BEGIN
                  TempSalesCycleStageNext.FINDFIRST;
                  "Sales Cycle Stage" := TempSalesCycleStageNext.Stage;
                  "Sales Cycle Stage Description" := TempSalesCycleStageNext.Description;
                  "Action Taken" := "Action Taken"::Next;
                END;
              "Action Type"::Previous:
                BEGIN
                  TempSalesCycleStagePrevious.FINDFIRST;
                  "Sales Cycle Stage" := TempSalesCycleStagePrevious.Stage;
                  "Sales Cycle Stage Description" := TempSalesCycleStagePrevious.Description;
                  "Action Taken" := "Action Taken"::Previous;
                END;
              "Action Type"::Skip:
                BEGIN
                  TempSalesCycleStageSkip.FINDFIRST;
                  "Sales Cycle Stage" := TempSalesCycleStageSkip.Stage;
                  "Sales Cycle Stage Description" := TempSalesCycleStageSkip.Description;
                  "Action Taken" := "Action Taken"::Jumped;
                END;
              "Action Type"::Update:
                BEGIN
                  TempSalesCycleStageUpdate.FINDFIRST;
                  "Sales Cycle Stage" := TempSalesCycleStageUpdate.Stage;
                  "Sales Cycle Stage Description" := TempSalesCycleStageUpdate.Description;
                  "Action Taken" := "Action Taken"::Updated;
                  "Cancel Old To Do" := FALSE;
                END;
              "Action Type"::Jump:
                BEGIN
                  TempSalesCycleStageJump.FINDLAST;
                  "Sales Cycle Stage" := TempSalesCycleStageJump.Stage;
                  "Sales Cycle Stage Description" := TempSalesCycleStageJump.Description;
                  "Action Taken" := "Action Taken"::Jumped;
                END;
            END;
            ToDo.RESET;
            ToDo.SETCURRENTKEY("Opportunity No.");
            ToDo.SETRANGE("Opportunity No.","Opportunity No.");
            IF ToDo.FINDFIRST THEN
              "Cancel Old To Do" := FALSE;
            MODIFY;
          END;
        "Wizard Step"::"2":
          BEGIN
            CASE "Action Type" OF
              "Action Type"::Skip:
                BEGIN
                  IF TempSalesCycleStageNext.GET("Sales Cycle Code","Sales Cycle Stage")THEN
                    "Action Taken" := "Action Taken"::Next;
                  MODIFY;
                END;
              "Action Type"::Jump:
                BEGIN
                  IF TempSalesCycleStagePrevious.GET("Sales Cycle Code","Sales Cycle Stage")THEN
                    "Action Taken" := "Action Taken"::Previous;
                  MODIFY;
                END;
            END;
            ValidateStage;
          END;
      END;
      "Wizard Step" := "Wizard Step" + 1;
    END;

    PROCEDURE PerformPrevWizardStatus2@21();
    BEGIN
      "Wizard Step" := "Wizard Step" - 1;
    END;

    PROCEDURE CheckStatus2@11();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          IF "Action Type" = "Action Type"::" " THEN
            ERROR(Text006,"Sales Cycle Code");
        "Wizard Step"::"2":
          BEGIN
            IF EntryExists THEN
              IF "Date of Change" < PreviousDateOfChange THEN
                ERROR(Text007);
            IF "Date of Change" = 0D THEN
              ErrorMessage(FIELDCAPTION("Date of Change"));
            ValidateStage;
          END;
        "Wizard Step"::"3":
          BEGIN
            IF "Estimated Value (LCY)" <= 0 THEN
              ERROR(Text008,FIELDCAPTION("Estimated Value (LCY)"));
            IF "Chances of Success %" <= 0 THEN
              ERROR(Text008,FIELDCAPTION("Chances of Success %"));
            IF "Estimated Close Date" = 0D THEN
              ErrorMessage(FIELDCAPTION("Estimated Close Date"));
            IF "Estimated Close Date" < "Date of Change" THEN
              ERROR(Text009);
          END;
      END;
    END;

    PROCEDURE FinishWizard2@8();
    VAR
      CreateNewToDo@1000 : Boolean;
      CancelOldToDo@1001 : Boolean;
    BEGIN
      CancelOldToDo := "Cancel Old To Do";
      CreateNewToDo := "Action Taken" <> "Action Taken"::Updated;
      "Wizard Step" := "Wizard Step"::" ";
      "Cancel Old To Do" := FALSE;
      UpdateEstimates;
      "Action Type" := "Action Type"::" ";
      "Sales Cycle Stage Description" := '';
      OppEntry := Rec;
      InsertEntry(OppEntry,CancelOldToDo,CreateNewToDo);
      DELETE;
    END;

    LOCAL PROCEDURE ErrorMessage2@7();
    BEGIN
      ERROR(Text001);
    END;

    PROCEDURE CreateStageList@13();
    VAR
      Stop@1000 : Boolean;
    BEGIN
      TempSalesCycleStageFirst.DELETEALL;
      TempSalesCycleStageNext.DELETEALL;
      TempSalesCycleStagePrevious.DELETEALL;
      TempSalesCycleStageSkip.DELETEALL;
      TempSalesCycleStageUpdate.DELETEALL;
      TempSalesCycleStageJump.DELETEALL;

      OppEntry.RESET;
      OppEntry.SETCURRENTKEY(Active,"Opportunity No.");
      OppEntry.SETRANGE(Active,TRUE);
      OppEntry.SETRANGE("Opportunity No.","Opportunity No.");
      SalesCycleStage.RESET;
      SalesCycleStage.SETRANGE("Sales Cycle Code","Sales Cycle Code");

      IF OppEntry.FIND('-') THEN BEGIN
        PreviousDateOfChange := OppEntry."Date of Change";
        EntryExists := TRUE;
      END ELSE BEGIN
        PreviousDateOfChange := WORKDATE;
        EntryExists := FALSE;
      END;

      // Option 1 Activate first Stage
      IF NOT OppEntry.FIND('-') THEN
        IF SalesCycleStage.FIND('-') THEN BEGIN
          TempSalesCycleStageFirst := SalesCycleStage;
          TempSalesCycleStageFirst.INSERT;
        END;

      // Option 2 Goto next Stage
      IF OppEntry.FIND('-') THEN
        IF SalesCycleStage.FIND('-') THEN BEGIN
          SalesCycleStage.GET(OppEntry."Sales Cycle Code",OppEntry."Sales Cycle Stage");
          IF SalesCycleStage.FIND('>') THEN BEGIN
            TempSalesCycleStageNext := SalesCycleStage;
            TempSalesCycleStageNext.INSERT;
          END;
        END;

      // Option 3 Goto Previous Stage
      IF OppEntry.FIND('-') THEN
        IF SalesCycleStage.FIND('-') THEN BEGIN
          SalesCycleStage.GET(OppEntry."Sales Cycle Code",OppEntry."Sales Cycle Stage");
          IF SalesCycleStage.FIND('<') THEN BEGIN
            TempSalesCycleStagePrevious := SalesCycleStage;
            TempSalesCycleStagePrevious.INSERT;
          END;
        END;

      // Option 4 Skip Stages
      IF OppEntry.FIND('-') THEN
        IF SalesCycleStage.FIND('-') THEN BEGIN
          SalesCycleStage.GET(OppEntry."Sales Cycle Code",OppEntry."Sales Cycle Stage");
          IF SalesCycleStage.FIND('>') THEN
            IF SalesCycleStage."Allow Skip" THEN BEGIN
              Stop := FALSE;
              REPEAT
                TempSalesCycleStageSkip := SalesCycleStage;
                TempSalesCycleStageSkip.INSERT;
                Stop := NOT SalesCycleStage."Allow Skip";
              UNTIL (SalesCycleStage.NEXT = 0) OR Stop;
            END;
        END ELSE
          IF SalesCycleStage.FIND('-') THEN
            IF SalesCycleStage."Allow Skip" THEN BEGIN
              Stop := FALSE;
              REPEAT
                TempSalesCycleStageSkip := SalesCycleStage;
                TempSalesCycleStageSkip.INSERT;
                Stop := NOT SalesCycleStage."Allow Skip";
              UNTIL (SalesCycleStage.NEXT = 0) OR Stop;
            END;

      // Option 5 Update Current
      IF OppEntry.FIND('-') THEN
        IF SalesCycleStage.FIND('-') THEN BEGIN
          SalesCycleStage.GET(OppEntry."Sales Cycle Code",OppEntry."Sales Cycle Stage");
          TempSalesCycleStageUpdate := SalesCycleStage;
          TempSalesCycleStageUpdate.INSERT;
        END;

      // Option 6 jump to Previous Stage
      IF OppEntry.FIND('-') THEN
        IF SalesCycleStage.FIND('-') THEN BEGIN
          Stop := FALSE;
          REPEAT
            TempSalesCycleStageJump := SalesCycleStage;
            IF TempSalesCycleStageJump.Stage <> OppEntry."Sales Cycle Stage" THEN
              TempSalesCycleStageJump.INSERT
            ELSE
              Stop := TRUE;
          UNTIL (SalesCycleStage.NEXT = 0) OR Stop;
        END;

      CASE TRUE OF
        NoOfSalesCyclesFirst > 0:
          "Action Type" := "Action Type"::First;
        NoOfSalesCyclesNext > 0:
          "Action Type" := "Action Type"::Next;
        NoOfSalesCyclesPrev > 0:
          "Action Type" := "Action Type"::Previous;
        NoOfSalesCyclesUpdate > 0:
          "Action Type" := "Action Type"::Update;
        NoOfSalesCyclesSkip > 1:
          "Action Type" := "Action Type"::Skip;
        NoOfSalesCyclesJump > 1:
          "Action Type" := "Action Type"::Jump;
      END;
    END;

    LOCAL PROCEDURE TestQuote@14();
    VAR
      Opp@1000 : Record 5092;
      SalesHeader@1001 : Record 36;
    BEGIN
      Opp.GET("Opportunity No.");
      IF NOT SalesHeader.GET(SalesHeader."Document Type"::Quote,Opp."Sales Document No.") THEN
        ERROR(Text005);
    END;

    PROCEDURE ValidateStage@17();
    BEGIN
      CASE "Action Type" OF
        "Action Type"::First:
          TempSalesCycleStageFirst.GET("Sales Cycle Code","Sales Cycle Stage");
        "Action Type"::Next:
          BEGIN;
            TempSalesCycleStageNext.GET("Sales Cycle Code","Sales Cycle Stage");
            IF TempSalesCycleStageNext."Quote Required" THEN
              TestQuote;
          END;
        "Action Type"::Previous:
          TempSalesCycleStagePrevious.GET("Sales Cycle Code","Sales Cycle Stage");
        "Action Type"::Skip:
          BEGIN
            TempSalesCycleStageSkip.GET("Sales Cycle Code","Sales Cycle Stage");
            IF TempSalesCycleStageSkip."Quote Required" THEN
              TestQuote;
          END;
        "Action Type"::Update:
          TempSalesCycleStageUpdate.GET("Sales Cycle Code","Sales Cycle Stage");
        "Action Type"::Jump:
          TempSalesCycleStageJump.GET("Sales Cycle Code","Sales Cycle Stage");
      END;
    END;

    PROCEDURE NoOfSalesCyclesFirst@22() : Integer;
    BEGIN
      EXIT(TempSalesCycleStageFirst.COUNT);
    END;

    PROCEDURE NoOfSalesCyclesNext@23() : Integer;
    BEGIN
      EXIT(TempSalesCycleStageNext.COUNT);
    END;

    PROCEDURE NoOfSalesCyclesPrev@24() : Integer;
    BEGIN
      EXIT(TempSalesCycleStagePrevious.COUNT);
    END;

    PROCEDURE NoOfSalesCyclesSkip@25() : Integer;
    BEGIN
      EXIT(TempSalesCycleStageSkip.COUNT);
    END;

    PROCEDURE NoOfSalesCyclesUpdate@26() : Integer;
    BEGIN
      EXIT(TempSalesCycleStageUpdate.COUNT);
    END;

    PROCEDURE NoOfSalesCyclesJump@27() : Integer;
    BEGIN
      EXIT(TempSalesCycleStageJump.COUNT);
    END;

    PROCEDURE ValidateSalesCycleStage@28();
    BEGIN
      CASE "Action Type" OF
        "Action Type"::First:
          TempSalesCycleStageFirst.GET("Sales Cycle Code","Sales Cycle Stage");
        "Action Type"::Next:
          BEGIN;
            TempSalesCycleStageNext.GET("Sales Cycle Code","Sales Cycle Stage");
            IF TempSalesCycleStageNext."Quote Required" THEN
              TestQuote;
          END;
        "Action Type"::Previous:
          TempSalesCycleStagePrevious.GET("Sales Cycle Code","Sales Cycle Stage");
        "Action Type"::Skip:
          BEGIN
            TempSalesCycleStageSkip.GET("Sales Cycle Code","Sales Cycle Stage");
            IF TempSalesCycleStageSkip."Quote Required" THEN
              TestQuote;
          END;
        "Action Type"::Update:
          TempSalesCycleStageUpdate.GET("Sales Cycle Code","Sales Cycle Stage");
        "Action Type"::Jump:
          TempSalesCycleStageJump.GET("Sales Cycle Code","Sales Cycle Stage");
      END;

      IF SalesCycleStage.GET("Sales Cycle Code","Sales Cycle Stage") THEN
        "Sales Cycle Stage Description" := SalesCycleStage.Description;
    END;

    PROCEDURE LookupSalesCycleStage@29();
    BEGIN
      CASE "Action Type" OF
        "Action Type"::First:
          IF ACTION::LookupOK = PAGE.RUNMODAL(0,TempSalesCycleStageFirst) THEN
            "Sales Cycle Stage" := TempSalesCycleStageFirst.Stage;
        "Action Type"::Next:
          IF ACTION::LookupOK = PAGE.RUNMODAL(0,TempSalesCycleStageNext) THEN
            "Sales Cycle Stage" := TempSalesCycleStageNext.Stage;
        "Action Type"::Previous:
          IF ACTION::LookupOK = PAGE.RUNMODAL(0,TempSalesCycleStagePrevious) THEN
            "Sales Cycle Stage" := TempSalesCycleStagePrevious.Stage;
        "Action Type"::Skip:
          IF ACTION::LookupOK = PAGE.RUNMODAL(0,TempSalesCycleStageSkip) THEN
            "Sales Cycle Stage" := TempSalesCycleStageSkip.Stage;
        "Action Type"::Update:
          IF ACTION::LookupOK = PAGE.RUNMODAL(0,TempSalesCycleStageUpdate) THEN
            "Sales Cycle Stage" := TempSalesCycleStageUpdate.Stage;
        "Action Type"::Jump:
          IF ACTION::LookupOK = PAGE.RUNMODAL(0,TempSalesCycleStageJump) THEN
            "Sales Cycle Stage" := TempSalesCycleStageJump.Stage;
      END;
      VALIDATE("Sales Cycle Stage");
    END;

    BEGIN
    END.
  }
}

