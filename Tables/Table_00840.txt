OBJECT Table 840 Cash Flow Forecast
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    Permissions=TableData 355=rimd,
                TableData 842=rimd,
                TableData 847=rimd;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 CFSetup.GET;
                 CFSetup.TESTFIELD("Cash Flow Forecast No. Series");
                 NoSeriesMgt.InitSeries(CFSetup."Cash Flow Forecast No. Series",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Creation Date" := WORKDATE;
               "Created By" := USERID;
               "Manual Payments From" := WORKDATE;
               "G/L Budget From" := WORKDATE;
             END;

    OnDelete=BEGIN
               IF GetShowInChart THEN
                 CFSetup.SetChartRoleCenterCFNo('');

               CFAccountComment.RESET;
               CFAccountComment.SETRANGE("Table Name",CFAccountComment."Table Name"::"Cash Flow Forecast");
               CFAccountComment.SETRANGE("No.","No.");
               CFAccountComment.DELETEALL;

               CFForecastEntry.RESET;
               CFForecastEntry.SETCURRENTKEY("Cash Flow Forecast No.");
               CFForecastEntry.SETRANGE("Cash Flow Forecast No.","No.");
               CFForecastEntry.DELETEALL;
             END;

    CaptionML=[ENU=Cash Flow Forecast;
               ESM=Previsi¢n flujos efectivo;
               FRC=Pr‚vision de la tr‚sorerie;
               ENC=Cash Flow Forecast];
    LookupPageID=Page849;
    DrillDownPageID=Page849;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Name;
                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 2   ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              ESM=Alias;
                                                              FRC=Rechercher un nom;
                                                              ENC=Search Name] }
    { 3   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Description)) OR ("Search Name" = '') THEN
                                                                  "Search Name" := Description;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESM=Descripci¢n 2;
                                                              FRC=Description 2;
                                                              ENC=Description 2] }
    { 5   ;   ;Consider Discount   ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Consider Discount" THEN
                                                                  "Consider Pmt. Disc. Tol. Date" := FALSE;
                                                              END;

                                                   CaptionML=[ENU=Consider Discount;
                                                              ESM=Considerar descuento;
                                                              FRC=Envisager escompte;
                                                              ENC=Consider Discount] }
    { 6   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESM=Fecha creaci¢n;
                                                              FRC=Date cr‚ation;
                                                              ENC=Creation Date] }
    { 7   ;   ;Created By          ;Code50        ;CaptionML=[ENU=Created By;
                                                              ESM=Creado por;
                                                              FRC=Cr‚‚ par;
                                                              ENC=Created By] }
    { 8   ;   ;Manual Payments To  ;Date          ;CaptionML=[ENU=Manual Payments To;
                                                              ESM=Pagos manuales a;
                                                              FRC=Paiements manuels jusqu'au;
                                                              ENC=Manual Payments To] }
    { 9   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Cash Flow Account Comment" WHERE (Table Name=CONST(Cash Flow Forecast),
                                                                                                        No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment] }
    { 10  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series];
                                                   Editable=No }
    { 11  ;   ;Manual Payments From;Date          ;CaptionML=[ENU=Manual Payments From;
                                                              ESM=Pagos manuales de;
                                                              FRC=Paiements manuels … partir du;
                                                              ENC=Manual Payments From] }
    { 12  ;   ;G/L Budget From     ;Date          ;AccessByPermission=TableData 95=R;
                                                   CaptionML=[ENU=G/L Budget From;
                                                              ESM=Presupuesto contable de;
                                                              FRC=Budget comptable … partir du;
                                                              ENC=G/L Budget From] }
    { 13  ;   ;G/L Budget To       ;Date          ;AccessByPermission=TableData 95=R;
                                                   CaptionML=[ENU=G/L Budget To;
                                                              ESM=Presupuesto contable a;
                                                              FRC=Budget comptable jusqu'au;
                                                              ENC=G/L Budget To] }
    { 14  ;   ;Consider CF Payment Terms;Boolean  ;CaptionML=[ENU=Consider CF Payment Terms;
                                                              ESM=Considerar t‚rminos pago flujos efectivo;
                                                              FRC=Envisager modalit‚s de paiement tr‚sorerie;
                                                              ENC=Consider CF Payment Terms] }
    { 15  ;   ;Consider Pmt. Disc. Tol. Date;Boolean;
                                                   CaptionML=[ENU=Consider Pmt. Disc. Tol. Date;
                                                              ESM=Considerar fecha told. desc. pago;
                                                              FRC=Envisager date ‚cart d'escompte de paiement;
                                                              ENC=Consider Pmt. Disc. Tol. Date] }
    { 16  ;   ;Consider Pmt. Tol. Amount;Boolean  ;CaptionML=[ENU=Consider Pmt. Tol. Amount;
                                                              ESM=Considerar importe tol. pago;
                                                              FRC=Envisager montant tol‚rance rŠglement;
                                                              ENC=Consider Pmt. Tol. Amount] }
    { 17  ;   ;Account No. Filter  ;Code20        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Account No. Filter;
                                                              ESM=Filtro n§ cuenta;
                                                              FRC=Filtre nø compte;
                                                              ENC=Account No. Filter] }
    { 18  ;   ;Source Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Source Type Filter;
                                                              ESM=Filtro tipo origen;
                                                              FRC=Filtre type d'origine;
                                                              ENC=Source Type Filter];
                                                   OptionCaptionML=[ENU=" ,Receivables,Payables,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Fixed Assets Budget,Fixed Assets Disposal,Service Orders,G/L Budget";
                                                                    ESM=" ,Cobros,Pagos,Fondos l¡quidos,Gastos manuales flujos efectivo,Ingresos manuales flujos efectivo,Pedido ventas,Pedido compra,Presupuesto activos fijos,Venta/Baja activos fijos,Pedidos servicio,Presupuesto contable";
                                                                    FRC=" ,Clients,Fournisseurs,Fonds liquides,D‚penses manuelles de tr‚sorerie,Revenus manuels de tr‚sorerie,Document de vente,Bon de commande,Budget immobilisations,Cession d'immobilisations,Commandes service,Budget";
                                                                    ENC=" ,Receivables,Payables,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Fixed Assets Budget,Fixed Assets Disposal,Service Orders,G/L Budget"];
                                                   OptionString=[ ,Receivables,Payables,Liquid Funds,Cash Flow Manual Expense,Cash Flow Manual Revenue,Sales Order,Purchase Order,Fixed Assets Budget,Fixed Assets Disposal,Service Orders,G/L Budget] }
    { 19  ;   ;Cash Flow Date Filter;Date         ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Cash Flow Date Filter;
                                                              ESM=Filtro fechas flujos efectivo;
                                                              FRC=Filtre date de tr‚sorerie;
                                                              ENC=Cash Flow Date Filter] }
    { 20  ;   ;Amount (LCY)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Cash Flow Forecast Entry"."Amount (LCY)" WHERE (Cash Flow Forecast No.=FIELD(No.),
                                                                                                                    Cash Flow Date=FIELD(Cash Flow Date Filter),
                                                                                                                    Source Type=FIELD(Source Type Filter),
                                                                                                                    Cash Flow Account No.=FIELD(Account No. Filter),
                                                                                                                    Positive=FIELD(Positive Filter)));
                                                   CaptionML=[ENU=Amount ($);
                                                              ESM=Importe ($);
                                                              FRC=Montant $;
                                                              ENC=Amount ($)] }
    { 21  ;   ;Positive Filter     ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=Positive Filter;
                                                              ESM=Filtro positivo;
                                                              FRC=Filtre positif;
                                                              ENC=Positive Filter] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Description 2,Creation Date }
  }
  CODE
  {
    VAR
      CFSetup@1000 : Record 843;
      CashFlowForecast@1001 : Record 840;
      CFAccountComment@1002 : Record 842;
      CFForecastEntry@1003 : Record 847;
      NoSeriesMgt@1005 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldCashFlowForecast@1000 : Record 840) : Boolean;
    BEGIN
      WITH CashFlowForecast DO BEGIN
        CashFlowForecast := Rec;
        CFSetup.GET;
        CFSetup.TESTFIELD("Cash Flow Forecast No. Series");
        IF NoSeriesMgt.SelectSeries(CFSetup."Cash Flow Forecast No. Series",OldCashFlowForecast."No. Series","No. Series") THEN BEGIN
          CFSetup.GET;
          CFSetup.TESTFIELD("Cash Flow Forecast No. Series");
          NoSeriesMgt.SetSeries("No.");
          Rec := CashFlowForecast;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE DrillDown@17();
    VAR
      CFForecastEntry@1000 : Record 847;
    BEGIN
      CFForecastEntry.DrillDownOnEntries(Rec);
    END;

    PROCEDURE CalcAmountForPosNeg@18(PositiveAmount@1000 : Boolean) : Decimal;
    BEGIN
      SETRANGE("Positive Filter",PositiveAmount);
      EXIT(CalcAmount);
    END;

    PROCEDURE DrillDownPosNegEntries@16(PositiveAmount@1000 : Boolean);
    BEGIN
      SETRANGE("Positive Filter",PositiveAmount);
      DrillDown;
    END;

    PROCEDURE CalcAmountForAccountNo@12(AccountNo@1000 : Code[20]) : Decimal;
    BEGIN
      SetAccountNoFilter(AccountNo);
      EXIT(CalcAmount);
    END;

    PROCEDURE SetAccountNoFilter@13(AccountNo@1000 : Code[20]);
    BEGIN
      IF AccountNo = '' THEN
        SETRANGE("Account No. Filter")
      ELSE
        SETRANGE("Account No. Filter",AccountNo);
    END;

    PROCEDURE DrillDownEntriesForAccNo@15(AccountNo@1000 : Code[20]);
    BEGIN
      SetAccountNoFilter(AccountNo);
      DrillDown;
    END;

    PROCEDURE CalcAmountFromSource@4(SourceType@1001 : Option) : Decimal;
    BEGIN
      SetSourceTypeFilter(SourceType);
      EXIT(CalcAmount);
    END;

    PROCEDURE SetSourceTypeFilter@7(SourceType@1000 : Option);
    BEGIN
      IF SourceType = 0 THEN
        SETRANGE("Source Type Filter")
      ELSE
        SETRANGE("Source Type Filter",SourceType);
    END;

    PROCEDURE DrillDownEntriesFromSource@5(SourceType@1000 : Option);
    BEGIN
      SetSourceTypeFilter(SourceType);
      DrillDown;
    END;

    PROCEDURE CalcAmount@14() : Decimal;
    BEGIN
      CALCFIELDS("Amount (LCY)");
      EXIT("Amount (LCY)");
    END;

    PROCEDURE SetCashFlowDateFilter@6(FromDate@1000 : Date;ToDate@1001 : Date);
    BEGIN
      IF (FromDate = 0D) AND (ToDate = 0D) THEN
        SETRANGE("Cash Flow Date Filter")
      ELSE
        IF ToDate = 0D THEN
          SETFILTER("Cash Flow Date Filter",'%1..',FromDate)
        ELSE
          SETRANGE("Cash Flow Date Filter",FromDate,ToDate);
    END;

    PROCEDURE PrintRecords@1();
    VAR
      CashFlowForecast@1001 : Record 840;
      CFReportSelection@1002 : Record 856;
    BEGIN
      WITH CashFlowForecast DO BEGIN
        COPY(Rec);
        CFReportSelection.SETFILTER("Report ID",'<>0');
        IF CFReportSelection.FINDSET THEN
          REPEAT
            REPORT.RUNMODAL(CFReportSelection."Report ID",TRUE,FALSE,CashFlowForecast);
          UNTIL CFReportSelection.NEXT = 0;
      END;
    END;

    PROCEDURE LookupCashFlowFilter@9(VAR Text@1000 : Text[1024]) : Boolean;
    VAR
      CashFlowForecastList@1002 : Page 849;
    BEGIN
      CashFlowForecastList.LOOKUPMODE(TRUE);
      IF CashFlowForecastList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        Text := CashFlowForecastList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    PROCEDURE CalculateAllAmounts@3(FromDate@1003 : Date;ToDate@1002 : Date;VAR Amounts@1010 : ARRAY [11] OF Decimal;VAR TotalAmount@1001 : Decimal);
    VAR
      SourceType@1000 : Option;
    BEGIN
      CLEAR(Amounts);
      SetCashFlowDateFilter(FromDate,ToDate);
      FOR SourceType := 1 TO ARRAYLEN(Amounts) DO
        Amounts[SourceType] := CalcAmountFromSource(SourceType);
      TotalAmount := CalcAmountFromSource(0);
    END;

    PROCEDURE ValidateShowInChart@8(ShowInChart@1000 : Boolean) : Boolean;
    VAR
      NewCashFlowNo@1003 : Code[20];
    BEGIN
      IF ShowInChart THEN
        NewCashFlowNo := "No."
      ELSE
        NewCashFlowNo := '';

      CFSetup.GET;
      CFSetup.VALIDATE("CF No. on Chart in Role Center",NewCashFlowNo);
      CFSetup.MODIFY;
      EXIT(GetShowInChart);
    END;

    PROCEDURE GetShowInChart@10() : Boolean;
    VAR
      ChartRoleCenterCFNo@1000 : Code[20];
    BEGIN
      ChartRoleCenterCFNo := CFSetup.GetChartRoleCenterCFNo;
      IF ChartRoleCenterCFNo = '' THEN
        EXIT(FALSE);

      EXIT("No." = ChartRoleCenterCFNo);
    END;

    PROCEDURE GetEntryDate@11(Which@1001 : 'First,Last') : Date;
    VAR
      CFForecastEntry@1000 : Record 847;
    BEGIN
      CFForecastEntry.SETCURRENTKEY("Cash Flow Forecast No.","Cash Flow Date");
      CFForecastEntry.SETRANGE("Cash Flow Forecast No.","No.");
      CASE Which OF
        Which::First:
          IF NOT CFForecastEntry.FINDFIRST THEN
            EXIT(0D);
        Which::Last:
          IF NOT CFForecastEntry.FINDLAST THEN
            EXIT(0D);
      END;
      EXIT(CFForecastEntry."Cash Flow Date");
    END;

    BEGIN
    END.
  }
}

