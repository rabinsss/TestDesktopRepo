OBJECT Table 5936 Service Document Register
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Service Document Register;
               ESM=Registro documento servicio;
               FRC=Registres doc. serv.;
               ENC=Service Document Register];
    LookupPageID=Page5968;
    DrillDownPageID=Page5968;
  }
  FIELDS
  {
    { 1   ;   ;Source Document Type;Option        ;CaptionML=[ENU=Source Document Type;
                                                              ESM=Tipo documento origen;
                                                              FRC=Type document d'origine;
                                                              ENC=Source Document Type];
                                                   OptionCaptionML=[ENU=Order,Contract;
                                                                    ESM=Pedido,Contrato;
                                                                    FRC=Commande,Contrat;
                                                                    ENC=Order,Contract];
                                                   OptionString=Order,Contract }
    { 2   ;   ;Source Document No. ;Code20        ;CaptionML=[ENU=Source Document No.;
                                                              ESM=N§ documento origen;
                                                              FRC=Nø document d'origine;
                                                              ENC=Source Document No.] }
    { 3   ;   ;Destination Document Type;Option   ;CaptionML=[ENU=Destination Document Type;
                                                              ESM=Tipo documento destino;
                                                              FRC=Type document de destination;
                                                              ENC=Destination Document Type];
                                                   OptionCaptionML=[ENU=Invoice,Credit Memo,Posted Invoice,Posted Credit Memo;
                                                                    ESM=Factura,Nota cr‚dito,Factura registrada,Nota cr‚dito regis.;
                                                                    FRC=Facture,Note de cr‚dit,Facture report‚e,Note de cr‚dit report‚e;
                                                                    ENC=Invoice,Credit Memo,Posted Invoice,Posted Credit Memo];
                                                   OptionString=Invoice,Credit Memo,Posted Invoice,Posted Credit Memo }
    { 4   ;   ;Destination Document No.;Code20    ;CaptionML=[ENU=Destination Document No.;
                                                              ESM=N§ documento destino;
                                                              FRC=Nø de document de destination;
                                                              ENC=Destination Document No.] }
  }
  KEYS
  {
    {    ;Source Document Type,Source Document No.,Destination Document Type,Destination Document No.;
                                                   Clustered=Yes }
    {    ;Destination Document Type,Destination Document No. }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE InsertServSalesDocument@2(ServDocType@1000 : 'Order,Contract';ServDocNo@1001 : Code[20];SalesDocType@1002 : 'Invoice,Credit Memo,Posted Invoice,Posted Credit Memo';SalesDocNo@1003 : Code[20]);
    VAR
      ServDocReg@1004 : Record 5936;
    BEGIN
      IF NOT GET(ServDocType,ServDocNo,SalesDocType,SalesDocNo) THEN BEGIN
        ServDocReg.RESET;
        ServDocReg."Source Document Type" := ServDocType;
        ServDocReg."Source Document No." := ServDocNo;
        ServDocReg."Destination Document Type" := SalesDocType;
        ServDocReg."Destination Document No." := SalesDocNo;
        IF ServDocReg.INSERT THEN;
      END;
    END;

    PROCEDURE PostServSalesDocument@1(SalesDocType@1000 : 'Sales Invoice,Posted Sales Invoice,Credit Memo,Posted Credit Memo';SalesDocNo@1001 : Code[20];InvoiceNo@1002 : Code[20]);
    VAR
      ServDocReg@1003 : Record 5936;
      PostedServDocReg@1004 : Record 5936;
    BEGIN
      ServDocReg.RESET;
      ServDocReg.SETCURRENTKEY("Destination Document Type","Destination Document No.");
      ServDocReg.SETRANGE("Destination Document Type",SalesDocType);
      ServDocReg.SETRANGE("Destination Document No.",SalesDocNo);
      IF ServDocReg.FIND('-') THEN
        REPEAT
          PostedServDocReg := ServDocReg;
          CASE PostedServDocReg."Destination Document Type" OF
            PostedServDocReg."Destination Document Type"::Invoice:
              PostedServDocReg."Destination Document Type" := PostedServDocReg."Destination Document Type"::"Posted Invoice";
            PostedServDocReg."Destination Document Type"::"Credit Memo":
              PostedServDocReg."Destination Document Type" := PostedServDocReg."Destination Document Type"::"Posted Credit Memo";
          END;
          PostedServDocReg."Destination Document No." := InvoiceNo;
          PostedServDocReg.INSERT;
          ServDocReg.DELETE;
        UNTIL ServDocReg.NEXT = 0;
    END;

    PROCEDURE ServiceDocument@3(SalesDocType@1000 : 'Quote,Order,Invoice,Credit Memo,Blanket Order';SalesDocNo@1001 : Code[20];VAR ServTable@1002 : Integer;VAR ServDocNo@1003 : Code[20]) : Boolean;
    VAR
      ServDocReg@1004 : Record 5936;
    BEGIN
      ServTable := 0;
      CASE SalesDocType OF
        SalesDocType::Invoice:
          BEGIN
            CLEAR(ServDocReg);
            ServDocReg.SETCURRENTKEY("Destination Document Type","Destination Document No.");
            ServDocReg.SETRANGE("Destination Document Type",ServDocReg."Destination Document Type"::Invoice);
            ServDocReg.SETRANGE("Destination Document No.",SalesDocNo);
            IF ServDocReg.FINDFIRST THEN
              CASE ServDocReg."Source Document Type" OF
                ServDocReg."Source Document Type"::Order:
                  BEGIN
                    ServTable := DATABASE::"Service Header";
                    ServDocNo := ServDocReg."Source Document No.";
                  END;
                ServDocReg."Source Document Type"::Contract:
                  BEGIN
                    ServTable := DATABASE::"Service Contract Header";
                    ServDocNo := ServDocReg."Source Document No.";
                  END;
              END;
          END;
        SalesDocType::"Credit Memo":
          BEGIN
            CLEAR(ServDocReg);
            ServDocReg.SETCURRENTKEY("Destination Document Type","Destination Document No.");
            ServDocReg.SETRANGE("Destination Document Type",ServDocReg."Destination Document Type"::"Credit Memo");
            ServDocReg.SETRANGE("Destination Document No.",SalesDocNo);
            IF ServDocReg.FINDFIRST THEN
              CASE ServDocReg."Source Document Type" OF
                ServDocReg."Source Document Type"::Order:
                  BEGIN
                    ServTable := DATABASE::"Service Header";
                    ServDocNo := ServDocReg."Source Document No.";
                  END;
                ServDocReg."Source Document Type"::Contract:
                  BEGIN
                    ServTable := DATABASE::"Service Contract Header";
                    ServDocNo := ServDocReg."Source Document No.";
                  END;
              END;
          END;
      END;

      EXIT(ServTable <> 0)
    END;

    BEGIN
    END.
  }
}

