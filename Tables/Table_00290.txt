OBJECT Table 290 VAT Amount Line
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00,NAVNA9.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Tax Amount Line;
               ESM=L¡n. importe IVA;
               FRC=Ligne montant de la taxe;
               ENC=Tax Amount Line];
  }
  FIELDS
  {
    { 1   ;   ;VAT %               ;Decimal       ;CaptionML=[ENU=Tax %;
                                                              ESM=% IVA;
                                                              FRC=% TVA;
                                                              ENC=Tax %];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 2   ;   ;VAT Base            ;Decimal       ;CaptionML=[ENU=Tax Base;
                                                              ESM=Base IVA;
                                                              FRC=Base de TVA;
                                                              ENC=Tax Base];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 3   ;   ;VAT Amount          ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("VAT %");
                                                                TESTFIELD("VAT Base");
                                                                IF "VAT Amount" / "VAT Base" < 0 THEN
                                                                  ERROR(Text002,FIELDCAPTION("VAT Amount"));
                                                                "VAT Difference" := "VAT Amount" - "Calculated VAT Amount";
                                                              END;

                                                   CaptionML=[ENU=Tax Amount;
                                                              ESM=Importe IVA;
                                                              FRC=Montant de TVA;
                                                              ENC=Tax Amount];
                                                   AutoFormatType=1 }
    { 4   ;   ;Amount Including VAT;Decimal       ;CaptionML=[ENU=Amount Including Tax;
                                                              ESM=Importe IVA incl.;
                                                              FRC=Montant avec TVA;
                                                              ENC=Amount Including Tax];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 5   ;   ;VAT Identifier      ;Code10        ;CaptionML=[ENU=Tax Identifier;
                                                              ESM=Identific. IVA;
                                                              FRC=Identificateur de TVA;
                                                              ENC=Tax Identifier];
                                                   Editable=No }
    { 6   ;   ;Line Amount         ;Decimal       ;CaptionML=[ENU=Line Amount;
                                                              ESM=Importe l¡nea;
                                                              FRC=Montant ligne;
                                                              ENC=Line Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 7   ;   ;Inv. Disc. Base Amount;Decimal     ;CaptionML=[ENU=Inv. Disc. Base Amount;
                                                              ESM=Base descuento factura;
                                                              FRC=Montant base escompte facture;
                                                              ENC=Inv. Disc. Base Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 8   ;   ;Invoice Discount Amount;Decimal    ;OnValidate=BEGIN
                                                                TESTFIELD("Inv. Disc. Base Amount");
                                                                IF "Invoice Discount Amount" / "Inv. Disc. Base Amount" > 1 THEN
                                                                  ERROR(
                                                                    InvoiceDiscAmtIsGreaterThanBaseAmtErr,
                                                                    FIELDCAPTION("Invoice Discount Amount"),"Inv. Disc. Base Amount");
                                                                "VAT Base" := "Line Amount" - "Invoice Discount Amount";
                                                              END;

                                                   CaptionML=[ENU=Invoice Discount Amount;
                                                              ESM=Importe descuento factura;
                                                              FRC=Montant d'escompte de la facture;
                                                              ENC=Invoice Discount Amount];
                                                   AutoFormatType=1 }
    { 9   ;   ;VAT Calculation Type;Option        ;CaptionML=[ENU=Tax Calculation Type;
                                                              ESM=Tipo c lculo IVA;
                                                              FRC=Type de calcul taxe;
                                                              ENC=Tax Calculation Type];
                                                   OptionCaptionML=[ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                                    ESM=Normal,Reversi¢n,Total,Impto. venta;
                                                                    FRC=TVA normale,Frais renvers‚s TVA,TVA complŠte,Taxe de vente;
                                                                    ENC=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 10  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   CaptionML=[ENU=Tax Group Code;
                                                              ESM=C¢d. grupo impuesto;
                                                              FRC=Code groupe fiscal;
                                                              ENC=Tax Group Code];
                                                   Editable=No }
    { 11  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESM=Cantidad;
                                                              FRC=Quantit‚;
                                                              ENC=Quantity];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 12  ;   ;Modified            ;Boolean       ;CaptionML=[ENU=Modified;
                                                              ESM=Modificado;
                                                              FRC=Modifi‚;
                                                              ENC=Modified] }
    { 13  ;   ;Use Tax             ;Boolean       ;CaptionML=[ENU=Use Tax;
                                                              ESM=Impuesto de importaci¢n;
                                                              FRC=Taxe de service;
                                                              ENC=Use Tax] }
    { 14  ;   ;Calculated VAT Amount;Decimal      ;CaptionML=[ENU=Calculated Tax Amount;
                                                              ESM=Importe IVA calculado;
                                                              FRC=Montant TVA calcul‚e;
                                                              ENC=Calculated Tax Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 15  ;   ;VAT Difference      ;Decimal       ;CaptionML=[ENU=Tax Difference;
                                                              ESM=Diferencia  IVA;
                                                              FRC=Diff‚rence TVA;
                                                              ENC=Tax Difference];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 16  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              ESM=Positivo;
                                                              FRC=Positif;
                                                              ENC=Positive] }
    { 17  ;   ;Includes Prepayment ;Boolean       ;CaptionML=[ENU=Includes Prepayment;
                                                              ESM=Incluye anticipo;
                                                              FRC=Comprend le paiement anticip‚;
                                                              ENC=Includes Prepayment] }
    { 18  ;   ;VAT Clause Code     ;Code10        ;TableRelation="VAT Clause";
                                                   CaptionML=[ENU=Tax Clause Code;
                                                              ESM=C¢digo de cl usula de IVA;
                                                              FRC=Code clause TVA;
                                                              ENC=Tax Clause Code] }
    { 19  ;   ;Tax Category        ;Code10        ;CaptionML=[ENU=Tax Category;
                                                              ESM=Categor¡a de impuesto;
                                                              FRC=Cat‚gorie de taxe;
                                                              ENC=Tax Category] }
    { 10001;  ;Tax Area Code       ;Code20        ;CaptionML=[ENU=Tax Area Code;
                                                              ESM=C¢d.  rea impuesto;
                                                              FRC=Code de r‚gion fiscale;
                                                              ENC=Tax Area Code] }
  }
  KEYS
  {
    {    ;VAT Identifier,VAT Calculation Type,Tax Group Code,Tax Area Code,Use Tax,Positive;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1% Tax;ESM=%1% IVA;FRC=%1% Taxe;ENC=%1% Tax';
      Text001@1001 : TextConst 'ENU=Tax Amount;ESM=Importe IVA;FRC=Montant de TVA;ENC=Tax Amount';
      Text002@1002 : TextConst 'ENU=%1 must not be negative.;ESM=%1 no debe ser negativo.;FRC=%1 ne doit pas ˆtre n‚gatif.;ENC=%1 must not be negative.';
      InvoiceDiscAmtIsGreaterThanBaseAmtErr@1003 : TextConst '@@@=1 Invoice Discount Amount that should be set 2 Maximum Amount that you can assign;ENU=The maximum %1 that you can apply is %2.;ESM=El %1 m ximo que puede aplicar es de %2.;FRC=Le %1 maximal que vous pouvez affecter est %2.;ENC=The maximum %1 that you can apply is %2.';
      Text004@1004 : TextConst 'ENU="%1 for %2 must not exceed %3 = %4.";ESM="%1 para %2 no debe exceder %3 = %4.";FRC="La %1 du %2 ne doit pas d‚passer la %3 = %4.";ENC="%1 for %2 must not exceed %3 = %4."';
      Currency@1005 : Record 4;
      AllowVATDifference@1006 : Boolean;
      PricesIncludingVAT@1007 : Boolean;
      GlobalsInitialized@1008 : Boolean;
      Text005@1009 : TextConst 'ENU="%1 must not exceed %2 = %3.";ESM="%1 no debe exceder %2 = %3.";FRC="%1 ne doit pas d‚passer %2 = %3.";ENC="%1 must not exceed %2 = %3."';

    PROCEDURE CheckVATDifference@11(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewPricesIncludingVAT@1002 : Boolean);
    VAR
      GLSetup@1003 : Record 98;
    BEGIN
      InitGlobals(NewCurrencyCode,NewAllowVATDifference,NewPricesIncludingVAT);
      IF NOT AllowVATDifference THEN
        TESTFIELD("VAT Difference",0);
      IF ABS("VAT Difference") > Currency."Max. VAT Difference Allowed" THEN
        IF NewCurrencyCode <> '' THEN
          ERROR(
            Text004,FIELDCAPTION("VAT Difference"),Currency.Code,
            Currency.FIELDCAPTION("Max. VAT Difference Allowed"),Currency."Max. VAT Difference Allowed")
        ELSE BEGIN
          IF GLSetup.GET THEN;
          IF ABS("VAT Difference") > GLSetup."Max. VAT Difference Allowed" THEN
            ERROR(
              Text005,FIELDCAPTION("VAT Difference"),
              GLSetup.FIELDCAPTION("Max. VAT Difference Allowed"),GLSetup."Max. VAT Difference Allowed");
        END;
    END;

    LOCAL PROCEDURE InitGlobals@4(NewCurrencyCode@1000 : Code[10];NewAllowVATDifference@1001 : Boolean;NewPricesIncludingVAT@1002 : Boolean);
    BEGIN
      SetCurrency(NewCurrencyCode);
      AllowVATDifference := NewAllowVATDifference;
      PricesIncludingVAT := NewPricesIncludingVAT;
      GlobalsInitialized := TRUE;
    END;

    LOCAL PROCEDURE SetCurrency@15(CurrencyCode@1000 : Code[10]);
    BEGIN
      IF CurrencyCode = '' THEN
        Currency.InitRoundingPrecision
      ELSE
        Currency.GET(CurrencyCode);
    END;

    PROCEDURE InsertLine@1();
    VAR
      VATAmountLine@1000 : Record 290;
    BEGIN
      IF ("VAT Base" <> 0) OR ("Amount Including VAT" <> 0) THEN BEGIN
        Positive := "Line Amount" >= 0;
        VATAmountLine := Rec;
        IF FIND THEN BEGIN
          "Line Amount" := "Line Amount" + VATAmountLine."Line Amount";
          "Inv. Disc. Base Amount" := "Inv. Disc. Base Amount" + VATAmountLine."Inv. Disc. Base Amount";
          "Invoice Discount Amount" := "Invoice Discount Amount" + VATAmountLine."Invoice Discount Amount";
          Quantity := Quantity + VATAmountLine.Quantity;
          "VAT Base" := "VAT Base" + VATAmountLine."VAT Base";
          "Amount Including VAT" := "Amount Including VAT" + VATAmountLine."Amount Including VAT";
          "VAT Difference" := "VAT Difference" + VATAmountLine."VAT Difference";
          "VAT Amount" := "Amount Including VAT" - "VAT Base";
          "Calculated VAT Amount" := "Calculated VAT Amount" + VATAmountLine."Calculated VAT Amount";
          MODIFY;
        END ELSE BEGIN
          "VAT Amount" := "Amount Including VAT" - "VAT Base";
          INSERT;
        END;
      END;
    END;

    PROCEDURE GetLine@2(Number@1000 : Integer);
    BEGIN
      IF Number = 1 THEN
        FIND('-')
      ELSE
        NEXT;
    END;

    PROCEDURE VATAmountText@3() : Text[30];
    BEGIN
      IF COUNT = 1 THEN BEGIN
        FINDFIRST;
        IF "VAT %" <> 0 THEN
          EXIT(STRSUBSTNO(Text000,"VAT %"));
      END;
      EXIT(Text001);
    END;

    LOCAL PROCEDURE CopyFrom@8(VAR FromVATAmountLine@1000 : Record 290);
    BEGIN
      DELETEALL;
      IF FromVATAmountLine.FIND('-') THEN
        REPEAT
          Rec := FromVATAmountLine;
          INSERT;
        UNTIL FromVATAmountLine.NEXT = 0;
    END;

    PROCEDURE GetTotalLineAmount@10(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      LineAmount@1002 : Decimal;
    BEGIN
      IF SubtractVAT THEN
        SetCurrency(CurrencyCode);

      LineAmount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            LineAmount :=
              LineAmount + ROUND("Line Amount" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            LineAmount := LineAmount + "Line Amount";
        UNTIL NEXT = 0;

      EXIT(LineAmount);
    END;

    PROCEDURE GetTotalVATAmount@5() : Decimal;
    BEGIN
      CALCSUMS("VAT Amount");
      EXIT("VAT Amount");
    END;

    PROCEDURE GetTotalInvDiscAmount@9() : Decimal;
    BEGIN
      CALCSUMS("Invoice Discount Amount");
      EXIT("Invoice Discount Amount");
    END;

    PROCEDURE GetTotalInvDiscBaseAmount@6(SubtractVAT@1000 : Boolean;CurrencyCode@1001 : Code[10]) : Decimal;
    VAR
      InvDiscBaseAmount@1002 : Decimal;
    BEGIN
      IF SubtractVAT THEN
        SetCurrency(CurrencyCode);

      InvDiscBaseAmount := 0;

      IF FIND('-') THEN
        REPEAT
          IF SubtractVAT THEN
            InvDiscBaseAmount :=
              InvDiscBaseAmount +
              ROUND("Inv. Disc. Base Amount" / (1 + "VAT %" / 100),Currency."Amount Rounding Precision")
          ELSE
            InvDiscBaseAmount := InvDiscBaseAmount + "Inv. Disc. Base Amount";
        UNTIL NEXT = 0;
      EXIT(InvDiscBaseAmount);
    END;

    PROCEDURE GetTotalVATBase@14() : Decimal;
    BEGIN
      CALCSUMS("VAT Base");
      EXIT("VAT Base");
    END;

    PROCEDURE GetTotalAmountInclVAT@17() : Decimal;
    BEGIN
      CALCSUMS("Amount Including VAT");
      EXIT("Amount Including VAT");
    END;

    PROCEDURE GetTotalVATDiscount@19(CurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1000 : Boolean) : Decimal;
    VAR
      VATDiscount@1002 : Decimal;
    BEGIN
      SetCurrency(CurrencyCode);

      VATDiscount := 0;

      IF FIND('-') THEN
        REPEAT
          IF NewPricesIncludingVAT THEN
            VATDiscount :=
              VATDiscount +
              ROUND(
                ("Line Amount" - "Invoice Discount Amount") * "VAT %" / (100 + "VAT %"),
                Currency."Amount Rounding Precision",Currency.VATRoundingDirection) -
              "VAT Amount" + "VAT Difference"
          ELSE
            VATDiscount :=
              VATDiscount +
              ROUND(
                "VAT Base" * "VAT %" / 100,
                Currency."Amount Rounding Precision",Currency.VATRoundingDirection) -
              "VAT Amount";
        UNTIL NEXT = 0;
      EXIT(VATDiscount);
    END;

    PROCEDURE GetAnyLineModified@7() : Boolean;
    BEGIN
      IF FIND('-') THEN
        REPEAT
          IF Modified THEN
            EXIT(TRUE);
        UNTIL NEXT = 0;
      EXIT(FALSE);
    END;

    PROCEDURE SetInvoiceDiscountAmount@13(NewInvoiceDiscount@1000 : Decimal;NewCurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1002 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    VAR
      TotalInvDiscBaseAmount@1003 : Decimal;
      NewRemainder@1004 : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);
      TotalInvDiscBaseAmount := GetTotalInvDiscBaseAmount(FALSE,Currency.Code);
      IF TotalInvDiscBaseAmount = 0 THEN
        EXIT;
      FIND('-');
      REPEAT
        IF "Inv. Disc. Base Amount" <> 0 THEN BEGIN
          IF TotalInvDiscBaseAmount = 0 THEN
            NewRemainder := 0
          ELSE
            NewRemainder :=
              NewRemainder + NewInvoiceDiscount * "Inv. Disc. Base Amount" / TotalInvDiscBaseAmount;
          IF "Invoice Discount Amount" <> ROUND(NewRemainder,Currency."Amount Rounding Precision") THEN BEGIN
            VALIDATE(
              "Invoice Discount Amount",ROUND(NewRemainder,Currency."Amount Rounding Precision"));
            CalcVATFields(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);
            Modified := TRUE;
            MODIFY;
          END;
          NewRemainder := NewRemainder - "Invoice Discount Amount";
        END;
      UNTIL NEXT = 0;
    END;

    PROCEDURE SetInvoiceDiscountPercent@16(NewInvoiceDiscountPct@1000 : Decimal;NewCurrencyCode@1001 : Code[10];NewPricesIncludingVAT@1002 : Boolean;CalcInvDiscPerVATID@1003 : Boolean;NewVATBaseDiscPct@1005 : Decimal);
    VAR
      NewRemainder@1004 : Decimal;
    BEGIN
      InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);
      IF FIND('-') THEN
        REPEAT
          IF "Inv. Disc. Base Amount" <> 0 THEN BEGIN
            NewRemainder :=
              NewRemainder + NewInvoiceDiscountPct * "Inv. Disc. Base Amount" / 100;
            IF "Invoice Discount Amount" <> ROUND(NewRemainder,Currency."Amount Rounding Precision") THEN BEGIN
              VALIDATE(
                "Invoice Discount Amount",ROUND(NewRemainder,Currency."Amount Rounding Precision"));
              CalcVATFields(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);
              "VAT Difference" := 0;
              Modified := TRUE;
              MODIFY;
            END;
            IF CalcInvDiscPerVATID THEN
              NewRemainder := 0
            ELSE
              NewRemainder := NewRemainder - "Invoice Discount Amount";
          END;
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE GetCalculatedVAT@12(NewCurrencyCode@1000 : Code[10];NewPricesIncludingVAT@1001 : Boolean;NewVATBaseDiscPct@1002 : Decimal) : Decimal;
    BEGIN
      IF NOT GlobalsInitialized THEN
        InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);

      IF NewPricesIncludingVAT THEN
        EXIT(
          ROUND(
            ("Line Amount" - "Invoice Discount Amount") * "VAT %" / (100 + "VAT %") * (1 - NewVATBaseDiscPct / 100),
            Currency."Amount Rounding Precision",Currency.VATRoundingDirection));

      EXIT(
        ROUND(
          ("Line Amount" - "Invoice Discount Amount") * "VAT %" / 100 * (1 - NewVATBaseDiscPct / 100),
          Currency."Amount Rounding Precision",Currency.VATRoundingDirection));
    END;

    PROCEDURE CalcVATFields@23(NewCurrencyCode@1000 : Code[10];NewPricesIncludingVAT@1001 : Boolean;NewVATBaseDiscPct@1002 : Decimal);
    BEGIN
      IF NOT GlobalsInitialized THEN
        InitGlobals(NewCurrencyCode,FALSE,NewPricesIncludingVAT);

      "VAT Amount" := GetCalculatedVAT(NewCurrencyCode,NewPricesIncludingVAT,NewVATBaseDiscPct);

      IF NewPricesIncludingVAT THEN BEGIN
        IF NewVATBaseDiscPct = 0 THEN BEGIN
          "Amount Including VAT" := "Line Amount" - "Invoice Discount Amount";
          "VAT Base" := "Amount Including VAT" - "VAT Amount";
        END ELSE BEGIN
          "VAT Base" :=
            ROUND(
              ("Line Amount" - "Invoice Discount Amount") / (1 + "VAT %" / 100),
              Currency."Amount Rounding Precision");
          "Amount Including VAT" := "VAT Base" + "VAT Amount";
        END;
      END ELSE BEGIN
        "VAT Base" := "Line Amount" - "Invoice Discount Amount";
        "Amount Including VAT" := "VAT Base" + "VAT Amount";
      END;
      "Calculated VAT Amount" := "VAT Amount";
      "VAT Difference" := 0;
      Modified := TRUE;
    END;

    LOCAL PROCEDURE CalcValueLCY@18(Value@1003 : Decimal;PostingDate@1006 : Date;CurrencyCode@1005 : Code[10];CurrencyFactor@1004 : Decimal) : Decimal;
    VAR
      CurrencyExchangeRate@1000 : Record 330;
    BEGIN
      EXIT(CurrencyExchangeRate.ExchangeAmtFCYToLCY(PostingDate,CurrencyCode,Value,CurrencyFactor));
    END;

    PROCEDURE GetBaseLCY@20(PostingDate@1003 : Date;CurrencyCode@1002 : Code[10];CurrencyFactor@1001 : Decimal) : Decimal;
    BEGIN
      EXIT(ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor)));
    END;

    PROCEDURE GetAmountLCY@21(PostingDate@1000 : Date;CurrencyCode@1001 : Code[10];CurrencyFactor@1002 : Decimal) : Decimal;
    BEGIN
      EXIT(
        ROUND(CalcValueLCY("Amount Including VAT",PostingDate,CurrencyCode,CurrencyFactor)) -
        ROUND(CalcValueLCY("VAT Base",PostingDate,CurrencyCode,CurrencyFactor)));
    END;

    PROCEDURE GetAmtSubjectToSalesTax@1020000() : Decimal;
    VAR
      SalesTaxBase@1020000 : Decimal;
    BEGIN
      IF FINDSET THEN
        REPEAT
          IF "VAT Amount" <> 0 THEN
            SalesTaxBase += "VAT Base";
        UNTIL NEXT = 0;
      EXIT(SalesTaxBase);
    END;

    PROCEDURE GetAmtExemptFromSalesTax@1020001() : Decimal;
    VAR
      SalesTaxBase@1020000 : Decimal;
    BEGIN
      IF FINDSET THEN
        REPEAT
          IF "VAT Amount" = 0 THEN
            SalesTaxBase += "VAT Base";
        UNTIL NEXT = 0;
      EXIT(SalesTaxBase);
    END;

    PROCEDURE DeductVATAmountLine@22(VAR VATAmountLineDeduct@1001 : Record 290);
    BEGIN
      IF FINDSET THEN
        REPEAT
          VATAmountLineDeduct := Rec;
          IF VATAmountLineDeduct.FIND THEN BEGIN
            "VAT Base" -= VATAmountLineDeduct."VAT Base";
            "VAT Amount" -= VATAmountLineDeduct."VAT Amount";
            "Amount Including VAT" -= VATAmountLineDeduct."Amount Including VAT";
            "Line Amount" -= VATAmountLineDeduct."Line Amount";
            "Inv. Disc. Base Amount" -= VATAmountLineDeduct."Inv. Disc. Base Amount";
            "Invoice Discount Amount" -= VATAmountLineDeduct."Invoice Discount Amount";
            "Calculated VAT Amount" -= VATAmountLineDeduct."Calculated VAT Amount";
            "VAT Difference" -= VATAmountLineDeduct."VAT Difference";
            MODIFY;
          END;
        UNTIL NEXT = 0;
    END;

    BEGIN
    END.
  }
}

