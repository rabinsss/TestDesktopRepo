OBJECT Table 10140 Deposit Header
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVNA7.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.;
    OnInsert=BEGIN
               "Journal Template Name" := GETRANGEMAX("Journal Template Name");
               "Journal Batch Name" := GETRANGEMAX("Journal Batch Name");
               GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
               GenJnlManagement.LookupName("Journal Batch Name",GenJnlLine);
               FILTERGROUP(2);
               SETRANGE("Journal Batch Name","Journal Batch Name");
               FILTERGROUP(0);
               DepositHeader2.COPY(Rec);
               DepositHeader2.SETRANGE("Journal Template Name","Journal Template Name");
               DepositHeader2.SETRANGE("Journal Batch Name","Journal Batch Name");
               IF DepositHeader2.FINDFIRST THEN
                 ERROR(Text002,TABLECAPTION,GenJnlBatch.TABLECAPTION);
               GLSetup.GET;
               IF "No." = '' THEN BEGIN
                 GLSetup.TESTFIELD("Deposit Nos.");
                 NoSeriesMgt.InitSeries(GLSetup."Deposit Nos.",xRec."No. Series","Posting Date","No.","No. Series");
               END;
               IF "Posting Date" = 0D THEN
                 VALIDATE("Posting Date",WORKDATE);
               "Posting Description" := STRSUBSTNO(Text000,FIELDNAME("No."),"No.");

               GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
               IF (GenJnlBatch."Bal. Account Type" = GenJnlBatch."Bal. Account Type"::"Bank Account") AND
                  (GenJnlBatch."Bal. Account No." <> '')
               THEN
                 VALIDATE("Bank Account No.",GenJnlBatch."Bal. Account No.");
               "Reason Code" := GenJnlBatch."Reason Code";
             END;

    OnDelete=BEGIN
               GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
               GenJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               ERROR(Text003,TABLECAPTION);
             END;

    CaptionML=[ENU=Deposit Header;
               ESM=Cabecera dep¢sito;
               FRC=En-tˆte de d‚p“t;
               ENC=Deposit Header];
    LookupPageID=Page10146;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  GLSetup.GET;
                                                                  NoSeriesMgt.TestManual(GLSetup."Deposit Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 2   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   OnValidate=BEGIN
                                                                BankAccount.GET("Bank Account No.");
                                                                VALIDATE("Currency Code",BankAccount."Currency Code");
                                                                "Bank Acc. Posting Group" := BankAccount."Bank Acc. Posting Group";
                                                                "Language Code" := BankAccount."Language Code";

                                                                CreateDim(DATABASE::"Bank Account","Bank Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              ESM=C¢d. cuenta banco;
                                                              FRC=Nø de compte bancaire;
                                                              ENC=Bank Account No.] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                UpdateCurrencyFactor;
                                                                IF "Currency Code" <> xRec."Currency Code" THEN BEGIN
                                                                  GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                                                  GenJnlLine.MODIFYALL("Currency Code","Currency Code",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              ESM=C¢d. divisa;
                                                              FRC=Code de devise;
                                                              ENC=Currency Code];
                                                   Editable=No }
    { 4   ;   ;Currency Factor     ;Decimal       ;CaptionML=[ENU=Currency Factor;
                                                              ESM=Factor divisa;
                                                              FRC=Facteur de devise;
                                                              ENC=Currency Factor];
                                                   DecimalPlaces=0:15;
                                                   MinValue=0;
                                                   Editable=No }
    { 5   ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                UpdateCurrencyFactor;
                                                                IF "Document Date" = 0D THEN
                                                                  "Document Date" := "Posting Date";
                                                                GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                                                GenJnlLine.MODIFYALL("Posting Date","Posting Date",TRUE);
                                                              END;

                                                   CaptionML=[ENU=Posting Date;
                                                              ESM=Fecha registro;
                                                              FRC=Date de report;
                                                              ENC=Posting Date] }
    { 6   ;   ;Total Deposit Amount;Decimal       ;CaptionML=[ENU=Total Deposit Amount;
                                                              ESM=Total imp. dep¢sito;
                                                              FRC=Montant total de d‚p“t;
                                                              ENC=Total Deposit Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 7   ;   ;Document Date       ;Date          ;OnValidate=BEGIN
                                                                IF "Posting Date" = 0D THEN
                                                                  VALIDATE("Posting Date","Document Date");
                                                              END;

                                                   CaptionML=[ENU=Document Date;
                                                              ESM=Fecha emisi¢n documento;
                                                              FRC=Date de document;
                                                              ENC=Document Date] }
    { 8   ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              ESM=C¢d. dim. acceso dir. 1;
                                                              FRC=Code raccourci de dimension 1;
                                                              ENC=Shortcut Dimension 1 Code];
                                                   CaptionClass='1,2,1' }
    { 9   ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              ESM=C¢d. dim. acceso dir. 2;
                                                              FRC=Code raccourci de dimension 2;
                                                              ENC=Shortcut Dimension 2 Code];
                                                   CaptionClass='1,2,2' }
    { 10  ;   ;Bank Acc. Posting Group;Code10     ;TableRelation="Bank Account Posting Group";
                                                   CaptionML=[ENU=Bank Acc. Posting Group;
                                                              ESM=Grupo contable banco;
                                                              FRC=Groupe de report de compte bancaire;
                                                              ENC=Bank Acc. Posting Group] }
    { 11  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESM=C¢d. idioma;
                                                              FRC=Code de langue;
                                                              ENC=Language Code] }
    { 12  ;   ;No. Printed         ;Integer       ;CaptionML=[ENU=No. Printed;
                                                              ESM=N§ copias impresas;
                                                              FRC=Nombre d'impressions;
                                                              ENC=No. Printed];
                                                   Editable=No }
    { 13  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              ESM=C¢d. auditor¡a;
                                                              FRC=Code de motif;
                                                              ENC=Reason Code] }
    { 14  ;   ;Correction          ;Boolean       ;CaptionML=[ENU=Correction;
                                                              ESM=Correcci¢n;
                                                              FRC=Correction;
                                                              ENC=Correction] }
    { 15  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series];
                                                   Editable=No }
    { 16  ;   ;Posting Description ;Text50        ;CaptionML=[ENU=Posting Description;
                                                              ESM=Texto de registro;
                                                              FRC=Description du report;
                                                              ENC=Posting Description] }
    { 17  ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              ESM=Nombre libro diario;
                                                              FRC=Nom modŠle journal;
                                                              ENC=Journal Template Name];
                                                   Editable=No }
    { 18  ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              ESM=Nombre secci¢n diario;
                                                              FRC=Nom lot de journal;
                                                              ENC=Journal Batch Name];
                                                   Editable=No }
    { 21  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Bank Comment Line" WHERE (Table Name=CONST(Deposit),
                                                                                                Bank Account No.=FIELD(Bank Account No.),
                                                                                                No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 22  ;   ;Total Deposit Lines ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Gen. Journal Line".Amount WHERE (Journal Template Name=FIELD(Journal Template Name),
                                                                                                      Journal Batch Name=FIELD(Journal Batch Name)));
                                                   CaptionML=[ENU=Total Deposit Lines;
                                                              ESM=L¡neas total dep.;
                                                              FRC=Lignes de d‚p“t total;
                                                              ENC=Total Deposit Lines];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 480 ;   ;Dimension Set ID    ;Integer       ;TableRelation="Dimension Set Entry";
                                                   OnLookup=BEGIN
                                                              ShowDocDim;
                                                            END;

                                                   CaptionML=[ENU=Dimension Set ID;
                                                              ESM=Id. grupo dimensiones;
                                                              FRC=Code ensemble de dimensions;
                                                              ENC=Dimension Set ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Bank Account No.                         }
    {    ;Journal Template Name,Journal Batch Name }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      GenJnlLine@1020006 : Record 81;
      BankAccount@1020000 : Record 270;
      CurrExchRate@1020001 : Record 330;
      GLSetup@1020002 : Record 98;
      DepositHeader2@1020011 : Record 10140;
      GenJnlBatch@1020013 : Record 232;
      NoSeriesMgt@1020003 : Codeunit 396;
      DimMgt@1020004 : Codeunit 408;
      Text000@1020005 : TextConst 'ENU=Deposit %1 %2;ESM=Dep¢sito %1 %2;FRC=D‚p“t %1 %2;ENC=Deposit %1 %2';
      GenJnlManagement@1020010 : Codeunit 230;
      Text002@1020012 : TextConst 'ENU=Only one %1 is allowed for each %2. You can use Deposit, Change Batch if you want to create a new Deposit.;ESM=S¢lo puede utilizarse un %1 para cada %2. Puede utilizar Dep¢sito, Cambiar secci¢n si desea crear un nuevo dep¢sito.;FRC=Un seul %1 est permis pour chaque %2. Si vous voulez cr‚er un nouveau d‚p“t, vous pouvez utiliser D‚p“t, Modifier lot.;ENC=Only one %1 is allowed for each %2. You can use Deposit, Change Batch if you want to create a new Deposit.';
      Text003@1480000 : TextConst 'ENU=You cannot rename a %1.;ESM=No se puede cambiar el nombre a %1.;FRC=Vous ne pouvez pas renommer un %1.;ENC=You cannot rename a %1.';

    LOCAL PROCEDURE UpdateCurrencyFactor@1();
    VAR
      CurrencyDate@1020000 : Date;
    BEGIN
      IF "Currency Code" <> '' THEN BEGIN
        IF "Posting Date" <> 0D THEN
          CurrencyDate := "Posting Date"
        ELSE
          CurrencyDate := WORKDATE;
        "Currency Factor" := CurrExchRate.ExchangeRate(CurrencyDate,"Currency Code");
      END ELSE
        "Currency Factor" := 0;
    END;

    LOCAL PROCEDURE CreateDim@2(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      SourceCodeSetup@1010 : Record 242;
      TableID@1011 : ARRAY [10] OF Integer;
      No@1012 : ARRAY [10] OF Code[20];
    BEGIN
      SourceCodeSetup.GET;
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      "Dimension Set ID" :=
        DimMgt.GetDefaultDimID(
          TableID,No,SourceCodeSetup.Deposits,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code",0,0);
    END;

    LOCAL PROCEDURE ValidateShortcutDimCode@3(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateShortcutDimValues(FieldNumber,ShortcutDimCode,"Dimension Set ID");
    END;

    PROCEDURE AssistEdit@1020000(OldDepositHeader@1000 : Record 10140) : Boolean;
    VAR
      DepositHeader@1020000 : Record 10140;
    BEGIN
      WITH DepositHeader DO BEGIN
        DepositHeader := Rec;
        GLSetup.GET;
        GLSetup.TESTFIELD("Deposit Nos.");
        IF NoSeriesMgt.SelectSeries(GLSetup."Deposit Nos.",OldDepositHeader."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := DepositHeader;
          EXIT(TRUE);
        END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE ShowDocDim@1020001();
    BEGIN
      "Dimension Set ID" :=
        DimMgt.EditDimensionSet2(
          "Dimension Set ID",STRSUBSTNO('%1 %2',"Bank Account No.","No."),
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    BEGIN
    END.
  }
}

