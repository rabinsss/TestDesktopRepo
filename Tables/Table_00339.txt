OBJECT Table 339 Item Application Entry
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    Permissions=TableData 339=rm,
                TableData 343=ri;
    CaptionML=[ENU=Item Application Entry;
               ESM=Liq. mov. producto;
               FRC=êcriture affectation article;
               ENC=Item Application Entry];
    LookupPageID=Page506;
    DrillDownPageID=Page506;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=Nß mov.;
                                                              FRC=N¯ Çcriture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Item Ledger Entry No.;Integer      ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Item Ledger Entry No.;
                                                              ESM=Nß mov. producto;
                                                              FRC=N¯ Çcriture article gr. livre;
                                                              ENC=Item Ledger Entry No.] }
    { 3   ;   ;Inbound Item Entry No.;Integer     ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Inbound Item Entry No.;
                                                              ESM=Nß mov. prod. entrada;
                                                              FRC=N¯ Çcriture article entrant;
                                                              ENC=Inbound Item Entry No.] }
    { 4   ;   ;Outbound Item Entry No.;Integer    ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Outbound Item Entry No.;
                                                              ESM=Nß mov. prod. salida;
                                                              FRC=N¯ Çcriture article sortant;
                                                              ENC=Outbound Item Entry No.] }
    { 11  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESM=Cantidad;
                                                              FRC=QuantitÇ;
                                                              ENC=Quantity];
                                                   DecimalPlaces=0:5 }
    { 21  ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ESM=Fecha registro;
                                                              FRC=Date de report;
                                                              ENC=Posting Date] }
    { 23  ;   ;Transferred-from Entry No.;Integer ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Transferred-from Entry No.;
                                                              ESM=Transferido desde nß mov.;
                                                              FRC=Transf. Ö partir d'Çcriture n¯;
                                                              ENC=Transferred-from Entry No.] }
    { 25  ;   ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              ESM=Fecha creaci¢n;
                                                              FRC=Date crÇation;
                                                              ENC=Creation Date] }
    { 26  ;   ;Created By User     ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Created By User");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Created By User;
                                                              ESM=Creado por usuario;
                                                              FRC=CrÇÇ par l'utilisateur;
                                                              ENC=Created By User] }
    { 27  ;   ;Last Modified Date  ;DateTime      ;CaptionML=[ENU=Last Modified Date;
                                                              ESM=Fecha £ltima modificaci¢n;
                                                              FRC=Date de derniäre modification;
                                                              ENC=Last Modified Date] }
    { 28  ;   ;Last Modified By User;Code50       ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("Last Modified By User");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modified By User;
                                                              ESM=Èlt. modificaci¢n por usuario;
                                                              FRC=Derniäre modification par utilisateur;
                                                              ENC=Last Modified By User] }
    { 5800;   ;Cost Application    ;Boolean       ;CaptionML=[ENU=Cost Application;
                                                              ESM=Costo liquidaci¢n;
                                                              FRC=Coñt appliquÇ;
                                                              ENC=Cost Application] }
    { 5804;   ;Output Completely Invd. Date;Date  ;CaptionML=[ENU=Output Completely Invd. Date;
                                                              ESM=Fecha salida completam. factur.;
                                                              FRC=Date prod. entiärement fact.;
                                                              ENC=Output Completely Invd. Date] }
    { 5805;   ;Outbound Entry is Updated;Boolean  ;InitValue=No;
                                                   CaptionML=[ENU=Outbound Entry is Updated;
                                                              ESM=Movimiento de salida actualizado;
                                                              FRC=êcriture sortante mise Ö jour;
                                                              ENC=Outbound Entry is Updated] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Inbound Item Entry No.,Item Ledger Entry No.,Outbound Item Entry No.,Cost Application }
    {    ;Outbound Item Entry No.,Item Ledger Entry No.,Cost Application,Transferred-from Entry No. }
    {    ;Transferred-from Entry No.,Cost Application }
    {    ;Inbound Item Entry No.,Outbound Item Entry No.,Cost Application }
    {    ;Item Ledger Entry No.,Output Completely Invd. Date }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1000 : TextConst 'ENU=You have to run the %1 batch job, before you can revalue %2 %3.;ESM=Debe ejecutar el trabajo por lotes %1, para poder revaluar %2 %3.;FRC=Vous devez exÇcuter le traitement en lot %1 avant de pouvoir rÇÇvaluer %2 %3.;ENC=You have to run the %1 batch job, before you can revalue %2 %3.';
      VisitedItemApplnEntry@1001 : TEMPORARY Record 339;
      ItemLedgEntryInChainNo@1003 : TEMPORARY Record 2000000026;
      TrackChain@1002 : Boolean;
      MaxValuationDate@1004 : Date;

    PROCEDURE AppliedOutbndEntryExists@43(InbndItemLedgEntryNo@1001 : Integer;CostApplication@1000 : Boolean;FilterOnOnlyCostNotAdjusted@1002 : Boolean) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY(
        "Inbound Item Entry No.","Item Ledger Entry No.","Outbound Item Entry No.","Cost Application");
      SETRANGE("Inbound Item Entry No.",InbndItemLedgEntryNo);
      SETFILTER("Item Ledger Entry No.",'<>%1',InbndItemLedgEntryNo);
      SETFILTER("Outbound Item Entry No.",'<>%1',0);
      IF CostApplication THEN
        SETRANGE("Cost Application",TRUE);

      IF FilterOnOnlyCostNotAdjusted THEN
        SETRANGE("Outbound Entry is Updated",FALSE);

      EXIT(FINDSET);
    END;

    PROCEDURE AppliedInbndTransEntryExists@45(InbndItemLedgEntryNo@1001 : Integer;CostApplication@1002 : Boolean) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Transferred-from Entry No.","Cost Application");
      SETRANGE("Transferred-from Entry No.",InbndItemLedgEntryNo);
      IF CostApplication THEN
        SETRANGE("Cost Application",TRUE);
      EXIT(FINDSET);
    END;

    PROCEDURE AppliedInbndEntryExists@49(OutbndItemLedgEntryNo@1001 : Integer;CostApplication@1000 : Boolean) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY(
        "Outbound Item Entry No.","Item Ledger Entry No.","Cost Application","Transferred-from Entry No.");
      SETRANGE("Outbound Item Entry No.",OutbndItemLedgEntryNo);
      SETFILTER("Item Ledger Entry No.",'<>%1',OutbndItemLedgEntryNo);
      SETRANGE("Transferred-from Entry No.",0);
      IF CostApplication THEN
        SETRANGE("Cost Application",TRUE);
      EXIT(FINDSET);
    END;

    PROCEDURE AppliedFromEntryExists@3(InbndItemLedgEntryNo@1001 : Integer) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY("Outbound Item Entry No.","Item Ledger Entry No.");
      SETFILTER("Outbound Item Entry No.",'<>%1',0);
      SETRANGE("Item Ledger Entry No.",InbndItemLedgEntryNo);
      EXIT(FINDSET);
    END;

    PROCEDURE CheckAppliedFromEntryToAdjust@1(InbndItemLedgEntryNo@1000 : Integer);
    VAR
      OutbndItemLedgEntry@1001 : Record 32;
      ObjTransl@1002 : Record 377;
    BEGIN
      IF AppliedFromEntryExists(InbndItemLedgEntryNo) THEN
        REPEAT
          OutbndItemLedgEntry.GET("Outbound Item Entry No.");
          IF OutbndItemLedgEntry."Applied Entry to Adjust" THEN
            ERROR(
              Text001,
              ObjTransl.TranslateObject(ObjTransl."Object Type"::Report,REPORT::"Adjust Cost - Item Entries"),
              OutbndItemLedgEntry.TABLECAPTION,InbndItemLedgEntryNo);
        UNTIL NEXT = 0;
    END;

    PROCEDURE CostReceiver@2() : Integer;
    BEGIN
      IF "Outbound Item Entry No." = 0 THEN
        EXIT(0);
      IF "Item Ledger Entry No." = "Outbound Item Entry No." THEN
        EXIT("Outbound Item Entry No.");
      IF "Item Ledger Entry No." = "Inbound Item Entry No." THEN
        EXIT("Inbound Item Entry No.");
      EXIT(0);
    END;

    PROCEDURE Fixed@4() : Boolean;
    VAR
      InboundItemLedgerEntry@1000 : Record 32;
      OutboundItemLedgerEntry@1001 : Record 32;
    BEGIN
      IF "Outbound Item Entry No." = 0 THEN
        EXIT(FALSE);
      IF NOT InboundItemLedgerEntry.GET("Inbound Item Entry No.") THEN
        EXIT(TRUE);
      IF InboundItemLedgerEntry."Applies-to Entry" = "Outbound Item Entry No." THEN
        EXIT(TRUE);
      IF NOT OutboundItemLedgerEntry.GET("Outbound Item Entry No.") THEN
        EXIT(TRUE);
      IF OutboundItemLedgerEntry."Applies-to Entry" = "Inbound Item Entry No." THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    PROCEDURE InsertHistory@5() : Integer;
    VAR
      ItemApplnEntryHistory@1000 : Record 343;
      EntryNo@1001 : Integer;
    BEGIN
      ItemApplnEntryHistory.SETCURRENTKEY("Primary Entry No.");
      IF NOT ItemApplnEntryHistory.FINDLAST THEN
        EntryNo := 1;
      EntryNo := ItemApplnEntryHistory."Primary Entry No.";
      ItemApplnEntryHistory.TRANSFERFIELDS(Rec,TRUE);
      ItemApplnEntryHistory."Deleted Date" := CURRENTDATETIME;
      ItemApplnEntryHistory."Deleted By User" := USERID;
      ItemApplnEntryHistory."Primary Entry No." := EntryNo + 1;
      ItemApplnEntryHistory.INSERT;
      EXIT(ItemApplnEntryHistory."Primary Entry No.");
    END;

    PROCEDURE CostApplication@50() : Boolean;
    BEGIN
      EXIT((Quantity > 0) AND ("Item Ledger Entry No." = "Inbound Item Entry No."))
    END;

    PROCEDURE CheckIsCyclicalLoop@9(CheckItemLedgEntry@1004 : Record 32;FromItemLedgEntry@1005 : Record 32) : Boolean;
    BEGIN
      IF CheckItemLedgEntry."Entry No." = FromItemLedgEntry."Entry No." THEN
        EXIT(TRUE);
      VisitedItemApplnEntry.DELETEALL;
      ItemLedgEntryInChainNo.DELETEALL;

      IF FromItemLedgEntry.Positive THEN BEGIN
        IF CheckCyclicFwdToAppliedOutbnds(CheckItemLedgEntry,FromItemLedgEntry."Entry No.") THEN
          EXIT(TRUE);
        EXIT(CheckCyclicFwdToInbndTransfers(CheckItemLedgEntry,FromItemLedgEntry."Entry No."));
      END;
      IF FromItemLedgEntry."Entry Type" = FromItemLedgEntry."Entry Type"::Consumption THEN
        IF CheckCyclicProdCyclicalLoop(CheckItemLedgEntry,FromItemLedgEntry) THEN
          EXIT(TRUE);
      IF FromItemLedgEntry."Entry Type" = FromItemLedgEntry."Entry Type"::"Assembly Consumption" THEN
        IF CheckCyclicAsmCyclicalLoop(CheckItemLedgEntry,FromItemLedgEntry) THEN
          EXIT(TRUE);
      EXIT(CheckCyclicFwdToAppliedInbnds(CheckItemLedgEntry,FromItemLedgEntry."Entry No."));
    END;

    LOCAL PROCEDURE CheckCyclicProdCyclicalLoop@1100(CheckItemLedgEntry@1000 : Record 32;ItemLedgEntry@1304 : Record 32) : Boolean;
    BEGIN
      IF NOT IsItemEverOutput(ItemLedgEntry."Item No.") THEN
        EXIT(FALSE);

      IF ItemLedgEntry."Order Type" <> ItemLedgEntry."Order Type"::Production THEN
        EXIT(FALSE);
      IF ItemLedgEntry."Entry Type" = ItemLedgEntry."Entry Type"::Output THEN
        EXIT(FALSE);
      IF ItemLedgEntry.Positive THEN
        EXIT(FALSE);
      IF (CheckItemLedgEntry."Entry Type" = CheckItemLedgEntry."Entry Type"::Output) AND
         (ItemLedgEntry."Order Type" = CheckItemLedgEntry."Order Type") AND
         (ItemLedgEntry."Order No." = CheckItemLedgEntry."Order No.") AND
         (ItemLedgEntry."Order Line No." = CheckItemLedgEntry."Order Line No.")
      THEN
        EXIT(TRUE);

      ItemLedgEntry.SETCURRENTKEY("Order Type","Order No.","Order Line No.","Entry Type");
      ItemLedgEntry.SETRANGE("Order Type",ItemLedgEntry."Order Type");
      ItemLedgEntry.SETRANGE("Order No.",ItemLedgEntry."Order No.");
      ItemLedgEntry.SETRANGE("Order Line No.",ItemLedgEntry."Order Line No.");
      ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Output);
      IF MaxValuationDate <> 0D THEN
        ItemLedgEntry.SETRANGE("Posting Date",0D,MaxValuationDate);
      IF ItemLedgEntry.FINDSET THEN
        REPEAT
          IF TrackChain THEN
            IF NOT ItemLedgEntryInChainNo.GET(ItemLedgEntry."Entry No.") THEN BEGIN
              ItemLedgEntryInChainNo.Number := ItemLedgEntry."Entry No.";
              ItemLedgEntryInChainNo.INSERT;
            END;

          IF ItemLedgEntry."Entry No." = CheckItemLedgEntry."Entry No." THEN
            EXIT(TRUE);

          IF ItemLedgEntry.Positive THEN
            IF CheckCyclicFwdToAppliedOutbnds(CheckItemLedgEntry,ItemLedgEntry."Entry No.") THEN
              EXIT(TRUE);
        UNTIL ItemLedgEntry.NEXT = 0;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckCyclicAsmCyclicalLoop@1101(CheckItemLedgEntry@1000 : Record 32;ItemLedgEntry@1304 : Record 32) : Boolean;
    BEGIN
      IF ItemLedgEntry."Order Type" <> ItemLedgEntry."Order Type"::Assembly THEN
        EXIT(FALSE);
      IF ItemLedgEntry."Entry Type" = ItemLedgEntry."Entry Type"::"Assembly Output" THEN
        EXIT(FALSE);
      IF ItemLedgEntry.Positive THEN
        EXIT(FALSE);
      IF (CheckItemLedgEntry."Entry Type" = CheckItemLedgEntry."Entry Type"::"Assembly Output") AND
         (ItemLedgEntry."Order Type" = CheckItemLedgEntry."Order Type") AND
         (ItemLedgEntry."Order No." = CheckItemLedgEntry."Order No.")
      THEN
        EXIT(TRUE);

      ItemLedgEntry.SETCURRENTKEY("Order Type","Order No.","Order Line No.","Entry Type");
      ItemLedgEntry.SETRANGE("Order Type",ItemLedgEntry."Order Type");
      ItemLedgEntry.SETRANGE("Order No.",ItemLedgEntry."Order No.");
      ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::"Assembly Output");
      IF MaxValuationDate <> 0D THEN
        ItemLedgEntry.SETRANGE("Posting Date",0D,MaxValuationDate);
      IF ItemLedgEntry.FINDSET THEN
        REPEAT
          IF TrackChain THEN
            IF NOT ItemLedgEntryInChainNo.GET(ItemLedgEntry."Entry No.") THEN BEGIN
              ItemLedgEntryInChainNo.Number := ItemLedgEntry."Entry No.";
              ItemLedgEntryInChainNo.INSERT;
            END;

          IF ItemLedgEntry."Entry No." = CheckItemLedgEntry."Entry No." THEN
            EXIT(TRUE);

          IF ItemLedgEntry.Positive THEN
            IF CheckCyclicFwdToAppliedOutbnds(CheckItemLedgEntry,ItemLedgEntry."Entry No.") THEN
              EXIT(TRUE);
        UNTIL ItemLedgEntry.NEXT = 0;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckCyclicFwdToAppliedOutbnds@11(CheckItemLedgEntry@1002 : Record 32;EntryNo@1000 : Integer) : Boolean;
    VAR
      ItemApplnEntry@1001 : Record 339;
    BEGIN
      IF ItemApplnEntry.AppliedOutbndEntryExists(EntryNo,FALSE,FALSE) THEN
        EXIT(CheckCyclicFwdToAppliedEntries(CheckItemLedgEntry,ItemApplnEntry,EntryNo,TRUE));
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckCyclicFwdToAppliedInbnds@7(CheckItemLedgEntry@1002 : Record 32;EntryNo@1001 : Integer) : Boolean;
    VAR
      ItemApplnEntry@1000 : Record 339;
    BEGIN
      IF ItemApplnEntry.AppliedInbndEntryExists(EntryNo,FALSE) THEN
        EXIT(CheckCyclicFwdToAppliedEntries(CheckItemLedgEntry,ItemApplnEntry,EntryNo,FALSE));
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckCyclicFwdToInbndTransfers@6(CheckItemLedgEntry@1001 : Record 32;EntryNo@1000 : Integer) : Boolean;
    VAR
      ItemApplnEntry@1002 : Record 339;
    BEGIN
      IF ItemApplnEntry.AppliedInbndTransEntryExists(EntryNo,FALSE) THEN
        EXIT(CheckCyclicFwdToAppliedEntries(CheckItemLedgEntry,ItemApplnEntry,EntryNo,FALSE));
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE CheckCyclicFwdToProdOutput@1264(CheckItemLedgEntry@1000 : Record 32;EntryNo@1670 : Integer) : Boolean;
    VAR
      ItemLedgEntry@1991 : Record 32;
    BEGIN
      ItemLedgEntry.GET(EntryNo);
      EXIT(CheckCyclicProdCyclicalLoop(CheckItemLedgEntry,ItemLedgEntry));
    END;

    LOCAL PROCEDURE CheckCyclicFwdToAsmOutput@1102(CheckItemLedgEntry@1000 : Record 32;EntryNo@1670 : Integer) : Boolean;
    VAR
      ItemLedgEntry@1991 : Record 32;
    BEGIN
      ItemLedgEntry.GET(EntryNo);
      EXIT(CheckCyclicAsmCyclicalLoop(CheckItemLedgEntry,ItemLedgEntry));
    END;

    LOCAL PROCEDURE CheckCyclicFwdToAppliedEntries@8(CheckItemLedgEntry@1001 : Record 32;VAR ItemApplnEntry@1005 : Record 339;FromEntryNo@1002 : Integer;IsPositiveToNegativeFlow@1009 : Boolean) : Boolean;
    VAR
      ValueEntry@1000 : Record 5802;
      ToEntryNo@1008 : Integer;
    BEGIN
      IF EntryIsVisited(FromEntryNo) THEN
        EXIT(FALSE);

      REPEAT
        IF IsPositiveToNegativeFlow THEN
          ToEntryNo := ItemApplnEntry."Outbound Item Entry No."
        ELSE
          ToEntryNo := ItemApplnEntry."Inbound Item Entry No.";

        ValueEntry.SETCURRENTKEY("Item Ledger Entry No.","Valuation Date");
        ValueEntry.SETRANGE("Item Ledger Entry No.",ItemApplnEntry."Item Ledger Entry No.");
        ValueEntry.FINDLAST;
        IF (MaxValuationDate = 0D) OR (ValueEntry."Valuation Date" <= MaxValuationDate) THEN BEGIN
          IF TrackChain THEN
            IF NOT ItemLedgEntryInChainNo.GET(ToEntryNo) THEN BEGIN
              ItemLedgEntryInChainNo.Number := ToEntryNo;
              ItemLedgEntryInChainNo.INSERT;
            END;

          IF ToEntryNo = CheckItemLedgEntry."Entry No." THEN
            EXIT(TRUE);
          IF CheckCyclicFwdToAppliedOutbnds(CheckItemLedgEntry,ToEntryNo) THEN
            EXIT(TRUE);
          IF CheckCyclicFwdToAppliedInbnds(CheckItemLedgEntry,ToEntryNo) THEN
            EXIT(TRUE);
          IF CheckCyclicFwdToProdOutput(CheckItemLedgEntry,ToEntryNo) THEN
            EXIT(TRUE);
          IF CheckCyclicFwdToAsmOutput(CheckItemLedgEntry,ToEntryNo) THEN
            EXIT(TRUE);
        END;
      UNTIL ItemApplnEntry.NEXT = 0;
      EXIT(CheckCyclicFwdToInbndTransfers(CheckItemLedgEntry,FromEntryNo));
    END;

    LOCAL PROCEDURE EntryIsVisited@10(EntryNo@1000 : Integer) : Boolean;
    BEGIN
      WITH VisitedItemApplnEntry DO BEGIN
        IF GET(EntryNo) THEN BEGIN
          // This is to take into account quantity flows from an inbound entry to an inbound transfer
          IF Quantity = 2 THEN
            EXIT(TRUE);
          Quantity := Quantity + 1;
          MODIFY;
          EXIT(FALSE);
        END;
        INIT;
        "Entry No." := EntryNo;
        Quantity := Quantity + 1;
        INSERT;
        EXIT(FALSE);
      END;
    END;

    PROCEDURE GetVisitedEntries@15(FromItemLedgEntry@1000 : Record 32;VAR ItemLedgEntryInChain@1001 : Record 32;WithinValuationDate@1004 : Boolean);
    VAR
      ToItemLedgEntry@1002 : Record 32;
      DummyItemLedgEntry@1003 : Record 32;
      AvgCostAdjmtEntryPoint@1005 : Record 5804;
      ValueEntry@1006 : Record 5802;
    BEGIN
      MaxValuationDate := 0D;
      IF WithinValuationDate THEN BEGIN
        ValueEntry.SETCURRENTKEY("Item Ledger Entry No.","Entry Type");
        ValueEntry.SETRANGE("Item Ledger Entry No.",FromItemLedgEntry."Entry No.");
        ValueEntry.FINDFIRST;
        AvgCostAdjmtEntryPoint.SETRANGE("Item No.",FromItemLedgEntry."Item No.");
        AvgCostAdjmtEntryPoint.SETRANGE("Valuation Date",ValueEntry."Valuation Date",12319999D);
        IF AvgCostAdjmtEntryPoint.FINDFIRST THEN
          MaxValuationDate := AvgCostAdjmtEntryPoint."Valuation Date"
      END;

      TrackChain := TRUE;
      ItemLedgEntryInChain.RESET;
      ItemLedgEntryInChain.DELETEALL;
      DummyItemLedgEntry.INIT;
      DummyItemLedgEntry."Entry No." := -1;
      CheckIsCyclicalLoop(DummyItemLedgEntry,FromItemLedgEntry);
      IF ItemLedgEntryInChainNo.FINDSET THEN
        REPEAT
          ToItemLedgEntry.GET(ItemLedgEntryInChainNo.Number);
          ItemLedgEntryInChain := ToItemLedgEntry;
          ItemLedgEntryInChain.INSERT;
        UNTIL ItemLedgEntryInChainNo.NEXT = 0;
    END;

    PROCEDURE OutboundApplied@12(EntryNo@1002 : Integer;SameType@1005 : Boolean) : Decimal;
    VAR
      Applications@1001 : Record 339;
      ItemEntry@1000 : Record 32;
      Quantity@1003 : Decimal;
      OriginalEntry@1004 : Record 32;
    BEGIN
      IF NOT OriginalEntry.GET(EntryNo) THEN
        EXIT(0);
      IF OriginalEntry."Entry Type" = OriginalEntry."Entry Type"::Transfer THEN
        EXIT(0);
      Applications.SETCURRENTKEY("Outbound Item Entry No.");
      Applications.SETRANGE("Outbound Item Entry No.",EntryNo);
      Applications.SETRANGE("Item Ledger Entry No.",EntryNo);
      Quantity := 0;
      IF Applications.FINDSET THEN
        REPEAT
          IF ItemEntry.GET(Applications."Inbound Item Entry No.") THEN
            IF SameType THEN BEGIN
              IF ItemEntry."Entry Type" = OriginalEntry."Entry Type" THEN
                Quantity := Quantity + Applications.Quantity
            END ELSE
              Quantity := Quantity + Applications.Quantity;
        UNTIL Applications.NEXT <= 0;
      EXIT(Quantity);
    END;

    PROCEDURE InboundApplied@13(EntryNo@1002 : Integer;SameType@1005 : Boolean) : Decimal;
    VAR
      Applications@1001 : Record 339;
      ItemEntry@1000 : Record 32;
      Quantity@1003 : Decimal;
      OriginalEntry@1004 : Record 32;
    BEGIN
      IF NOT OriginalEntry.GET(EntryNo) THEN
        EXIT(0);
      IF OriginalEntry."Entry Type" = OriginalEntry."Entry Type"::Transfer THEN
        EXIT(0);
      Applications.SETCURRENTKEY("Inbound Item Entry No.");
      Applications.SETFILTER("Outbound Item Entry No.",'<>%1',0);
      Applications.SETRANGE("Inbound Item Entry No.",EntryNo);
      IF OriginalEntry.Quantity < 0 THEN
        Applications.SETRANGE("Item Ledger Entry No.",EntryNo);
      Quantity := 0;
      IF Applications.FINDSET THEN
        REPEAT
          IF ItemEntry.GET(Applications."Outbound Item Entry No.") THEN
            IF SameType THEN BEGIN
              IF (ItemEntry."Entry Type" = OriginalEntry."Entry Type") OR
                 (ItemEntry."Applies-to Entry" <> 0)
              THEN
                Quantity := Quantity + Applications.Quantity
            END ELSE
              Quantity := Quantity + Applications.Quantity;
        UNTIL Applications.NEXT = 0;
      EXIT(Quantity);
    END;

    PROCEDURE Returned@14(EntryNo@1002 : Integer) : Decimal;
    BEGIN
      EXIT(-OutboundApplied(EntryNo,TRUE) - InboundApplied(EntryNo,TRUE));
    END;

    PROCEDURE ExistsBetween@1102601000(ILE1@1102601000 : Integer;ILE2@1102601001 : Integer) : Boolean;
    VAR
      Applications@1102601002 : Record 339;
    BEGIN
      Applications.SETCURRENTKEY("Inbound Item Entry No.");
      Applications.SETRANGE("Inbound Item Entry No.",ILE1);
      Applications.SETRANGE("Outbound Item Entry No.",ILE2);
      IF Applications.FINDFIRST THEN
        EXIT(TRUE);
      Applications.SETRANGE("Inbound Item Entry No.",ILE2);
      Applications.SETRANGE("Outbound Item Entry No.",ILE1);
      IF Applications.FINDFIRST THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE IsItemEverOutput@16(ItemNo@1000 : Code[20]) : Boolean;
    VAR
      ItemLedgEntry@1001 : Record 32;
    BEGIN
      ItemLedgEntry.SETRANGE("Item No.",ItemNo);
      ItemLedgEntry.SETRANGE("Entry Type",ItemLedgEntry."Entry Type"::Output);
      EXIT(NOT ItemLedgEntry.ISEMPTY);
    END;

    PROCEDURE SetOutboundsNotUpdated@110(ItemLedgEntry@1000 : Record 32);
    VAR
      ItemApplnEntry@1001 : Record 339;
    BEGIN
      IF NOT (ItemLedgEntry."Applied Entry to Adjust" OR ItemLedgEntry.Open) THEN
        EXIT;

      IF ItemLedgEntry.Quantity < 0 THEN
        EXIT;

      ItemApplnEntry.SETRANGE("Inbound Item Entry No.",ItemLedgEntry."Entry No.");
      ItemApplnEntry.MODIFYALL("Outbound Entry is Updated",FALSE);
    END;

    PROCEDURE SetInboundToUpdated@84(ItemLedgEntry@1000 : Record 32);
    VAR
      ItemApplnEntry@1001 : Record 339;
    BEGIN
      ItemApplnEntry.SETRANGE("Outbound Item Entry No.",ItemLedgEntry."Entry No.");
      IF ItemLedgEntry."Completely Invoiced" THEN
        IF ItemApplnEntry.COUNT = 1 THEN BEGIN
          ItemApplnEntry.FINDFIRST;
          ItemApplnEntry."Outbound Entry is Updated" := TRUE;
          ItemApplnEntry.MODIFY;
        END;
    END;

    PROCEDURE IsAppliedFromIncrease@17(InbndItemLedgEntryNo@1001 : Integer) : Boolean;
    BEGIN
      RESET;
      SETCURRENTKEY(
        "Inbound Item Entry No.","Item Ledger Entry No.","Outbound Item Entry No.","Cost Application");
      SETRANGE("Inbound Item Entry No.",InbndItemLedgEntryNo);
      SETRANGE("Item Ledger Entry No.",InbndItemLedgEntryNo);
      SETFILTER("Outbound Item Entry No.",'<>%1',0);
      SETRANGE("Cost Application",TRUE);
      EXIT(FINDFIRST);
    END;

    BEGIN
    END.
  }
}

