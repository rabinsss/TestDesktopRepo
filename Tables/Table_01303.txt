OBJECT Table 1303 Mini Vendor Template
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               FieldRefArray@1001 : ARRAY [16] OF FieldRef;
               RecRef@1000 : RecordRef;
             BEGIN
               TESTFIELD("Template Name");
               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);

               InsertConfigurationTemplateHeaderAndLines;
             END;

    OnModify=VAR
               FieldRefArray@1000 : ARRAY [16] OF FieldRef;
               RecRef@1001 : RecordRef;
             BEGIN
               TESTFIELD(Code);
               TESTFIELD("Template Name");
               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);
               ConfigTemplateManagement.UpdateConfigTemplateAndLines(Code,"Template Name",DATABASE::Vendor,FieldRefArray);
             END;

    OnDelete=VAR
               ConfigTemplateHeader@1000 : Record 8618;
             BEGIN
               IF ConfigTemplateHeader.GET(Code) THEN BEGIN
                 ConfigTemplateManagement.DeleteRelatedTemplates(Code,DATABASE::"Default Dimension");
                 ConfigTemplateHeader.DELETE(TRUE);
               END;
             END;

    CaptionML=[ENU=Mini Vendor Template;
               ESM=Miniplantilla proveedor;
               FRC=Modäle fournisseur mini;
               ENC=Mini Vendor Template];
  }
  FIELDS
  {
    { 1   ;   ;Key                 ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Key;
                                                              ESM=Clave;
                                                              FRC=ClÇ;
                                                              ENC=Key] }
    { 2   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Template Name       ;Text50        ;CaptionML=[ENU=Template Name;
                                                              ESM=Nombre libro;
                                                              FRC=Nom modäle;
                                                              ENC=Template Name];
                                                   NotBlank=Yes }
    { 7   ;   ;City                ;Text30        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code".City
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code".City WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=VAR
                                                                PostCodeRec@1000 : Record 225;
                                                              BEGIN
                                                                IF City <> '' THEN BEGIN
                                                                  PostCodeRec.SETFILTER("Search City",City);

                                                                  CASE PostCodeRec.COUNT OF
                                                                    0:
                                                                      EXIT;
                                                                    1:
                                                                      PostCodeRec.FINDFIRST;
                                                                    ELSE
                                                                      IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
                                                                        EXIT;
                                                                  END;

                                                                  "Post Code" := PostCodeRec.Code;
                                                                  "Country/Region Code" := PostCodeRec."Country/Region Code";
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              ESM=Municipio/Ciudad;
                                                              FRC=Ville;
                                                              ENC=City] }
    { 21  ;   ;Vendor Posting Group;Code10        ;TableRelation="Vendor Posting Group";
                                                   CaptionML=[ENU=Vendor Posting Group;
                                                              ESM=Grupo contable proveedor;
                                                              FRC=Paramätre report fournisseur;
                                                              ENC=Vendor Posting Group] }
    { 22  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESM=C¢d. divisa;
                                                              FRC=Code devise;
                                                              ENC=Currency Code] }
    { 24  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESM=C¢d. idioma;
                                                              FRC=Code langue;
                                                              ENC=Language Code] }
    { 27  ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   CaptionML=[ENU=Payment Terms Code;
                                                              ESM=C¢d. tÇrminos pago;
                                                              FRC=Code modalitÇs de paiement;
                                                              ENC=Payment Terms Code] }
    { 28  ;   ;Fin. Charge Terms Code;Code10      ;TableRelation="Finance Charge Terms";
                                                   CaptionML=[ENU=Fin. Charge Terms Code;
                                                              ESM=C¢d. interÇs;
                                                              FRC=Code condition intÇràts;
                                                              ENC=Fin. Charge Terms Code] }
    { 33  ;   ;Invoice Disc. Code  ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[ENU=Invoice Disc. Code;
                                                              ESM=C¢d. dto. factura;
                                                              FRC=Code escompte facture;
                                                              ENC=Invoice Disc. Code] }
    { 35  ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              ESM=C¢d. pa°s/regi¢n;
                                                              FRC=Code pays/rÇgion;
                                                              ENC=Country/Region Code] }
    { 47  ;   ;Payment Method Code ;Code10        ;TableRelation="Payment Method";
                                                   CaptionML=[ENU=Payment Method Code;
                                                              ESM=C¢d. forma pago;
                                                              FRC=Code mode de paiement;
                                                              ENC=Payment Method Code] }
    { 80  ;   ;Application Method  ;Option        ;CaptionML=[ENU=Application Method;
                                                              ESM=MÇtodo liquidaci¢n;
                                                              FRC=MÇthode d'affectation;
                                                              ENC=Application Method];
                                                   OptionCaptionML=[ENU=Manual,Apply to Oldest;
                                                                    ESM=Manual,Por antigÅedad;
                                                                    FRC=Manuel,Au plus ancien;
                                                                    ENC=Manual,Apply to Oldest];
                                                   OptionString=Manual,Apply to Oldest }
    { 82  ;   ;Prices Including VAT;Boolean       ;CaptionML=[ENU=Prices Including VAT;
                                                              ESM=Precios IVA incluido;
                                                              FRC=Prix incluant la TVA;
                                                              ENC=Prices Including VAT] }
    { 88  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   CaptionML=[ENU=Gen. Bus. Posting Group;
                                                              ESM=Grupo contable negocio;
                                                              FRC=Groupe de report de marchÇ;
                                                              ENC=Gen. Bus. Posting Group] }
    { 91  ;   ;Post Code           ;Code20        ;TableRelation=IF (Country/Region Code=CONST()) "Post Code"
                                                                 ELSE IF (Country/Region Code=FILTER(<>'')) "Post Code" WHERE (Country/Region Code=FIELD(Country/Region Code));
                                                   OnValidate=VAR
                                                                PostCodeRec@1000 : Record 225;
                                                              BEGIN
                                                                IF "Post Code" <> '' THEN BEGIN
                                                                  PostCodeRec.SETFILTER(Code,"Post Code");

                                                                  CASE PostCodeRec.COUNT OF
                                                                    0:
                                                                      EXIT;
                                                                    1:
                                                                      PostCodeRec.FINDFIRST;
                                                                    ELSE
                                                                      IF PAGE.RUNMODAL(PAGE::"Post Codes",PostCodeRec,PostCodeRec.Code) <> ACTION::LookupOK THEN
                                                                        EXIT;
                                                                  END;

                                                                  City := PostCodeRec.City;
                                                                  "Country/Region Code" := PostCodeRec."Country/Region Code";
                                                                END;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=ZIP Code;
                                                              ESM=C.P.;
                                                              FRC=Code postal;
                                                              ENC=Postal/ZIP Code] }
    { 92  ;   ;County              ;Text30        ;CaptionML=[ENU=State;
                                                              ESM=Provincia;
                                                              FRC=ComtÇ;
                                                              ENC=Province/State] }
    { 110 ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[ENU=Tax Bus. Posting Group;
                                                              ESM=Grupo registro IVA neg.;
                                                              FRC=Groupe de reports de taxe sur la valeur ajoutÇe de l'entreprise;
                                                              ENC=Tax Bus. Posting Group] }
    { 116 ;   ;Block Payment Tolerance;Boolean    ;CaptionML=[ENU=Block Payment Tolerance;
                                                              ESM=Bloquear tolerancia pagos;
                                                              FRC=TolÇr. vers. en fonds bloquÇs;
                                                              ENC=Block Payment Tolerance] }
  }
  KEYS
  {
    {    ;Key                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfigTemplateManagement@1000 : Codeunit 8612;

    PROCEDURE CreateFieldRefArray@12(VAR FieldRefArray@1000 : ARRAY [16] OF FieldRef;RecRef@1003 : RecordRef);
    VAR
      I@1002 : Integer;
    BEGIN
      I := 1;

      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO(City)));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Vendor Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Currency Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Language Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Payment Terms Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Fin. Charge Terms Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Invoice Disc. Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Country/Region Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Payment Method Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Application Method")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Prices Including VAT")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Gen. Bus. Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Post Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO(County)));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("VAT Bus. Posting Group")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Block Payment Tolerance")));
    END;

    LOCAL PROCEDURE AddToArray@4(VAR FieldRefArray@1000 : ARRAY [18] OF FieldRef;VAR I@1001 : Integer;CurrFieldRef@1002 : FieldRef);
    BEGIN
      FieldRefArray[I] := CurrFieldRef;
      I += 1;
    END;

    PROCEDURE InitializeTempRecordFromConfigTemplate@1(VAR TempMiniVendorTemplate@1000 : TEMPORARY Record 1303;ConfigTemplateHeader@1001 : Record 8618);
    VAR
      RecRef@1003 : RecordRef;
    BEGIN
      TempMiniVendorTemplate.INIT;
      TempMiniVendorTemplate.Code := ConfigTemplateHeader.Code;
      TempMiniVendorTemplate."Template Name" := ConfigTemplateHeader.Description;
      TempMiniVendorTemplate.INSERT;

      RecRef.GETTABLE(TempMiniVendorTemplate);

      ConfigTemplateManagement.ApplyTemplateLinesWithoutValidation(ConfigTemplateHeader,RecRef);

      RecRef.SETTABLE(TempMiniVendorTemplate);
    END;

    PROCEDURE CreateConfigTemplateFromExistingVendor@5(Vendor@1000 : Record 23;VAR TempMiniVendorTemplate@1001 : TEMPORARY Record 1303);
    VAR
      MiniDimensionsTemplate@1002 : Record 1302;
      ConfigTemplateHeader@1006 : Record 8618;
      RecRef@1005 : RecordRef;
      FieldRefArray@1004 : ARRAY [16] OF FieldRef;
      NewTemplateCode@1003 : Code[10];
    BEGIN
      RecRef.GETTABLE(Vendor);
      CreateFieldRefArray(FieldRefArray,RecRef);

      ConfigTemplateManagement.CreateConfigTemplateAndLines(NewTemplateCode,'',DATABASE::Vendor,FieldRefArray);
      ConfigTemplateHeader.GET(NewTemplateCode);
      MiniDimensionsTemplate.CreateTemplatesFromExistingMasterRecord(Vendor."No.",NewTemplateCode,DATABASE::Vendor);
      InitializeTempRecordFromConfigTemplate(TempMiniVendorTemplate,ConfigTemplateHeader);
    END;

    PROCEDURE SaveAsTemplate@7(Vendor@1000 : Record 23);
    VAR
      TempMiniVendorTemplate@1001 : TEMPORARY Record 1303;
      MiniVendorTemplateCard@1002 : Page 1344;
    BEGIN
      MiniVendorTemplateCard.CreateFromVend(Vendor);
      MiniVendorTemplateCard.SETRECORD(TempMiniVendorTemplate);
      MiniVendorTemplateCard.LOOKUPMODE := TRUE;
      IF MiniVendorTemplateCard.RUNMODAL = ACTION::LookupOK THEN;
    END;

    LOCAL PROCEDURE InsertConfigurationTemplateHeaderAndLines@2();
    VAR
      FieldRefArray@1001 : ARRAY [16] OF FieldRef;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      CreateFieldRefArray(FieldRefArray,RecRef);
      ConfigTemplateManagement.CreateConfigTemplateAndLines(Code,"Template Name",DATABASE::Vendor,FieldRefArray);
    END;

    PROCEDURE NewVendorFromTemplate@3() : Code[20];
    VAR
      Vendor@1000 : Record 23;
      ConfigTemplateHeader@1004 : Record 8618;
      MiniConfigTemplates@1001 : Page 1340;
    BEGIN
      ConfigTemplateHeader.SETRANGE("Table ID",DATABASE::Vendor);
      CASE ConfigTemplateHeader.COUNT OF
        0:
          Vendor.INSERT(TRUE);
        1:
          BEGIN
            ConfigTemplateHeader.FINDFIRST;
            InsertVendorFromTemplate(ConfigTemplateHeader,Vendor);
          END;
        ELSE BEGIN
          MiniConfigTemplates.SETTABLEVIEW(ConfigTemplateHeader);
          MiniConfigTemplates.LOOKUPMODE(TRUE);
          IF MiniConfigTemplates.RUNMODAL = ACTION::LookupOK THEN BEGIN
            MiniConfigTemplates.GETRECORD(ConfigTemplateHeader);
            InsertVendorFromTemplate(ConfigTemplateHeader,Vendor);
          END ELSE
            EXIT;
        END;
      END;

      EXIT(Vendor."No.");
    END;

    LOCAL PROCEDURE OpenVendorCardWithNewVendorFromTemplate@6();
    VAR
      Vendor@1000 : Record 23;
      NewVendorCode@1002 : Code[20];
    BEGIN
      NewVendorCode := NewVendorFromTemplate;

      IF NewVendorCode <> '' THEN BEGIN
        Vendor.GET(NewVendorCode);
        PAGE.RUN(PAGE::"Mini Vendor Card",Vendor);
      END;
    END;

    PROCEDURE InsertVendorFromTemplate@16(ConfigTemplateHeader@1006 : Record 8618;VAR Vendor@1003 : Record 23);
    VAR
      MiniDimensionsTemplate@1001 : Record 1302;
      ConfigTemplateMgt@1002 : Codeunit 8612;
      RecRef@1000 : RecordRef;
    BEGIN
      Vendor.INSERT(TRUE);
      RecRef.GETTABLE(Vendor);
      ConfigTemplateMgt.UpdateRecord(ConfigTemplateHeader,RecRef);
      RecRef.SETTABLE(Vendor);

      MiniDimensionsTemplate.InsertDimensionsFromTemplates(ConfigTemplateHeader,Vendor."No.",DATABASE::Vendor);
    END;

    BEGIN
    END.
  }
}

