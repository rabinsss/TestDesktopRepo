OBJECT Table 5075 Logged Segment
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Logged Segment;
               ESM=Segmento archivado;
               FRC=Segment journalis‚;
               ENC=Logged Segment];
    LookupPageID=Page5139;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=N§ mov.;
                                                              FRC=Nø ‚criture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Segment No.         ;Code20        ;CaptionML=[ENU=Segment No.;
                                                              ESM=N§ segmento;
                                                              FRC=Nø segment;
                                                              ENC=Segment No.] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;No. of Interactions ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Logged Segment Entry No.=FIELD(Entry No.),
                                                                                                    Canceled=FIELD(Canceled)));
                                                   CaptionML=[ENU=No. of Interactions;
                                                              ESM=N§ de interacciones;
                                                              FRC=Nbre interactions;
                                                              ENC=No. of Interactions];
                                                   Editable=No }
    { 5   ;   ;No. of Campaign Entries;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Campaign Entry" WHERE (Register No.=FIELD(Entry No.),
                                                                                             Canceled=FIELD(Canceled)));
                                                   CaptionML=[ENU=No. of Campaign Entries;
                                                              ESM=Movs. n§ campa¤a;
                                                              FRC=Nbre ‚critures promotion;
                                                              ENC=No. of Campaign Entries];
                                                   Editable=No }
    { 6   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESM=Fecha creaci¢n;
                                                              FRC=Date cr‚ation;
                                                              ENC=Creation Date] }
    { 7   ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESM=Id. usuario;
                                                              FRC=Code utilisateur;
                                                              ENC=User ID] }
    { 8   ;   ;Canceled            ;Boolean       ;CaptionML=[ENU=Canceled;
                                                              ESM=Cancelado;
                                                              FRC=Annul‚e;
                                                              ENC=Cancelled] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Segment No.                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Description,Segment No.,Creation Date }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 is marked %3.\Do you wish to remove the checkmark?;ESM=%1 %2 est  marcado %3.\¨Desea quitar la marca?;FRC=%1 %2 est marqu‚ %3.\Souhaitez-vous d‚sactiver le champÿ?;ENC=%1 %2 is marked %3.\Do you wish to remove the checkmark?';
      Text002@1002 : TextConst 'ENU=Do you wish to mark %1 %2 as %3?;ESM=¨Confirma que desea se¤alar %1 %2 como %3?;FRC=Souhaitez-vous mettre %1 %2 … %3 ?;ENC=Do you wish to mark %1 %2 as %3?';
      Text005@1004 : TextConst 'ENU=Do you wish to remove the checkmark from the selected %1 lines?;ESM=¨Confirma que desea eliminar la marca de activaci¢n para las l¡n. %1 selec.?;FRC=Souhaitez-vous d‚sactiver les %1 lignes s‚lectionn‚es ?;ENC=Do you wish to remove the checkmark from the selected %1 lines?';
      Text006@1003 : TextConst 'ENU="Do you wish to mark the selected %1 lines as %2? ";ESM="¨Confirma que desea se¤alar las l¡neas %1 seleccionadas como %2? ";FRC="Souhaitez-vous marquer les %1 lignes s‚lectionn‚es comme %2 ? ";ENC="Do you wish to mark the selected %1 lines as %2? "';

    PROCEDURE ToggleCanceledCheckmark@2();
    VAR
      MasterCanceledCheckmark@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN BEGIN
        IF ConfirmToggleCanceledCheckmark(COUNT) THEN BEGIN
          MasterCanceledCheckmark := NOT Canceled;
          REPEAT
            SetCanceledCheckmark(MasterCanceledCheckmark);
          UNTIL NEXT = 0
        END;
      END
    END;

    PROCEDURE SetCanceledCheckmark@1(CanceledCheckmark@1002 : Boolean);
    VAR
      InteractLogEntry@1000 : Record 5065;
      CampaignEntry@1001 : Record 5072;
    BEGIN
      Canceled := CanceledCheckmark;
      MODIFY;

      CampaignEntry.SETCURRENTKEY("Register No.");
      CampaignEntry.SETRANGE("Register No.","Entry No.");
      CampaignEntry.MODIFYALL(Canceled,Canceled);

      InteractLogEntry.SETCURRENTKEY("Logged Segment Entry No.");
      InteractLogEntry.SETRANGE("Logged Segment Entry No.","Entry No.");
      InteractLogEntry.MODIFYALL(Canceled,Canceled);
    END;

    LOCAL PROCEDURE ConfirmToggleCanceledCheckmark@3(NumberOfSelectedLines@1000 : Integer) : Boolean;
    BEGIN
      IF NumberOfSelectedLines = 1 THEN BEGIN
        IF Canceled THEN
          EXIT(CONFIRM(Text000,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));

        EXIT(CONFIRM(Text002,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      END;

      IF Canceled THEN
        EXIT(CONFIRM(Text005,TRUE,TABLECAPTION));

      EXIT(CONFIRM(Text006,TRUE,TABLECAPTION,FIELDCAPTION(Canceled)));
    END;

    BEGIN
    END.
  }
}

