OBJECT Table 99000772 Production BOM Line
{
  OBJECT-PROPERTIES
  {
    Date=12/22/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00.00.44365;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatus;
             END;

    OnModify=BEGIN
               IF Type <> Type::" " THEN
                 TestStatus;
             END;

    OnDelete=VAR
               ProdBOMComment@1000 : Record 99000776;
               PlanningAssignment@1001 : Record 99000850;
             BEGIN
               IF Type <> Type::" " THEN BEGIN
                 TestStatus;
                 IF Type = Type::Item THEN
                   PlanningAssignment.AssignPlannedOrders("No.",FALSE)
                 ELSE
                   IF Type = Type::"Production BOM" THEN
                     PlanningAssignment.OldBom("No.");
               END;

               ProdBOMComment.SETRANGE("Production BOM No.","Production BOM No.");
               ProdBOMComment.SETRANGE("BOM Line No.","Line No.");
               ProdBOMComment.SETRANGE("Version Code","Version Code");
               ProdBOMComment.DELETEALL;
             END;

    CaptionML=[ENU=Production BOM Line;
               ESM=L¡nea L.M. producci¢n;
               FRC=Ligne nomenclature production;
               ENC=Production BOM Line];
  }
  FIELDS
  {
    { 1   ;   ;Production BOM No.  ;Code20        ;TableRelation="Production BOM Header";
                                                   CaptionML=[ENU=Production BOM No.;
                                                              ESM=N§ L.M. producci¢n;
                                                              FRC=Nø production nomenclature;
                                                              ENC=Production BOM No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=N§ l¡nea;
                                                              FRC=Nø ligne;
                                                              ENC=Line No.] }
    { 3   ;   ;Version Code        ;Code20        ;TableRelation="Production BOM Version"."Version Code" WHERE (Production BOM No.=FIELD(Production BOM No.));
                                                   CaptionML=[ENU=Version Code;
                                                              ESM=C¢d. versi¢n;
                                                              FRC=Code version;
                                                              ENC=Version Code] }
    { 10  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TestStatus;

                                                                xRec.Type := Type;

                                                                INIT;
                                                                Type := xRec.Type;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=" ,Item,Production BOM";
                                                                    ESM=" ,Producto,L.M. producci¢n";
                                                                    FRC=" ,Article,Nomenclature de production";
                                                                    ENC=" ,Item,Production BOM"];
                                                   OptionString=[ ,Item,Production BOM] }
    { 11  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item WHERE (Type=CONST(Inventory))
                                                                 ELSE IF (Type=CONST(Production BOM)) "Production BOM Header";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);

                                                                TestStatus;

                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      Item.GET("No.");
                                                                      Description := Item.Description;
                                                                      Item.TESTFIELD("Base Unit of Measure");
                                                                      "Unit of Measure Code" := Item."Base Unit of Measure";
                                                                      IF "No." <> xRec."No." THEN
                                                                        "Variant Code" := '';
                                                                    END;
                                                                  Type::"Production BOM":
                                                                    BEGIN
                                                                      ProdBOMHeader.GET("No.");
                                                                      ProdBOMHeader.TESTFIELD("Unit of Measure Code");
                                                                      Description := ProdBOMHeader.Description;
                                                                      "Unit of Measure Code" := ProdBOMHeader."Unit of Measure Code";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 12  ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 13  ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE IF (Type=CONST(Production BOM)) "Unit of Measure";
                                                   OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                                IF xRec."Unit of Measure Code" <> "Unit of Measure Code" THEN
                                                                  TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESM=C¢d. unidad medida;
                                                              FRC=Code unit‚ de mesure;
                                                              ENC=Unit of Measure Code] }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESM=Cantidad;
                                                              FRC=Quantit‚;
                                                              ENC=Quantity];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 15  ;   ;Position            ;Code10        ;CaptionML=[ENU=Position;
                                                              ESM=Posici¢n;
                                                              FRC=Poste;
                                                              ENC=Position] }
    { 16  ;   ;Position 2          ;Code10        ;CaptionML=[ENU=Position 2;
                                                              ESM=Posici¢n 2;
                                                              FRC=Position 2;
                                                              ENC=Position 2] }
    { 17  ;   ;Position 3          ;Code10        ;CaptionML=[ENU=Position 3;
                                                              ESM=Posici¢n 3;
                                                              FRC=Position 3;
                                                              ENC=Position 3] }
    { 18  ;   ;Lead-Time Offset    ;DateFormula   ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[ENU=Lead-Time Offset;
                                                              ESM=Desfase plazo entrega;
                                                              FRC=D‚calage du d‚lai;
                                                              ENC=Lead-Time Offset] }
    { 19  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   OnValidate=BEGIN
                                                                IF "Routing Link Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Item);
                                                                  TESTFIELD("No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Routing Link Code;
                                                              ESM=C¢d. conexi¢n ruta;
                                                              FRC=Code lien proc‚dure;
                                                              ENC=Routing Link Code] }
    { 20  ;   ;Scrap %             ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[ENU=Scrap %;
                                                              ESM=% Rechazo;
                                                              FRC=% rebut;
                                                              ENC=Scrap %];
                                                   DecimalPlaces=0:5;
                                                   MaxValue=100;
                                                   BlankNumbers=BlankNeg }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESM=C¢d. variante;
                                                              FRC=Code variante;
                                                              ENC=Variant Code] }
    { 22  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Production BOM Comment Line" WHERE (Production BOM No.=FIELD(Production BOM No.),
                                                                                                          Version Code=FIELD(Version Code),
                                                                                                          BOM Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 28  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                IF "Starting Date" > 0D THEN
                                                                  VALIDATE("Ending Date");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESM=Fecha inicial;
                                                              FRC=Date d‚but;
                                                              ENC=Starting Date] }
    { 29  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                IF ("Ending Date" > 0D) AND
                                                                   ("Starting Date" > 0D) AND
                                                                   ("Starting Date" > "Ending Date")
                                                                THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    FIELDCAPTION("Ending Date"),
                                                                    FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESM=Fecha final;
                                                              FRC=Date fin;
                                                              ENC=Ending Date] }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              ESM=Longitud;
                                                              FRC=Longueur;
                                                              ENC=Length];
                                                   DecimalPlaces=0:5 }
    { 41  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Width;
                                                              ESM=Ancho;
                                                              FRC=Largeur;
                                                              ENC=Width];
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Weight              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Weight;
                                                              ESM=Peso;
                                                              FRC=Poids;
                                                              ENC=Weight];
                                                   DecimalPlaces=0:5 }
    { 43  ;   ;Depth               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Depth;
                                                              ESM=Altura;
                                                              FRC=Profondeur;
                                                              ENC=Depth];
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;Calculation Formula ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                CASE "Calculation Formula" OF
                                                                  "Calculation Formula"::" ":
                                                                    Quantity := "Quantity per";
                                                                  "Calculation Formula"::Length:
                                                                    Quantity := ROUND(Length * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width":
                                                                    Quantity := ROUND(Length * Width * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width * Depth":
                                                                    Quantity := ROUND(Length * Width * Depth * "Quantity per",0.00001);
                                                                  "Calculation Formula"::Weight:
                                                                    Quantity := ROUND(Weight * "Quantity per",0.00001);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Calculation Formula;
                                                              ESM=Tipo c lculo;
                                                              FRC=Formule de calcul;
                                                              ENC=Calculation Formula];
                                                   OptionCaptionML=[ENU=" ,Length,Length * Width,Length * Width * Depth,Weight";
                                                                    ESM=" ,Largo,Largo * Ancho,Largo * Ancho * Alto,Peso";
                                                                    FRC=" ,Longueur,Longueur*Largeur,Longueur*Largeur*Profondeur,Poids";
                                                                    ENC=" ,Length,Length * Width,Length * Width * Depth,Weight"];
                                                   OptionString=[ ,Length,Length * Width,Length * Width * Depth,Weight] }
    { 45  ;   ;Quantity per        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Quantity per;
                                                              ESM=Cantidad por;
                                                              FRC=Quantit‚ par;
                                                              ENC=Quantity per];
                                                   DecimalPlaces=0:5 }
  }
  KEYS
  {
    {    ;Production BOM No.,Version Code,Line No.;Clustered=Yes }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be later than %2.;ESM=%1 debe ser posterior que %2.;FRC=%1 doit ˆtre ult‚rieur … %2.;ENC=%1 must be later than %2.';
      Item@1001 : Record 27;
      ProdBOMHeader@1002 : Record 99000771;
      ItemVariant@1003 : Record 5401;
      BOMVersionUOMErr@1004 : TextConst '@@@="%1=UOM Code;%2=Item No.;%3=Production BOM No.;%4=Version Code";ENU="The Unit of Measure Code %1 for Item %2 does not exist. Identification fields and values: Production BOM No. = %3, Version Code = %4.";ESM="No existe el c¢digo de unidad de medida %1 para el elemento %2. Campos y valores de identificaci¢n: N.§ L.M. producci¢n = %3, C¢digo de versi¢n = %4.";FRC="Le Code unit‚ de mesure %1 de l''article %2 n''existe pas. Valeurs et champs d''identificationÿ: Nø nomenclature production = %3, Code version = %4.";ENC="The Unit of Measure Code %1 for Item %2 does not exist. Identification fields and values: Production BOM No. = %3, Version Code = %4."';
      BOMHeaderUOMErr@1005 : TextConst '@@@="%1=UOM Code;%2=Item No.;%3=Production BOM No.";ENU="The Unit of Measure Code %1 for Item %2 does not exist. Identification fields and values: Production BOM No. = %3.";ESM="No existe el c¢digo de unidad de medida %1 para el elemento %2. Campos y valores de identificaci¢n: N.§ L.M. producci¢n = %3.";FRC="Le Code unit‚ de mesure %1 de l''article %2 n''existe pas. Valeurs et champs d''identificationÿ: Nø nomenclature production = %3.";ENC="The Unit of Measure Code %1 for Item %2 does not exist. Identification fields and values: Production BOM No. = %3."';
      BOMLineUOMErr@1006 : TextConst '@@@="%1=UOM Code;%2=Item No.;%3=Production BOM No.;%4=Version Code;%5=Line No.";ENU="The Unit of Measure Code %1 for Item %2 does not exist. Identification fields and values: Production BOM No. = %3, Version Code = %4, Line No. = %5.";ESM="No existe el c¢digo de unidad de medida %1 para el elemento %2. Campos y valores de identificaci¢n: N.§ L.M. producci¢n = %3, C¢digo de versi¢n = %4, N.§ l¡nea = %5.";FRC="Le Code unit‚ de mesure %1 de l''article %2 n''existe pas. Valeurs et champs d''identificationÿ: Nø nomenclature production = %3, Code version = %4, Nø ligne = %5.";ENC="The Unit of Measure Code %1 for Item %2 does not exist. Identification fields and values: Production BOM No. = %3, Version Code = %4, Line No. = %5."';

    LOCAL PROCEDURE TestStatus@1();
    VAR
      ProdBOMVersion@1000 : Record 99000779;
    BEGIN
      IF "Version Code" = '' THEN BEGIN
        ProdBOMHeader.GET("Production BOM No.");
        IF ProdBOMHeader.Status = ProdBOMHeader.Status::Certified THEN
          ProdBOMHeader.FIELDERROR(Status);
      END ELSE BEGIN
        ProdBOMVersion.GET("Production BOM No.","Version Code");
        IF ProdBOMVersion.Status = ProdBOMVersion.Status::Certified THEN
          ProdBOMVersion.FIELDERROR(Status);
      END;
    END;

    PROCEDURE GetQtyPerUnitOfMeasure@2() : Decimal;
    VAR
      Item@1000 : Record 27;
      UOMMgt@1001 : Codeunit 5402;
    BEGIN
      IF Type = Type::Item THEN BEGIN
        Item.GET("No.");
        EXIT(
          UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
      END;
      EXIT(1);
    END;

    PROCEDURE GetBOMHeaderQtyPerUOM@3(Item@1000 : Record 27) : Decimal;
    VAR
      ProdBOMHeader@1003 : Record 99000771;
      ProdBOMVersion@1002 : Record 99000779;
      ItemUnitOfMeasure@1004 : Record 5404;
      UOMMgt@1001 : Codeunit 5402;
    BEGIN
      IF "Production BOM No." = '' THEN
        EXIT(1);

      IF "Version Code" <> '' THEN BEGIN
        ProdBOMVersion.GET("Production BOM No.","Version Code");
        IF NOT ItemUnitOfMeasure.GET(Item."No.",ProdBOMVersion."Unit of Measure Code") THEN
          ERROR(BOMVersionUOMErr,ProdBOMVersion."Unit of Measure Code",Item."No.","Production BOM No.","Version Code");
        EXIT(UOMMgt.GetQtyPerUnitOfMeasure(Item,ProdBOMVersion."Unit of Measure Code"));
      END;

      ProdBOMHeader.GET("Production BOM No.");
      IF NOT ItemUnitOfMeasure.GET(Item."No.",ProdBOMHeader."Unit of Measure Code") THEN
        ERROR(BOMHeaderUOMErr,ProdBOMHeader."Unit of Measure Code",Item."No.","Production BOM No.");
      EXIT(UOMMgt.GetQtyPerUnitOfMeasure(Item,ProdBOMHeader."Unit of Measure Code"));
    END;

    PROCEDURE GetBOMLineQtyPerUOM@4(Item@1000 : Record 27) : Decimal;
    VAR
      ItemUnitOfMeasure@1001 : Record 5404;
      UOMMgt@1002 : Codeunit 5402;
    BEGIN
      IF "No." = '' THEN
        EXIT(1);

      IF NOT ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code") THEN
        ERROR(BOMLineUOMErr,"Unit of Measure Code",Item."No.","Production BOM No.","Version Code","Line No.");
      EXIT(UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
    END;

    BEGIN
    END.
  }
}

