OBJECT Table 5088 Profile Questionnaire Line
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Profile Questionnaire Code,Description;
    OnDelete=VAR
               Rating@1000 : Record 5111;
               ProfileQuestionnaireLine@1001 : Record 5088;
             BEGIN
               CALCFIELDS("No. of Contacts");
               TESTFIELD("No. of Contacts",0);

               Rating.SETRANGE("Rating Profile Quest. Code","Profile Questionnaire Code");
               Rating.SETRANGE("Rating Profile Quest. Line No.","Line No.");
               IF Rating.FINDFIRST THEN
                 ERROR(Text002);

               Rating.RESET;
               Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
               Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
               IF Rating.FINDFIRST THEN
                 ERROR(Text003);

               IF Type = Type::Question THEN BEGIN
                 ProfileQuestionnaireLine.GET("Profile Questionnaire Code","Line No.");
                 IF (ProfileQuestionnaireLine.NEXT <> 0) AND
                    (ProfileQuestionnaireLine.Type = ProfileQuestnLine.Type::Answer)
                 THEN
                   ERROR(Text004);
               END;
             END;

    CaptionML=[ENU=Profile Questionnaire Line;
               ESM=L°n. cuestionario perfil;
               FRC=Ligne questionnaire profil;
               ENC=Profile Questionnaire Line];
    LookupPageID=Page5149;
  }
  FIELDS
  {
    { 1   ;   ;Profile Questionnaire Code;Code10  ;TableRelation="Profile Questionnaire Header";
                                                   CaptionML=[ENU=Profile Questionnaire Code;
                                                              ESM=C¢d. cuestionario perfil;
                                                              FRC=Code questionnaire profil;
                                                              ENC=Profile Questionnaire Code] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=Nß l°nea;
                                                              FRC=N¯ ligne;
                                                              ENC=Line No.] }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Question:
                                                                    BEGIN
                                                                      CALCFIELDS("No. of Contacts");
                                                                      TESTFIELD("No. of Contacts",0);
                                                                      TESTFIELD("From Value",0);
                                                                      TESTFIELD("To Value",0);
                                                                    END;
                                                                  Type::Answer:
                                                                    BEGIN
                                                                      TESTFIELD("Multiple Answers",FALSE);
                                                                      TESTFIELD("Auto Contact Classification",FALSE);
                                                                      TESTFIELD("Customer Class. Field",0);
                                                                      TESTFIELD("Vendor Class. Field",0);
                                                                      TESTFIELD("Contact Class. Field",0);
                                                                      TESTFIELD("Starting Date Formula",ZeroDateFormula);
                                                                      TESTFIELD("Ending Date Formula",ZeroDateFormula);
                                                                      TESTFIELD("Classification Method",0);
                                                                      TESTFIELD("Sorting Method",0);
                                                                      TESTFIELD("No. of Decimals",0);
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=Question,Answer;
                                                                    ESM=Pregunta,Respuesta;
                                                                    FRC=Question,RÇponse;
                                                                    ENC=Question,Answer];
                                                   OptionString=Question,Answer }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description];
                                                   NotBlank=Yes }
    { 5   ;   ;Multiple Answers    ;Boolean       ;OnValidate=BEGIN
                                                                IF "Multiple Answers" THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Multiple Answers;
                                                              ESM=M£ltiples respuestas;
                                                              FRC=RÇponses multiples;
                                                              ENC=Multiple Answers] }
    { 6   ;   ;Auto Contact Classification;Boolean;OnValidate=BEGIN
                                                                IF "Auto Contact Classification" THEN
                                                                  TESTFIELD(Type,Type::Question)
                                                                ELSE BEGIN
                                                                  TESTFIELD("Customer Class. Field","Customer Class. Field"::" ");
                                                                  TESTFIELD("Vendor Class. Field","Vendor Class. Field"::" ");
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::" ");
                                                                  TESTFIELD("Starting Date Formula",ZeroDateFormula);
                                                                  TESTFIELD("Ending Date Formula",ZeroDateFormula);
                                                                  TESTFIELD("Classification Method","Classification Method"::" ");
                                                                  TESTFIELD("Sorting Method","Sorting Method"::" ");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Auto Contact Classification;
                                                              ESM=Clasificaci¢n contacto autom.;
                                                              FRC=Classification contact auto.;
                                                              ENC=Auto Contact Classification] }
    { 7   ;   ;Customer Class. Field;Option       ;OnValidate=BEGIN
                                                                IF "Customer Class. Field" <> "Customer Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Vendor Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  IF "Classification Method" = "Classification Method"::" " THEN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Customer Class. Field;
                                                              ESM=Campo clasific. cliente;
                                                              FRC=Champ class. client;
                                                              ENC=Customer Class. Field];
                                                   OptionCaptionML=[ENU=" ,Sales ($),Profit ($),Sales Frequency (Invoices/Year),Avg. Invoice Amount ($),Discount (%),Avg. Overdue (Day)";
                                                                    ESM=" ,Ventas ($),Ganancia ($),Frecuencia vta. (Facturas/A§o),Imp. fact. promedio ($),Descuento (%),Vencido promedio (D°a)";
                                                                    FRC=" ,Ventes ($),Marge ($),FrÇquence ventes (factures/an),Montant moyen des factures ($),Remise (%),Retard de räglement (jours)";
                                                                    ENC=" ,Sales ($),Profit ($),Sales Frequency (Invoices/Year),Avg. Invoice Amount ($),Discount (%),Avg. Overdue (Day)"];
                                                   OptionString=[ ,Sales (LCY),Profit (LCY),Sales Frequency (Invoices/Year),Avg. Invoice Amount (LCY),Discount (%),Avg. Overdue (Day)] }
    { 8   ;   ;Vendor Class. Field ;Option        ;OnValidate=BEGIN
                                                                IF "Vendor Class. Field" <> "Vendor Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Contact Class. Field");
                                                                  IF "Classification Method" = "Classification Method"::" " THEN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Vendor Class. Field;
                                                              ESM=Campo clasific. proveedor;
                                                              FRC=Champ class. fournisseur;
                                                              ENC=Vendor Class. Field];
                                                   OptionCaptionML=[ENU=" ,Purchase ($),Purchase Frequency (Invoices/Year),Avg. Ticket Size ($),Discount (%),Avg. Overdue (Day)";
                                                                    ESM=" ,Compras ($),Frecuencia compra (Facturas/A§o),Imp. fact. promedio ($),Descuento (%),Vencido promedio (D°a)";
                                                                    FRC=" ,Achats ($),FrÇquence achats (factures/an),Montant moyen des factures ($),Remise (%),Retard de räglement (jours)";
                                                                    ENC=" ,Purchase ($),Purchase Frequency (Invoices/Year),Avg. Ticket Size ($),Discount (%),Avg. Overdue (Day)"];
                                                   OptionString=[ ,Purchase (LCY),Purchase Frequency (Invoices/Year),Avg. Ticket Size (LCY),Discount (%),Avg. Overdue (Day)] }
    { 9   ;   ;Contact Class. Field;Option        ;OnValidate=VAR
                                                                Rating@1000 : Record 5111;
                                                              BEGIN
                                                                IF xRec."Contact Class. Field" = "Contact Class. Field"::Rating THEN BEGIN
                                                                  Rating.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                  Rating.SETRANGE("Profile Questionnaire Line No.","Line No.");
                                                                  IF Rating.FINDFIRST THEN
                                                                    IF CONFIRM(Text000,FALSE) THEN
                                                                      Rating.DELETEALL
                                                                    ELSE
                                                                      ERROR(Text001,FIELDCAPTION("Contact Class. Field"));
                                                                END;

                                                                IF "Contact Class. Field" <> "Contact Class. Field"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  CLEAR("Customer Class. Field");
                                                                  CLEAR("Vendor Class. Field");
                                                                  IF ("Classification Method" = "Classification Method"::" ") OR
                                                                     ("Contact Class. Field" = "Contact Class. Field"::Rating)
                                                                  THEN BEGIN
                                                                    "Classification Method" := "Classification Method"::"Defined Value";
                                                                    "Sorting Method" := "Sorting Method"::" ";
                                                                  END;
                                                                  IF "Contact Class. Field" = "Contact Class. Field"::Rating THEN BEGIN
                                                                    CLEAR("Starting Date Formula");
                                                                    CLEAR("Ending Date Formula");
                                                                  END;
                                                                END ELSE
                                                                  ResetFields;
                                                              END;

                                                   CaptionML=[ENU=Contact Class. Field;
                                                              ESM=Campo clasific. contacto;
                                                              FRC=Champ class. contact;
                                                              ENC=Contact Class. Field];
                                                   OptionCaptionML=[ENU=" ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost ($),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating";
                                                                    ESM=" ,Cdad. interacci¢n,Frecuencia interacci¢n (Nß/A§o),Costo interac. promedio ($),Durac. inter. promedio (Min.),(%) oportunidad ganada,Clasificaci¢n";
                                                                    FRC=" ,Nbre d'interactions,FrÇquence interactions (nbre/an),Coñt moyen interaction ($),DurÇe moyenne (min.),OpportunitÇs gagnÇes (%),êvaluation";
                                                                    ENC=" ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost ($),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating"];
                                                   OptionString=[ ,Interaction Quantity,Interaction Frequency (No./Year),Avg. Interaction Cost (LCY),Avg. Interaction Duration (Min.),Opportunity Won (%),Rating] }
    { 10  ;   ;Starting Date Formula;DateFormula  ;OnValidate=BEGIN
                                                                IF FORMAT("Starting Date Formula") <> '' THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Starting Date Formula;
                                                              ESM=F¢rmula fecha inicio;
                                                              FRC=Formule date dÇbut;
                                                              ENC=Starting Date Formula] }
    { 11  ;   ;Ending Date Formula ;DateFormula   ;OnValidate=BEGIN
                                                                IF FORMAT("Ending Date Formula") <> '' THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Ending Date Formula;
                                                              ESM=F¢rmula fecha fin;
                                                              FRC=Formule date fin;
                                                              ENC=Ending Date Formula] }
    { 12  ;   ;Classification Method;Option       ;OnValidate=BEGIN
                                                                IF "Classification Method" <> "Classification Method"::" " THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  IF "Classification Method" <> "Classification Method"::"Defined Value" THEN
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::Descending
                                                                  ELSE
                                                                    "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                                END ELSE
                                                                  "Sorting Method" := ProfileQuestnLine."Sorting Method"::" ";
                                                              END;

                                                   CaptionML=[ENU=Classification Method;
                                                              ESM=MÇtodo clasificaci¢n;
                                                              FRC=MÇthode classification;
                                                              ENC=Classification Method];
                                                   OptionCaptionML=[ENU=" ,Defined Value,Percentage of Value,Percentage of Contacts";
                                                                    ESM=" ,Valor definido,Porcentaje de valor,Porcentaje de contactos";
                                                                    FRC=" ,Valeur dÇfinie,Pourcentage valeur,Pourcentage contacts";
                                                                    ENC=" ,Defined Value,Percentage of Value,Percentage of Contacts"];
                                                   OptionString=[ ,Defined Value,Percentage of Value,Percentage of Contacts] }
    { 13  ;   ;Sorting Method      ;Option        ;OnValidate=BEGIN
                                                                IF "Sorting Method" <> "Sorting Method"::" " THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=Sorting Method;
                                                              ESM=MÇtodo ordenaci¢n;
                                                              FRC=MÇthode de tri;
                                                              ENC=Sorting Method];
                                                   OptionCaptionML=[ENU=" ,Descending,Ascending";
                                                                    ESM=" ,Descendente,Ascendente";
                                                                    FRC=" ,DÇcroissant,Croissant";
                                                                    ENC=" ,Descending,Ascending"];
                                                   OptionString=[ ,Descending,Ascending] }
    { 14  ;   ;From Value          ;Decimal       ;OnValidate=BEGIN
                                                                IF "From Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=From Value;
                                                              ESM=Desde valor;
                                                              FRC=Valeur dÇbut;
                                                              ENC=From Value];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 15  ;   ;To Value            ;Decimal       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=To Value;
                                                              ESM=Hasta valor;
                                                              FRC=Valeur fin;
                                                              ENC=To Value];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 16  ;   ;No. of Contacts     ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Contact Profile Answer" WHERE (Profile Questionnaire Code=FIELD(Profile Questionnaire Code),
                                                                                                     Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=No. of Contacts;
                                                              ESM=Nß de contactos;
                                                              FRC=Nbre contacts;
                                                              ENC=No. of Contacts];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 17  ;   ;Priority            ;Option        ;InitValue=Normal;
                                                   OnValidate=VAR
                                                                ContProfileAnswer@1000 : Record 5089;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Answer);
                                                                ContProfileAnswer.SETCURRENTKEY("Profile Questionnaire Code","Line No.");
                                                                ContProfileAnswer.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
                                                                ContProfileAnswer.SETRANGE("Line No.","Line No.");
                                                                ContProfileAnswer.MODIFYALL("Answer Priority",Priority);
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Priority;
                                                              ESM=Prioridad;
                                                              FRC=PrioritÇ;
                                                              ENC=Priority];
                                                   OptionCaptionML=[ENU=Very Low (Hidden),Low,Normal,High,Very High;
                                                                    ESM=Muy baja (Oculta),Baja,Normal,Alta,Muy alta;
                                                                    FRC=Träs faible,Faible,Normal,Haut,Träs haute;
                                                                    ENC=Very Low (Hidden),Low,Normal,High,Very High];
                                                   OptionString=Very Low (Hidden),Low,Normal,High,Very High }
    { 18  ;   ;No. of Decimals     ;Integer       ;OnValidate=BEGIN
                                                                IF "No. of Decimals" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Question);
                                                              END;

                                                   CaptionML=[ENU=No. of Decimals;
                                                              ESM=Nß de decimales;
                                                              FRC=Nombre dÇcimales;
                                                              ENC=No. of Decimals];
                                                   MinValue=-25;
                                                   MaxValue=25 }
    { 19  ;   ;Min. % Questions Answered;Decimal  ;OnValidate=BEGIN
                                                                IF "Min. % Questions Answered" <> 0 THEN BEGIN
                                                                  TESTFIELD(Type,Type::Question);
                                                                  TESTFIELD("Contact Class. Field","Contact Class. Field"::Rating);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Min. % Questions Answered;
                                                              ESM=% M°n. preguntas respondidas;
                                                              FRC=% min. questions traitÇes;
                                                              ENC=Min. % Questions Answered];
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 9501;   ;Wizard Step         ;Option        ;CaptionML=[ENU=Wizard Step;
                                                              ESM=Paso asistente;
                                                              FRC=êtape assistant;
                                                              ENC=Wizard Step];
                                                   OptionCaptionML=[ENU=" ,1,2,3,4,5,6";
                                                                    ESM=" ,1,2,3,4,5,6";
                                                                    FRC=" ,1,2,3,4,5,6";
                                                                    ENC=" ,1,2,3,4,5,6"];
                                                   OptionString=[ ,1,2,3,4,5,6];
                                                   Editable=No }
    { 9502;   ;Interval Option     ;Option        ;CaptionML=[ENU=Interval Option;
                                                              ESM=Opci¢n intervalo;
                                                              FRC=Option intervalle;
                                                              ENC=Interval Option];
                                                   OptionCaptionML=[ENU=Minimum,Maximum,Interval;
                                                                    ESM=M°nimo,M†ximo,Intervalo;
                                                                    FRC=Minimum,Maximum,Intervalle;
                                                                    ENC=Minimum,Maximum,Interval];
                                                   OptionString=Minimum,Maximum,Interval }
    { 9503;   ;Answer Option       ;Option        ;CaptionML=[ENU=Answer Option;
                                                              ESM=Opci¢n respuesta;
                                                              FRC=Option rÇponse;
                                                              ENC=Answer Option];
                                                   OptionCaptionML=[ENU=HighLow,ABC,Custom;
                                                                    ESM=AltaBaja,ABC,Personalizada;
                                                                    FRC=SupInf,ABC,PersonnalisÇ;
                                                                    ENC=HighLow,ABC,Custom];
                                                   OptionString=HighLow,ABC,Custom }
    { 9504;   ;Answer Description  ;Text50        ;CaptionML=[ENU=Answer Description;
                                                              ESM=Descripci¢n respuesta;
                                                              FRC=Description rÇponse;
                                                              ENC=Answer Description] }
    { 9505;   ;Wizard From Value   ;Decimal       ;OnValidate=BEGIN
                                                                IF "From Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard From Value;
                                                              ESM=Asistente desde valor;
                                                              FRC=Assistant - Valeur dÇbut;
                                                              ENC=Wizard From Value];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 9506;   ;Wizard To Value     ;Decimal       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard To Value;
                                                              ESM=Asistente hasta valor;
                                                              FRC=Assistant - Valeur fin;
                                                              ENC=Wizard To Value];
                                                   DecimalPlaces=0:25;
                                                   BlankZero=Yes }
    { 9707;   ;Wizard From Line No.;Integer       ;OnValidate=BEGIN
                                                                IF "To Value" <> 0 THEN
                                                                  TESTFIELD(Type,Type::Answer);
                                                              END;

                                                   CaptionML=[ENU=Wizard From Line No.;
                                                              ESM=Asistente desde nß l°nea;
                                                              FRC=Assistant - Nß ligne dÇbut;
                                                              ENC=Wizard From Line No.];
                                                   BlankZero=Yes }
  }
  KEYS
  {
    {    ;Profile Questionnaire Code,Line No.     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProfileQuestnLine@1000 : Record 5088;
      TempProfileQuestionnaireLine@1007 : TEMPORARY Record 5088;
      ZeroDateFormula@1001 : DateFormula;
      Text000@1002 : TextConst 'ENU=Do you want to delete the rating values?;ESM=®Confirma que desea eliminar los valores de clasificaci¢n?;FRC=Souhaitez-vous supprimer les valeurs d''Çvaluation ?;ENC=Do you want to delete the rating values?';
      Text001@1003 : TextConst 'ENU=%1 cannot be changed until the rating value is deleted.;ESM=No se puede cambiar %1 hasta que se haya eliminado el valor de clasificaci¢n.;FRC=Vous ne pouvez pas modifier l''enregistrement %1 avant d''avoir supprimer la valeur d''Çvaluation.;ENC=%1 cannot be changed until the rating value is deleted.';
      Text002@1004 : TextConst 'ENU=You cannot delete this line because one or more questions are depending on it.;ESM=No puede eliminar esta l°nea porque una o m†s preguntas dependen de ella.;FRC=Vous ne pouvez pas supprimer cette ligne car une ou plusieurs questions y sont associÇes.;ENC=You cannot delete this line because one or more questions are depending on it.';
      Text003@1005 : TextConst 'ENU=You cannot delete this line because one or more rating values exists.;ESM=No puede eliminar esta l°nea porque existen uno o m†s valores clasificaci¢n.;FRC=Vous ne pouvez pas supprimer cette ligne car il existe une ou plusieurs valeurs d''Çvaluation.;ENC=You cannot delete this line because one or more rating values exists.';
      Text004@1006 : TextConst 'ENU=You cannot delete this question while answers exists.;ESM=No puede eliminar esta pregunta porque existen respuestas.;FRC=Vous ne pouvez pas supprimer cette question car il existe des rÇponses correspondantes.;ENC=You cannot delete this question while answers exists.';
      Text005@1011 : TextConst 'ENU=Please select for which questionnaire this rating should be created.;ESM=Seleccione el cuestionario para el que debe crearse esta clasificaci¢n.;FRC=Indiquez Ö quel questionnaire cette Çvaluation est destinÇe.;ENC=Please select for which questionnaire this rating should be created.';
      Text006@1010 : TextConst 'ENU=Please describe the rating.;ESM=Describa la clasificaci¢n.;FRC=DÇcrivez l''Çvaluation.;ENC=Please describe the rating.';
      Text007@1009 : TextConst 'ENU=Please create one or more different answers.;ESM=Cree una o m†s respuestas diferentes.;FRC=CrÇez plusieurs rÇponses diffÇrentes.;ENC=Please create one or more different answers.';
      Text008@1008 : TextConst 'ENU=Please enter which range of points this answer should require.;ESM=Introduzca el rango de puntos que necesitar°a esta respuesta.;FRC=Entrez l''intervalle de points requis par cette rÇponse.;ENC=Please enter which range of points this answer should require.';
      Text009@1012 : TextConst 'ENU=High;ESM=Alto;FRC=ElevÇe;ENC=High';
      Text010@1013 : TextConst 'ENU=Low;ESM=Bajo;FRC=Faible;ENC=Low';
      Text011@1014 : TextConst '@@@=Selecting answer A;ENU=A;ESM=A;FRC=A;ENC=A';
      Text012@1015 : TextConst '@@@=Selecting answer B;ENU=B;ESM=B;FRC=B;ENC=B';
      Text013@1016 : TextConst '@@@=Selecting answer C;ENU=C;ESM=U;FRC=C;ENC=C';

    PROCEDURE MoveUp@1();
    VAR
      UpperProfileQuestnLine@1000 : Record 5088;
      LineNo@1001 : Integer;
      UpperRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      UpperProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      UpperProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      IF UpperProfileQuestnLine.FIND('<') AND
         (UpperProfileQuestnLine.Type = UpperProfileQuestnLine.Type::Answer)
      THEN BEGIN
        UpperRecLineNo := UpperProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        UpperProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",UpperRecLineNo);
      END;
    END;

    PROCEDURE MoveDown@2();
    VAR
      LowerProfileQuestnLine@1000 : Record 5088;
      LineNo@1001 : Integer;
      LowerRecLineNo@1002 : Integer;
    BEGIN
      TESTFIELD(Type,Type::Answer);
      LowerProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      LineNo := "Line No.";
      LowerProfileQuestnLine.GET("Profile Questionnaire Code","Line No.");

      IF LowerProfileQuestnLine.FIND('>') AND
         (LowerProfileQuestnLine.Type = LowerProfileQuestnLine.Type::Answer)
      THEN BEGIN
        LowerRecLineNo := LowerProfileQuestnLine."Line No.";
        RENAME("Profile Questionnaire Code",-1);
        LowerProfileQuestnLine.RENAME("Profile Questionnaire Code",LineNo);
        RENAME("Profile Questionnaire Code",LowerRecLineNo);
      END;
    END;

    PROCEDURE Question@4() : Text[50];
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      IF ProfileQuestnLine.FINDLAST THEN
        EXIT(ProfileQuestnLine.Description);
    END;

    PROCEDURE FindQuestionLine@3() : Integer;
    BEGIN
      ProfileQuestnLine.RESET;
      ProfileQuestnLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      ProfileQuestnLine.SETFILTER("Line No.",'<%1',"Line No.");
      ProfileQuestnLine.SETRANGE(Type,Type::Question);
      IF ProfileQuestnLine.FINDLAST THEN
        EXIT(ProfileQuestnLine."Line No.");
    END;

    LOCAL PROCEDURE ResetFields@5();
    BEGIN
      CLEAR("Starting Date Formula");
      CLEAR("Ending Date Formula");
      "Classification Method" := "Classification Method"::" ";
      "Sorting Method" := "Sorting Method"::" ";
      "No. of Decimals" := 0;
      "Min. % Questions Answered" := 0;
    END;

    PROCEDURE CreateRatingFromProfQuestnLine@6(VAR ProfileQuestnLine@1001 : Record 5088);
    BEGIN
      INIT;
      "Profile Questionnaire Code" := ProfileQuestnLine."Profile Questionnaire Code";
      StartWizard;
    END;

    LOCAL PROCEDURE StartWizard@16();
    BEGIN
      "Wizard Step" := "Wizard Step"::"1";
      VALIDATE("Auto Contact Classification",TRUE);
      VALIDATE("Contact Class. Field","Contact Class. Field"::Rating);
      INSERT;

      ValidateAnswerOption;
      ValidateIntervalOption;

      PAGE.RUNMODAL(PAGE::"Create Rating",Rec);
    END;

    PROCEDURE CheckStatus@9();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          BEGIN
            IF "Profile Questionnaire Code" = '' THEN
              ERROR(Text005);
            IF Description = '' THEN
              ERROR(Text006);
          END;
        "Wizard Step"::"2":
          BEGIN
            IF TempProfileQuestionnaireLine.COUNT = 0 THEN
              ERROR(Text007);
          END;
        "Wizard Step"::"3":
          IF ("Wizard From Value" = 0) AND ("Wizard To Value" = 0) THEN
            ERROR(Text008);
      END;
    END;

    PROCEDURE PerformNextWizardStatus@8();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"1":
          "Wizard Step" := "Wizard Step" + 1;
        "Wizard Step"::"2":
          BEGIN
            "Wizard From Line No." := 0;
            "Wizard Step" := "Wizard Step" + 1;
            TempProfileQuestionnaireLine.SETRANGE("Line No.");
            TempProfileQuestionnaireLine.FIND('-');
            SetIntervalOption;
          END;
        "Wizard Step"::"3":
          BEGIN
            TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
            TempProfileQuestionnaireLine.FIND('-');
            TempProfileQuestionnaireLine."From Value" := "Wizard From Value";
            TempProfileQuestionnaireLine."To Value" := "Wizard To Value";
            TempProfileQuestionnaireLine.MODIFY;
            IF TempProfileQuestionnaireLine.NEXT <> 0 THEN BEGIN
              TempProfileQuestionnaireLine.SETRANGE("Line No.",TempProfileQuestionnaireLine."Line No.");
              "Wizard From Line No." := TempProfileQuestionnaireLine."Line No.";
              "Wizard From Value" := TempProfileQuestionnaireLine."From Value";
              "Wizard To Value" := TempProfileQuestionnaireLine."To Value";
              SetIntervalOption;
            END ELSE BEGIN
              TempProfileQuestionnaireLine.SETRANGE("Line No.");
              TempProfileQuestionnaireLine.FIND('-');
              "Wizard Step" := "Wizard Step" + 1;
            END;
          END;
      END;
    END;

    PROCEDURE PerformPrevWizardStatus@17();
    BEGIN
      CASE "Wizard Step" OF
        "Wizard Step"::"3":
          BEGIN
            TempProfileQuestionnaireLine.SETFILTER("Line No.",'..%1',"Wizard From Line No.");
            IF TempProfileQuestionnaireLine.FIND('+') THEN BEGIN
              TempProfileQuestionnaireLine."From Value" := "Wizard From Value";
              TempProfileQuestionnaireLine."To Value" := "Wizard To Value";
              TempProfileQuestionnaireLine.MODIFY;
            END;
            IF TempProfileQuestionnaireLine.NEXT(-1) <> 0 THEN BEGIN
              "Wizard From Line No." := TempProfileQuestionnaireLine."Line No.";
              "Wizard From Value" := TempProfileQuestionnaireLine."From Value";
              "Wizard To Value" := TempProfileQuestionnaireLine."To Value";
              SetIntervalOption
            END ELSE BEGIN
              TempProfileQuestionnaireLine.SETRANGE("Line No.");
              TempProfileQuestionnaireLine.FIND('-');
              "Wizard Step" := "Wizard Step" - 1;
            END;
          END;
        ELSE
          "Wizard Step" := "Wizard Step" - 1;
      END;
    END;

    PROCEDURE FinishWizard@7();
    VAR
      ProfileQuestionnaireLine@1000 : Record 5088;
      ProfileMgt@1004 : Codeunit 5059;
      NextLineNo@1002 : Integer;
      QuestionLineNo@1001 : Integer;
    BEGIN
      ProfileQuestionnaireLine.SETRANGE("Profile Questionnaire Code","Profile Questionnaire Code");
      IF ProfileQuestionnaireLine.FINDLAST THEN
        QuestionLineNo := ProfileQuestionnaireLine."Line No." + 10000
      ELSE
        QuestionLineNo := 10000;

      ProfileQuestionnaireLine := Rec;
      ProfileQuestionnaireLine."Line No." := QuestionLineNo;
      ProfileQuestionnaireLine.INSERT(TRUE);

      NextLineNo := QuestionLineNo;
      TempProfileQuestionnaireLine.RESET;
      IF TempProfileQuestionnaireLine.FINDSET THEN
        REPEAT
          NextLineNo := NextLineNo + 10000;
          ProfileQuestionnaireLine := TempProfileQuestionnaireLine;
          ProfileQuestionnaireLine."Profile Questionnaire Code" := "Profile Questionnaire Code";
          ProfileQuestionnaireLine."Line No." := NextLineNo;
          ProfileQuestionnaireLine.INSERT(TRUE);
        UNTIL TempProfileQuestionnaireLine.NEXT = 0;

      COMMIT;

      ProfileQuestionnaireLine.GET("Profile Questionnaire Code",QuestionLineNo);
      ProfileMgt.ShowAnswerPoints(ProfileQuestionnaireLine);
    END;

    PROCEDURE SetIntervalOption@10();
    BEGIN
      CASE TRUE OF
        (TempProfileQuestionnaireLine."From Value" = 0) AND (TempProfileQuestionnaireLine."To Value" <> 0):
          "Interval Option" := "Interval Option"::Maximum;
        (TempProfileQuestionnaireLine."From Value" <> 0) AND (TempProfileQuestionnaireLine."To Value" = 0):
          "Interval Option" := "Interval Option"::Minimum
        ELSE
          "Interval Option" := "Interval Option"::Interval
      END;

      ValidateIntervalOption;
    END;

    PROCEDURE ValidateIntervalOption@14();
    BEGIN
      TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileQuestionnaireLine.FIND('-');
      IF "Interval Option" = "Interval Option"::Minimum THEN
        TempProfileQuestionnaireLine."To Value" := 0;
      IF "Interval Option" = "Interval Option"::Maximum THEN
        TempProfileQuestionnaireLine."From Value" := 0;
      TempProfileQuestionnaireLine.MODIFY;
    END;

    PROCEDURE ValidateAnswerOption@13();
    BEGIN
      IF "Answer Option" = "Answer Option"::Custom THEN
        EXIT;

      TempProfileQuestionnaireLine.DELETEALL;

      CASE "Answer Option" OF
        "Answer Option"::HighLow:
          BEGIN
            CreateAnswer(Text009);
            CreateAnswer(Text010);
          END;
        "Answer Option"::ABC:
          BEGIN
            CreateAnswer(Text011);
            CreateAnswer(Text012);
            CreateAnswer(Text013);
          END;
      END;
    END;

    LOCAL PROCEDURE CreateAnswer@15(AnswerDescription@1000 : Text[50]);
    BEGIN
      TempProfileQuestionnaireLine.INIT;
      TempProfileQuestionnaireLine."Line No." := (TempProfileQuestionnaireLine.COUNT + 1) * 10000;
      TempProfileQuestionnaireLine.Type := TempProfileQuestionnaireLine.Type::Answer;
      TempProfileQuestionnaireLine.Description := AnswerDescription;
      TempProfileQuestionnaireLine.INSERT;
    END;

    PROCEDURE NoOfProfileAnswers@11() : Decimal;
    BEGIN
      EXIT(TempProfileQuestionnaireLine.COUNT);
    END;

    PROCEDURE ShowAnswers@12();
    VAR
      TempProfileQuestionnaireLine2@1000 : TEMPORARY Record 5088;
    BEGIN
      IF "Answer Option" <> "Answer Option"::Custom THEN
        IF TempProfileQuestionnaireLine.FIND('-') THEN
          REPEAT
            TempProfileQuestionnaireLine2 := TempProfileQuestionnaireLine;
            TempProfileQuestionnaireLine2.INSERT;
          UNTIL TempProfileQuestionnaireLine.NEXT = 0;

      PAGE.RUNMODAL(PAGE::"Rating Answers",TempProfileQuestionnaireLine);

      IF "Answer Option" <> "Answer Option"::Custom THEN
        IF TempProfileQuestionnaireLine.COUNT <> TempProfileQuestionnaireLine2.COUNT THEN
          "Answer Option" := "Answer Option"::Custom
        ELSE BEGIN
          IF TempProfileQuestionnaireLine.FIND('-') THEN
            REPEAT
              IF NOT TempProfileQuestionnaireLine2.GET(
                   TempProfileQuestionnaireLine."Profile Questionnaire Code",TempProfileQuestionnaireLine."Line No.")
              THEN
                "Answer Option" := "Answer Option"::Custom
              ELSE
                IF TempProfileQuestionnaireLine.Description <> TempProfileQuestionnaireLine2.Description THEN
                  "Answer Option" := "Answer Option"::Custom
            UNTIL (TempProfileQuestionnaireLine.NEXT = 0) OR ("Answer Option" = "Answer Option"::Custom);
        END;
    END;

    PROCEDURE GetProfileLineAnswerDesc@18() : Text[100];
    BEGIN
      TempProfileQuestionnaireLine.SETFILTER("Line No.",'%1..',"Wizard From Line No.");
      TempProfileQuestionnaireLine.FIND('-');
      EXIT(TempProfileQuestionnaireLine.Description);
    END;

    PROCEDURE GetAnswers@19(VAR ProfileQuestionnaireLine@1000 : Record 5088);
    BEGIN
      TempProfileQuestionnaireLine.RESET;
      ProfileQuestionnaireLine.RESET;
      ProfileQuestionnaireLine.DELETEALL;
      IF TempProfileQuestionnaireLine.FIND('-') THEN
        REPEAT
          ProfileQuestionnaireLine.INIT;
          ProfileQuestionnaireLine := TempProfileQuestionnaireLine;
          ProfileQuestionnaireLine.INSERT;
        UNTIL TempProfileQuestionnaireLine.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

