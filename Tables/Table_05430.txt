OBJECT Table 5430 Planning Error Log
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Planning Error Log;
               ESM=Registro error planificaci¢n;
               FRC=Journal des erreurs de planification;
               ENC=Planning Error Log];
    LookupPageID=Page5430;
    DrillDownPageID=Page5430;
  }
  FIELDS
  {
    { 1   ;   ;Worksheet Template Name;Code10     ;TableRelation="Req. Wksh. Template";
                                                   CaptionML=[ENU=Worksheet Template Name;
                                                              ESM=Nombre libro hoja dem.;
                                                              FRC=Nom modŠle feuille de travail;
                                                              ENC=Worksheet Template Name] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Requisition Wksh. Name".Name WHERE (Worksheet Template Name=FIELD(Worksheet Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              ESM=Nombre secci¢n diario;
                                                              FRC=Nom lot de journal;
                                                              ENC=Journal Batch Name] }
    { 3   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=N§ mov.;
                                                              FRC=Nø ‚criture;
                                                              ENC=Entry No.] }
    { 4   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ESM=N§ producto;
                                                              FRC=Nø d'article;
                                                              ENC=Item No.] }
    { 5   ;   ;Error Description   ;Text250       ;CaptionML=[ENU=Error Description;
                                                              ESM=Descripci¢n error;
                                                              FRC=Description de l'erreur;
                                                              ENC=Error Description] }
    { 6   ;   ;Table ID            ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table ID;
                                                              ESM=Id. tabla;
                                                              FRC=Code table;
                                                              ENC=Table ID] }
    { 7   ;   ;Table Position      ;Text250       ;CaptionML=[ENU=Table Position;
                                                              ESM=Posici¢n tabla;
                                                              FRC=Position de la table;
                                                              ENC=Table Position] }
  }
  KEYS
  {
    {    ;Worksheet Template Name,Journal Batch Name,Entry No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE Caption@5() : Text[100];
    VAR
      ReqWkshName@1000 : Record 245;
    BEGIN
      CASE TRUE OF
        GETFILTERS = '':
          EXIT('');
        NOT ReqWkshName.GET("Worksheet Template Name","Journal Batch Name"):
          EXIT('');
        ELSE
          EXIT(STRSUBSTNO('%1 %2',"Journal Batch Name",ReqWkshName.Description));
      END;
    END;

    PROCEDURE SetJnlBatch@1(WkshTemplName@1000 : Code[10];JnlBatchName@1001 : Code[10];ItemNo@1002 : Code[20]);
    BEGIN
      SETRANGE("Worksheet Template Name",WkshTemplName);
      SETRANGE("Journal Batch Name",JnlBatchName);
      IF FIND('+') THEN;
      "Worksheet Template Name" := WkshTemplName;
      "Journal Batch Name" := JnlBatchName;
      "Item No." := ItemNo;
    END;

    PROCEDURE SetError@40(TheError@1000 : Text[250];TheTableID@1002 : Integer;TheTablePosition@1003 : Text[250]);
    BEGIN
      "Entry No." := "Entry No." + 1;
      "Error Description" := TheError;
      "Table ID" := TheTableID;
      "Table Position" := TheTablePosition;
      INSERT;
    END;

    PROCEDURE GetError@47(VAR PlanningErrorLog@1000 : Record 5430) : Boolean;
    BEGIN
      IF NOT FIND('-') THEN
        EXIT(FALSE);
      DELETE;
      PlanningErrorLog.SETRANGE("Worksheet Template Name","Worksheet Template Name");
      PlanningErrorLog.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF PlanningErrorLog.FIND('+') THEN
        "Entry No." := PlanningErrorLog."Entry No." + 1;

      PlanningErrorLog := Rec;
      PlanningErrorLog.INSERT;
      EXIT(TRUE);
    END;

    PROCEDURE ShowError@2();
    VAR
      NoSeries@1012 : Record 308;
      Item@1010 : Record 27;
      SKU@1013 : Record 5700;
      MfgSetup@1009 : Record 99000765;
      Vendor@1008 : Record 23;
      Currency@1007 : Record 4;
      ProdBOMHeader@1006 : Record 99000771;
      ProdBOMVersion@1005 : Record 99000779;
      RtngHeader@1004 : Record 99000763;
      RtngVersion@1003 : Record 99000786;
      WorkCenter@1002 : Record 99000754;
      MachCenter@1001 : Record 99000758;
      RecRef@1000 : RecordRef;
    BEGIN
      IF "Table ID" = 0 THEN
        EXIT;

      RecRef.OPEN("Table ID");
      RecRef.SETPOSITION("Table Position");

      CASE "Table ID" OF
        DATABASE::Item:
          BEGIN
            RecRef.SETTABLE(Item);
            Item.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Item Card",Item);
          END;
        DATABASE::"Stockkeeping Unit":
          BEGIN
            RecRef.SETTABLE(SKU);
            SKU.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Stockkeeping Unit Card",SKU);
          END;
        DATABASE::Currency:
          BEGIN
            RecRef.SETTABLE(Currency);
            Currency.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::Currencies,Currency);
          END;
        DATABASE::Vendor:
          BEGIN
            RecRef.SETTABLE(Vendor);
            Vendor.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Vendor Card",Vendor);
          END;
        DATABASE::"No. Series":
          BEGIN
            RecRef.SETTABLE(NoSeries);
            NoSeries.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"No. Series",NoSeries);
          END;
        DATABASE::"Production BOM Header":
          BEGIN
            RecRef.SETTABLE(ProdBOMHeader);
            ProdBOMHeader.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Production BOM",ProdBOMHeader);
          END;
        DATABASE::"Routing Header":
          BEGIN
            RecRef.SETTABLE(RtngHeader);
            RtngHeader.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::Routing,RtngHeader);
          END;
        DATABASE::"Production BOM Version":
          BEGIN
            RecRef.SETTABLE(ProdBOMVersion);
            ProdBOMVersion.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Production BOM Version",ProdBOMVersion);
          END;
        DATABASE::"Routing Version":
          BEGIN
            RecRef.SETTABLE(RtngVersion);
            RtngVersion.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Routing Version",RtngVersion);
          END;
        DATABASE::"Machine Center":
          BEGIN
            RecRef.SETTABLE(MachCenter);
            MachCenter.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Machine Center Card",MachCenter);
          END;
        DATABASE::"Work Center":
          BEGIN
            RecRef.SETTABLE(WorkCenter);
            WorkCenter.SETRECFILTER;
            PAGE.RUNMODAL(PAGE::"Work Center Card",WorkCenter);
          END;
        DATABASE::"Manufacturing Setup":
          BEGIN
            RecRef.SETTABLE(MfgSetup);
            PAGE.RUNMODAL(0,MfgSetup);
          END;
        DATABASE::"Transfer Route":
          PAGE.RUNMODAL(PAGE::"Transfer Routes");
      END;
    END;

    BEGIN
    END.
  }
}

