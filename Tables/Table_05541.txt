OBJECT Table 5541 Timeline Event Change
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Timeline Event Change;
               ESM=Cambio evento escala de tiempo;
               FRC=Changement ÇvÇnement chronologie;
               ENC=Timeline Event Change];
  }
  FIELDS
  {
    { 1   ;   ;Reference No.       ;Text200       ;CaptionML=[ENU=Reference No.;
                                                              ESM=Nß referencia;
                                                              FRC=N¯ rÇfÇrence;
                                                              ENC=Reference No.];
                                                   Editable=No }
    { 2   ;   ;Changes             ;Integer       ;CaptionML=[ENU=Changes;
                                                              ESM=Cambios;
                                                              FRC=Changements;
                                                              ENC=Changes];
                                                   Editable=No }
    { 4   ;   ;Original Due Date   ;Date          ;CaptionML=[ENU=Original Due Date;
                                                              ESM=Fecha vencim. orig.;
                                                              FRC=êchÇance initiale;
                                                              ENC=Original Due Date];
                                                   Editable=No }
    { 5   ;   ;Due Date            ;Date          ;CaptionML=[ENU=Due Date;
                                                              ESM=Fecha vencimiento;
                                                              FRC=Date d'ÇchÇance;
                                                              ENC=Due Date] }
    { 6   ;   ;ChangeRefNo         ;Text250       ;CaptionML=[ENU=ChangeRefNo;
                                                              ESM=Nß ref. cambio;
                                                              FRC=ChangeRefNo;
                                                              ENC=ChangeRefNo];
                                                   Editable=No }
    { 20  ;   ;Original Quantity   ;Decimal       ;CaptionML=[ENU=Original Quantity;
                                                              ESM=Cantidad original;
                                                              FRC=QuantitÇ initiale;
                                                              ENC=Original Quantity];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   Editable=No }
    { 21  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              ESM=Cantidad;
                                                              FRC=QuantitÇ;
                                                              ENC=Quantity];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 1000;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ESM=Id.;
                                                              FRC=Code;
                                                              ENC=ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
    {    ;Reference No.                            }
    {    ;Due Date                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE TransferFromTransactionChangeTable@18(VAR TimelineEventChange@1004 : Record 5541;VAR changeTable@1003 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesDataTable");
    VAR
      changeRow@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesRow";
      rowEnum@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Collections.Generic.IEnumerator`1";
      Id@1001 : Integer;
    BEGIN
      TimelineEventChange.RESET;
      TimelineEventChange.DELETEALL;

      rowEnum := changeTable.GetEnumerator;
      Id := 1;

      WHILE rowEnum.MoveNext DO BEGIN
        changeRow := rowEnum.Current;
        InsertTransactionRow(TimelineEventChange,changeRow,Id);
        Id += 1;
      END;

      TimelineEventChange.SETCURRENTKEY("Due Date"); // Restore key to Due Date
    END;

    LOCAL PROCEDURE InsertTransactionRow@1(VAR TimelineEventChange@1001 : Record 5541;changeRow@1002 : DotNet "'Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.Timeline, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Framework.UI.WinForms.DataVisualization.TimelineVisualization.DataModel+TransactionChangesRow";Id@1000 : Integer);
    BEGIN
      TimelineEventChange.INIT;
      TimelineEventChange.ID := Id;
      TimelineEventChange."Reference No." := changeRow.RefNo;

      IF NOT changeRow.IsNewDateNull THEN
        TimelineEventChange."Due Date" := DT2DATE(changeRow.NewDate);

      IF NOT changeRow.IsOriginalDateNull THEN
        TimelineEventChange."Original Due Date" := DT2DATE(changeRow.OriginalDate);

      IF NOT changeRow.IsNewQuantityNull THEN
        TimelineEventChange.Quantity := changeRow.NewQuantity;

      IF NOT changeRow.IsOriginalQuantityNull THEN
        TimelineEventChange."Original Quantity" := changeRow.OriginalQuantity;

      TimelineEventChange.Changes := changeRow.Changes;
      TimelineEventChange.ChangeRefNo := changeRow.ChangeRefNo;
      TimelineEventChange.INSERT;
    END;

    PROCEDURE ActionMessage@7() : Integer;
    VAR
      ActionMsg@1000 : ' ,New,Change Qty.,Reschedule,Resched. & Chg. Qty.,Cancel';
    BEGIN
      IF ("Original Due Date" = 0D) AND ("Original Quantity" = 0) THEN
        EXIT(ActionMsg::New);

      IF Quantity = 0 THEN
        EXIT(ActionMsg::Cancel);

      IF ("Due Date" <> "Original Due Date") AND (Quantity <> "Original Quantity") THEN
        EXIT(ActionMsg::"Resched. & Chg. Qty.");

      IF "Due Date" <> "Original Due Date" THEN
        EXIT(ActionMsg::Reschedule);

      IF Quantity <> "Original Quantity" THEN
        EXIT(ActionMsg::"Change Qty.");

      EXIT(0);
    END;

    PROCEDURE NewSupply@1203() : Boolean;
    BEGIN
      EXIT((Changes = 1) AND (ChangeRefNo = ''));
    END;

    BEGIN
    END.
  }
}

