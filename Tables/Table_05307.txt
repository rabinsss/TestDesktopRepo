OBJECT Table 5307 Outlook Synch. Option Correl.
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Outlook Value");

               CheckDuplicatedRecords;
             END;

    OnModify=BEGIN
               CheckDuplicatedRecords;
             END;

    CaptionML=[ENU=Outlook Synch. Option Correl.;
               ESM=Correl. opciones sinc. Outlook;
               FRC=CorrÇl. option synchro Outlook;
               ENC=Outlook Synch. Option Correl.];
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                SetDefaults;
                                                              END;

                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              ESM=C¢digo entidad sinc.;
                                                              FRC=Code d'entitÇ de synchro;
                                                              ENC=Synch. Entity Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=[ENU=Element No.;
                                                              ESM=Nß elemento;
                                                              FRC=N¯ d'ÇlÇment;
                                                              ENC=Element No.] }
    { 3   ;   ;Field Line No.      ;Integer       ;CaptionML=[ENU=Field Line No.;
                                                              ESM=Nß l°nea campo;
                                                              FRC=N¯ champ ligne;
                                                              ENC=Field Line No.] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=Nß l°nea;
                                                              FRC=N¯ ligne;
                                                              ENC=Line No.] }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=[ENU=Outlook Object;
                                                              ESM=Objeto Outlook;
                                                              FRC=Objet Outlook;
                                                              ENC=Outlook Object] }
    { 6   ;   ;Outlook Property    ;Text80        ;CaptionML=[ENU=Outlook Property;
                                                              ESM=Propiedad Outlook;
                                                              FRC=PropriÇtÇ Outlook;
                                                              ENC=Outlook Property] }
    { 7   ;   ;Outlook Value       ;Text80        ;OnValidate=VAR
                                                                IntVar@1000 : Integer;
                                                              BEGIN
                                                                OSynchField.RESET;
                                                                OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");

                                                                IF OSynchSetupMgt.CheckOEnumeration(OSynchField) THEN BEGIN
                                                                  IF "Element No." = 0 THEN
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchField."Outlook Object",
                                                                      '',
                                                                      OSynchField."Outlook Property")
                                                                  ELSE BEGIN
                                                                    OSynchEntity.GET("Synch. Entity Code");
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchEntity."Outlook Item",
                                                                      OSynchField."Outlook Object",
                                                                      OSynchField."Outlook Property");
                                                                  END;
                                                                END ELSE BEGIN
                                                                  IF NOT EVALUATE(IntVar,"Outlook Value") THEN
                                                                    ERROR(Text002);

                                                                  "Enumeration No." := IntVar;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              OutlookValue@1000 : Text[80];
                                                              EnumerationNo@1001 : Integer;
                                                            BEGIN
                                                              OSynchField.RESET;
                                                              OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
                                                              IF NOT OSynchSetupMgt.CheckOEnumeration(OSynchField) THEN
                                                                ERROR(Text003);

                                                              IF "Element No." = 0 THEN
                                                                OutlookValue := OSynchSetupMgt.ShowEnumerationsLookup("Outlook Object",'',"Outlook Property",EnumerationNo)
                                                              ELSE BEGIN
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                OutlookValue :=
                                                                  OSynchSetupMgt.ShowEnumerationsLookup(
                                                                    OSynchEntity."Outlook Item",
                                                                    "Outlook Object",
                                                                    "Outlook Property",
                                                                    EnumerationNo);
                                                              END;

                                                              IF OutlookValue = '' THEN
                                                                EXIT;

                                                              "Outlook Value" := OutlookValue;
                                                              "Enumeration No." := EnumerationNo;
                                                            END;

                                                   CaptionML=[ENU=Outlook Value;
                                                              ESM=Valor Outlook;
                                                              FRC=Valeur Outlook;
                                                              ENC=Outlook Value] }
    { 8   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Table No.;
                                                              ESM=Nß tabla;
                                                              FRC=N¯ de la table;
                                                              ENC=Table No.] }
    { 9   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   CaptionML=[ENU=Field No.;
                                                              ESM=Nß campo;
                                                              FRC=N¯ de champ;
                                                              ENC=Field No.] }
    { 11  ;   ;Option No.          ;Integer       ;CaptionML=[ENU=Option No.;
                                                              ESM=Nß opci¢n;
                                                              FRC=N¯ d'option;
                                                              ENC=Option No.];
                                                   Editable=No }
    { 12  ;   ;Enumeration No.     ;Integer       ;CaptionML=[ENU=Enumeration No.;
                                                              ESM=Nß enumeraci¢n;
                                                              FRC=N¯ d'ÇnumÇration;
                                                              ENC=Enumeration No.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.,Field Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1000 : Record 5300;
      OSynchEntityElement@1001 : Record 5301;
      OSynchField@1002 : Record 5304;
      OSynchSetupMgt@1003 : Codeunit 5300;
      Text001@1006 : TextConst 'ENU=The line you are trying to create already exists.;ESM=La l°nea que intenta crear ya existe.;FRC=La ligne que vous tentez de crÇer existe dÇjÖ.;ENC=The line you are trying to create already exists.';
      Text002@1007 : TextConst 'ENU=This value is not valid. It must be either an integer or an enumeration element.;ESM=Este valor no es v†lido. Debe ser un entero o un elemento de enumeraci¢n.;FRC=Cette valeur n''est pas valide. Elle doit àtre un nombre entier ou un ÇlÇment d''ÇnumÇration.;ENC=This value is not valid. It must be either an integer or an enumeration element.';
      Text003@1008 : TextConst 'ENU=The look up window table cannot be opened because this Outlook property is not of the enumeration type.;ESM=La tabla de ventana de b£squeda no se puede abrir porque esta propiedad de Outlook no es del tipo de enumeraci¢n.;FRC=Impossible d''ouvrir la table de la fenàtre de consultation, car cette propriÇtÇ Outlook n''est pas du type ênumÇration.;ENC=The look up window table cannot be opened because this Outlook property is not of the enumeration type.';

    PROCEDURE SetDefaults@1();
    BEGIN
      IF "Element No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        "Outlook Object" := OSynchEntity."Outlook Item";
      END ELSE BEGIN
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      END;

      OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
      "Outlook Property" := OSynchField."Outlook Property";
      "Field No." := OSynchField."Field No.";

      IF OSynchField."Table No." = 0 THEN
        "Table No." := OSynchField."Master Table No."
      ELSE
        "Table No." := OSynchField."Table No.";
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@2();
    VAR
      OSynchOptionCorrel@1000 : Record 5307;
    BEGIN
      OSynchOptionCorrel.RESET;
      OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      OSynchOptionCorrel.SETRANGE("Field Line No.","Field Line No.");
      OSynchOptionCorrel.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchOptionCorrel.SETRANGE("Option No.","Option No.");
      OSynchOptionCorrel.SETRANGE("Enumeration No.","Enumeration No.");
      IF NOT OSynchOptionCorrel.ISEMPTY THEN
        ERROR(Text001);
    END;

    PROCEDURE GetFieldValue@3() FieldValue : Text;
    VAR
      OutlookSynchTypeConv@1002 : Codeunit 5302;
      LookupRecRef@1000 : RecordRef;
      LookupFieldRef@1001 : FieldRef;
    BEGIN
      LookupRecRef.OPEN("Table No.");
      LookupFieldRef := LookupRecRef.FIELD("Field No.");
      FieldValue := OutlookSynchTypeConv.OptionValueToText("Option No.",LookupFieldRef.OPTIONCAPTION);
      LookupRecRef.CLOSE;
    END;

    BEGIN
    END.
  }
}

