OBJECT Table 90 BOM Component
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Item.GET("Parent Item No.");
               IF Type = Type::Item THEN
                 ValidateAgainstRecursion("No.")
             END;

    OnModify=BEGIN
               Item.GET("Parent Item No.");
               IF Type = Type::Item THEN
                 ValidateAgainstRecursion("No.")
             END;

    OnRename=BEGIN
               Item.GET("Parent Item No.");
               IF Type = Type::Item THEN
                 ValidateAgainstRecursion("No.")
             END;

    CaptionML=[ENU=BOM Component;
               ESM=Componentes L.M.;
               FRC=Composantes nomenclature;
               ENC=BOM Component];
    LookupPageID=Page36;
    DrillDownPageID=Page36;
  }
  FIELDS
  {
    { 1   ;   ;Parent Item No.     ;Code20        ;TableRelation=Item WHERE (Type=CONST(Inventory));
                                                   CaptionML=[ENU=Parent Item No.;
                                                              ESM=N§ L.M.;
                                                              FRC=Nø article reli‚;
                                                              ENC=Parent Item No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=N§ l¡nea;
                                                              FRC=Nø ligne;
                                                              ENC=Line No.] }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "No." := '';
                                                                "Variant Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=" ,Item,Resource";
                                                                    ESM=" ,Producto,Recurso";
                                                                    FRC=" ,Article,Ressource";
                                                                    ENC=" ,Item,Resource"];
                                                   OptionString=[ ,Item,Resource] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item WHERE (Type=CONST(Inventory))
                                                                 ELSE IF (Type=CONST(Resource)) Resource;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                                "Variant Code" := '';
                                                                IF "No." = '' THEN
                                                                  EXIT;

                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      Item.GET("No.");
                                                                      ValidateAgainstRecursion("No.");
                                                                      Item.CALCFIELDS("Assembly BOM");
                                                                      "Assembly BOM" := Item."Assembly BOM";
                                                                      Description := Item.Description;
                                                                      "Unit of Measure Code" := Item."Base Unit of Measure";
                                                                      ParentItem.GET("Parent Item No.");
                                                                      CalcLowLevelCode.SetRecursiveLevelsOnItem(Item,ParentItem."Low-Level Code" + 1,TRUE);
                                                                      Item.FIND;
                                                                      ParentItem.FIND;
                                                                      IF ParentItem."Low-Level Code" >= Item."Low-Level Code" THEN
                                                                        ERROR(Text001,"No.");
                                                                    END;
                                                                  Type::Resource:
                                                                    BEGIN
                                                                      Res.GET("No.");
                                                                      "Assembly BOM" := FALSE;
                                                                      Description := Res.Name;
                                                                      "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 5   ;   ;Assembly BOM        ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("BOM Component" WHERE (Type=CONST(Item),
                                                                                            Parent Item No.=FIELD(No.)));
                                                   CaptionML=[ENU=Assembly BOM;
                                                              ESM=L.M. de ensamblado;
                                                              FRC=Nomenclature d'assemblage;
                                                              ENC=Assembly BOM];
                                                   Editable=No }
    { 6   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 7   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE IF (Type=CONST(Resource)) "Resource Unit of Measure".Code WHERE (Resource No.=FIELD(No.));
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESM=C¢d. unidad medida;
                                                              FRC=Code unit‚ de mesure;
                                                              ENC=Unit of Measure Code] }
    { 8   ;   ;Quantity per        ;Decimal       ;CaptionML=[ENU=Quantity per;
                                                              ESM=Cantidad por;
                                                              FRC=Quantit‚ par;
                                                              ENC=Quantity per];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 9   ;   ;Position            ;Code10        ;CaptionML=[ENU=Position;
                                                              ESM=Posici¢n;
                                                              FRC=Poste;
                                                              ENC=Position] }
    { 10  ;   ;Position 2          ;Code10        ;CaptionML=[ENU=Position 2;
                                                              ESM=Posici¢n 2;
                                                              FRC=Position 2;
                                                              ENC=Position 2] }
    { 11  ;   ;Position 3          ;Code10        ;CaptionML=[ENU=Position 3;
                                                              ESM=Posici¢n 3;
                                                              FRC=Position 3;
                                                              ENC=Position 3] }
    { 12  ;   ;Machine No.         ;Code10        ;CaptionML=[ENU=Machine No.;
                                                              ESM=N§ maquina;
                                                              FRC=Nø machine;
                                                              ENC=Machine No.] }
    { 13  ;   ;Lead-Time Offset    ;DateFormula   ;CaptionML=[ENU=Lead-Time Offset;
                                                              ESM=Desfase plazo entrega;
                                                              FRC=D‚calage du d‚lai;
                                                              ENC=Lead-Time Offset] }
    { 14  ;   ;BOM Description     ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Parent Item No.)));
                                                   CaptionML=[ENU=BOM Description;
                                                              ESM=Descripci¢n L.M.;
                                                              FRC=Description nomenclature;
                                                              ENC=BOM Description];
                                                   Editable=No }
    { 20  ;   ;Resource Usage Type ;Option        ;OnValidate=BEGIN
                                                                IF "Resource Usage Type" = xRec."Resource Usage Type" THEN
                                                                  EXIT;

                                                                TESTFIELD(Type,Type::Resource);
                                                              END;

                                                   CaptionML=[ENU=Resource Usage Type;
                                                              ESM=Tipo uso recursos;
                                                              FRC=Type d'utilisation des ressources;
                                                              ENC=Resource Usage Type];
                                                   OptionCaptionML=[ENU=Direct,Fixed;
                                                                    ESM=Directo,Fijo;
                                                                    FRC=Direct,Fixe;
                                                                    ENC=Direct,Fixed];
                                                   OptionString=Direct,Fixed }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESM=C¢d. variante;
                                                              FRC=Code variante;
                                                              ENC=Variant Code] }
    { 5900;   ;Installed in Line No.;Integer      ;OnValidate=BEGIN
                                                                IF "Installed in Line No." <> 0 THEN BEGIN
                                                                  IF "Installed in Line No." = "Line No." THEN
                                                                    ERROR(Text000,FIELDCAPTION("Installed in Line No."));
                                                                  BOMComp.RESET;
                                                                  BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                                  BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                                  BOMComp.SETRANGE("Line No.","Installed in Line No.");
                                                                  BOMComp.FINDFIRST;
                                                                  BOMComp.TESTFIELD("Quantity per",1);
                                                                  "Installed in Item No." := BOMComp."No.";
                                                                END ELSE
                                                                  "Installed in Item No." := '';
                                                              END;

                                                   OnLookup=BEGIN
                                                              BOMComp.RESET;
                                                              BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                              BOMComp.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              CLEAR(AssemblyBOM);
                                                              AssemblyBOM.SETTABLEVIEW(BOMComp);
                                                              AssemblyBOM.EDITABLE(FALSE);
                                                              AssemblyBOM.LOOKUPMODE(TRUE);
                                                              IF AssemblyBOM.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                AssemblyBOM.GETRECORD(BOMComp);
                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Installed in Line No.;
                                                              ESM=Instalado en n§ l¡n.;
                                                              FRC=Install‚ en ligne nø;
                                                              ENC=Installed in Line No.] }
    { 5901;   ;Installed in Item No.;Code20       ;TableRelation=IF (Type=CONST(Item)) Item;
                                                   OnValidate=BEGIN
                                                                IF "Installed in Item No." <> '' THEN BEGIN
                                                                  BOMComp.RESET;
                                                                  BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                                  BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                                  BOMComp.SETRANGE("No.","Installed in Item No.");
                                                                  BOMComp.FINDFIRST;
                                                                END;

                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              BOMComp.RESET;
                                                              BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                              BOMComp."No." := "Installed in Item No.";
                                                              BOMComp.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              CLEAR(AssemblyBOM);
                                                              AssemblyBOM.SETTABLEVIEW(BOMComp);
                                                              AssemblyBOM.EDITABLE(FALSE);
                                                              AssemblyBOM.LOOKUPMODE(TRUE);
                                                              IF AssemblyBOM.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                AssemblyBOM.GETRECORD(BOMComp);
                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Installed in Item No.;
                                                              ESM=Instalado en n§ prod.;
                                                              FRC=Install‚ sur article nø;
                                                              ENC=Installed in Item No.] }
  }
  KEYS
  {
    {    ;Parent Item No.,Line No.                ;Clustered=Yes }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 cannot be component of itself.;ESM=%1 no puede ser componente de si mismo.;FRC=%1 ne peut ˆtre une composante de lui-mˆme.;ENC=%1 cannot be component of itself.';
      Text001@1006 : TextConst 'ENU=You cannot insert item %1 as an assembly component of itself.;ESM=No puede insertar el producto %1 como un componente del ensamblado de si mismo.;FRC=Vous ne pouvez pas ins‚rer l''article %1 en tant que composante d''assemblage.;ENC=You cannot insert item %1 as an assembly component of itself.';
      Item@1001 : Record 27;
      ParentItem@1007 : Record 27;
      Res@1002 : Record 156;
      ItemVariant@1003 : Record 5401;
      BOMComp@1004 : Record 90;
      CalcLowLevelCode@1008 : Codeunit 99000793;
      AssemblyBOM@1005 : Page 36;

    PROCEDURE ValidateAgainstRecursion@3(ItemNo@1003 : Code[20]);
    VAR
      BOMComp@1001 : Record 90;
    BEGIN
      IF "Parent Item No." = ItemNo THEN
        ERROR(Text001,ItemNo);
      IF Type = Type::Item THEN BEGIN
        BOMComp.SETCURRENTKEY(Type,"No.");
        BOMComp.SETRANGE(Type,Type::Item);
        BOMComp.SETRANGE("No.","Parent Item No.");
        IF BOMComp.FINDSET THEN
          REPEAT
            BOMComp.ValidateAgainstRecursion(ItemNo);
          UNTIL BOMComp.NEXT = 0
      END
    END;

    BEGIN
    END.
  }
}

