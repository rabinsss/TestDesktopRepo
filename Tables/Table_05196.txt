OBJECT Table 5196 To-do Interaction Language
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    CaptionML=[ENU=To-do Interaction Language;
               ESM=Idioma interacci¢n tarea;
               FRC=Langue d'interaction de tƒche;
               ENC=To-do Interaction Language];
    LookupPageID=Page5196;
  }
  FIELDS
  {
    { 1   ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[ENU=To-do No.;
                                                              ESM=N§ tarea;
                                                              FRC=Nø tƒche;
                                                              ENC=To-do No.] }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              ESM=C¢d. idioma;
                                                              FRC=Code langue;
                                                              ENC=Language Code] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=[ENU=Attachment No.;
                                                              ESM=N§ anexo;
                                                              FRC=Nø piŠce jointe;
                                                              ENC=Attachment No.] }
  }
  KEYS
  {
    {    ;To-do No.,Language Code                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;ESM=Ha cancelado el proceso crear.;FRC=Vous avez annul‚ la cr‚ation.;ENC=You have cancelled the create process.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;ESM=¨Sustituir el anexo actual?;FRC=Remplacer la piŠce jointe existante?;ENC=Replace existing attachment?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;ESM=Ha cancelado el proceso importar.;FRC=Vous avez annul‚ l''import.;ENC=You have cancelled the import process.';
      Text003@1003 : TextConst 'ENU=You cannot create attachments here.;ESM=No puede crear anexos aqu¡.;FRC=Vous ne pouvez pas cr‚er des piŠces jointes ici.;ENC=You cannot create attachments here.';

    PROCEDURE CreateAttachment@1(PageNotEditable@1004 : Boolean) : Boolean;
    VAR
      Attachment@1000 : Record 5062;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF PageNotEditable THEN
        ERROR(Text003);
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      NewAttachNo := WordManagement.CreateWordAttachment("To-do No." + ' ' + Description,"Language Code");
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0 THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        MODIFY;
        EXIT(TRUE);
      END;
      ERROR(Text000);
    END;

    PROCEDURE OpenAttachment@2(PageNotEditable@1001 : Boolean);
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      IF PageNotEditable THEN
        Attachment."Read Only" := TRUE;
      Attachment.OpenAttachment("To-do No." + ' ' + Description,FALSE,"Language Code");
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      TodoTmplLanguage@1000 : Record 5196;
      Attachment@1001 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      TodoTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF PAGE.RUNMODAL(0,TodoTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(TodoTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      TempAttachment@1001 : TEMPORARY Record 5062;
      MarketingSetup@1003 : Record 5079;
      AttachmentManagement@1002 : Codeunit 5052;
      FileName@1004 : Text;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      CLEAR(TempAttachment);
      IF TempAttachment.ImportAttachmentFromClientFile('',TRUE,FALSE) THEN BEGIN
        IF "Attachment No." = 0 THEN
          Attachment.GET(AttachmentManagement.InsertAttachment(0))
        ELSE
          Attachment.GET("Attachment No.");
        TempAttachment."No." := Attachment."No.";
        TempAttachment."Storage Pointer" := Attachment."Storage Pointer";
        TempAttachment.WizSaveAttachment;
        MarketingSetup.GET;
        IF MarketingSetup."Attachment Storage Type" = MarketingSetup."Attachment Storage Type"::"Disk File" THEN
          IF TempAttachment."No." <> 0 THEN BEGIN
            FileName := TempAttachment.ConstDiskFileName;
            IF FileName <> '' THEN
              Attachment.ExportAttachmentToServerFile(FileName);
          END;

        Attachment."Storage Type" := TempAttachment."Storage Type";
        Attachment."Storage Pointer" := TempAttachment."Storage Pointer";
        Attachment.Attachment := TempAttachment.Attachment;
        Attachment."File Extension" := TempAttachment."File Extension";
        Attachment.MODIFY;
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      Attachment@1000 : Record 5062;
      FileName@1001 : Text[1024];
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.ExportAttachmentToClientFile(FileName);
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean) : Boolean;
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
          EXIT(TRUE);
        END;
    END;

    BEGIN
    END.
  }
}

