OBJECT Table 5620 FA Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               FAJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               FAJnlLine.SETRANGE("Journal Batch Name",Name);
               FAJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               FAJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               FAJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE FAJnlLine.FINDFIRST DO
                 FAJnlLine.RENAME("Journal Template Name",Name,FAJnlLine."Line No.");
             END;

    CaptionML=[ENU=FA Journal Batch;
               ESM=A/F Secci¢n diario;
               FRC=Lot de piŠces journal immo.;
               ENC=FA Journal Batch];
    LookupPageID=Page5633;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="FA Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              ESM=Nombre libro diario;
                                                              FRC=Nom modŠle journal;
                                                              ENC=Journal Template Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              ESM=Nombre;
                                                              FRC=Nom;
                                                              ENC=Name];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  FAJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  FAJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              ESM=C¢d. auditor¡a;
                                                              FRC=Code motif;
                                                              ENC=Reason Code] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  FAJnlTemplate.GET("Journal Template Name");
                                                                  IF FAJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series] }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                FAJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                FAJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              ESM=N§ serie registro;
                                                              FRC=S‚ries de nø report;
                                                              ENC=Posting No. Series] }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("FA Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              ESM=Peri¢dico;
                                                              FRC=R‚current;
                                                              ENC=Recurring];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;ESM=S¢lo se debe completar el campo %1 en los diarios peri¢dicos.;FRC=Seul le champ %1 peut ˆtre rempli sur les journaux r‚currents.;ENC=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'ENU=must not be %1;ESM=No puede ser %1.;FRC=ne doit pas ˆtre %1;ENC=must not be %1';
      FAJnlTemplate@1002 : Record 5619;
      FAJnlLine@1003 : Record 5621;

    PROCEDURE SetupNewBatch@3();
    BEGIN
      FAJnlTemplate.GET("Journal Template Name");
      "No. Series" := FAJnlTemplate."No. Series";
      "Posting No. Series" := FAJnlTemplate."Posting No. Series";
      "Reason Code" := FAJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}

