OBJECT Table 5715 Item Substitution
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ItemSubstitution@1001 : Record 5715;
             BEGIN
               IF Interchangeable THEN
                 IF CONFIRM(Text001 + Text002) THEN
                   DeleteInterchangeableItem(Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code")
                 ELSE
                   IF ItemSubstitution.GET(
                        "Substitute Type",
                        "Substitute No.",
                        "Substitute Variant Code",
                        Type,
                        "No.",
                        "Variant Code")
                   THEN BEGIN
                     ItemSubstitution.Interchangeable := FALSE;
                     ItemSubstitution.MODIFY;
                   END;

               IF Condition THEN BEGIN
                 SubCondition.SETRANGE(Type,Type);
                 SubCondition.SETRANGE("No.","No.");
                 SubCondition.SETRANGE("Variant Code","Variant Code");
                 SubCondition.SETRANGE("Substitute Type","Substitute Type");
                 SubCondition.SETRANGE("Substitute No.","Substitute No.");
                 SubCondition.SETRANGE("Substitute Variant Code","Substitute Variant Code");
                 SubCondition.DELETEALL;
               END;
             END;

    CaptionML=[ENU=Item Substitution;
               ESM=Sustituci¢n producto;
               FRC=Article de substitution;
               ENC=Item Substitution];
    LookupPageID=Page5716;
    DrillDownPageID=Page5716;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item.No.
                                                                 ELSE IF (Type=CONST(Nonstock Item)) "Nonstock Item"."Entry No.";
                                                   OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  "Variant Code" := '';
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      xRec."No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=Nß;
                                                              FRC=N¯;
                                                              ENC=No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" <> xRec."Variant Code" THEN
                                                                  RecreateSubstEntry(xRec."Variant Code","Substitute Variant Code");
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              ESM=C¢d. variante;
                                                              FRC=Code variante;
                                                              ENC=Variant Code] }
    { 3   ;   ;Substitute No.      ;Code20        ;TableRelation=IF (Substitute Type=CONST(Item)) Item.No.
                                                                 ELSE IF (Substitute Type=CONST(Nonstock Item)) "Nonstock Item"."Entry No.";
                                                   OnValidate=BEGIN
                                                                IF (Type = "Substitute Type") AND
                                                                   ("No." = "Substitute No.") AND
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF "Substitute No." <> xRec."Substitute No." THEN
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      xRec."Substitute No.",
                                                                      "Substitute Variant Code");

                                                                SetItemVariantDescription("Substitute Type","Substitute No.","Substitute Variant Code",Description);
                                                              END;

                                                   CaptionML=[ENU=Substitute No.;
                                                              ESM=Nß alterno;
                                                              FRC=N¯ substitution;
                                                              ENC=Substitute No.];
                                                   NotBlank=Yes }
    { 4   ;   ;Substitute Variant Code;Code10     ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Substitute No.));
                                                   OnValidate=BEGIN
                                                                IF (Type = "Substitute Type") AND
                                                                   ("No." = "Substitute No.") AND
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF "Substitute Variant Code" <> xRec."Substitute Variant Code" THEN
                                                                  RecreateSubstEntry("Variant Code",xRec."Substitute Variant Code");

                                                                Description := GetItemVariantDescription;
                                                              END;

                                                   CaptionML=[ENU=Substitute Variant Code;
                                                              ESM=C¢d. variante sustitutivo;
                                                              FRC=Code variante substitut;
                                                              ENC=Substitute Variant Code] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description];
                                                   Editable=No }
    { 6   ;   ;Inventory           ;Decimal       ;CaptionML=[ENU=Quantity on Hand;
                                                              ESM=Inventario;
                                                              FRC=Inventaire;
                                                              ENC=Quantity on Hand];
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Interchangeable     ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                                TESTFIELD("Substitute No.");
                                                                IF NOT Interchangeable THEN
                                                                  DeleteInterchangeableItem(Type,"No.","Variant Code","Substitute Type","Substitute No.","Substitute Variant Code")
                                                                ELSE
                                                                  CreateInterchangeableItem;
                                                              END;

                                                   CaptionML=[ENU=Interchangeable;
                                                              ESM=Intercambiable;
                                                              FRC=Interchangeable;
                                                              ENC=Interchangeable] }
    { 8   ;   ;Condition           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Substitution Condition" WHERE (Type=FIELD(Type),
                                                                                                     No.=FIELD(No.),
                                                                                                     Variant Code=FIELD(Variant Code),
                                                                                                     Substitute Type=FIELD(Substitute Type),
                                                                                                     Substitute No.=FIELD(Substitute No.),
                                                                                                     Substitute Variant Code=FIELD(Substitute Variant Code)));
                                                   CaptionML=[ENU=Condition;
                                                              ESM=Condici¢n;
                                                              FRC=êtat;
                                                              ENC=Condition];
                                                   Editable=No }
    { 9   ;   ;Location Filter     ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Location;
                                                   CaptionML=[ENU=Location Filter;
                                                              ESM=Filtro almacÇn;
                                                              FRC=Filtre emplacement;
                                                              ENC=Location Filter] }
    { 100 ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN BEGIN
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      xRec.Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      "Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                  VALIDATE("No.",'');
                                                                  VALIDATE("Substitute No.",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              ESM=Tipo;
                                                              FRC=Type;
                                                              ENC=Type];
                                                   OptionCaptionML=[ENU=Item,Nonstock Item;
                                                                    ESM=Prod.,Prod. no inventariable;
                                                                    FRC=Article,Article non stockÇ;
                                                                    ENC=Item,Nonstock Item];
                                                   OptionString=Item,Nonstock Item }
    { 101 ;   ;Substitute Type     ;Option        ;OnValidate=BEGIN
                                                                IF (Type = "Substitute Type") AND
                                                                   ("No." = "Substitute No.") AND
                                                                   ("Variant Code" = "Substitute Variant Code")
                                                                THEN
                                                                  ERROR(Text000);

                                                                IF "Substitute Type" <> xRec."Substitute Type" THEN BEGIN
                                                                  IF Interchangeable THEN
                                                                    DeleteInterchangeableItem(
                                                                      Type,
                                                                      "No.",
                                                                      "Variant Code",
                                                                      xRec."Substitute Type",
                                                                      "Substitute No.",
                                                                      "Substitute Variant Code");
                                                                  Description := '';
                                                                  "Variant Code" := '';
                                                                  "Substitute No." := '';
                                                                  Interchangeable := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Substitute Type;
                                                              ESM=Tipo sustitutivo;
                                                              FRC=Type substitution;
                                                              ENC=Substitute Type];
                                                   OptionCaptionML=[ENU=Item,Nonstock Item;
                                                                    ESM=Prod.,Prod. no inventariable;
                                                                    FRC=Article,Article non stockÇ;
                                                                    ENC=Item,Nonstock Item];
                                                   OptionString=Item,Nonstock Item }
    { 102 ;   ;Sub. Item No.       ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Nonstock Item"."Item No." WHERE (Entry No.=FIELD(Substitute No.)));
                                                   CaptionML=[ENU=Sub. Item No.;
                                                              ESM=Nß prod. sustitutivo;
                                                              FRC=N¯ article substitution;
                                                              ENC=Sub. Item No.];
                                                   Editable=No }
    { 103 ;   ;Relations Level     ;Integer       ;CaptionML=[ENU=Relations Level;
                                                              ESM=Nivel relaciones;
                                                              FRC=Niveau substitution;
                                                              ENC=Relations Level];
                                                   Editable=No }
    { 104 ;   ;Quantity Avail. on Shpt. Date;Decimal;
                                                   CaptionML=[ENU=Quantity Avail. on Shpt. Date;
                                                              ESM=Cdad. disponible a fecha env°o;
                                                              FRC=QtÇ dispo. en date de prÇp.;
                                                              ENC=Quantity Avail. on Shpt. Date];
                                                   DecimalPlaces=0:5 }
    { 105 ;   ;Shipment Date       ;Date          ;CaptionML=[ENU=Shipment Date;
                                                              ESM=Fecha env°o;
                                                              FRC=Date de livraison;
                                                              ENC=Shipment Date] }
  }
  KEYS
  {
    {    ;Type,No.,Variant Code,Substitute Type,Substitute No.,Substitute Variant Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You can not set up an item to be substituted by itself.;ESM=Un producto no puede ser sustituido por Çl mismo.;FRC=Impossible de configurer un article Ö substituer Ö lui-màme.;ENC=You can not set up an item to be substituted by itself.';
      Text001@1001 : TextConst 'ENU=This substitute is interchangeable. \;ESM=Este prod. sustitutivo es intercambiable. \;FRC=Ce substitut est interchangeable. \;ENC=This substitute is interchangeable. \';
      Text002@1002 : TextConst 'ENU=Do you want to delete the corresponding substitute?;ESM=®Confirma que desea borrar el sustitutivo correspondiente?;FRC=Voulez-vous supprimer le substitut correspondant?;ENC=Do you want to delete the corresponding substitute?';
      SubCondition@1007 : Record 5716;
      ItemVariant@1003 : Record 5401;

    LOCAL PROCEDURE CreateSubstitution@6(ItemNo1@1000 : Code[20];Variant1@1001 : Code[10];ItemNo2@1002 : Code[20];Variant2@1003 : Code[10];Substitutable@1004 : Boolean);
    BEGIN
      INIT;
      Type := Type::Item;
      "No." := ItemNo1;
      "Variant Code" := Variant1;
      "Substitute Type" := "Substitute Type"::Item;
      "Substitute No." := ItemNo2;
      "Substitute Variant Code" := Variant2;
      Interchangeable := Substitutable;
      SetItemVariantDescription(Type,"No.","Variant Code",Description);
    END;

    PROCEDURE CreateSubstitutionItem2Item@5(ItemNo1@1000 : Code[20];Variant1@1001 : Code[10];ItemNo2@1002 : Code[20];Variant2@1003 : Code[10];Substitutable@1004 : Boolean);
    BEGIN
      CreateSubstitution(ItemNo1,Variant1,ItemNo2,Variant2,Substitutable);
      INSERT;
      IF Substitutable THEN
        CreateSubstitution(ItemNo2,Variant2,ItemNo1,Variant1,Substitutable);
    END;

    LOCAL PROCEDURE CreateInterchangeableItem@1();
    VAR
      ItemSubstitution@1001 : Record 5715;
    BEGIN
      ItemSubstitution.Type := "Substitute Type";
      ItemSubstitution."No." := "Substitute No.";
      ItemSubstitution."Variant Code" := "Substitute Variant Code";
      ItemSubstitution."Substitute Type" := Type;
      ItemSubstitution."Substitute No." := "No.";
      ItemSubstitution."Substitute Variant Code" := "Variant Code";
      SetDescription(Type,"No.",ItemSubstitution.Description);
      ItemSubstitution.Interchangeable := TRUE;
      IF ItemSubstitution.FIND THEN
        ItemSubstitution.MODIFY
      ELSE
        ItemSubstitution.INSERT;
    END;

    LOCAL PROCEDURE DeleteInterchangeableItem@2(XType@1000 : Integer;XNo@1001 : Code[20];XVariantCode@1002 : Code[10];XSubstType@1003 : Integer;XSubstNo@1004 : Code[20];XSubstVariantCode@1005 : Code[10]);
    VAR
      ItemSubstitution@1007 : Record 5715;
    BEGIN
      ItemSubstitution.Type := XSubstType;
      ItemSubstitution."No." := XSubstNo;
      ItemSubstitution."Variant Code" := XSubstVariantCode;
      ItemSubstitution."Substitute Type" := XType;
      ItemSubstitution."Substitute No." := XNo;
      ItemSubstitution."Substitute Variant Code" := XVariantCode;
      IF ItemSubstitution.FIND THEN BEGIN
        ItemSubstitution.CALCFIELDS(Condition);
        IF ItemSubstitution.Condition THEN BEGIN
          SubCondition.SETRANGE(Type,XType);
          SubCondition.SETRANGE("No.",XNo);
          SubCondition.SETRANGE("Variant Code",XVariantCode);
          SubCondition.SETRANGE("Substitute Type",XSubstType);
          SubCondition.SETRANGE("Substitute No.",XSubstNo);
          SubCondition.SETRANGE("Substitute Variant Code",XSubstVariantCode);
          SubCondition.DELETEALL;
        END;
        ItemSubstitution.DELETE;
        Interchangeable := FALSE;
      END;
    END;

    LOCAL PROCEDURE RecreateSubstEntry@3(XVariantCode@1000 : Code[10];XSubstVariantCode@1001 : Code[10]);
    BEGIN
      IF xRec.Interchangeable THEN
        DeleteInterchangeableItem(
          xRec.Type,
          xRec."No.",
          XVariantCode,
          xRec."Substitute Type",
          xRec."Substitute No.",
          XSubstVariantCode);
    END;

    LOCAL PROCEDURE GetItemVariantDescription@4() : Text[50];
    VAR
      Item@1002 : Record 27;
    BEGIN
      IF "Substitute Variant Code" <> '' THEN
        IF ItemVariant.GET("Substitute No.","Substitute Variant Code") THEN
          IF ItemVariant.Description <> '' THEN
            EXIT(ItemVariant.Description);

      IF Item.GET("Substitute No.") THEN
        EXIT(Item.Description);
    END;

    PROCEDURE SetDescription@8(Type@1000 : Integer;Number@1001 : Code[20];VAR Description@1002 : Text[50]);
    VAR
      Item@1003 : Record 27;
      NonstockItem@1004 : Record 5718;
    BEGIN
      Description := '';
      IF Type = 1 THEN BEGIN
        IF NonstockItem.GET(Number) THEN
          Description := NonstockItem.Description;
      END ELSE
        IF Item.GET(Number) THEN
          Description := Item.Description;
    END;

    PROCEDURE SetItemVariantDescription@11(Type@1002 : Integer;Number@1001 : Code[20];Variant@1003 : Code[10];VAR Description@1000 : Text[50]);
    VAR
      Item@1004 : Record 27;
      NonstockItem@1005 : Record 5718;
      ItemVariant@1006 : Record 5401;
    BEGIN
      Description := '';
      IF Type = 1 THEN BEGIN
        IF NonstockItem.GET(Number) THEN
          Description := NonstockItem.Description;
      END ELSE BEGIN
        IF Variant <> '' THEN
          IF ItemVariant.GET(Number,Variant) THEN
            IF ItemVariant.Description <> '' THEN BEGIN
              Description := ItemVariant.Description;
              EXIT;
            END;
        IF Item.GET(Number) THEN
          Description := Item.Description;
      END;
    END;

    BEGIN
    END.
  }
}

