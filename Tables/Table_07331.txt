OBJECT Table 7331 Whse. Internal Put-away Header
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               WhseSetup.GET;
               IF "No." = '' THEN BEGIN
                 WhseSetup.TESTFIELD("Whse. Internal Put-away Nos.");
                 NoSeriesMgt.InitSeries(
                   WhseSetup."Whse. Internal Put-away Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnDelete=BEGIN
               TESTFIELD(Status,Status::Open);
               DeleteRelatedLines;
             END;

    OnRename=BEGIN
               ERROR(Text004,TABLECAPTION);
             END;

    CaptionML=[ENU=Whse. Internal Put-away Header;
               ESM=Cab. ubicaci¢n interna alm.;
               FRC=En-tàte rangement interne mag.;
               ENC=Whse. Internal Put-away Header];
    LookupPageID=Page7356;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                WhseSetup.GET;
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  NoSeriesMgt.TestManual(WhseSetup."Whse. Internal Put-away Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=Nß;
                                                              FRC=N¯;
                                                              ENC=No.] }
    { 2   ;   ;Location Code       ;Code10        ;TableRelation=Location WHERE (Use As In-Transit=CONST(No));
                                                   OnValidate=BEGIN
                                                                IF NOT WmsManagement.LocationIsAllowed("Location Code") THEN
                                                                  ERROR(Text006,FIELDCAPTION("Location Code"),"Location Code");

                                                                CheckPutawayRequired("Location Code");
                                                                IF "Location Code" <> '' THEN BEGIN
                                                                  Location.GET("Location Code");
                                                                  Location.TESTFIELD("Directed Put-away and Pick");
                                                                END;
                                                                IF "Location Code" <> xRec."Location Code" THEN BEGIN
                                                                  "From Zone Code" := '';
                                                                  "From Bin Code" := '';
                                                                  WhseInternalPutAwayLine.SETRANGE("No.","No.");
                                                                  IF WhseInternalPutAwayLine.FIND('-') THEN
                                                                    ERROR(
                                                                      Text005,
                                                                      FIELDCAPTION("Location Code"));
                                                                END;
                                                                IF USERID <> '' THEN BEGIN
                                                                  FILTERGROUP := 2;
                                                                  SETRANGE("Location Code","Location Code");
                                                                  FILTERGROUP := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Location Code;
                                                              ESM=C¢d. almacÇn;
                                                              FRC=Code d'emplacement;
                                                              ENC=Location Code] }
    { 3   ;   ;Assigned User ID    ;Code50        ;TableRelation="Warehouse Employee" WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF "Assigned User ID" <> '' THEN BEGIN
                                                                  "Assignment Date" := TODAY;
                                                                  "Assignment Time" := TIME;
                                                                END ELSE BEGIN
                                                                  "Assignment Date" := 0D;
                                                                  "Assignment Time" := 0T;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Assigned User ID;
                                                              ESM=Id. usuario asignado;
                                                              FRC=Code utilisateur affectÇ;
                                                              ENC=Assigned User ID] }
    { 4   ;   ;Assignment Date     ;Date          ;CaptionML=[ENU=Assignment Date;
                                                              ESM=Fecha asignaci¢n;
                                                              FRC=Date affectation;
                                                              ENC=Assignment Date];
                                                   Editable=No }
    { 5   ;   ;Assignment Time     ;Time          ;CaptionML=[ENU=Assignment Time;
                                                              ESM=Hora asignaci¢n;
                                                              FRC=Heure affectation;
                                                              ENC=Assignment Time];
                                                   Editable=No }
    { 6   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=SÇries de n¯;
                                                              ENC=No. Series] }
    { 7   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Warehouse Comment Line" WHERE (Table Name=CONST(Internal Put-away),
                                                                                                     Type=CONST(" "),
                                                                                                     No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 8   ;   ;From Bin Code       ;Code20        ;TableRelation=IF (From Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code))
                                                                 ELSE IF (From Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                       Zone Code=FIELD(From Zone Code));
                                                   OnValidate=VAR
                                                                BinType@1000 : Record 7303;
                                                              BEGIN
                                                                IF xRec."From Bin Code" <> "From Bin Code" THEN BEGIN
                                                                  IF "From Bin Code" <> '' THEN BEGIN
                                                                    GetLocation("Location Code");
                                                                    Location.TESTFIELD("Bin Mandatory");
                                                                    IF "From Bin Code" = Location."Adjustment Bin Code" THEN
                                                                      FIELDERROR(
                                                                        "From Bin Code",
                                                                        STRSUBSTNO(
                                                                          Text001,Location.FIELDCAPTION("Adjustment Bin Code"),
                                                                          Location.TABLECAPTION));

                                                                    Bin.GET("Location Code","From Bin Code");
                                                                    IF Bin."Bin Type Code" <> '' THEN
                                                                      IF BinType.GET(Bin."Bin Type Code") THEN
                                                                        BinType.TESTFIELD(Receive,FALSE);

                                                                    "From Zone Code" := Bin."Zone Code";
                                                                  END;
                                                                  MessageIfIntPutawayLinesExist(FIELDCAPTION("From Bin Code"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=From Bin Code;
                                                              ESM=Desde c¢d. ubicaci¢n;
                                                              FRC=Du code de zone;
                                                              ENC=From Bin Code] }
    { 9   ;   ;From Zone Code      ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF "From Zone Code" <> xRec."From Zone Code" THEN BEGIN
                                                                  GetLocation("Location Code");
                                                                  IF "From Zone Code" <> '' THEN
                                                                    Location.TESTFIELD("Directed Put-away and Pick");
                                                                  "From Bin Code" := '';
                                                                  MessageIfIntPutawayLinesExist(FIELDCAPTION("From Zone Code"));
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=From Zone Code;
                                                              ESM=Desde c¢d. zona;
                                                              FRC=Du code zone;
                                                              ENC=From Zone Code] }
    { 10  ;   ;Due Date            ;Date          ;OnValidate=BEGIN
                                                                MessageIfIntPutawayLinesExist(FIELDCAPTION("Due Date"));
                                                              END;

                                                   CaptionML=[ENU=Due Date;
                                                              ESM=Fecha vencimiento;
                                                              FRC=Date d'ÇchÇance;
                                                              ENC=Due Date] }
    { 11  ;   ;Document Status     ;Option        ;OnValidate=VAR
                                                                WhsePutAwayRqst@1000 : Record 7324;
                                                              BEGIN
                                                                IF "Document Status" <> xRec."Document Status" THEN BEGIN
                                                                  WhsePutAwayRqst.SETRANGE("Document Type",WhsePutAwayRqst."Document Type"::"Internal Put-away");
                                                                  WhsePutAwayRqst.SETRANGE("Document No.","No.");
                                                                  WhsePutAwayRqst.MODIFYALL(
                                                                    "Completely Put Away","Document Status" = "Document Status"::"Completely Put Away");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Document Status;
                                                              ESM=Estado documento;
                                                              FRC=êtat document;
                                                              ENC=Document Status];
                                                   OptionCaptionML=[ENU=" ,Partially Put Away,Completely Put Away";
                                                                    ESM=" ,Ubicado parcialmente,Ubicado completamente";
                                                                    FRC=" ,Partiellement rangÇ,Entiärement rangÇ";
                                                                    ENC=" ,Partially Put Away,Completely Put Away"];
                                                   OptionString=[ ,Partially Put Away,Completely Put Away];
                                                   Editable=No }
    { 12  ;   ;Sorting Method      ;Option        ;OnValidate=BEGIN
                                                                IF "Sorting Method" <> xRec."Sorting Method" THEN
                                                                  SortWhseDoc;
                                                              END;

                                                   CaptionML=[ENU=Sorting Method;
                                                              ESM=MÇtodo ordenaci¢n;
                                                              FRC=MÇthode de tri;
                                                              ENC=Sorting Method];
                                                   OptionCaptionML=[ENU=" ,Item,Shelf or Bin,Due Date";
                                                                    ESM=" ,Prod.,Estant. o ubic.,Fecha vto.";
                                                                    FRC=" ,Article,Tablette ou Zone,Date d'ÇchÇance";
                                                                    ENC=" ,Item,Shelf or Bin,Due Date"];
                                                   OptionString=[ ,Item,Shelf or Bin,Due Date] }
    { 13  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              ESM=Estado;
                                                              FRC=êtat;
                                                              ENC=Status];
                                                   OptionCaptionML=[ENU=Open,Released;
                                                                    ESM=Abierto,Lanzado;
                                                                    FRC=Ouvert,LibÇrÇ;
                                                                    ENC=Open,Released];
                                                   OptionString=Open,Released;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1009 : Record 14;
      Bin@1011 : Record 7354;
      WhseInternalPutAwayLine@1000 : Record 7332;
      WhseSetup@1001 : Record 5769;
      WhseInternalPutAwayHeader@1002 : Record 7331;
      NoSeriesMgt@1003 : Codeunit 396;
      WmsManagement@1014 : Codeunit 7302;
      ItemTrackingMgt@1015 : Codeunit 6500;
      HideValidationDialog@1005 : Boolean;
      Text001@1008 : TextConst 'ENU=must not be the %1 of the %2;ESM=no debe ser el %1 del %2;FRC=ne doit pas àtre le %1 de %2;ENC=must not be the %1 of the %2';
      Text002@1007 : TextConst 'ENU=You have changed %1 on the %2, but it has not been changed on the existing Warehouse Internal Put-Away Lines.\;ESM=Ha modificado %1 en la %2, pero no ha sido modificado en las l°ns. ubicaci¢n interna alm. actuales.\;FRC=Vous avez modifiÇ l''enregistrement %1 dans la table %2, mais il n''a pas ÇtÇ modifiÇ sur les lignes Rangement d''entrepìt interne existantes.\;ENC=You have changed %1 on the %2, but it has not been changed on the existing Warehouse Internal Put-Away Lines.\';
      Text003@1006 : TextConst 'ENU=You must update the existing Warehouse Internal Put-Away Lines manually.;ESM=Debe actualizar las l°ns. ubicaci¢n interna alm. actuales manualmente.;FRC=Vous devez mettre manuellement Ö jour les lignes Rangement d''entrepìt interne existantes.;ENC=You must update the existing Warehouse Internal Put-Away Lines manually.';
      Text004@1010 : TextConst 'ENU=You cannot rename a %1.;ESM=No se puede cambiar el nombre a %1.;FRC=Vous ne pouvez pas renommer un %1.;ENC=You cannot rename a %1.';
      Text005@1012 : TextConst 'ENU=You cannot change the %1, because the document has one or more lines.;ESM=No puede cambiar el %1, porque el documento tiene una o m†s l°neas.;FRC=Vous ne pouvez pas modifier le %1 car le document contient une ou plusieurs ligne(s).;ENC=You cannot change the %1, because the document has one or more lines.';
      Text006@1013 : TextConst 'ENU=You are not allowed to use %1 %2.;ESM=No le est† permitido utilizar %1 %2.;FRC=Vous n''àtes pas autorisÇ Ö utiliser %1 %2.;ENC=You are not allowed to use %1 %2.';

    LOCAL PROCEDURE SortWhseDoc@3();
    VAR
      SequenceNo@1000 : Integer;
    BEGIN
      WhseInternalPutAwayLine.RESET;
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      CASE "Sorting Method" OF
        "Sorting Method"::Item:
          WhseInternalPutAwayLine.SETCURRENTKEY("No.","Item No.");
        "Sorting Method"::"Shelf or Bin":
          BEGIN
            GetLocation("Location Code");
            IF Location."Bin Mandatory" THEN
              WhseInternalPutAwayLine.SETCURRENTKEY("No.","From Bin Code")
            ELSE
              WhseInternalPutAwayLine.SETCURRENTKEY("No.","Shelf No.");
          END;
        "Sorting Method"::"Due Date":
          WhseInternalPutAwayLine.SETCURRENTKEY("No.","Due Date");
      END;

      IF WhseInternalPutAwayLine.FIND('-') THEN BEGIN
        SequenceNo := 10000;
        REPEAT
          WhseInternalPutAwayLine."Sorting Sequence No." := SequenceNo;
          WhseInternalPutAwayLine.MODIFY;
          SequenceNo := SequenceNo + 10000;
        UNTIL WhseInternalPutAwayLine.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE MessageIfIntPutawayLinesExist@7(ChangedFieldName@1000 : Text[80]);
    VAR
      WhseInternalPutAwayLine@1001 : Record 7332;
    BEGIN
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      IF WhseInternalPutAwayLine.FINDFIRST THEN
        IF NOT HideValidationDialog THEN
          MESSAGE(
            STRSUBSTNO(
              Text002,ChangedFieldName,TABLECAPTION) + Text003);
    END;

    PROCEDURE AssistEdit@8(OldWhseInternalPutAwayHeader@1000 : Record 7331) : Boolean;
    BEGIN
      WhseSetup.GET;
      WITH WhseInternalPutAwayHeader DO BEGIN
        WhseInternalPutAwayHeader := Rec;
        WhseSetup.TESTFIELD("Whse. Internal Put-away Nos.");
        IF NoSeriesMgt.SelectSeries(
             WhseSetup."Whse. Internal Put-away Nos.",
             OldWhseInternalPutAwayHeader."No. Series","No. Series")
        THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := WhseInternalPutAwayHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE GetDocumentStatus@15(LineNo@1003 : Integer) : Integer;
    VAR
      WhseInternalPutAwayLine@1001 : Record 7332;
    BEGIN
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      WITH WhseInternalPutAwayLine DO BEGIN
        IF LineNo <> 0 THEN
          SETFILTER("Line No.",'<>%1',LineNo);
        IF NOT FINDFIRST THEN
          EXIT(Status::" ");

        SETRANGE(Status,Status::"Partially Put Away");
        IF FINDFIRST THEN
          EXIT(Status);

        SETRANGE(Status,Status::"Completely Put Away");
        IF FINDFIRST THEN BEGIN
          SETFILTER(Status,'<%1',Status::"Completely Put Away");
          IF FINDFIRST THEN
            EXIT(Status::"Partially Put Away");

          EXIT(Status);
        END;

        EXIT(Status);
      END;
    END;

    LOCAL PROCEDURE GetLocation@10(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    PROCEDURE SetWhseLocationFilter@2();
    VAR
      WmsManagement@1000 : Codeunit 7302;
    BEGIN
      IF USERID <> '' THEN BEGIN
        FILTERGROUP := 2;
        SETRANGE("Location Code",WmsManagement.GetAllowedLocation("Location Code"));
        FILTERGROUP := 0;
      END;
    END;

    PROCEDURE LookupInternalPutAwayHeader@1(VAR WhseInternalPutAwayHeader@1001 : Record 7331) : Boolean;
    BEGIN
      COMMIT;
      IF USERID <> '' THEN BEGIN
        WhseInternalPutAwayHeader.FILTERGROUP := 2;
        WhseInternalPutAwayHeader.SETRANGE("Location Code");
      END;
      IF PAGE.RUNMODAL(0,WhseInternalPutAwayHeader) = ACTION::LookupOK THEN;
      IF USERID <> '' THEN BEGIN
        WhseInternalPutAwayHeader.FILTERGROUP := 2;
        WhseInternalPutAwayHeader.SETRANGE("Location Code",WhseInternalPutAwayHeader."Location Code");
        WhseInternalPutAwayHeader.FILTERGROUP := 0;
      END;
    END;

    PROCEDURE LookupLocation@4(VAR WhseInternalPutAwayHeader@1001 : Record 7331) : Boolean;
    VAR
      Location@1000 : Record 14;
    BEGIN
      COMMIT;
      Location.FILTERGROUP := 2;
      Location.SETRANGE(Code);
      IF PAGE.RUNMODAL(PAGE::"Locations with Warehouse List",Location) = ACTION::LookupOK THEN
        WhseInternalPutAwayHeader.VALIDATE("Location Code",Location.Code);
      Location.FILTERGROUP := 0;
    END;

    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    PROCEDURE DeleteRelatedLines@9();
    VAR
      WhseInternalPutAwayLine@1003 : Record 7332;
      WhsePutAwayRqst@1002 : Record 7324;
      WhseCommentLine@1001 : Record 5770;
    BEGIN
      WhseInternalPutAwayLine.SETRANGE("No.","No.");
      WhseInternalPutAwayLine.DELETEALL;

      WhsePutAwayRqst.SETRANGE("Document Type",WhsePutAwayRqst."Document Type"::"Internal Put-away");
      WhsePutAwayRqst.SETRANGE("Document No.","No.");
      WhsePutAwayRqst.DELETEALL;

      WhseCommentLine.SETRANGE("Table Name",WhseCommentLine."Table Name"::"Internal Put-away");
      WhseCommentLine.SETRANGE(Type,WhseCommentLine.Type::" ");
      WhseCommentLine.SETRANGE("No.","No.");
      WhseCommentLine.DELETEALL;

      ItemTrackingMgt.DeleteWhseItemTrkgLines(
        DATABASE::"Whse. Internal Put-away Line",0,"No.",'',0,0,'',FALSE);
    END;

    PROCEDURE CheckPutawayRequired@5(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN BEGIN
        WhseSetup.GET;
        WhseSetup.TESTFIELD("Require Put-away");
      END ELSE BEGIN
        GetLocation(LocationCode);
        Location.TESTFIELD("Require Put-away");
      END;
    END;

    BEGIN
    END.
  }
}

