OBJECT Table 99000771 Production BOM Header
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               MfgSetup.GET;
               IF "No." = '' THEN BEGIN
                 MfgSetup.TESTFIELD("Production BOM Nos.");
                 NoSeriesMgt.InitSeries(MfgSetup."Production BOM Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               "Creation Date" := TODAY;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=BEGIN
               Item.SETCURRENTKEY("Production BOM No.");
               Item.SETRANGE("Production BOM No.","No.");
               IF Item.FINDFIRST THEN
                 ERROR(Text000);

               ProdBOMLine.SETRANGE("Production BOM No.","No.");
               ProdBOMLine.DELETEALL(TRUE);

               ProdBOMVersion.SETRANGE("Production BOM No.","No.");
               ProdBOMVersion.DELETEALL;

               MfgComment.SETRANGE("Table Name",MfgComment."Table Name"::"Production BOM Header");
               MfgComment.SETRANGE("No.","No.");
               MfgComment.DELETEALL;
             END;

    OnRename=BEGIN
               IF Status = Status::Certified THEN
                 ERROR(Text002,TABLECAPTION,FIELDCAPTION(Status),FORMAT(Status));
             END;

    CaptionML=[ENU=Production BOM Header;
               ESM=Cab. L.M. producci¢n;
               FRC=En-tàte nomencl. production;
               ENC=Production BOM Header];
    LookupPageID=Page99000787;
    DrillDownPageID=Page99000787;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              ESM=Nß;
                                                              FRC=N¯;
                                                              ENC=No.] }
    { 10  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                "Search Name" := Description;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 11  ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESM=Descripci¢n 2;
                                                              FRC=Description 2;
                                                              ENC=Description 2] }
    { 12  ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              ESM=Alias;
                                                              FRC=Rechercher un nom;
                                                              ENC=Search Name] }
    { 21  ;   ;Unit of Measure Code;Code10        ;TableRelation="Unit of Measure";
                                                   OnValidate=VAR
                                                                Item@1000 : Record 27;
                                                                ItemUnitOfMeasure@1001 : Record 5404;
                                                              BEGIN
                                                                IF Status = Status::Certified THEN
                                                                  FIELDERROR(Status);
                                                                Item.SETCURRENTKEY("Production BOM No.");
                                                                Item.SETRANGE("Production BOM No.","No.");
                                                                IF Item.FINDSET THEN
                                                                  REPEAT
                                                                    ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code");
                                                                  UNTIL Item.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESM=C¢d. unidad medida;
                                                              FRC=Code unitÇ de mesure;
                                                              ENC=Unit of Measure Code] }
    { 22  ;   ;Low-Level Code      ;Integer       ;CaptionML=[ENU=Low-Level Code;
                                                              ESM=C¢d. nivel m†s bajo;
                                                              FRC=Dernier niveau nomenclature;
                                                              ENC=Low-Level Code];
                                                   Editable=No }
    { 25  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Manufacturing Comment Line" WHERE (Table Name=CONST(Production BOM Header),
                                                                                                         No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 40  ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ESM=Fecha creaci¢n;
                                                              FRC=Date crÇation;
                                                              ENC=Creation Date];
                                                   Editable=No }
    { 43  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESM=Fecha £lt. modificaci¢n;
                                                              FRC=Date derniäre modification;
                                                              ENC=Last Date Modified];
                                                   Editable=No }
    { 45  ;   ;Status              ;Option        ;OnValidate=VAR
                                                                PlanningAssignment@1000 : Record 99000850;
                                                                MfgSetup@1004 : Record 99000765;
                                                                ProdBOMCheck@1001 : Codeunit 99000769;
                                                              BEGIN
                                                                IF (Status <> xRec.Status) AND (Status = Status::Certified) THEN BEGIN
                                                                  MfgSetup.LOCKTABLE;
                                                                  MfgSetup.GET;
                                                                  ProdBOMCheck.ProdBOMLineCheck("No.",'');
                                                                  "Low-Level Code" := 0;
                                                                  ProdBOMCheck.RUN(Rec);
                                                                  PlanningAssignment.NewBOM("No.");
                                                                END;
                                                                IF Status = Status::Closed THEN BEGIN
                                                                  IF CONFIRM(
                                                                       Text001,FALSE)
                                                                  THEN BEGIN
                                                                    ProdBOMVersion.SETRANGE("Production BOM No.","No.");
                                                                    IF ProdBOMVersion.FIND('-') THEN
                                                                      REPEAT
                                                                        ProdBOMVersion.Status := ProdBOMVersion.Status::Closed;
                                                                        ProdBOMVersion.MODIFY;
                                                                      UNTIL ProdBOMVersion.NEXT = 0;
                                                                  END ELSE
                                                                    Status := xRec.Status;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Status;
                                                              ESM=Estado;
                                                              FRC=êtat;
                                                              ENC=Status];
                                                   OptionCaptionML=[ENU=New,Certified,Under Development,Closed;
                                                                    ESM=Nueva,Certificada,En desarrollo,Cerrada;
                                                                    FRC=Nouveau,CertifiÇ,Sous-dÇveloppÇ,FermÇ;
                                                                    ENC=New,Certified,Under Development,Closed];
                                                   OptionString=New,Certified,Under Development,Closed }
    { 50  ;   ;Version Nos.        ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=Version Nos.;
                                                              ESM=Nos. versi¢n;
                                                              FRC=Nß versions;
                                                              ENC=Version Nos.] }
    { 51  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=SÇries de n¯;
                                                              ENC=No. Series];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
    {    ;Description                              }
    {    ;Status                                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=This Production BOM is being used on Items.;ESM=Esta L.M. producci¢n se utiliza en productos.;FRC=Cette nomenclature production est utilisÇe sur des articles.;ENC=This Production BOM is being used on Items.';
      Text001@1001 : TextConst 'ENU=All versions attached to the BOM will be closed. Close BOM?;ESM=Se cerrar†n todas las versiones asociadas con la L.M. ®Se cierra la L.M.?;FRC=Toutes les versions associÇes Ö la nomenclature seront fermÇes. Fermer nomenclature?;ENC=All versions attached to the BOM will be closed. Close BOM?';
      MfgSetup@1002 : Record 99000765;
      Item@1003 : Record 27;
      ProdBOMHeader@1004 : Record 99000771;
      ProdBOMVersion@1005 : Record 99000779;
      ProdBOMLine@1006 : Record 99000772;
      MfgComment@1007 : Record 99000770;
      NoSeriesMgt@1008 : Codeunit 396;
      Text002@1009 : TextConst 'ENU=You cannot rename the %1 when %2 is %3.;ESM=No puede renombrar el %1 cuando %2 es %3.;FRC=Vous ne pouvez pas renommer le %1 lorsque %2 est %3.;ENC=You cannot rename the %1 when %2 is %3.';

    PROCEDURE AssistEdit@2(OldProdBOMHeader@1000 : Record 99000771) : Boolean;
    BEGIN
      WITH ProdBOMHeader DO BEGIN
        ProdBOMHeader := Rec;
        MfgSetup.GET;
        MfgSetup.TESTFIELD("Production BOM Nos.");
        IF NoSeriesMgt.SelectSeries(MfgSetup."Production BOM Nos.",OldProdBOMHeader."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := ProdBOMHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

