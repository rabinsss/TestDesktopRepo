OBJECT Table 1302 Mini Dimensions Template
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Master Record Template Code" := GetParentTemplateCode;
               VALIDATE(Description);
               VALIDATE("Table Id");
               InsertConfigurationTemplateHeaderAndLines;
             END;

    OnModify=VAR
               FieldRefArray@1000 : ARRAY [3] OF FieldRef;
               RecRef@1001 : RecordRef;
             BEGIN
               TESTFIELD(Code);
               VALIDATE("Table Id");

               RecRef.GETTABLE(Rec);
               CreateFieldRefArray(FieldRefArray,RecRef);
               ConfigTemplateManagement.UpdateConfigTemplateAndLines(Code,Description,DATABASE::"Default Dimension",FieldRefArray);
             END;

    OnDelete=VAR
               ConfigTemplateHeader@1000 : Record 8618;
             BEGIN
               IF ConfigTemplateHeader.GET(Code) THEN BEGIN
                 ConfigTemplateManagement.RemoveRelatedTemplate("Master Record Template Code",Code);
                 ConfigTemplateHeader.DELETE(TRUE);
               END;
             END;

    CaptionML=[ENU=Mini Dimensions Template;
               ESM=Mini plantilla dimensiones;
               FRC=Modäle dimensions mini;
               ENC=Mini Dimensions Template];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF xRec."Dimension Code" <> "Dimension Code" THEN BEGIN
                                                                  "Dimension Value Code" := '';
                                                                  "Value Posting" := "Value Posting"::" ";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Dimension Code;
                                                              ESM=C¢d. dimensi¢n;
                                                              FRC=Code de dimension;
                                                              ENC=Dimension Code];
                                                   NotBlank=Yes }
    { 4   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   CaptionML=[ENU=Dimension Value Code;
                                                              ESM=C¢d. valor dimensi¢n;
                                                              FRC=Code section;
                                                              ENC=Dimension Value Code] }
    { 5   ;   ;Value Posting       ;Option        ;OnValidate=BEGIN
                                                                IF "Value Posting" = "Value Posting"::"No Code" THEN
                                                                  TESTFIELD("Dimension Value Code",'');
                                                              END;

                                                   CaptionML=[ENU=Value Posting;
                                                              ESM=Registro valor;
                                                              FRC=Report valeur;
                                                              ENC=Value Posting];
                                                   OptionCaptionML=[ENU=" ,Code Mandatory,Same Code,No Code";
                                                                    ESM=" ,Obligatorio,Igual c¢digo,Sin c¢digo";
                                                                    FRC=" ,Code obligatoire,Màme code,Pas de code";
                                                                    ENC=" ,Code Mandatory,Same Code,No Code"];
                                                   OptionString=[ ,Code Mandatory,Same Code,No Code] }
    { 50  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                Description := GetParentTemplateCode;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 51  ;   ;Table Id            ;Integer       ;OnValidate=VAR
                                                                TableIdFilter@1000 : Text;
                                                              BEGIN
                                                                TableIdFilter := GETFILTER("Table Id");
                                                                EVALUATE("Table Id",TableIdFilter);
                                                              END;

                                                   CaptionML=[ENU=Table Id;
                                                              ESM=Id. tabla;
                                                              FRC=Code table;
                                                              ENC=Table Id] }
    { 52  ;   ;Master Record Template Code;Code10 ;CaptionML=[ENU=Master Record Template Code;
                                                              ESM=C¢d. plantilla registro maestro;
                                                              FRC=Code modäle enregistrement de base;
                                                              ENC=Master Record Template Code] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ConfigTemplateManagement@1000 : Codeunit 8612;

    LOCAL PROCEDURE CreateFieldRefArray@12(VAR FieldRefArray@1000 : ARRAY [3] OF FieldRef;RecRef@1003 : RecordRef);
    VAR
      I@1002 : Integer;
    BEGIN
      I := 1;

      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Dimension Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Dimension Value Code")));
      AddToArray(FieldRefArray,I,RecRef.FIELD(FIELDNO("Value Posting")));
    END;

    LOCAL PROCEDURE AddToArray@4(VAR FieldRefArray@1000 : ARRAY [23] OF FieldRef;VAR I@1001 : Integer;CurrFieldRef@1002 : FieldRef);
    BEGIN
      FieldRefArray[I] := CurrFieldRef;
      I += 1;
    END;

    PROCEDURE InitializeTemplatesFromMasterRecordTemplate@20(MasterRecordTemplateCode@1001 : Code[10];VAR TempMiniDimensionsTemplate@1003 : TEMPORARY Record 1302;TableID@1004 : Integer);
    VAR
      ConfigTemplateHeader@1002 : Record 8618;
      ConfigTemplateLine@1000 : Record 8619;
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",MasterRecordTemplateCode);
      ConfigTemplateLine.SETRANGE(Type,ConfigTemplateLine.Type::"Related Template");

      IF ConfigTemplateLine.FINDSET THEN
        REPEAT
          ConfigTemplateHeader.GET(ConfigTemplateLine."Template Code");
          InitializeTempRecordFromConfigTemplate(TempMiniDimensionsTemplate,ConfigTemplateHeader,MasterRecordTemplateCode,TableID);
        UNTIL ConfigTemplateLine.NEXT = 0;
    END;

    PROCEDURE InitializeTempRecordFromConfigTemplate@1(VAR TempMiniDimensionsTemplate@1000 : TEMPORARY Record 1302;ConfigTemplateHeader@1001 : Record 8618;MasterRecordTemplateCode@1004 : Code[10];TableID@1002 : Integer);
    VAR
      RecRef@1003 : RecordRef;
    BEGIN
      TempMiniDimensionsTemplate.INIT;
      TempMiniDimensionsTemplate.Code := ConfigTemplateHeader.Code;
      TempMiniDimensionsTemplate.Description := ConfigTemplateHeader.Description;
      TempMiniDimensionsTemplate."Master Record Template Code" := MasterRecordTemplateCode;
      TempMiniDimensionsTemplate."Dimension Code" := GetDefaultDimensionCode(ConfigTemplateHeader);
      TempMiniDimensionsTemplate."Table Id" := TableID;
      TempMiniDimensionsTemplate.INSERT;

      RecRef.GETTABLE(TempMiniDimensionsTemplate);

      ConfigTemplateManagement.ApplyTemplateLinesWithoutValidation(ConfigTemplateHeader,RecRef);

      RecRef.SETTABLE(TempMiniDimensionsTemplate);
    END;

    LOCAL PROCEDURE InsertConfigurationTemplateHeaderAndLines@2();
    VAR
      FieldRefArray@1001 : ARRAY [3] OF FieldRef;
      RecRef@1000 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      CreateFieldRefArray(FieldRefArray,RecRef);
      ConfigTemplateManagement.CreateConfigTemplateAndLines(Code,Description,DATABASE::"Default Dimension",FieldRefArray);
      ConfigTemplateManagement.AddRelatedTemplate(GetParentTemplateCode,Code);
    END;

    PROCEDURE InsertDimensionsFromTemplates@17(ConfigTemplateHeader@1003 : Record 8618;MasterRecordNo@1005 : Code[20];TableID@1006 : Integer);
    VAR
      ConfigTemplateLine@1000 : Record 8619;
    BEGIN
      ConfigTemplateLine.SETRANGE(Type,ConfigTemplateLine.Type::"Related Template");
      ConfigTemplateLine.SETRANGE("Data Template Code",ConfigTemplateHeader.Code);

      IF ConfigTemplateLine.FINDSET THEN
        REPEAT
          ConfigTemplateHeader.GET(ConfigTemplateLine."Template Code");
          IF ConfigTemplateHeader."Table ID" = DATABASE::"Default Dimension" THEN
            InsertDimensionFromTemplate(ConfigTemplateHeader,MasterRecordNo,TableID);
        UNTIL ConfigTemplateLine.NEXT = 0;
    END;

    LOCAL PROCEDURE InsertDimensionFromTemplate@16(ConfigTemplateHeader@1006 : Record 8618;MasterRecordNo@1001 : Code[20];TableID@1004 : Integer);
    VAR
      DefaultDimension@1003 : Record 352;
      ConfigTemplateMgt@1002 : Codeunit 8612;
      RecRef@1000 : RecordRef;
    BEGIN
      DefaultDimension.INIT;
      DefaultDimension."No." := MasterRecordNo;
      DefaultDimension."Table ID" := TableID;
      DefaultDimension."Dimension Code" := GetDefaultDimensionCode(ConfigTemplateHeader);
      DefaultDimension.INSERT;

      RecRef.GETTABLE(DefaultDimension);
      ConfigTemplateMgt.UpdateRecord(ConfigTemplateHeader,RecRef);
      RecRef.SETTABLE(DefaultDimension);
    END;

    LOCAL PROCEDURE GetDefaultDimensionCode@9(ConfigTemplateHeader@1000 : Record 8618) : Text[20];
    VAR
      ConfigTemplateLine@1001 : Record 8619;
    BEGIN
      ConfigTemplateLine.SETRANGE("Data Template Code",ConfigTemplateHeader.Code);
      ConfigTemplateLine.SETRANGE("Field ID",FIELDNO("Dimension Code"));
      ConfigTemplateLine.FINDFIRST;

      EXIT(ConfigTemplateLine."Default Value");
    END;

    LOCAL PROCEDURE GetParentTemplateCode@8() : Text[10];
    BEGIN
      EXIT(GETFILTER("Master Record Template Code"));
    END;

    PROCEDURE CreateTemplatesFromExistingMasterRecord@3(MasterRecordNo@1001 : Code[20];MasterRecordTemplateCode@1003 : Code[10];TableID@1000 : Integer);
    VAR
      DefaultDimension@1002 : Record 352;
    BEGIN
      DefaultDimension.SETRANGE("No.",MasterRecordNo);
      DefaultDimension.SETRANGE("Table ID",TableID);

      IF DefaultDimension.FINDSET THEN
        REPEAT
          CreateTemplateFromExistingDefaultDimension(DefaultDimension,MasterRecordTemplateCode);
        UNTIL DefaultDimension.NEXT = 0;
    END;

    LOCAL PROCEDURE CreateTemplateFromExistingDefaultDimension@10(DefaultDimension@1010 : Record 352;MasterRecordTemplateCode@1011 : Code[10]);
    VAR
      RecRef@1005 : RecordRef;
      FieldRefArray@1000 : ARRAY [3] OF FieldRef;
      NewTemplateCode@1009 : Code[10];
    BEGIN
      RecRef.GETTABLE(DefaultDimension);
      CreateFieldRefArray(FieldRefArray,RecRef);

      ConfigTemplateManagement.CreateConfigTemplateAndLines(
        NewTemplateCode,MasterRecordTemplateCode,DATABASE::"Default Dimension",FieldRefArray);
      ConfigTemplateManagement.AddRelatedTemplate(MasterRecordTemplateCode,NewTemplateCode);
    END;

    BEGIN
    END.
  }
}

