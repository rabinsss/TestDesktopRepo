OBJECT Table 5913 Loaner
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 ServMgtSetup.GET;
                 ServMgtSetup.TESTFIELD("Loaner Nos.");
                 NoSeriesMgt.InitSeries(ServMgtSetup."Loaner Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               ServCommentLine@1000 : Record 5906;
             BEGIN
               CALCFIELDS(Lent,"Document No.");

               IF Lent THEN
                 ERROR(Text000);

               IF NOT Blocked THEN
                 ERROR(Text001,TABLECAPTION,FIELDCAPTION(Blocked));

               CLEAR(LoanerEntry);
               LoanerEntry.SETCURRENTKEY("Loaner No.");
               LoanerEntry.SETRANGE("Loaner No.","No.");
               LoanerEntry.DELETEALL;

               ServCommentLine.RESET;
               ServCommentLine.SETRANGE("Table Name",ServCommentLine."Table Name"::Loaner);
               ServCommentLine.SETRANGE("Table Subtype",0);
               ServCommentLine.SETRANGE("No.","No.");
               ServCommentLine.DELETEALL;
             END;

    CaptionML=[ENU=Loaner;
               ESM=Prod. prestado;
               FRC=Article de prˆt;
               ENC=Loaner];
    LookupPageID=Page5923;
    DrillDownPageID=Page5923;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServMgtSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServMgtSetup."Loaner Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 4   ;   ;Description 2       ;Text50        ;CaptionML=[ENU=Description 2;
                                                              ESM=Descripci¢n 2;
                                                              FRC=Description 2;
                                                              ENC=Description 2] }
    { 5   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Item No.=CONST(<>'')) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.))
                                                                 ELSE "Unit of Measure";
                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              ESM=C¢d. unidad medida;
                                                              FRC=Code unit‚ de mesure;
                                                              ENC=Unit of Measure Code] }
    { 6   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> '' THEN BEGIN
                                                                  Item.GET("Item No.");
                                                                  Description := Item.Description;
                                                                  "Description 2" := Item."Description 2";
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Description 2" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              ESM=N§ producto;
                                                              FRC=Nø d'article;
                                                              ENC=Item No.] }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESM=Fecha £lt. modificaci¢n;
                                                              FRC=Date derniŠre modification;
                                                              ENC=Last Date Modified];
                                                   Editable=No }
    { 8   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Service Comment Line" WHERE (Table Name=CONST(Loaner),
                                                                                                   Table Subtype=CONST(0),
                                                                                                   No.=FIELD(No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 9   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              ESM=Bloqueado;
                                                              FRC=Bloqu‚;
                                                              ENC=Blocked] }
    { 11  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series];
                                                   Editable=No }
    { 12  ;   ;Document No.        ;Code20        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Loaner Entry"."Document No." WHERE (Loaner No.=FIELD(No.),
                                                                                                           Lent=CONST(Yes)));
                                                   CaptionML=[ENU=Document No.;
                                                              ESM=N§ documento;
                                                              FRC=Nø de document;
                                                              ENC=Document No.];
                                                   Editable=No }
    { 13  ;   ;Lent                ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Loaner Entry" WHERE (Loaner No.=FIELD(No.),
                                                                                           Lent=CONST(Yes)));
                                                   CaptionML=[ENU=Lent;
                                                              ESM=Prestado;
                                                              FRC=Prˆt‚;
                                                              ENC=Lent];
                                                   Editable=No }
    { 14  ;   ;Serial No.          ;Code20        ;CaptionML=[ENU=Serial No.;
                                                              ESM=N§ serie;
                                                              FRC=Nø de s‚rie;
                                                              ENC=Serial No.] }
    { 15  ;   ;Document Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Loaner Entry"."Document Type" WHERE (Loaner No.=FIELD(No.),
                                                                                                            Lent=CONST(Yes)));
                                                   CaptionML=[ENU=Document Type;
                                                              ESM=Tipo documento;
                                                              FRC=Type de document;
                                                              ENC=Document Type];
                                                   OptionCaptionML=[ENU=" ,Quote,Order";
                                                                    ESM=" ,Cotizaci¢n,Pedido";
                                                                    FRC=" ,Devis,Commande";
                                                                    ENC=" ,Quote,Order"];
                                                   OptionString=[ ,Quote,Order];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Description                              }
    {    ;Item No.                                 }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Item No.                 }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot delete a loaner that is lent.;ESM=No se puede eliminar un producto prestado que ha sido prestado.;FRC=Vous ne pouvez pas supprimer un article qui est prˆt‚.;ENC=You cannot delete a loaner that is lent.';
      Text001@1001 : TextConst 'ENU=You can only delete a %1 that is %2.;ESM=S¢lo puede eliminar un %1 que sea %2.;FRC=Vous ne pouvez supprimer qu''un %1 qui est %2.;ENC=You can only delete a %1 that is %2.';
      ServMgtSetup@1002 : Record 5911;
      Item@1005 : Record 27;
      Loaner@1003 : Record 5913;
      LoanerEntry@1004 : Record 5914;
      NoSeriesMgt@1006 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldLoaner@1000 : Record 5913) : Boolean;
    BEGIN
      WITH Loaner DO BEGIN
        Loaner := Rec;
        ServMgtSetup.GET;
        ServMgtSetup.TESTFIELD("Loaner Nos.");
        IF NoSeriesMgt.SelectSeries(ServMgtSetup."Loaner Nos.",OldLoaner."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := Loaner;
          EXIT(TRUE);
        END;
      END;
    END;

    BEGIN
    END.
  }
}

