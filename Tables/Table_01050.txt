OBJECT Table 1050 Additional Fee Setup
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Additional Fee Setup;
               ESM=Configuraci¢n recargo fijo;
               FRC=Configuration des frais suppl‚mentaires;
               ENC=Additional Fee Setup];
    LookupPageID=Page1050;
    DrillDownPageID=Page1050;
  }
  FIELDS
  {
    { 1   ;   ;Charge Per Line     ;Boolean       ;CaptionML=[ENU=Charge Per Line;
                                                              ESM=Cargo por l¡nea;
                                                              FRC=Frais par ligne;
                                                              ENC=Charge Per Line] }
    { 2   ;   ;Reminder Terms Code ;Code10        ;TableRelation="Reminder Terms".Code;
                                                   CaptionML=[ENU=Reminder Terms Code;
                                                              ESM=C¢d. recordatorio;
                                                              FRC=Code modalit‚ de rappel;
                                                              ENC=Reminder Terms Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Reminder Level No.  ;Integer       ;TableRelation="Reminder Level".No.;
                                                   CaptionML=[ENU=Reminder Level No.;
                                                              ESM=N.§ nivel recordatorio;
                                                              FRC=Nø du niveau de rappel;
                                                              ENC=Reminder Level No.];
                                                   NotBlank=Yes }
    { 4   ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[ENU=Currency Code;
                                                              ESM=C¢d. divisa;
                                                              FRC=Code devise;
                                                              ENC=Currency Code] }
    { 5   ;   ;Threshold Remaining Amount;Decimal ;CaptionML=[ENU=Threshold Remaining Amount;
                                                              ESM=Importe restante de umbral;
                                                              FRC=Seuil montant ouvert;
                                                              ENC=Threshold Remaining Amount];
                                                   MinValue=0 }
    { 6   ;   ;Additional Fee Amount;Decimal      ;CaptionML=[ENU=Additional Fee Amount;
                                                              ESM=Importe recargo fijo;
                                                              FRC=Montant frais suppl‚mentaires;
                                                              ENC=Additional Fee Amount];
                                                   MinValue=0 }
    { 7   ;   ;Additional Fee %    ;Decimal       ;CaptionML=[ENU=Additional Fee %;
                                                              ESM=% recargo fijo;
                                                              FRC=% frais suppl‚mentaires;
                                                              ENC=Additional Fee %];
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 8   ;   ;Min. Additional Fee Amount;Decimal ;OnValidate=BEGIN
                                                                IF ("Max. Additional Fee Amount" > 0) AND ("Min. Additional Fee Amount" > "Max. Additional Fee Amount") THEN
                                                                  ERROR(STRSUBSTNO(InvalidMaxAddFeeErr,FIELDCAPTION("Min. Additional Fee Amount"),FIELDCAPTION("Max. Additional Fee Amount")));
                                                              END;

                                                   CaptionML=[ENU=Min. Additional Fee Amount;
                                                              ESM=Importe recargo fijo m¡n.;
                                                              FRC=Montant frais suppl‚mentaires min.;
                                                              ENC=Min. Additional Fee Amount];
                                                   MinValue=0 }
    { 9   ;   ;Max. Additional Fee Amount;Decimal ;OnValidate=BEGIN
                                                                IF ("Max. Additional Fee Amount" > 0) AND ("Min. Additional Fee Amount" > "Max. Additional Fee Amount") THEN
                                                                  ERROR(STRSUBSTNO(InvalidMaxAddFeeErr,FIELDCAPTION("Min. Additional Fee Amount"),FIELDCAPTION("Max. Additional Fee Amount")));
                                                              END;

                                                   CaptionML=[ENU=Max. Additional Fee Amount;
                                                              ESM=Importe recargo fijo m x.;
                                                              FRC=Montant frais suppl‚mentaires max.;
                                                              ENC=Max. Additional Fee Amount];
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Reminder Terms Code,Reminder Level No.,Charge Per Line,Currency Code,Threshold Remaining Amount;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      InvalidMaxAddFeeErr@1001 : TextConst 'ENU=The %1 is greater than the %2. You must change one of the values.;ESM=El %1 es mayor que el %2. Debe cambiar uno de los valores.;FRC=%1 est plus grand que %2. Vous devez modifier une des valeurs.;ENC=The %1 is greater than the %2. You must change one of the values.';

    LOCAL PROCEDURE CalculateAddFeeSingleDynamic@1(VAR AdditionalFeeSetup@1000 : Record 1050;RemainingAmount@1002 : Decimal) : Decimal;
    VAR
      AdditionalFee@1001 : Decimal;
    BEGIN
      WITH AdditionalFeeSetup DO BEGIN
        IF NOT FINDSET THEN
          EXIT(0);
        REPEAT
          IF RemainingAmount >= "Threshold Remaining Amount" THEN BEGIN
            IF "Additional Fee Amount" > 0 THEN
              AdditionalFee := "Additional Fee Amount";

            IF "Additional Fee %" > 0 THEN
              AdditionalFee += RemainingAmount * "Additional Fee %" / 100;

            IF ("Max. Additional Fee Amount" > 0) AND (AdditionalFee > "Max. Additional Fee Amount") THEN
              AdditionalFee := "Max. Additional Fee Amount";

            IF AdditionalFee < "Min. Additional Fee Amount" THEN
              AdditionalFee := "Min. Additional Fee Amount";

            EXIT(AdditionalFee);
          END;
        UNTIL NEXT = 0;
        EXIT(0);
      END;
    END;

    LOCAL PROCEDURE CalculateAddFeeAccumulatedDynamic@1000(VAR AdditionalFeeSetup@1000 : Record 1050;RemainingAmount@1002 : Decimal) : Decimal;
    VAR
      AdditionalFee@1001 : Decimal;
      RangeAddFeeAmount@1004 : Decimal;
    BEGIN
      WITH AdditionalFeeSetup DO BEGIN
        IF NOT FINDSET THEN
          EXIT(0);
        REPEAT
          IF RemainingAmount >= "Threshold Remaining Amount" THEN BEGIN
            RangeAddFeeAmount := 0;

            IF "Additional Fee Amount" > 0 THEN
              RangeAddFeeAmount := "Additional Fee Amount";

            IF "Additional Fee %" > 0 THEN
              RangeAddFeeAmount += ((RemainingAmount - "Threshold Remaining Amount") * "Additional Fee %") / 100;

            IF "Max. Additional Fee Amount" > 0 THEN
              IF RangeAddFeeAmount > "Max. Additional Fee Amount" THEN
                RangeAddFeeAmount := "Max. Additional Fee Amount";

            IF RangeAddFeeAmount < "Min. Additional Fee Amount" THEN
              RangeAddFeeAmount := "Min. Additional Fee Amount";

            RemainingAmount := "Threshold Remaining Amount";
            AdditionalFee += RangeAddFeeAmount;
          END;
        UNTIL NEXT = 0;
      END;
      EXIT(AdditionalFee);
    END;

    PROCEDURE GetAdditionalFeeFromSetup@5(ReminderLevel@1001 : Record 293;RemAmount@1002 : Decimal;CurrencyCode@1003 : Code[10];ChargePerLine@1004 : Boolean;AddFeeCalcType@1000 : Option;PostingDate@1008 : Date) : Decimal;
    VAR
      AdditionalFeeSetup@1010 : Record 1050;
      CurrExchRate@1007 : Record 330;
      FeeAmountInLCY@1006 : Decimal;
      RemAmountLCY@1009 : Decimal;
    BEGIN
      WITH AdditionalFeeSetup DO BEGIN
        ASCENDING(FALSE);
        SETRANGE("Charge Per Line",ChargePerLine);
        SETRANGE("Reminder Terms Code",ReminderLevel."Reminder Terms Code");
        SETRANGE("Reminder Level No.",ReminderLevel."No.");
        SETRANGE("Currency Code",CurrencyCode);
        IF FINDFIRST THEN BEGIN
          IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Single Dynamic" THEN
            EXIT(CalculateAddFeeSingleDynamic(AdditionalFeeSetup,RemAmount));

          IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Accumulated Dynamic" THEN
            EXIT(CalculateAddFeeAccumulatedDynamic(AdditionalFeeSetup,RemAmount));
        END ELSE
          IF CurrencyCode <> '' THEN BEGIN
            SETRANGE("Currency Code",'');
            IF FINDFIRST THEN BEGIN
              RemAmountLCY :=
                CurrExchRate.ExchangeAmtFCYToLCY(
                  PostingDate,CurrencyCode,RemAmount,CurrExchRate.ExchangeRate(PostingDate,CurrencyCode));
              IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Single Dynamic" THEN
                FeeAmountInLCY := CalculateAddFeeSingleDynamic(AdditionalFeeSetup,RemAmountLCY)
              ELSE
                IF AddFeeCalcType = ReminderLevel."Add. Fee Calculation Type"::"Accumulated Dynamic" THEN
                  FeeAmountInLCY := CalculateAddFeeAccumulatedDynamic(AdditionalFeeSetup,RemAmountLCY);
              EXIT(CurrExchRate.ExchangeAmtLCYToFCY(
                  PostingDate,CurrencyCode,
                  FeeAmountInLCY,
                  CurrExchRate.ExchangeRate(PostingDate,CurrencyCode)));
            END;
            EXIT(0);
          END;
        EXIT(0);
      END;
    END;

    BEGIN
    END.
  }
}

