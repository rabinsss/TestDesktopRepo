OBJECT Table 5076 Segment Header
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 RMSetup.GET;
                 RMSetup.TESTFIELD("Segment Nos.");
                 NoSeriesMgt.InitSeries(RMSetup."Segment Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               Date := WORKDATE;
             END;

    OnDelete=VAR
               SegHist@1000 : Record 5078;
               SegCriteriaLine@1001 : Record 5097;
               SegInteractLanguage@1002 : Record 5104;
             BEGIN
               SegCriteriaLine.SETRANGE("Segment No.","No.");
               SegCriteriaLine.DELETEALL; // Must be deleted first!
               SegHist.SETRANGE("Segment No.","No.");
               SegHist.DELETEALL;

               SegInteractLanguage.SETRANGE("Segment No.","No.");
               SegInteractLanguage.DELETEALL(TRUE);

               SegLine.SETRANGE("Segment No.","No.");
               SegLine.DELETEALL(TRUE);

               GET("No.");
             END;

    CaptionML=[ENU=Segment Header;
               ESM=Cab. segmento;
               FRC=En-tˆte segment;
               ENC=Segment Header];
    LookupPageID=Page5093;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  RMSetup.GET;
                                                                  NoSeriesMgt.TestManual(RMSetup."Segment Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 2   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION(Description),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 3   ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Campaign No."),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Campaign No.;
                                                              ESM=N§ campa¤a;
                                                              FRC=Nø promotion;
                                                              ENC=Campaign No.] }
    { 4   ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Salesperson Code"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Salesperson Code;
                                                              ESM=C¢d. vendedor;
                                                              FRC=Code repr‚sentant;
                                                              ENC=Salesperson Code] }
    { 5   ;   ;Correspondence Type (Default);Option;
                                                   OnValidate=VAR
                                                                Attachment@1001 : Record 5062;
                                                                ErrorText@1000 : Text[80];
                                                              BEGIN
                                                                IF NOT Attachment.GET("Attachment No.") THEN
                                                                  EXIT;

                                                                ErrorText := Attachment.CheckCorrespondenceType("Correspondence Type (Default)");
                                                                IF ErrorText <> '' THEN
                                                                  ERROR(
                                                                    Text000 + ErrorText,
                                                                    FIELDCAPTION("Correspondence Type (Default)"),"Correspondence Type (Default)",TABLECAPTION,"No.");
                                                                IF "Correspondence Type (Default)" <> "Correspondence Type (Default)"::" " THEN
                                                                  UpdateSegLines(FIELDCAPTION("Correspondence Type (Default)"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Correspondence Type (Default);
                                                              ESM=Tipo correspondencia (Gen‚r.);
                                                              FRC=Type correspondance (par d‚f.);
                                                              ENC=Correspondence Type (Default)];
                                                   OptionCaptionML=[ENU=" ,Hard Copy,E-Mail,Fax";
                                                                    ESM=" ,Impresi¢n,Correo electr¢nico,Fax";
                                                                    FRC=" ,Copie papier,Courriel,T‚l‚copie";
                                                                    ENC=" ,Hard Copy,E-Mail,Fax"];
                                                   OptionString=[ ,Hard Copy,E-Mail,Fax] }
    { 6   ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Interaction Template Code"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Interaction Template Code;
                                                              ESM=C¢d. plantilla interacci¢n;
                                                              FRC=Code modŠle interaction;
                                                              ENC=Interaction Template Code] }
    { 7   ;   ;Unit Cost (LCY)     ;Decimal       ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Unit Cost (LCY)"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Unit Cost ($);
                                                              ESM=Costo unitario ($);
                                                              FRC=Co–t unitaire ($);
                                                              ENC=Unit Cost ($)];
                                                   MinValue=0;
                                                   AutoFormatType=1 }
    { 8   ;   ;Unit Duration (Min.);Decimal       ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Unit Duration (Min.)"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Unit Duration (Min.);
                                                              ESM=Duraci¢n unitaria (Min.);
                                                              FRC=Dur‚e unitaire (min.);
                                                              ENC=Unit Duration (Min.)];
                                                   DecimalPlaces=0:0;
                                                   MinValue=0 }
    { 9   ;   ;Attachment No.      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Segment Interaction Language"."Attachment No." WHERE (Segment No.=FIELD(No.),
                                                                                                                             Segment Line No.=CONST(0),
                                                                                                                             Language Code=FIELD("Language Code (Default)")));
                                                   OnValidate=VAR
                                                                Attachment@1000 : Record 5062;
                                                              BEGIN
                                                                UpdateSegLines(Attachment.TABLECAPTION,CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Attachment No.;
                                                              ESM=N§ anexo;
                                                              FRC=Nø piŠce jointe;
                                                              ENC=Attachment No.] }
    { 10  ;   ;Date                ;Date          ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION(Date),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Date;
                                                              ESM=Fecha;
                                                              FRC=Date;
                                                              ENC=Date] }
    { 11  ;   ;Campaign Target     ;Boolean       ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Campaign Target"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Campaign Target;
                                                              ESM=Objetivo campa¤a;
                                                              FRC=Cible promotion;
                                                              ENC=Campaign Target] }
    { 12  ;   ;Information Flow    ;Option        ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Information Flow"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Information Flow;
                                                              ESM=Flujo informaci¢n;
                                                              FRC=Flux d'informations;
                                                              ENC=Information Flow];
                                                   OptionCaptionML=[ENU=" ,Outbound,Inbound";
                                                                    ESM=" ,Salida,Entrada";
                                                                    FRC=" ,Sortant,Entrant";
                                                                    ENC=" ,Outbound,Inbound"];
                                                   OptionString=[ ,Outbound,Inbound];
                                                   BlankZero=Yes }
    { 13  ;   ;Initiated By        ;Option        ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Initiated By"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Initiated By;
                                                              ESM=Iniciado por;
                                                              FRC=Initi‚ par;
                                                              ENC=Initiated By];
                                                   OptionCaptionML=[ENU=" ,Us,Them";
                                                                    ESM=" ,Nosotros,Ellos";
                                                                    FRC=" ,Notre soci‚t‚,Le contact";
                                                                    ENC=" ,Us,Them"];
                                                   OptionString=[ ,Us,Them];
                                                   BlankZero=Yes }
    { 14  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series];
                                                   Editable=No }
    { 15  ;   ;Campaign Response   ;Boolean       ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Campaign Response"),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Campaign Response;
                                                              ESM=Respuesta campa¤a;
                                                              FRC=R‚ponse promotion;
                                                              ENC=Campaign Response] }
    { 16  ;   ;No. of Lines        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Segment Line" WHERE (Segment No.=FIELD(No.)));
                                                   CaptionML=[ENU=No. of Lines;
                                                              ESM=N§ l¡neas;
                                                              FRC=Nombre de lignes;
                                                              ENC=No. of Lines];
                                                   Editable=No }
    { 17  ;   ;Cost (LCY)          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Segment Line"."Cost (LCY)" WHERE (Segment No.=FIELD(No.)));
                                                   CaptionML=[ENU=Cost ($);
                                                              ESM=Costo ($);
                                                              FRC=Co–t ($);
                                                              ENC=Cost ($)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 18  ;   ;Duration (Min.)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Segment Line"."Duration (Min.)" WHERE (Segment No.=FIELD(No.)));
                                                   CaptionML=[ENU=Duration (Min.);
                                                              ESM=Duraci¢n (Min.);
                                                              FRC=Dur‚e (min.);
                                                              ENC=Duration (Min.)];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 19  ;   ;Language Code (Default);Code10     ;TableRelation=Language;
                                                   OnValidate=VAR
                                                                SegInteractLanguage@1001 : Record 5104;
                                                              BEGIN
                                                                IF "Language Code (Default)" = xRec."Language Code (Default)" THEN
                                                                  EXIT;

                                                                IF NOT SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN BEGIN
                                                                  "Subject (Default)" := '';
                                                                  IF CONFIRM(Text010,TRUE,SegInteractLanguage.TABLECAPTION,"Language Code (Default)") THEN BEGIN
                                                                    SegInteractLanguage.INIT;
                                                                    SegInteractLanguage."Segment No." := "No.";
                                                                    SegInteractLanguage."Segment Line No." := 0;
                                                                    SegInteractLanguage."Language Code" := "Language Code (Default)";
                                                                    SegInteractLanguage.Description := FORMAT("Interaction Template Code") + ' ' + FORMAT("Language Code (Default)");
                                                                    SegInteractLanguage.INSERT(TRUE);
                                                                  END ELSE
                                                                    ERROR('');
                                                                END ELSE
                                                                  "Subject (Default)" := SegInteractLanguage.Subject;
                                                              END;

                                                   OnLookup=VAR
                                                              SegInteractLanguage@1000 : Record 5104;
                                                            BEGIN
                                                              MODIFY;
                                                              COMMIT;

                                                              SegInteractLanguage.SETRANGE("Segment No.","No.");
                                                              SegInteractLanguage.SETRANGE("Segment Line No.",0);
                                                              IF "Language Code (Default)" <> '' THEN
                                                                SegInteractLanguage.GET("No.",0,"Language Code (Default)");
                                                              IF PAGE.RUNMODAL(0,SegInteractLanguage) = ACTION::LookupOK THEN BEGIN
                                                                GET("No.");
                                                                "Language Code (Default)" := SegInteractLanguage."Language Code";
                                                                "Subject (Default)" := SegInteractLanguage.Subject;
                                                                MODIFY;
                                                              END ELSE
                                                                GET("No.");
                                                              CALCFIELDS("Attachment No.");
                                                            END;

                                                   CaptionML=[ENU=Language Code (Default);
                                                              ESM=C¢d. idioma (Gen‚rico);
                                                              FRC=Code langue (par d‚faut);
                                                              ENC=Language Code (Default)] }
    { 20  ;   ;Interaction Group Code;Code10      ;TableRelation="Interaction Group";
                                                   CaptionML=[ENU=Interaction Group Code;
                                                              ESM=C¢d. grupo interacci¢n;
                                                              FRC=Code groupe interactions;
                                                              ENC=Interaction Group Code] }
    { 21  ;   ;No. of Criteria Actions;Integer    ;FieldClass=FlowField;
                                                   CalcFormula=Count("Segment Criteria Line" WHERE (Segment No.=FIELD(No.),
                                                                                                    Type=CONST(Action)));
                                                   CaptionML=[ENU=No. of Criteria Actions;
                                                              ESM=N§ acciones de criterio;
                                                              FRC=Nbre d'actions du critŠre;
                                                              ENC=No. of Criteria Actions];
                                                   Editable=No }
    { 22  ;   ;Send Word Docs. as Attmt.;Boolean  ;OnValidate=BEGIN
                                                                UpdateSegLines(FIELDCAPTION("Send Word Docs. as Attmt."),CurrFieldNo <> 0);
                                                              END;

                                                   CaptionML=[ENU=Send Word Docs. as Attmt.;
                                                              ESM=Env¡a docs. Word como anexo;
                                                              FRC=Envoyer doc Word attach‚s;
                                                              ENC=Send Word Docs. as Attmt.] }
    { 23  ;   ;Ignore Contact Corres. Type;Boolean;CaptionML=[ENU=Ignore Contact Corres. Type;
                                                              ESM=Ignorar tipo corresp. contacto;
                                                              FRC=Ignorer type corresp. contact;
                                                              ENC=Ignore Contact Corres. Type] }
    { 24  ;   ;Subject (Default)   ;Text50        ;OnValidate=VAR
                                                                SegInteractLanguage@1000 : Record 5104;
                                                                UpdateLines@1001 : Boolean;
                                                              BEGIN
                                                                IF SegLinesExist(FIELDCAPTION("Subject (Default)")) THEN
                                                                  UpdateLines := CONFIRM(STRSUBSTNO(Text011,FIELDCAPTION("Subject (Default)")),TRUE);

                                                                IF SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN BEGIN
                                                                  SegInteractLanguage.Subject := "Subject (Default)";
                                                                  SegInteractLanguage.MODIFY;
                                                                  MODIFY;
                                                                END;

                                                                IF NOT UpdateLines THEN
                                                                  EXIT;

                                                                SegLine.SETRANGE("Segment No.","No.");
                                                                SegLine.SETRANGE("Interaction Template Code","Interaction Template Code");
                                                                SegLine.SETRANGE("Language Code","Language Code (Default)");
                                                                SegLine.SETRANGE(Subject,xRec."Subject (Default)");
                                                                SegLine.MODIFYALL(Subject,"Subject (Default)");
                                                              END;

                                                   CaptionML=[ENU=Subject (Default);
                                                              ESM=Asunto (Gen‚rico);
                                                              FRC=Sujet (par d‚faut);
                                                              ENC=Subject (Default)] }
    { 25  ;   ;Campaign Description;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Campaign.Description WHERE (No.=FIELD(Campaign No.)));
                                                   CaptionML=[ENU=Campaign Description;
                                                              ESM=Descripci¢n campa¤a;
                                                              FRC=D‚signation promotion;
                                                              ENC=Campaign Description];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Salesperson Code                         }
    {    ;Campaign No.                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Campaign No.             }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU="%1 = %2 can not be specified for %3 %4.\";ESM="%1 = %2 no puede ser especificado para %3 %4.\";FRC="%1 = %2 ne peut pas ˆtre sp‚cifi‚ pour %3 %4.\";ENC="%1 = %2 can not be specified for %3 %4.\"';
      Text002@1002 : TextConst 'ENU=You have modified %1.\\Do you want to update the segment lines where the Interaction Template Code is %2?;ESM=Ha modificado %1.\\¨Desea actualizar las l¡neas de segmento donde C¢d. plantilla interacci¢n es %2?;FRC=Vous avez modifi‚ %1.\\Souhaitez-vous mettre … jour les lignes segment dans lesquelles le code modŠle interaction est %2ÿ?;ENC=You have modified %1.\\Do you want to update the segment lines where the Interaction Template Code is %2?';
      Text003@1003 : TextConst 'ENU=%1 may not be modified without updating lines when inherited attachments exist.;ESM=No se puede modificar %1 sin actualizar l¡neas cuando existen anexos.;FRC=L''enregistrement %1 ne peut pas ˆtre modifi‚ sans mise … jour des lignes lorsqu''il existe des liens avec des piŠces jointes.;ENC=%1 may not be modified without updating lines when inherited attachments exist.';
      Text005@1005 : TextConst 'ENU=Segment %1 already contains %2 %3.\Are you sure you want to reuse a %4?;ESM=El segmento %1 ya contiene %2 %3.\¨Est  seguro de que desea reutilizar un %4?;FRC=Le segment %1 contient d‚j… %2 %3.\Souhaitez-vous vraiment r‚utiliser les enregistrements %4 ?;ENC=Segment %1 already contains %2 %3.\Are you sure you want to reuse a %4?';
      Text006@1006 : TextConst 'ENU=Segment %1 already contains %2 %3.\Are you sure you want to reuse a %4?;ESM=El segmento %1 ya contiene %2 %3.\¨Est  seguro de que desea reutilizar un %4?;FRC=Le segment %1 contient d‚j… %2 %3.\Souhaitez-vous vraiment r‚utiliser les enregistrements %4 ?;ENC=Segment %1 already contains %2 %3.\Are you sure you want to reuse a %4?';
      RMSetup@1010 : Record 5079;
      SegHeader@1011 : Record 5076;
      SegLine@1012 : Record 5077;
      InteractTmpl@1013 : Record 5064;
      NoSeriesMgt@1014 : Codeunit 396;
      SegCriteriaManagement@1015 : Codeunit 5062;
      SegHistMgt@1016 : Codeunit 5061;
      Text010@1007 : TextConst 'ENU=Do you want to create %1 %2?;ESM=¨Confirma que desea crear %1 %2?;FRC=Souhaitez-vous cr‚er %1 %2?;ENC=Do you want to create %1 %2?';
      Text011@1004 : TextConst 'ENU=You have modified %1.\\Do you want to update the corresponding segment lines?;ESM=Ha modificado el %1.\\¨Desea actualizar las l¡neas de segmento correspondientes?;FRC=Vous avez modifi‚ %1.\\Souhaitez-vous mettre … jour les lignes segment correspondantesÿ?;ENC=You have modified %1.\\Do you want to update the corresponding segment lines?';
      Text012@1030 : TextConst '@@@=You have modified Meeting.\\Do you want to apply the Meeting BUS to all segment lines?;ENU=You have modified %1.\\Do you want to apply the %1 %2 to all segment lines?;ESM=Ha modificado el %1.\\¨Desea aplicar %1 %2 a todas las l¡neas de segmento?;FRC=Vous avez modifi‚ %1.\\Souhaitez-vous appliquer le %1 %2 … toutes les lignes segmentÿ?;ENC=You have modified %1.\\Do you want to apply the %1 %2 to all segment lines?';

    PROCEDURE AssistEdit@2(OldSegHeader@1000 : Record 5076) : Boolean;
    BEGIN
      WITH SegHeader DO BEGIN
        SegHeader := Rec;
        RMSetup.GET;
        RMSetup.TESTFIELD("Segment Nos.");
        IF NoSeriesMgt.SelectSeries(RMSetup."Segment Nos.",OldSegHeader."No. Series","No. Series") THEN BEGIN
          RMSetup.GET;
          RMSetup.TESTFIELD("Segment Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := SegHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateSegLines@15(ChangedFieldName@1000 : Text[100];AskQuestion@1001 : Boolean);
    VAR
      Attachment@1003 : Record 5062;
      Question@1002 : Text[260];
    BEGIN
      IF SegLinesExist(ChangedFieldName) THEN BEGIN
        IF AskQuestion THEN BEGIN
          IF ChangedFieldName = FIELDCAPTION("Interaction Template Code") THEN
            Question := STRSUBSTNO(Text012,ChangedFieldName,"Interaction Template Code")
          ELSE
            Question := STRSUBSTNO(Text002,ChangedFieldName,"Interaction Template Code");
          IF NOT CONFIRM(Question,TRUE) THEN BEGIN
            IF ChangedFieldName = FIELDCAPTION("Interaction Template Code") THEN BEGIN
              SegLine.SETRANGE("Segment No.","No.");
              IF SegLine.FIND('-') THEN BEGIN
                REPEAT
                  IF SegLine.AttachmentInherited THEN
                    ERROR(Text003,FIELDCAPTION("Interaction Template Code"));
                UNTIL SegLine.NEXT = 0;
              END
            END;
            UpdateSegHeader("Interaction Template Code",
              ChangedFieldName = FIELDCAPTION("Interaction Template Code"));
            EXIT;
          END;
        END;

        IF ChangedFieldName = FIELDCAPTION("Interaction Template Code") THEN
          UpdateSegHeader("Interaction Template Code",TRUE);

        SegLine.LOCKTABLE;
        SegLine.RESET;
        SegLine.SETRANGE("Segment No.","No.");
        IF ChangedFieldName <> FIELDCAPTION("Interaction Template Code") THEN
          SegLine.SETRANGE("Interaction Template Code","Interaction Template Code");

        IF SegLine.FIND('-') THEN
          REPEAT
            CASE ChangedFieldName OF
              FIELDCAPTION(Description):
                SegLine.VALIDATE(Description,Description);
              FIELDCAPTION("Campaign No."):
                SegLine.VALIDATE("Campaign No.","Campaign No.");
              FIELDCAPTION("Salesperson Code"):
                SegLine.VALIDATE("Salesperson Code","Salesperson Code");
              FIELDCAPTION("Correspondence Type (Default)"):
                SegLine.VALIDATE("Correspondence Type","Correspondence Type (Default)");
              FIELDCAPTION("Interaction Template Code"):
                SegLine.VALIDATE("Interaction Template Code","Interaction Template Code");
              FIELDCAPTION("Unit Cost (LCY)"):
                SegLine.VALIDATE("Cost (LCY)","Unit Cost (LCY)");
              FIELDCAPTION("Unit Duration (Min.)"):
                SegLine.VALIDATE("Duration (Min.)","Unit Duration (Min.)");
              FIELDCAPTION(Date):
                SegLine.VALIDATE(Date,Date);
              FIELDCAPTION("Campaign Target"):
                SegLine.VALIDATE("Campaign Target","Campaign Target");
              FIELDCAPTION("Information Flow"):
                SegLine.VALIDATE("Information Flow","Information Flow");
              FIELDCAPTION("Initiated By"):
                SegLine.VALIDATE("Initiated By","Initiated By");
              FIELDCAPTION("Campaign Response"):
                SegLine.VALIDATE("Campaign Response","Campaign Response");
              FIELDCAPTION("Interaction Group Code"):
                SegLine.VALIDATE("Interaction Group Code","Interaction Group Code");
              FIELDCAPTION("Send Word Docs. as Attmt."):
                SegLine.VALIDATE("Send Word Doc. As Attmt.","Send Word Docs. as Attmt.");
              Attachment.TABLECAPTION:
                SegLine.VALIDATE("Attachment No.","Attachment No.");
            END;
            SegLine.MODIFY(TRUE);
          UNTIL SegLine.NEXT = 0;
      END ELSE
        IF "No." <> '' THEN
          UpdateSegHeader("Interaction Template Code",
            ChangedFieldName = FIELDCAPTION("Interaction Template Code"));
    END;

    PROCEDURE SegLinesExist@3(ChangedFieldName@1000 : Text[100]) : Boolean;
    BEGIN
      SegLine.RESET;
      SegLine.SETRANGE("Segment No.","No.");
      IF ChangedFieldName <> FIELDCAPTION("Interaction Template Code") THEN
        SegLine.SETRANGE("Interaction Template Code","Interaction Template Code");
      EXIT(SegLine.FIND('-'));
    END;

    LOCAL PROCEDURE Wizard@1();
    VAR
      TempSegWizardFilter@1000 : TEMPORARY Record 5096;
    BEGIN
      TempSegWizardFilter.WizardFromSegmentHeader(Rec);
    END;

    PROCEDURE ReuseLogged@7(LoggedSegEntryNo@1000 : Integer);
    VAR
      LoggedSeg@1001 : Record 5075;
      InteractLogEntry@1002 : Record 5065;
      SegLine@1003 : Record 5077;
      NextLineNo@1004 : Integer;
    BEGIN
      IF LoggedSegEntryNo = 0 THEN BEGIN
        CALCFIELDS("No. of Criteria Actions");
        IF "No. of Criteria Actions" <> 0 THEN
          IF NOT CONFIRM(
               Text005,FALSE,
               "No.","No. of Criteria Actions",FIELDCAPTION("No. of Criteria Actions"),LoggedSeg.TABLECAPTION)
          THEN
            EXIT;
        IF PAGE.RUNMODAL(PAGE::"Logged Segments",LoggedSeg) <> ACTION::LookupOK THEN
          EXIT;
      END ELSE
        LoggedSeg.GET(LoggedSegEntryNo);

      SegLine.LOCKTABLE;
      SegLine.SETRANGE("Segment No.","No.");
      IF SegLine.FINDLAST THEN
        NextLineNo := SegLine."Line No.";

      CLEAR(SegCriteriaManagement);
      SegCriteriaManagement.InsertReuseLogged("No.",LoggedSeg."Entry No.");

      InteractLogEntry.SETCURRENTKEY("Logged Segment Entry No.");
      InteractLogEntry.SETRANGE("Logged Segment Entry No.",LoggedSeg."Entry No.");
      IF InteractLogEntry.FIND('-') THEN
        REPEAT
          NextLineNo := NextLineNo + 10000;
          SegLine.INIT;
          SegLine."Segment No." := "No.";
          SegLine."Line No." := NextLineNo;
          SegLine.VALIDATE("Contact No.",InteractLogEntry."Contact No.");
          SegLine."Campaign No." := InteractLogEntry."Campaign No.";
          SegLine.INSERT(TRUE);
          SegHistMgt.InsertLine("No.",SegLine."Contact No.",SegLine."Line No.");
        UNTIL InteractLogEntry.NEXT = 0;
    END;

    PROCEDURE ReuseCriteria@4();
    VAR
      SavedSegCriteria@1000 : Record 5098;
      SavedSegCriteriaLineAction@1001 : Record 5099;
      SavedSegCriteriaLineFilter@1002 : Record 5099;
      Cont@1003 : Record 5050;
      ContProfileAnswer@1004 : Record 5089;
      ContMailingGrp@1005 : Record 5056;
      InteractLogEntry@1006 : Record 5065;
      ContJobResp@1007 : Record 5067;
      ContIndustGrp@1008 : Record 5058;
      ContBusRel@1009 : Record 5054;
      ValueEntry@1010 : Record 5802;
      AddContacts@1011 : Report 5198;
      ReduceContacts@1012 : Report 5197;
      RefineContacts@1013 : Report 5196;
    BEGIN
      CALCFIELDS("No. of Criteria Actions");
      IF "No. of Criteria Actions" <> 0 THEN
        IF NOT CONFIRM(
             Text006,FALSE,
             "No.","No. of Criteria Actions",FIELDCAPTION("No. of Criteria Actions"),SavedSegCriteria.TABLECAPTION)
        THEN
          EXIT;

      IF PAGE.RUNMODAL(PAGE::"Saved Segment Criteria List",SavedSegCriteria) <> ACTION::LookupOK THEN
        EXIT;

      SavedSegCriteriaLineAction.SETRANGE("Segment Criteria Code",SavedSegCriteria.Code);
      SavedSegCriteriaLineAction.SETRANGE(Type,SavedSegCriteriaLineAction.Type::Action);
      IF SavedSegCriteriaLineAction.FIND('-') THEN
        REPEAT
          SegHeader.SETRANGE("No.","No.");
          Cont.RESET;
          ContProfileAnswer.RESET;
          ContMailingGrp.RESET;
          InteractLogEntry.RESET;
          ContJobResp.RESET;
          ContIndustGrp.RESET;
          ContBusRel.RESET;
          ValueEntry.RESET;
          SavedSegCriteriaLineFilter.SETRANGE("Segment Criteria Code",SavedSegCriteria.Code);
          SavedSegCriteriaLineFilter.SETRANGE(
            "Line No.",SavedSegCriteriaLineAction."Line No." + 1,
            SavedSegCriteriaLineAction."Line No." + SavedSegCriteriaLineAction."No. of Filters");
          IF SavedSegCriteriaLineFilter.FIND('-') THEN
            REPEAT
              CASE SavedSegCriteriaLineFilter."Table No." OF
                DATABASE::Contact:
                  Cont.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Contact Profile Answer":
                  ContProfileAnswer.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Contact Mailing Group":
                  ContMailingGrp.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Interaction Log Entry":
                  InteractLogEntry.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Contact Job Responsibility":
                  ContJobResp.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Contact Industry Group":
                  ContIndustGrp.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Contact Business Relation":
                  ContBusRel.SETVIEW(SavedSegCriteriaLineFilter.View);
                DATABASE::"Value Entry":
                  ValueEntry.SETVIEW(SavedSegCriteriaLineFilter.View);
              END;
            UNTIL SavedSegCriteriaLineFilter.NEXT = 0;
          CASE SavedSegCriteriaLineAction.Action OF
            SavedSegCriteriaLineAction.Action::"Add Contacts":
              BEGIN
                CLEAR(AddContacts);
                AddContacts.SETTABLEVIEW(SegHeader);
                AddContacts.SETTABLEVIEW(Cont);
                AddContacts.SETTABLEVIEW(ContProfileAnswer);
                AddContacts.SETTABLEVIEW(ContMailingGrp);
                AddContacts.SETTABLEVIEW(InteractLogEntry);
                AddContacts.SETTABLEVIEW(ContJobResp);
                AddContacts.SETTABLEVIEW(ContIndustGrp);
                AddContacts.SETTABLEVIEW(ContBusRel);
                AddContacts.SETTABLEVIEW(ValueEntry);
                AddContacts.SetOptions(
                  SavedSegCriteriaLineAction."Allow Existing Contacts",
                  SavedSegCriteriaLineAction."Expand Contact",
                  SavedSegCriteriaLineAction."Allow Company with Persons",
                  SavedSegCriteriaLineAction."Ignore Exclusion");
                AddContacts.USEREQUESTPAGE(FALSE);
                AddContacts.RUNMODAL;
              END;
            SavedSegCriteriaLineAction.Action::"Remove Contacts (Reduce)":
              BEGIN
                CLEAR(ReduceContacts);
                ReduceContacts.SETTABLEVIEW(SegHeader);
                ReduceContacts.SETTABLEVIEW(Cont);
                ReduceContacts.SETTABLEVIEW(ContProfileAnswer);
                ReduceContacts.SETTABLEVIEW(ContMailingGrp);
                ReduceContacts.SETTABLEVIEW(InteractLogEntry);
                ReduceContacts.SETTABLEVIEW(ContJobResp);
                ReduceContacts.SETTABLEVIEW(ContIndustGrp);
                ReduceContacts.SETTABLEVIEW(ContBusRel);
                ReduceContacts.SETTABLEVIEW(ValueEntry);
                ReduceContacts.SetOptions(SavedSegCriteriaLineAction."Entire Companies");
                ReduceContacts.USEREQUESTPAGE(FALSE);
                ReduceContacts.RUNMODAL;
              END;
            SavedSegCriteriaLineAction.Action::"Remove Contacts (Refine)":
              BEGIN
                CLEAR(RefineContacts);
                RefineContacts.SETTABLEVIEW(SegHeader);
                RefineContacts.SETTABLEVIEW(Cont);
                RefineContacts.SETTABLEVIEW(ContProfileAnswer);
                RefineContacts.SETTABLEVIEW(ContMailingGrp);
                RefineContacts.SETTABLEVIEW(InteractLogEntry);
                RefineContacts.SETTABLEVIEW(ContJobResp);
                RefineContacts.SETTABLEVIEW(ContIndustGrp);
                RefineContacts.SETTABLEVIEW(ContBusRel);
                RefineContacts.SETTABLEVIEW(ValueEntry);
                ReduceContacts.SetOptions(SavedSegCriteriaLineAction."Entire Companies");
                RefineContacts.USEREQUESTPAGE(FALSE);
                RefineContacts.RUNMODAL;
              END;
          END;
        UNTIL SavedSegCriteriaLineAction.NEXT = 0;
    END;

    PROCEDURE SaveCriteria@5();
    VAR
      SegCriteriaLine@1000 : Record 5097;
      SavedSegCriteria@1001 : Record 5098;
      SavedSegCriteriaLine@1002 : Record 5099;
      SaveSegCriteria@1003 : Page 5142;
      FormAction@1004 : Action;
    BEGIN
      CALCFIELDS("No. of Criteria Actions");
      TESTFIELD("No. of Criteria Actions");
      SaveSegCriteria.RUNMODAL;
      SaveSegCriteria.GetValues(FormAction,SavedSegCriteria.Code,SavedSegCriteria.Description);
      IF FormAction = ACTION::OK THEN BEGIN
        SavedSegCriteria.INSERT(TRUE);
        SegCriteriaLine.SETRANGE("Segment No.","No.");
        SegCriteriaLine.FIND('-');
        REPEAT
          SavedSegCriteriaLine.INIT;
          SavedSegCriteriaLine."Segment Criteria Code" := SavedSegCriteria.Code;
          SavedSegCriteriaLine."Line No." := SegCriteriaLine."Line No.";
          SavedSegCriteriaLine.Action := SegCriteriaLine.Action;
          SavedSegCriteriaLine.Type := SegCriteriaLine.Type;
          SavedSegCriteriaLine."Table No." := SegCriteriaLine."Table No.";
          SavedSegCriteriaLine.View := SegCriteriaLine.View;
          SavedSegCriteriaLine."Allow Existing Contacts" := SegCriteriaLine."Allow Existing Contacts";
          SavedSegCriteriaLine."Expand Contact" := SegCriteriaLine."Expand Contact";
          SavedSegCriteriaLine."Allow Company with Persons" := SegCriteriaLine."Allow Company with Persons";
          SavedSegCriteriaLine."Ignore Exclusion" := SegCriteriaLine."Ignore Exclusion";
          SavedSegCriteriaLine."Entire Companies" := SegCriteriaLine."Entire Companies";
          SavedSegCriteriaLine."No. of Filters" := SegCriteriaLine."No. of Filters";
          SavedSegCriteriaLine.INSERT;
        UNTIL SegCriteriaLine.NEXT = 0;
      END;
    END;

    PROCEDURE MaintainAttachment@13();
    BEGIN
      IF "Interaction Template Code" = '' THEN
        EXIT;

      IF "Attachment No." <> 0 THEN
        OpenAttachment
      ELSE BEGIN
        CreateAttachment;
        CALCFIELDS("Attachment No.");
      END;
    END;

    PROCEDURE CreateAttachment@12();
    VAR
      SegInteractLanguage@1000 : Record 5104;
    BEGIN
      IF NOT SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN BEGIN
        SegInteractLanguage.INIT;
        SegInteractLanguage."Segment No." := "No.";
        SegInteractLanguage."Segment Line No." := 0;
        SegInteractLanguage."Language Code" := "Language Code (Default)";
        SegInteractLanguage.Description := FORMAT("Interaction Template Code") + ' ' + FORMAT("Language Code (Default)");
        SegInteractLanguage.Subject := "Subject (Default)";
      END;
      SegInteractLanguage.CreateAttachment;
    END;

    PROCEDURE OpenAttachment@6();
    VAR
      SegInteractLanguage@1001 : Record 5104;
    BEGIN
      IF SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN
        IF SegInteractLanguage."Attachment No." <> 0 THEN
          SegInteractLanguage.OpenAttachment;
    END;

    PROCEDURE ImportAttachment@10();
    VAR
      SegInteractLanguage@1003 : Record 5104;
    BEGIN
      IF NOT SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN BEGIN
        SegInteractLanguage.INIT;
        SegInteractLanguage."Segment No." := "No.";
        SegInteractLanguage."Segment Line No." := 0;
        SegInteractLanguage."Language Code" := "Language Code (Default)";
        SegInteractLanguage.Description :=
          FORMAT("Interaction Template Code") + ' ' + FORMAT("Language Code (Default)");
        SegInteractLanguage.INSERT(TRUE);
      END;
      SegInteractLanguage.ImportAttachment;
    END;

    PROCEDURE ExportAttachment@9();
    VAR
      SegInteractLanguage@1000 : Record 5104;
    BEGIN
      IF SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN
        IF SegInteractLanguage."Attachment No." <> 0 THEN
          SegInteractLanguage.ExportAttachment;
    END;

    PROCEDURE RemoveAttachment@8(Prompt@1000 : Boolean);
    VAR
      SegInteractLanguage@1001 : Record 5104;
    BEGIN
      IF SegInteractLanguage.GET("No.",0,"Language Code (Default)") THEN
        IF SegInteractLanguage."Attachment No." <> 0 THEN
          SegInteractLanguage.RemoveAttachment(Prompt);
    END;

    LOCAL PROCEDURE UpdateSegHeader@11(InteractTmplCode@1006 : Code[10];InteractTmplChange@1005 : Boolean);
    VAR
      InteractTemplLanguage@1002 : Record 5103;
      SegInteractLanguage@1001 : Record 5104;
      Attachment@1003 : Record 5062;
      AttachmentManagement@1000 : Codeunit 5052;
    BEGIN
      IF InteractTmplChange THEN BEGIN
        MODIFY;
        SegInteractLanguage.SETRANGE("Segment No.","No.");
        SegInteractLanguage.SETRANGE("Segment Line No.",0);
        SegInteractLanguage.DELETEALL(TRUE);
        GET("No.");
        "Interaction Template Code" := InteractTmplCode;

        IF InteractTmpl.GET("Interaction Template Code") THEN BEGIN
          "Language Code (Default)" := InteractTmpl."Language Code (Default)";
          "Subject (Default)" := '';
          "Interaction Group Code" := InteractTmpl."Interaction Group Code";
          "Unit Cost (LCY)" := InteractTmpl."Unit Cost (LCY)";
          "Unit Duration (Min.)" := InteractTmpl."Unit Duration (Min.)";
          "Information Flow" := InteractTmpl."Information Flow";
          "Initiated By" := InteractTmpl."Initiated By";
          "Campaign Target" := InteractTmpl."Campaign Target";
          "Campaign Response" := InteractTmpl."Campaign Response";
          "Correspondence Type (Default)" := InteractTmpl."Correspondence Type (Default)";
          "Ignore Contact Corres. Type" := InteractTmpl."Ignore Contact Corres. Type";
          IF (GETFILTER("Campaign No.") = '') AND (InteractTmpl."Campaign No." <> '') THEN
            "Campaign No." := InteractTmpl."Campaign No.";

          InteractTemplLanguage.RESET;
          InteractTemplLanguage.SETRANGE("Interaction Template Code","Interaction Template Code");
          IF InteractTemplLanguage.FIND('-') THEN
            REPEAT
              SegInteractLanguage.INIT;
              SegInteractLanguage."Segment No." := "No.";
              SegInteractLanguage."Segment Line No." := 0;
              SegInteractLanguage."Language Code" := InteractTemplLanguage."Language Code";
              SegInteractLanguage.Description := InteractTemplLanguage.Description;
              IF Attachment.GET(InteractTemplLanguage."Attachment No.") THEN
                SegInteractLanguage."Attachment No." := AttachmentManagement.InsertAttachment(InteractTemplLanguage."Attachment No.");
              SegInteractLanguage.INSERT;
            UNTIL InteractTemplLanguage.NEXT = 0;
        END ELSE BEGIN
          "Language Code (Default)" := '';
          "Subject (Default)" := '';
          "Interaction Group Code" := '';
          "Unit Cost (LCY)" := 0;
          "Unit Duration (Min.)" := 0;
          "Information Flow" := "Information Flow"::" ";
          "Initiated By" := "Initiated By"::" ";
          "Campaign Target" := FALSE;
          "Campaign Response" := FALSE;
          "Correspondence Type (Default)" := "Correspondence Type (Default)"::" ";
          "Ignore Contact Corres. Type" := FALSE;
          IF GETFILTER("Campaign No.") = '' THEN
            "Campaign No." := '';
        END;

        MODIFY;
        CALCFIELDS("Attachment No.");
      END;
    END;

    BEGIN
    END.
  }
}

