OBJECT Table 9701 Cue Setup
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Cue Setup;
               ESM=Configuraci¢n de pila;
               FRC=Configuration pile;
               ENC=Cue Setup];
  }
  FIELDS
  {
    { 1   ;   ;User Name           ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User Name");
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User Name;
                                                              ESM=Nombre de usuario;
                                                              FRC=Nom de l'utilisateur;
                                                              ENC=User Name] }
    { 2   ;   ;Table ID            ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Table),
                                                                                                      Object Name=FILTER(*Cue));
                                                   OnValidate=BEGIN
                                                                // Force a calculation, even if the FieldNo hasn't yet been filled out (i.e. the record hasn't been inserted yet)
                                                                CALCFIELDS("Table Name")
                                                              END;

                                                   CaptionML=[ENU=Table ID;
                                                              ESM=Id. tabla;
                                                              FRC=Code table;
                                                              ENC=Table ID] }
    { 3   ;   ;Field No.           ;Integer       ;TableRelation=Field.No.;
                                                   OnLookup=VAR
                                                              Field@1001 : Record 2000000041;
                                                              FieldsLookup@1000 : Page 9806;
                                                              Filter@1002 : Text[250];
                                                            BEGIN
                                                              // Look up in the Fields virtual table
                                                              // Filter on Table No=Table No and Type=Decimal|Integer. This should give us approximately the
                                                              // fields that are "valid" for a cue control.
                                                              Field.SETRANGE(TableNo,"Table ID");
                                                              Filter := FORMAT(Field.Type::Decimal) + '|' + FORMAT(Field.Type::Integer);
                                                              Field.SETFILTER(Type,Filter);
                                                              FieldsLookup.SETTABLEVIEW(Field);
                                                              FieldsLookup.LOOKUPMODE(TRUE);
                                                              IF FieldsLookup.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                FieldsLookup.GETRECORD(Field);
                                                                "Field No." := Field."No.";
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Cue ID;
                                                              ESM=Id. de pila;
                                                              FRC=Code pile;
                                                              ENC=Cue ID] }
    { 4   ;   ;Field Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Table ID),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Cue Name;
                                                              ESM=Nombre de pila;
                                                              FRC=Nom pile;
                                                              ENC=Cue Name] }
    { 5   ;   ;Low Range Style     ;Option        ;CaptionML=[@@@=The Style to use if the cue's value is below Threshold 1;
                                                              ENU=Low Range Style;
                                                              ESM=Estilo de rango bajo;
                                                              FRC=Style bas de gamme;
                                                              ENC=Low Range Style];
                                                   OptionCaptionML=[ENU=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate;
                                                                    ESM=Ninguno,,,,,,,Favorable,Desfavorable,Ambiguo,Subordinado;
                                                                    FRC=Aucun,,,,,,,Favorable,D‚favorable,Ambigu,Subordonn‚;
                                                                    ENC=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate];
                                                   OptionString=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate }
    { 6   ;   ;Threshold 1         ;Decimal       ;OnValidate=BEGIN
                                                                ValidateThresholds;
                                                              END;

                                                   CaptionML=[ENU=Threshold 1;
                                                              ESM=Umbral 1;
                                                              FRC=Seuilÿ1;
                                                              ENC=Threshold 1] }
    { 7   ;   ;Middle Range Style  ;Option        ;CaptionML=[@@@=The Style to use if the cue's value is between Threshold 1 and Threshold 2;
                                                              ENU=Middle Range Style;
                                                              ESM=Estilo de rango medio;
                                                              FRC=Style milieu de gamme;
                                                              ENC=Middle Range Style];
                                                   OptionCaptionML=[ENU=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate;
                                                                    ESM=Ninguno,,,,,,,Favorable,Desfavorable,Ambiguo,Subordinado;
                                                                    FRC=Aucun,,,,,,,Favorable,D‚favorable,Ambigu,Subordonn‚;
                                                                    ENC=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate];
                                                   OptionString=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate }
    { 8   ;   ;Threshold 2         ;Decimal       ;OnValidate=BEGIN
                                                                ValidateThresholds;
                                                              END;

                                                   CaptionML=[ENU=Threshold 2;
                                                              ESM=Umbral 2;
                                                              FRC=Seuilÿ2;
                                                              ENC=Threshold 2] }
    { 9   ;   ;High Range Style    ;Option        ;CaptionML=[@@@=The Style to use if the cue's value is above Threshold 2;
                                                              ENU=High Range Style;
                                                              ESM=Estilo de rango alto;
                                                              FRC=Style haut de gamme;
                                                              ENC=High Range Style];
                                                   OptionCaptionML=[ENU=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate;
                                                                    ESM=Ninguno,,,,,,,Favorable,Desfavorable,Ambiguo,Subordinado;
                                                                    FRC=Aucun,,,,,,,Favorable,D‚favorable,Ambigu,Subordonn‚;
                                                                    ENC=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate];
                                                   OptionString=None,,,,,,,Favorable,Unfavorable,Ambiguous,Subordinate }
    { 10  ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object ID=FIELD(Table ID),
                                                                                                                Object Type=CONST(Table)));
                                                   CaptionML=[ENU=Table Name;
                                                              ESM=Nombre tabla;
                                                              FRC=Nom de la table;
                                                              ENC=Table Name] }
    { 11  ;   ;Personalized        ;Boolean       ;CaptionML=[ENU=Personalized;
                                                              ESM=Personalizado;
                                                              FRC=Personnalis‚;
                                                              ENC=Personalized] }
  }
  KEYS
  {
    {    ;User Name,Table ID,Field No.            ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;Brick               ;Table Name,Field Name,Threshold 1,Personalized,Threshold 2 }
  }
  CODE
  {
    VAR
      TextErr001@1000 : TextConst 'ENU=%1 must be greater than %2.;ESM=%1 debe ser mayor que %2.;FRC=%1 doit ˆtre sup‚rieur … %2.;ENC=%1 must be greater than %2.';

    PROCEDURE ConvertStyleToStyleText@6(Style@1005 : Option) : Text;
    VAR
      RecordRef@1000 : RecordRef;
      FieldRef@1001 : FieldRef;
      StyleIndex@1002 : Integer;
    BEGIN
      RecordRef.GETTABLE(Rec);
      FieldRef := RecordRef.FIELD(FIELDNO("Low Range Style"));

      // Default to return the None Style
      StyleIndex := 1;

      // The style must be in the range of existing style options
      IF (Style > 0) AND (Style <= 10) THEN
        StyleIndex := Style + 1;

      EXIT(SELECTSTR(StyleIndex,FieldRef.OPTIONSTRING));
    END;

    PROCEDURE GetStyleForValue@1(CueValue@1000 : Decimal) : Integer;
    BEGIN
      IF CueValue < "Threshold 1" THEN
        EXIT("Low Range Style");
      IF CueValue > "Threshold 2" THEN
        EXIT("High Range Style");
      EXIT("Middle Range Style");
    END;

    LOCAL PROCEDURE ValidateThresholds@2();
    BEGIN
      IF "Threshold 2" <= "Threshold 1" THEN
        ERROR(
          TextErr001,
          FIELDCAPTION("Threshold 2"),
          FIELDCAPTION("Threshold 1"));
    END;

    BEGIN
    END.
  }
}

