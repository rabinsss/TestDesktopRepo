OBJECT Table 1107 Cost Allocation Target
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               // Default value according to previous line
               "Allocation Target Type" := xRec."Allocation Target Type";
               IF xRec."Target Cost Type" <> '' THEN
                 "Target Cost Type" := xRec."Target Cost Type";

               Base := xRec.Base;
               "Last Date Modified" := TODAY;
               "User ID" := USERID;
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
               "User ID" := USERID;
             END;

    CaptionML=[ENU=Cost Allocation Target;
               ESM=Destino asignaci¢n costos;
               FRC=Cible affectation des coñts;
               ENC=Cost Allocation Target];
    LookupPageID=Page1114;
    DrillDownPageID=Page1114;
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Code10        ;TableRelation="Cost Allocation Source";
                                                   CaptionML=[ENU=ID;
                                                              ESM=Id.;
                                                              FRC=Code;
                                                              ENC=ID];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Line No.;
                                                              ESM=Nß l°nea;
                                                              FRC=N¯ ligne;
                                                              ENC=Line No.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 5   ;   ;Target Cost Type    ;Code20        ;TableRelation="Cost Type";
                                                   CaptionML=[ENU=Target Cost Type;
                                                              ESM=Tipo costo destino;
                                                              FRC=Type coñt cible;
                                                              ENC=Target Cost Type] }
    { 6   ;   ;Target Cost Center  ;Code20        ;TableRelation="Cost Center";
                                                   OnValidate=BEGIN
                                                                IF ("Target Cost Center" <> '') AND ("Target Cost Object" <> '') THEN
                                                                  ERROR(Text000);
                                                              END;

                                                   CaptionML=[ENU=Target Cost Center;
                                                              ESM=Centro costo destino;
                                                              FRC=Centre de coñts cible;
                                                              ENC=Target Cost Centre] }
    { 7   ;   ;Target Cost Object  ;Code20        ;TableRelation="Cost Object";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Target Cost Center")
                                                              END;

                                                   CaptionML=[ENU=Target Cost Object;
                                                              ESM=Objeto costo destino;
                                                              FRC=Objet de coñts cible;
                                                              ENC=Target Cost Object] }
    { 8   ;   ;Static Base         ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Static Weighting");
                                                              END;

                                                   CaptionML=[ENU=Static Base;
                                                              ESM=Base est†tica;
                                                              FRC=Base statique;
                                                              ENC=Static Base];
                                                   DecimalPlaces=0:2;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 9   ;   ;Static Weighting    ;Decimal       ;OnValidate=BEGIN
                                                                IF Base = Base::Static THEN BEGIN
                                                                  Share := "Static Base" * "Static Weighting";
                                                                  VALIDATE(Share);
                                                                END ELSE BEGIN
                                                                  IF "Static Base" > 0 THEN
                                                                    ERROR(Text001,"Static Base");
                                                                  IF "Static Weighting" > 0 THEN
                                                                    ERROR(Text001,"Static Weighting");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Static Weighting;
                                                              ESM=Ponderaci¢n est†tica;
                                                              FRC=PondÇration statique;
                                                              ENC=Static Weighting];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 10  ;   ;Share               ;Decimal       ;OnValidate=BEGIN
                                                                CalcPercent;
                                                              END;

                                                   CaptionML=[ENU=Share;
                                                              ESM=Acci¢n;
                                                              FRC=Action;
                                                              ENC=Share];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0 }
    { 11  ;   ;Percent             ;Decimal       ;CaptionML=[ENU=Percent;
                                                              ESM=Porcentaje;
                                                              FRC=Pour cent;
                                                              ENC=Percent];
                                                   DecimalPlaces=2:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 20  ;   ;Comment             ;Text50        ;CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment] }
    { 30  ;   ;Base                ;Option        ;OnValidate=BEGIN
                                                                IF Base <> xRec.Base THEN BEGIN
                                                                  Share := 0;
                                                                  Percent := 0;
                                                                  "No. Filter" := '';
                                                                  "Group Filter" := '';
                                                                END;

                                                                IF Base = Base::Static THEN BEGIN
                                                                  "No. Filter" := '';
                                                                  "Cost Center Filter" := '';
                                                                  "Cost Object Filter" := '';
                                                                  "Date Filter Code" := 0;
                                                                  "Group Filter" := '';
                                                                END ELSE BEGIN
                                                                  "Static Base" := 0;
                                                                  "Static Weighting" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Base;
                                                              ESM=Base;
                                                              FRC=Base;
                                                              ENC=Base];
                                                   OptionCaptionML=[ENU=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount);
                                                                    ESM=Est†tica,Movs. CG,Movs ppto. CG,Movs. tipo costo,Movs. ppto. costos,,,,,Nß empleados,,Productos vendidos (cdad.),Productos comprados (cdad.),Productos vendidos (importe),Productos comprados(importe);
                                                                    FRC=Static,êcritures,êcritures budget,êcritures type coñt,êcritures budget des coñts,,,,,Nbre employÇs,,Articles vendus (qtÇ),Articles achetÇs (qtÇ),Articles vendus(montant),Articles achetÇs (montant);
                                                                    ENC=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount)];
                                                   OptionString=Static,G/L Entries,G/L Budget Entries,Cost Type Entries,Cost Budget Entries,,,,,No of Employees,,Items Sold (Qty.),Items Purchased (Qty.),Items Sold (Amount),Items Purchased (Amount) }
    { 31  ;   ;No. Filter          ;Text250       ;OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupNoFilter(SelectionFilter) THEN
                                                                VALIDATE("No. Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("No. Filter")));
                                                            END;

                                                   CaptionML=[ENU=No. Filter;
                                                              ESM=Filtro Nß;
                                                              FRC=Filtre n¯;
                                                              ENC=No. Filter] }
    { 32  ;   ;Cost Center Filter  ;Text250       ;TableRelation="Cost Center";
                                                   OnLookup=VAR
                                                              CostCenter@1000 : Record 1112;
                                                              SelectionFilter@1001 : Text[1024];
                                                            BEGIN
                                                              IF CostCenter.LookupCostCenterFilter(SelectionFilter) THEN
                                                                VALIDATE("Cost Center Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Cost Center Filter")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Center Filter;
                                                              ESM=Filtro centro costo;
                                                              FRC=Filtre centre de coñts;
                                                              ENC=Cost Centre Filter] }
    { 33  ;   ;Cost Object Filter  ;Text250       ;TableRelation="Cost Object";
                                                   OnLookup=VAR
                                                              CostObject@1001 : Record 1113;
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF CostObject.LookupCostObjectFilter(SelectionFilter) THEN
                                                                VALIDATE("Cost Object Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Cost Object Filter")));
                                                            END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[ENU=Cost Object Filter;
                                                              ESM=Filtro objeto costo;
                                                              FRC=Filtre objet de coñts;
                                                              ENC=Cost Object Filter] }
    { 34  ;   ;Date Filter Code    ;Option        ;CaptionML=[ENU=Date Filter Code;
                                                              ESM=Filtro fecha vto.;
                                                              FRC=Code filtre date;
                                                              ENC=Date Filter Code];
                                                   OptionCaptionML=[ENU=" ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year";
                                                                    ESM=" ,Semana,Semana pasada,Mes,Mes pasado,Mes del a§o pasado,A§o,A§o pasado,Periodo,Èltimo periodo,Periodo del a§o pasado,Ejercicio,Èltimo ejercicio";
                                                                    FRC=" ,Semaine,Semaine derniäre,Mois,Mois dernier,Mois de la derniäre annÇe,AnnÇe,Derniäre annÇe,PÇriode,Derniäre pÇriode,PÇriode de la derniäre annÇe,Exercice financier,Dernier exercice financier";
                                                                    ENC=" ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year"];
                                                   OptionString=[ ,Week,Last Week,Month,Last Month,Month of Last Year,Year,Last Year,Period,Last Period,Period of Last Year,Fiscal Year,Last Fiscal Year] }
    { 35  ;   ;Group Filter        ;Text250       ;OnLookup=VAR
                                                              SelectionFilter@1000 : Text[1024];
                                                            BEGIN
                                                              IF LookupGroupFilter(SelectionFilter) THEN
                                                                VALIDATE("Group Filter",COPYSTR(SelectionFilter,1,MAXSTRLEN("Group Filter")));
                                                            END;

                                                   CaptionML=[ENU=Group Filter;
                                                              ESM=Filtro grupo;
                                                              FRC=Filtre groupe;
                                                              ENC=Group Filter] }
    { 38  ;   ;Allocation Target Type;Option      ;OnValidate=BEGIN
                                                                IF "Allocation Target Type" <> xRec."Allocation Target Type" THEN BEGIN
                                                                  "Percent per Share" := 0;
                                                                  "Amount per Share" := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allocation Target Type;
                                                              ESM=Tipo destino asignaci¢n;
                                                              FRC=Type cible affectation;
                                                              ENC=Allocation Target Type];
                                                   OptionCaptionML=[ENU=All Costs,Percent per Share,Amount per Share;
                                                                    ESM=Todos los costos,Porcentaje por acci¢n,Importe por acci¢n;
                                                                    FRC=Tous les coñts,Pourcentage par action,Montant par action;
                                                                    ENC=All Costs,Percent per Share,Amount per Share];
                                                   OptionString=All Costs,Percent per Share,Amount per Share }
    { 40  ;   ;Percent per Share   ;Decimal       ;OnValidate=BEGIN
                                                                IF "Percent per Share" <> 0 THEN BEGIN
                                                                  "Allocation Target Type" := "Allocation Target Type"::"Percent per Share";
                                                                  "Amount per Share" := 0;
                                                                END ELSE
                                                                  "Allocation Target Type" := "Allocation Target Type"::"All Costs";
                                                              END;

                                                   CaptionML=[ENU=Percent per Share;
                                                              ESM=Porcentaje por acci¢n;
                                                              FRC=Pourcentage par action;
                                                              ENC=Percent per Share];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 41  ;   ;Amount per Share    ;Decimal       ;OnValidate=BEGIN
                                                                IF "Amount per Share" <> 0 THEN BEGIN
                                                                  "Allocation Target Type" := "Allocation Target Type"::"Amount per Share";
                                                                  "Percent per Share" := 0;
                                                                END ELSE
                                                                  "Allocation Target Type" := "Allocation Target Type"::"All Costs";
                                                              END;

                                                   CaptionML=[ENU=Amount per Share;
                                                              ESM=Importe por acci¢n;
                                                              FRC=Montant par action;
                                                              ENC=Amount per Share];
                                                   MinValue=0;
                                                   BlankZero=Yes }
    { 50  ;   ;Share Updated on    ;Date          ;CaptionML=[ENU=Share Updated on;
                                                              ESM=Acci¢n actualizada el d°a;
                                                              FRC=Action mise Ö jour le;
                                                              ENC=Share Updated on];
                                                   Editable=No }
    { 60  ;   ;Last Date Modified  ;Date          ;CaptionML=[ENU=Last Date Modified;
                                                              ESM=Fecha £lt. modificaci¢n;
                                                              FRC=Date derniäre modification;
                                                              ENC=Last Date Modified];
                                                   Editable=No }
    { 61  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESM=Id. usuario;
                                                              FRC=Code utilisateur;
                                                              ENC=User ID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;ID,Line No.                             ;SumIndexFields=Share,Percent;
                                                   Clustered=Yes }
    {    ;Target Cost Type                         }
    {    ;Target Cost Center                       }
    {    ;Target Cost Object                       }
    {    ;ID,Allocation Target Type                }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot define both cost center and cost object.;ESM=No se puede definir el centro de costo y el objeto de costo.;FRC=Il est impossible de dÇfinir le centre de coñts et les objets de coñts.;ENC=You cannot define both cost centre and cost object.';
      Text001@1001 : TextConst 'ENU=%1 can only be used with static allocations.;ESM=%1 s¢lo se puede usar con asignaciones est†ticas.;FRC=%1 ne peut àtre utilisÇ qu''avec des clÇs de rÇpartition.;ENC=%1 can only be used with static allocations.';

    LOCAL PROCEDURE CalcPercent@1();
    VAR
      CostAllocationTarget@1002 : Record 1107;
      PctTotal@1000 : Decimal;
      TotalShare@1001 : Decimal;
    BEGIN
      WITH CostAllocationTarget DO BEGIN
        SETRANGE(ID,Rec.ID);
        SETFILTER("Line No.",'<>%1',Rec."Line No.");
        CALCSUMS(Share);
        TotalShare := Share + Rec.Share;

        IF TotalShare = 0 THEN BEGIN
          MODIFYALL(Percent,0);
          MODIFYALL("Share Updated on",TODAY);
          Rec.Percent := 0;
          Rec."Share Updated on" := TODAY;
          EXIT;
        END;

        IF FINDSET THEN
          REPEAT
            Percent := ROUND(100 * Share / TotalShare,0.00001);
            "Share Updated on" := TODAY;
            MODIFY;
          UNTIL NEXT = 0;
        Rec.Percent := ROUND(100 * Rec.Share / TotalShare,0.00001);
        Rec."Share Updated on" := TODAY;
        Rec.MODIFY;
        // distribute rounding error:
        CALCSUMS(Percent);
        PctTotal := Percent + Rec.Percent;
        IF PctTotal <> 100 THEN BEGIN
          IF (Rec."Line No." <> 0) AND FINDLAST AND ("Line No." > Rec."Line No.") THEN BEGIN
            Percent := Percent + (100 - PctTotal);
            MODIFY;
          END ELSE
            Rec.Percent := Rec.Percent + (100 - PctTotal);
        END;
      END;
    END;

    LOCAL PROCEDURE LookupNoFilter@2(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      CostType@1001 : Record 1103;
    BEGIN
      CASE Base OF
        Base::"G/L Entries",
        Base::"G/L Budget Entries":
          EXIT(CostType.LookupGLAccFilter(SelectionFilter));
        Base::"Cost Type Entries",
        Base::"Cost Budget Entries":
          EXIT(CostType.LookupCostTypeFilter(SelectionFilter));
        Base::"Items Sold (Qty.)",
        Base::"Items Purchased (Qty.)",
        Base::"Items Sold (Amount)",
        Base::"Items Purchased (Amount)":
          EXIT(LookupItemFilter(SelectionFilter));
      END;
    END;

    LOCAL PROCEDURE LookupItemFilter@14(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      ItemList@1001 : Page 31;
    BEGIN
      ItemList.LOOKUPMODE(TRUE);
      IF ItemList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := ItemList.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupGroupFilter@4(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    BEGIN
      CASE Base OF
        Base::"G/L Budget Entries":
          EXIT(LookupGLBudgetFilter(SelectionFilter));
        Base::"Cost Budget Entries":
          EXIT(LookupCostBudgetFilter(SelectionFilter));
        Base::"Items Sold (Qty.)",
        Base::"Items Purchased (Qty.)",
        Base::"Items Sold (Amount)",
        Base::"Items Purchased (Amount)":
          EXIT(LookupInvtPostingGrFilter(SelectionFilter));
      END;
    END;

    LOCAL PROCEDURE LookupGLBudgetFilter@6(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      GLBudgetNames@1001 : Page 121;
    BEGIN
      GLBudgetNames.LOOKUPMODE(TRUE);
      IF GLBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := GLBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupCostBudgetFilter@7(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      CostBudgetNames@1001 : Page 1116;
    BEGIN
      CostBudgetNames.LOOKUPMODE(TRUE);
      IF CostBudgetNames.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := CostBudgetNames.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    LOCAL PROCEDURE LookupInvtPostingGrFilter@8(VAR SelectionFilter@1000 : Text[1024]) : Boolean;
    VAR
      InvtPostingGroups@1001 : Page 112;
    BEGIN
      InvtPostingGroups.LOOKUPMODE(TRUE);
      IF InvtPostingGroups.RUNMODAL = ACTION::LookupOK THEN BEGIN
        SelectionFilter := InvtPostingGroups.GetSelectionFilter;
        EXIT(TRUE);
      END;
      EXIT(FALSE)
    END;

    BEGIN
    END.
  }
}

