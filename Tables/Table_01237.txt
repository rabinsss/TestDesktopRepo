OBJECT Table 1237 Transformation Rule
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    CaptionML=[ENU=Transformation Rule;
               ESM=Regla de transformaci¢n;
               FRC=Rägle de transformation;
               ENC=Transformation Rule];
    LookupPageID=Page1237;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[ENU=Code;
                                                              ESM=C¢digo;
                                                              FRC=Code;
                                                              ENC=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text100       ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 3   ;   ;Transformation Type ;Option        ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Replace,"Transformation Type"::"Regular Expression"]) THEN BEGIN
                                                                  "Find Value" := '';
                                                                  "Replace Value" := '';
                                                                END;
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Substring]) THEN BEGIN
                                                                  "Start Position" := 0;
                                                                  Length := 0;
                                                                END;
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::"Date and Time Formatting","Transformation Type"::"Decimal Formatting"]) THEN BEGIN
                                                                  "Data Format" := '';
                                                                  "Data Formatting Culture" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Transformation Type;
                                                              ESM=Tipo de transformaci¢n;
                                                              FRC=Type de transformation;
                                                              ENC=Transformation Type];
                                                   OptionCaptionML=[ENU=Uppercase,Lowercase,Title Case,Trim,Substring,Replace,Regular Expression,Remove Non-Alphanumeric Characters,Date and Time Formatting,Decimal Formatting;
                                                                    ESM=May£scula,Min£scula,Tipo t°tulo,Recortar,Subcadena,Reemplazar,Expresi¢n regular,Quitar caracteres no alfanumÇricos,Formato de fecha y hora,Formato decimal;
                                                                    FRC=Majuscule,Minuscule,1reˇlettre des mots en majuscule,Couper,Sous-chaåne,Remplacer,Expression rÇguliäre,Supprimer caractäres non alphanumÇriques,Format de date et heure,Format dÇcimal;
                                                                    ENC=Uppercase,Lowercase,Title Case,Trim,Substring,Replace,Regular Expression,Remove Non-Alphanumeric Characters,Date and Time Formatting,Decimal Formatting];
                                                   OptionString=Uppercase,Lowercase,Title Case,Trim,Substring,Replace,Regular Expression,Remove Non-Alphanumeric Characters,Date and Time Formatting,Decimal Formatting }
    { 10  ;   ;Find Value          ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Replace,"Transformation Type"::"Regular Expression"]) THEN
                                                                  TESTFIELD("Find Value",'');
                                                              END;

                                                   CaptionML=[ENU=Find Value;
                                                              ESM=Buscar valor;
                                                              FRC=Rechercher une valeur;
                                                              ENC=Find Value] }
    { 11  ;   ;Replace Value       ;Text250       ;OnValidate=BEGIN
                                                                IF NOT ("Transformation Type" IN ["Transformation Type"::Replace,"Transformation Type"::"Regular Expression"]) THEN
                                                                  TESTFIELD("Replace Value",'');
                                                              END;

                                                   CaptionML=[ENU=Replace Value;
                                                              ESM=Reemplazar valor;
                                                              FRC=Remplacer une valeur;
                                                              ENC=Replace Value] }
    { 15  ;   ;Start Position      ;Integer       ;OnValidate=BEGIN
                                                                IF "Start Position" <> 0 THEN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::Substring);

                                                                IF "Transformation Type" = "Transformation Type"::Substring THEN
                                                                  IF "Start Position" <= 0 THEN
                                                                    ERROR(MustBeGreaterThanZeroErr);
                                                              END;

                                                   CaptionML=[ENU=Start Position;
                                                              ESM=Posici¢n inicial;
                                                              FRC=Position dÇbut;
                                                              ENC=Start Position];
                                                   BlankZero=Yes }
    { 16  ;   ;Length              ;Integer       ;OnValidate=BEGIN
                                                                IF Length <> 0 THEN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::Substring);

                                                                IF "Transformation Type" = "Transformation Type"::Substring THEN
                                                                  IF Length < 0 THEN
                                                                    ERROR(MustBeGreaterThanZeroErr);
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              ESM=Longitud;
                                                              FRC=Longueur;
                                                              ENC=Length];
                                                   BlankZero=Yes }
    { 18  ;   ;Data Format         ;Text100       ;OnValidate=BEGIN
                                                                IF "Data Format" <> '' THEN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::"Date and Time Formatting");
                                                              END;

                                                   CaptionML=[ENU=Data Format;
                                                              ESM=Formato de datos;
                                                              FRC=Format donnÇes;
                                                              ENC=Data Format] }
    { 20  ;   ;Data Formatting Culture;Text10     ;OnValidate=BEGIN
                                                                IF "Data Format" <> '' THEN
                                                                  TESTFIELD("Transformation Type","Transformation Type"::"Date and Time Formatting");
                                                              END;

                                                   CaptionML=[ENU=Data Formatting Culture;
                                                              ESM=Referencia cultural de formato de datos;
                                                              FRC=Culture mise en forme donnÇes;
                                                              ENC=Data Formatting Culture] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      MustBeGreaterThanZeroErr@1000 : TextConst 'ENU=The Value entered must be greater than zero.;ESM=El valor especificado debe ser mayor que cero.;FRC=La valeur entrÇe doit àtre supÇrieure Ö zÇro.;ENC=The Value entered must be greater than zero.';
      UPPERCASETxt@1001 : TextConst '@@@=Assigned to Transformation.Code field for Upper case;ENU=UPPERCASE;ESM=MAYÈSCULAS;FRC=MAJUSCULE;ENC=UPPERCASE';
      UpperCaseDescTxt@1007 : TextConst 'ENU=Upper Case Text;ESM=Texto en may£sculas;FRC=Texte en majuscule;ENC=Upper Case Text';
      LOWERCASETxt@1002 : TextConst '@@@=Assigned to Transformation.Code field for Lower case;ENU=LOWERCASE;ESM=MINÈSCULAS;FRC=MINUSCULE;ENC=LOWERCASE';
      LowerCaseDescTxt@1008 : TextConst 'ENU=Lower Case Text;ESM=Texto en min£sculas;FRC=Texte en minuscule;ENC=Lower Case Text';
      TITLECASETxt@1003 : TextConst '@@@=Assigned to Transformation.Code field for Title case;ENU=TITLECASE;ESM=TIPO T÷TULO;FRC=1reˇLETTRE MOTS MAJ.;ENC=TITLECASE';
      TitleCaseDescTxt@1009 : TextConst 'ENU=Title Case Text;ESM=Texto tipo t°tulo;FRC=Texte avec la 1re lettre des mots en majuscule;ENC=Title Case Text';
      TRIMTxt@1004 : TextConst '@@@=Assigned to Transformation.Code field for Trim;ENU=TRIM;ESM=RECORTE;FRC=COUPER;ENC=TRIM';
      TrimDescTxt@1010 : TextConst 'ENU=Trim Text;ESM=Recortar texto;FRC=Couper texte;ENC=Trim Text';
      FOURTH_TO_SIXTH_CHARTxt@1005 : TextConst '@@@=Assigned to Transformation.Code field for getting the 4th to 6th characters in a string;ENU=FOURTH_TO_SIXTH_CHAR;ESM=CUATRO_A_SEIS_CARACT;FRC=QUATRE_∑_SIX_CARACT;ENC=FOURTH_TO_SIXTH_CHAR';
      FourthToSixthCharactersDescTxt@1011 : TextConst 'ENU=Fourth to Sixth Characters Text;ESM=Texto con de cuatro a seis caracteres;FRC=Texte de quatre Ö six caractäres;ENC=Fourth to Sixth Characters Text';
      YYYYMMDDDateTxt@1014 : TextConst '@@@=Assigned to Transformation.Code field for converting dates from yyyyMMdd format;ENU=YYYYMMDD_DATE;ESM=FECHA_DDMMAAAA;FRC=DATE_JJMMAAAA;ENC=YYYYMMDD_DATE';
      YYYYMMDDDateDescTxt@1013 : TextConst 'ENU=yyyyMMdd Date Text;ESM=Texto de fecha ddMMaaaa;FRC=Texte date jjMMaaaa;ENC=yyyyMMdd Date Text';
      ALPHANUMERIC_ONLYTxt@1006 : TextConst '@@@=Assigned to Transformation.Code field for getting only the Alphanumeric characters in a string;ENU=ALPHANUMERIC_ONLY;ESM=SOLO_ALFANUMêRICO;FRC=ALPHANUM_UNIQUEMENT;ENC=ALPHANUMERIC_ONLY';
      AlphaNumericDescTxt@1012 : TextConst 'ENU=Alphanumeric Text Only;ESM=Texto solo alfanumÇrico;FRC=Texte alphanumÇrique uniquement;ENC=Alphanumeric Text Only';
      DKNUMBERFORMATTxt@1015 : TextConst '@@@=Assigned to Transformation.Code field for getting decimal formatting rule for Danish numbers;ENU=DK_DECIMAL_FORMAT;ESM=FORMATO_DECIMAL_DK;FRC=FORMAT_DêCIMAL_DK;ENC=DK_DECIMAL_FORMAT';
      DKNUMBERFORMATDescTxt@1016 : TextConst 'ENU=Danish Decimal Format;ESM=Formato decimal danÇs;FRC=Format dÇcimal danois;ENC=Danish Decimal Format';
      USDATEFORMATTxt@1017 : TextConst '@@@=Assigned to Transformation.Code field for getting date formatting rule from U.S. dates;ENU=US_DATE_FORMAT;ESM=US_DATE_FORMAT;FRC=US_DATE_FORMAT;ENC=US_DATE_FORMAT';
      USDATEFORMATDescTxt@1018 : TextConst 'ENU=U.S. Date Format;ESM=Formato de fecha de EE.ˇUU.;FRC=Format de date amÇricain;ENC=U.S. Date Format';

    PROCEDURE CreateDefaultTransformations@3();
    BEGIN
      InsertRec(UPPERCASETxt,UpperCaseDescTxt,"Transformation Type"::Uppercase,0,0,'','');
      InsertRec(LOWERCASETxt,LowerCaseDescTxt,"Transformation Type"::Lowercase,0,0,'','');
      InsertRec(TITLECASETxt,TitleCaseDescTxt,"Transformation Type"::"Title Case",0,0,'','');
      InsertRec(TRIMTxt,TrimDescTxt,"Transformation Type"::Trim,0,0,'','');
      InsertRec(FOURTH_TO_SIXTH_CHARTxt,FourthToSixthCharactersDescTxt,"Transformation Type"::Substring,4,3,'','');
      InsertRec(YYYYMMDDDateTxt,YYYYMMDDDateDescTxt,"Transformation Type"::"Date and Time Formatting",0,0,'yyyyMMdd','');
      InsertRec(ALPHANUMERIC_ONLYTxt,AlphaNumericDescTxt,"Transformation Type"::"Remove Non-Alphanumeric Characters",0,0,'','');
      InsertRec(DKNUMBERFORMATTxt,DKNUMBERFORMATDescTxt,"Transformation Type"::"Decimal Formatting",0,0,'','da-DK');
      InsertRec(USDATEFORMATTxt,USDATEFORMATDescTxt,"Transformation Type"::"Date and Time Formatting",0,0,'','en-US');
    END;

    LOCAL PROCEDURE InsertRec@8(NewCode@1000 : Code[20];NewDescription@1001 : Text[100];NewTransformationType@1002 : Option;NewStartPosition@1003 : Integer;NewLength@1004 : Integer;NewDataFormat@1006 : Text[100];NewDataFormattingCulture@1007 : Text[10]);
    VAR
      TransformationRule@1005 : Record 1237;
    BEGIN
      WITH TransformationRule DO BEGIN
        INIT;
        VALIDATE(Code,NewCode);
        VALIDATE(Description,NewDescription);
        VALIDATE("Transformation Type",NewTransformationType);
        VALIDATE("Start Position",NewStartPosition);
        VALIDATE(Length,NewLength);
        VALIDATE("Data Format",NewDataFormat);
        VALIDATE("Data Formatting Culture",NewDataFormattingCulture);
        IF INSERT(TRUE) THEN;
      END;
    END;

    PROCEDURE TransformText@5(OldValue@1001 : Text) : Text;
    VAR
      NewValue@1002 : Text;
    BEGIN
      NewValue := OldValue;

      CASE "Transformation Type" OF
        "Transformation Type"::Uppercase:
          NewValue := UPPERCASE(OldValue);
        "Transformation Type"::Lowercase:
          NewValue := LOWERCASE(OldValue);
        "Transformation Type"::"Title Case":
          NewValue := TextToTitleCase(OldValue);
        "Transformation Type"::Trim:
          NewValue := DELCHR(OldValue,'<>');
        "Transformation Type"::Substring:
          NewValue := Substring(OldValue);
        "Transformation Type"::Replace:
          NewValue := StringReplace(OldValue,"Find Value","Replace Value");
        "Transformation Type"::"Regular Expression":
          NewValue := RegularExpression(OldValue,"Find Value","Replace Value");
        "Transformation Type"::"Remove Non-Alphanumeric Characters":
          NewValue := RemoveNonAplhaNumericCharacters(OldValue);
        "Transformation Type"::"Date and Time Formatting":
          NewValue := DateTimeFormatting(OldValue);
        "Transformation Type"::"Decimal Formatting":
          NewValue := DecimalFormatting(OldValue);
      END;

      EXIT(NewValue);
    END;

    LOCAL PROCEDURE TextToTitleCase@17(OldValue@1001 : Text) : Text;
    VAR
      CultureInfo@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
    BEGIN
      CultureInfo := CultureInfo.GetCultureInfo("Data Formatting Culture");
      EXIT(CultureInfo.TextInfo.ToTitleCase(OldValue));
    END;

    LOCAL PROCEDURE StringReplace@4(StringToReplace@1000 : Text;OldValue@1001 : Text;NewValue@1002 : Text) : Text;
    VAR
      DotNetString@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      NewString@1004 : Text;
    BEGIN
      DotNetString := StringToReplace;
      NewString := DotNetString.Replace(OldValue,NewValue);
      EXIT(NewString);
    END;

    LOCAL PROCEDURE RegularExpression@2(StringToReplace@1000 : Text;Pattern@1001 : Text;NewValue@1002 : Text) : Text;
    VAR
      Regex@1003 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.Regex";
      RegexOptions@1005 : DotNet "'System, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.RegularExpressions.RegexOptions";
      NewString@1004 : Text;
    BEGIN
      Regex := Regex.Regex(Pattern,RegexOptions.IgnoreCase);
      NewString := Regex.Replace(StringToReplace,NewValue);
      EXIT(NewString);
    END;

    LOCAL PROCEDURE RemoveNonAplhaNumericCharacters@19(OldValue@1003 : Text) : Text;
    VAR
      DotNetChar@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Char";
      NewValue@1001 : Text;
      I@1000 : Integer;
    BEGIN
      NewValue := '';
      FOR I := 1 TO STRLEN(OldValue) DO BEGIN
        IF DotNetChar.IsLetterOrDigit(OldValue[I]) THEN
          NewValue += FORMAT(OldValue[I]);
      END;

      EXIT(NewValue);
    END;

    LOCAL PROCEDURE DateTimeFormatting@20(OldValue@1001 : Text) : Text;
    VAR
      DotNetDateTime@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.DateTime";
      CultureInfo@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
      DotNetDateTimeStyles@1005 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.DateTimeStyles";
      NewValue@1003 : Text;
    BEGIN
      NewValue := OldValue;

      DotNetDateTime := DotNetDateTime.DateTime(1);
      IF "Data Formatting Culture" = '' THEN BEGIN
        CultureInfo := CultureInfo.InvariantCulture;
        IF NOT DotNetDateTime.TryParseExact(
             OldValue,
             "Data Format",
             CultureInfo,
             DotNetDateTimeStyles.None,
             DotNetDateTime)
        THEN
          EXIT(NewValue);
      END ELSE BEGIN
        CultureInfo := CultureInfo.GetCultureInfo("Data Formatting Culture");
        IF NOT DotNetDateTime.TryParse(
             OldValue,
             CultureInfo,
             DotNetDateTimeStyles.None,
             DotNetDateTime)
        THEN
          EXIT(NewValue);
      END;

      NewValue := DotNetDateTime.ToString('yyyy-MM-dd',CultureInfo);
      EXIT(NewValue);
    END;

    LOCAL PROCEDURE DecimalFormatting@14(OldValue@1001 : Text) : Text;
    VAR
      TypeHelper@1006 : Codeunit 10;
      NewDecimalVariant@1000 : Variant;
      NewValue@1003 : Text;
      DummyDecimal@1007 : Decimal;
    BEGIN
      NewValue := OldValue;
      NewDecimalVariant := DummyDecimal;
      TypeHelper.Evaluate(NewDecimalVariant,OldValue,'',"Data Formatting Culture");

      NewValue := FORMAT(NewDecimalVariant,0,9);
      EXIT(NewValue);
    END;

    LOCAL PROCEDURE Substring@7(OldValue@1000 : Text) : Text;
    VAR
      StartPosition@1002 : Integer;
      NewLength@1003 : Integer;
    BEGIN
      StartPosition := "Start Position";
      NewLength := Length;

      IF StartPosition = 0 THEN
        StartPosition := 1;

      IF Length = 0 THEN
        NewLength := STRLEN(OldValue);

      EXIT(COPYSTR(OldValue,StartPosition,NewLength));
    END;

    PROCEDURE GetFourthToSixthSubstringCode@6() : Code[20];
    BEGIN
      EXIT(FOURTH_TO_SIXTH_CHARTxt);
    END;

    PROCEDURE GetUSDateFormatCode@1() : Code[20];
    BEGIN
      EXIT(USDATEFORMATTxt);
    END;

    PROCEDURE GetUppercaseCode@10() : Code[20];
    BEGIN
      EXIT(UPPERCASETxt);
    END;

    PROCEDURE GetLowercaseCode@9() : Code[20];
    BEGIN
      EXIT(LOWERCASETxt);
    END;

    PROCEDURE GetTitlecaseCode@12() : Code[20];
    BEGIN
      EXIT(TITLECASETxt);
    END;

    PROCEDURE GetTrimCode@11() : Code[20];
    BEGIN
      EXIT(TRIMTxt);
    END;

    PROCEDURE GetAlphanumericCode@13() : Code[20];
    BEGIN
      EXIT(ALPHANUMERIC_ONLYTxt);
    END;

    PROCEDURE GetDanishDecimalFormatCode@15() : Code[20];
    BEGIN
      EXIT(DKNUMBERFORMATTxt);
    END;

    PROCEDURE GetYYYYMMDDCode@16() : Code[20];
    BEGIN
      EXIT(YYYYMMDDDateTxt);
    END;

    BEGIN
    END.
  }
}

