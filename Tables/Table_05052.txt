OBJECT Table 5052 Contact Alt. Addr. Date Range
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TouchContact("Contact No.");
             END;

    OnModify=BEGIN
               TouchContact("Contact No.");
             END;

    OnDelete=BEGIN
               TouchContact("Contact No.");
             END;

    OnRename=BEGIN
               IF xRec."Contact No." = "Contact No." THEN
                 TouchContact("Contact No.")
               ELSE BEGIN
                 TouchContact("Contact No.");
                 TouchContact(xRec."Contact No.");
               END;
             END;

    CaptionML=[ENU=Contact Alt. Addr. Date Range;
               ESM=Rango fecha direcc. alt. cont.;
               FRC=Date adresse sec. contact;
               ENC=Contact Alt. Addr. Date Range];
  }
  FIELDS
  {
    { 1   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   CaptionML=[ENU=Contact No.;
                                                              ESM=N§ contacto;
                                                              FRC=Nø contact;
                                                              ENC=Contact No.];
                                                   Editable=No }
    { 2   ;   ;Contact Alt. Address Code;Code10   ;TableRelation="Contact Alt. Address".Code WHERE (Contact No.=FIELD(Contact No.));
                                                   CaptionML=[ENU=Contact Alt. Address Code;
                                                              ESM=C¢d. direcci¢n alt. contacto;
                                                              FRC=Code adr. secondaire contact;
                                                              ENC=Contact Alt. Address Code] }
    { 3   ;   ;Starting Date       ;Date          ;OnValidate=VAR
                                                                ContAltAddrDateRange@1000 : Record 5052;
                                                              BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" > 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
                                                                IF ContAltAddrDateRange.GET("Contact No.","Starting Date") THEN
                                                                  ERROR(Text001
                                                                    ,"Starting Date",TABLECAPTION,Cont.TABLECAPTION,"Contact No.");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              ESM=Fecha inicial;
                                                              FRC=Date d‚but;
                                                              ENC=Starting Date];
                                                   NotBlank=Yes }
    { 4   ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF ("Ending Date" < "Starting Date") AND ("Ending Date" > 0D) THEN
                                                                  ERROR(Text002,FIELDCAPTION("Ending Date"),FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              ESM=Fecha final;
                                                              FRC=Date fin;
                                                              ENC=Ending Date] }
  }
  KEYS
  {
    {    ;Contact No.,Starting Date               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be before %2.;ESM=%1 debe ser anterior a %2.;FRC=%1 doit ˆtre ant‚rieure … %2.;ENC=%1 must be before %2.';
      Text001@1001 : TextConst 'ENU=The starting date %1 already exists in a %2 for %3 %4.;ESM=La fecha inicio %1 ya existe en un %2 para %3 %4.;FRC=La date de d‚but %1 existe d‚j… dans un(e) %2 pour %3 %4.;ENC=The starting date %1 already exists in a %2 for %3 %4.';
      Text002@1002 : TextConst 'ENU=%1 must be after %2.;ESM=%1 debe ser posterior %2.;FRC=%1 doit ˆtre post‚rieur(e) … %2.;ENC=%1 must be after %2.';
      Cont@1003 : Record 5050;

    LOCAL PROCEDURE TouchContact@1(ContactNo@1000 : Code[20]);
    BEGIN
      Cont.LOCKTABLE;
      Cont.GET(ContactNo);
      Cont."Last Date Modified" := TODAY;
      Cont.MODIFY;
    END;

    BEGIN
    END.
  }
}

