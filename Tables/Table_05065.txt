OBJECT Table 5065 Interaction Log Entry
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               InteractionCommentLine@1000 : Record 5123;
               CampaignMgt@1001 : Codeunit 7030;
             BEGIN
               InteractionCommentLine.SETRANGE("Entry No.","Entry No.");
               InteractionCommentLine.DELETEALL;

               CampaignMgt.DeleteContfromTargetGr(Rec);
             END;

    CaptionML=[ENU=Interaction Log Entry;
               ESM=Mov. log de interacci¢n;
               FRC=êcriture du journal d'interaction;
               ENC=Interaction Log Entry];
    LookupPageID=Page5076;
    DrillDownPageID=Page5076;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ESM=Nß mov.;
                                                              FRC=N¯ Çcriture;
                                                              ENC=Entry No.] }
    { 2   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   CaptionML=[ENU=Contact No.;
                                                              ESM=Nß contacto;
                                                              FRC=N¯ contact;
                                                              ENC=Contact No.] }
    { 3   ;   ;Contact Company No. ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=[ENU=Contact Company No.;
                                                              ESM=Nß empresa contacto;
                                                              FRC=N¯ compagnie contact;
                                                              ENC=Contact Company No.] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[ENU=Date;
                                                              ESM=Fecha;
                                                              FRC=Date;
                                                              ENC=Date] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 6   ;   ;Information Flow    ;Option        ;CaptionML=[ENU=Information Flow;
                                                              ESM=Flujo informaci¢n;
                                                              FRC=Flux d'informations;
                                                              ENC=Information Flow];
                                                   OptionCaptionML=[ENU=" ,Outbound,Inbound";
                                                                    ESM=" ,Salida,Entrada";
                                                                    FRC=" ,Sortant,Entrant";
                                                                    ENC=" ,Outbound,Inbound"];
                                                   OptionString=[ ,Outbound,Inbound] }
    { 7   ;   ;Initiated By        ;Option        ;CaptionML=[ENU=Initiated By;
                                                              ESM=Iniciado por;
                                                              FRC=InitiÇ par;
                                                              ENC=Initiated By];
                                                   OptionCaptionML=[ENU=" ,Us,Them";
                                                                    ESM=" ,Nosotros,Ellos";
                                                                    FRC=" ,Notre sociÇtÇ,Le contact";
                                                                    ENC=" ,Us,Them"];
                                                   OptionString=[ ,Us,Them] }
    { 8   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=[ENU=Attachment No.;
                                                              ESM=Nß anexo;
                                                              FRC=N¯ piäce jointe;
                                                              ENC=Attachment No.] }
    { 9   ;   ;Cost (LCY)          ;Decimal       ;CaptionML=[ENU=Cost ($);
                                                              ESM=Costo ($);
                                                              FRC=Coñt ($);
                                                              ENC=Cost ($)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 10  ;   ;Duration (Min.)     ;Decimal       ;CaptionML=[ENU=Duration (Min.);
                                                              ESM=Duraci¢n (Min.);
                                                              FRC=DurÇe (min.);
                                                              ENC=Duration (Min.)];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 11  ;   ;User ID             ;Code50        ;TableRelation=User."User Name";
                                                   OnLookup=VAR
                                                              UserMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              UserMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=User ID;
                                                              ESM=Id. usuario;
                                                              FRC=Code utilisateur;
                                                              ENC=User ID] }
    { 12  ;   ;Interaction Group Code;Code10      ;TableRelation="Interaction Group";
                                                   CaptionML=[ENU=Interaction Group Code;
                                                              ESM=C¢d. grupo interacci¢n;
                                                              FRC=Code groupe interactions;
                                                              ENC=Interaction Group Code] }
    { 13  ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              ESM=C¢d. plantilla interacci¢n;
                                                              FRC=Code modäle interaction;
                                                              ENC=Interaction Template Code] }
    { 14  ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=[ENU=Campaign No.;
                                                              ESM=Nß campa§a;
                                                              FRC=N¯ promotion;
                                                              ENC=Campaign No.] }
    { 15  ;   ;Campaign Entry No.  ;Integer       ;TableRelation="Campaign Entry" WHERE (Campaign No.=FIELD(Campaign No.));
                                                   CaptionML=[ENU=Campaign Entry No.;
                                                              ESM=Nß mov. campa§a;
                                                              FRC=N¯ ligne promotion;
                                                              ENC=Campaign Entry No.] }
    { 16  ;   ;Campaign Response   ;Boolean       ;CaptionML=[ENU=Campaign Response;
                                                              ESM=Respuesta campa§a;
                                                              FRC=RÇponse promotion;
                                                              ENC=Campaign Response] }
    { 17  ;   ;Campaign Target     ;Boolean       ;CaptionML=[ENU=Campaign Target;
                                                              ESM=Objetivo campa§a;
                                                              FRC=Cible promotion;
                                                              ENC=Campaign Target] }
    { 18  ;   ;Segment No.         ;Code20        ;CaptionML=[ENU=Segment No.;
                                                              ESM=Nß segmento;
                                                              FRC=N¯ segment;
                                                              ENC=Segment No.] }
    { 19  ;   ;Evaluation          ;Option        ;CaptionML=[ENU=Evaluation;
                                                              ESM=Evaluaci¢n;
                                                              FRC=êvaluation;
                                                              ENC=Evaluation];
                                                   OptionCaptionML=[ENU=" ,Very Positive,Positive,Neutral,Negative,Very Negative";
                                                                    ESM=" ,Muy positiva,Positiva,Neutra,Negativa,Muy negativa";
                                                                    FRC=" ,Träs positif,Positif,Neutre,NÇgatif,Träs NÇgatif";
                                                                    ENC=" ,Very Positive,Positive,Neutral,Negative,Very Negative"];
                                                   OptionString=[ ,Very Positive,Positive,Neutral,Negative,Very Negative];
                                                   Editable=No }
    { 20  ;   ;Time of Interaction ;Time          ;CaptionML=[ENU=Time of Interaction;
                                                              ESM=Hora de interacci¢n;
                                                              FRC=Heure interaction;
                                                              ENC=Time of Interaction] }
    { 21  ;   ;Attempt Failed      ;Boolean       ;CaptionML=[ENU=Attempt Failed;
                                                              ESM=Intento fallido;
                                                              FRC=Echec de la tentative;
                                                              ENC=Attempt Failed] }
    { 23  ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[ENU=To-do No.;
                                                              ESM=Nß tarea;
                                                              FRC=N¯ tÉche;
                                                              ENC=To-do No.] }
    { 24  ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[ENU=Salesperson Code;
                                                              ESM=C¢d. vendedor;
                                                              FRC=Code reprÇsentant;
                                                              ENC=Salesperson Code] }
    { 25  ;   ;Delivery Status     ;Option        ;CaptionML=[ENU=Delivery Status;
                                                              ESM=Estado entrega;
                                                              FRC=êtat d'envoi;
                                                              ENC=Delivery Status];
                                                   OptionCaptionML=[ENU=" ,In Progress,Error";
                                                                    ESM=" ,En progreso,Error";
                                                                    FRC=" ,En cours,Erreur";
                                                                    ENC=" ,In Progress,Error"];
                                                   OptionString=[ ,In Progress,Error] }
    { 26  ;   ;Canceled            ;Boolean       ;CaptionML=[ENU=Canceled;
                                                              ESM=Cancelado;
                                                              FRC=AnnulÇe;
                                                              ENC=Cancelled] }
    { 27  ;   ;Correspondence Type ;Option        ;CaptionML=[ENU=Correspondence Type;
                                                              ESM=Tipo correspondencia;
                                                              FRC=Type correspondance;
                                                              ENC=Correspondence Type];
                                                   OptionCaptionML=[ENU=" ,Hard Copy,E-Mail,Fax";
                                                                    ESM=" ,Impresi¢n,Correo electr¢nico,Fax";
                                                                    FRC=" ,Copie papier,Courriel,TÇlÇcopie";
                                                                    ENC=" ,Hard Copy,E-Mail,Fax"];
                                                   OptionString=[ ,Hard Copy,E-Mail,Fax] }
    { 28  ;   ;Contact Alt. Address Code;Code10   ;TableRelation="Contact Alt. Address".Code WHERE (Contact No.=FIELD(Contact No.));
                                                   CaptionML=[ENU=Contact Alt. Address Code;
                                                              ESM=C¢d. direcci¢n alt. contacto;
                                                              FRC=Code adr. secondaire contact;
                                                              ENC=Contact Alt. Address Code] }
    { 29  ;   ;Logged Segment Entry No.;Integer   ;TableRelation="Logged Segment";
                                                   CaptionML=[ENU=Logged Segment Entry No.;
                                                              ESM=Nß mov. segmento archivado;
                                                              FRC=N¯ Çcriture segment journal;
                                                              ENC=Logged Segment Entry No.] }
    { 30  ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESM=Tipo documento;
                                                              FRC=Type de document;
                                                              ENC=Document Type];
                                                   OptionCaptionML=[ENU=" ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote";
                                                                    ESM=" ,Cotizaci¢n vta.,Ped. abierto vta.,Confirm. ped. vta.,Fact. vta.,Rem. remisi¢n vta.,Nota crÇdito vta.,Est. cta. vta.,Record. vta.,Crear ped. serv.,Regis. ped. serv.,Cotizaci¢n compra,Ped. abierto compra,Ped. compra,Fact. compra,Rem. compra,Nota crÇdito compra,Portada,Devoluci¢n venta,Cargo financiero ventas,Recepci¢n devoluci¢n venta,Env°os devol. compras,Confirm. devol. compra,Contrato de Servicio,Cotizaci¢n Contrato de Servicio,Cotizaci¢n servicio";
                                                                    FRC=" ,Devis de vente,Contrat-cadre de vente,Confirmation de bon de commande,Facture de vente,Note de livraison de vente,Note de crÇdit pour commande,Rel. de vente,Rappel de vente,CrÇer une commande de service,Report de commande de service,QuantitÇ Ö acheter,Contrat-cadre d'achat,Bon de commande,Facture d'achat,Reáu d'achat,Note de crÇdit d'achat,Page couverture,Commande de retour sur ventes,Note de frais financiers de vente,RÇception de retour sur ventes,Livraison de retour sur achats,Confirmation de commande de retour sur achats,Contrat de service,Devis de contrat de service,Devis de service";
                                                                    ENC=" ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote"];
                                                   OptionString=[ ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote] }
    { 31  ;   ;Document No.        ;Code20        ;TestTableRelation=No;
                                                   CaptionML=[ENU=Document No.;
                                                              ESM=Nß documento;
                                                              FRC=N¯ de document;
                                                              ENC=Document No.] }
    { 32  ;   ;Version No.         ;Integer       ;CaptionML=[ENU=Version No.;
                                                              ESM=Nß versi¢n;
                                                              FRC=N¯ version;
                                                              ENC=Version No.] }
    { 33  ;   ;Doc. No. Occurrence ;Integer       ;CaptionML=[ENU=Doc. No. Occurrence;
                                                              ESM=Ocurrencia Nß doc.;
                                                              FRC=Occurrence n¯ doc.;
                                                              ENC=Doc. No. Occurrence] }
    { 34  ;   ;Contact Via         ;Text80        ;CaptionML=[ENU=Contact Via;
                                                              ESM=Contacta v°a;
                                                              FRC=AppelÇ au N¯;
                                                              ENC=Contact Via] }
    { 35  ;   ;Send Word Docs. as Attmt.;Boolean  ;CaptionML=[ENU=Send Word Docs. as Attmt.;
                                                              ESM=Env°a docs. Word como anexo;
                                                              FRC=Envoyer doc Word attachÇs;
                                                              ENC=Send Word Docs. as Attmt.] }
    { 36  ;   ;Interaction Language Code;Code10   ;TableRelation=Language;
                                                   CaptionML=[ENU=Interaction Language Code;
                                                              ESM=C¢d. idioma interacci¢n;
                                                              FRC=Code langue interaction;
                                                              ENC=Interaction Language Code] }
    { 37  ;   ;E-Mail Logged       ;Boolean       ;CaptionML=[ENU=E-Mail Logged;
                                                              ESM=Correo electr¢nico archivado;
                                                              FRC=Courriel journalisÇ;
                                                              ENC=E-Mail Logged] }
    { 38  ;   ;Subject             ;Text50        ;CaptionML=[ENU=Subject;
                                                              ESM=Asunto;
                                                              FRC=Sujet;
                                                              ENC=Subject] }
    { 39  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[ENU=Contact Name;
                                                              ESM=Nombre contacto;
                                                              FRC=Nom du contact;
                                                              ENC=Contact Name];
                                                   Editable=No }
    { 40  ;   ;Contact Company Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Company No.),
                                                                                          Type=CONST(Company)));
                                                   CaptionML=[ENU=Contact Company Name;
                                                              ESM=Nombre empresa contacto;
                                                              FRC=Nom compagnie contact;
                                                              ENC=Contact Company Name];
                                                   Editable=No }
    { 43  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Inter. Log Entry Comment Line" WHERE (Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[ENU=Comment;
                                                              ESM=Comentario;
                                                              FRC=Commentaire;
                                                              ENC=Comment];
                                                   Editable=No }
    { 44  ;   ;Opportunity No.     ;Code20        ;TableRelation=Opportunity;
                                                   CaptionML=[ENU=Opportunity No.;
                                                              ESM=Nß oportunidad;
                                                              FRC=N¯ d'opportunitÇ;
                                                              ENC=Opportunity No.] }
    { 45  ;   ;Postponed           ;Boolean       ;CaptionML=[ENU=Postponed;
                                                              ESM=Aplazado;
                                                              FRC=ReportÇ;
                                                              ENC=Postponed] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Contact Company No.,Contact No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Contact Company No.,Date,Contact No.,Canceled,Initiated By,Attempt Failed,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.);
                                                   MaintainSIFTIndex=No }
    {    ;Interaction Group Code,Date              }
    {    ;Interaction Group Code,Canceled,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Interaction Template Code,Date           }
    {    ;Interaction Template Code,Canceled,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Canceled,Campaign No.,Campaign Entry No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Campaign No.,Campaign Entry No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Salesperson Code,Date,Postponed         ;SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Canceled,Salesperson Code,Date,Postponed;SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Logged Segment Entry No.,Postponed       }
    {    ;Attachment No.                           }
    {    ;To-do No.,Date                           }
    {    ;Contact No.,Correspondence Type,E-Mail Logged,Subject,Postponed }
    {    ;Campaign No.,Campaign Target             }
    {    ;Campaign No.,Contact Company No.,Campaign Target,Postponed }
    {    ;Opportunity No.,Date                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Description,Contact No.,Date   }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 %2 is marked %3.\;ESM=%1 %2 est† se§alado %3.\;FRC=%1 %2 est Ö %3.\;ENC=%1 %2 is marked %3.\';
      Text001@1001 : TextConst 'ENU=Do you wish to remove the checkmark?;ESM=®Confirma que desea eliminar la marca de activaci¢n?;FRC=Souhaitez-vous dÇsactiver le champ ?;ENC=Do you wish to remove the checkmark?';
      Text002@1002 : TextConst 'ENU=Do you wish to mark %1 %2 as %3?;ESM=®Confirma que desea se§alar %1 %2 como %3?;FRC=Souhaitez-vous mettre %1 %2 Ö %3 ?;ENC=Do you wish to mark %1 %2 as %3?';
      Text003@1003 : TextConst 'ENU=It is not possible to view sales statements after they have been printed.;ESM=No es posible ver informaci¢n de ventas antes de que hayan sido impresos.;FRC=Il n''est plus possible de consulter les rapports vente apräs impression.;ENC=It is not possible to view sales statements after they have been printed.';
      Text004@1004 : TextConst 'ENU=It is not possible to show cover sheets after they have been printed.;ESM=No es posible mostrar portadas despuÇs de haberse impreso.;FRC=Il n''est plus possible d''afficher les bordereaux d''envoi apräs impression.;ENC=It is not possible to show cover sheets after they have been printed.';
      Text005@1005 : TextConst 'ENU=Do you wish to remove the checkmark from the selected %1 lines?;ESM=®Confirma que desea eliminar la marca de activaci¢n para las l°n. %1 selec.?;FRC=Souhaitez-vous dÇsactiver les %1 lignes sÇlectionnÇes ?;ENC=Do you wish to remove the checkmark from the selected %1 lines?';
      Text006@1006 : TextConst 'ENU=Do you wish to mark the selected %1 lines as %2?;ESM=®Confirma que desea se§alar las l°neas %1 seleccionadas como %2?;FRC=Souhaitez-vous marquer les %1 lignes sÇlectionnÇes comme %2 ?;ENC=Do you wish to mark the selected %1 lines as %2?';
      Text009@1007 : TextConst 'ENU=Do you want to remove Attachment?;ESM=®Confirma que desea eliminar el anexo?;FRC=Souhaitez-vous supprimer les piäces jointes?;ENC=Do you want to remove Attachment?';
      Text010@1008 : TextConst 'ENU=Do you want to remove unique Attachments for the selected lines?;ESM=®Confirma que desea eliminar los anexos £nicos para las l°neas seleccionadas?;FRC=Souhaitez-vous supprimer les piäces jointes uniques des lignes sÇlectionnÇes?;ENC=Do you want to remove unique Attachments for the selected lines?';
      Text011@1009 : TextConst 'ENU=Very Positive,Positive,Neutral,Negative,Very Negative;ESM=Muy positiva,Positiva,Neutral,Negativa,Muy negativa;FRC=Träs positif,Positif,Neutre,NÇgatif,Träs nÇgatif;ENC=Very Positive,Positive,Neutral,Negative,Very Negative';

    PROCEDURE CreateInteraction@10();
    VAR
      SegLine@1000 : TEMPORARY Record 5077;
    BEGIN
      SegLine.CreateInteractionFromIntLogEnt(Rec);
    END;

    PROCEDURE CreateTodo@8();
    VAR
      TempTodo@1000 : TEMPORARY Record 5080;
    BEGIN
      TempTodo.CreateToDoFromInteractLogEntry(Rec)
    END;

    PROCEDURE OpenAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      SegLine@1001 : Record 5077;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");

      IF Attachment."Storage Type" <> Attachment."Storage Type"::"Exchange Storage" THEN BEGIN
        SegLine."Contact No." := "Contact No.";
        SegLine."Salesperson Code" := "Salesperson Code";
        SegLine."Contact Alt. Address Code" := "Contact Alt. Address Code";
        SegLine.Date := Date;
        SegLine."Campaign No." := "Campaign No.";
        SegLine."Segment No." := "Segment No.";
        SegLine."Line No." := "Entry No.";
        SegLine.Description := Description;
        SegLine.Subject := Subject;
        SegLine."Language Code" := "Interaction Language Code";
        Attachment.ShowAttachment(SegLine,FORMAT("Entry No.") + ' ' + Description,FALSE);
      END ELSE
        Attachment.DisplayInOutlook;
    END;

    PROCEDURE ToggleCanceledCheckmark@7();
    VAR
      ErrorTxt@1004 : Text[80];
      MasterCanceledCheckmark@1001 : Boolean;
      RemoveUniqueAttachment@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN BEGIN
        IF ConfirmToggleCanceledCheckmark(COUNT,ErrorTxt) THEN BEGIN
          MasterCanceledCheckmark := NOT Canceled;
          IF FindUniqueAttachment AND MasterCanceledCheckmark THEN
            RemoveUniqueAttachment := CONFIRM(ErrorTxt,FALSE);
          SETCURRENTKEY("Entry No.");
          IF FIND('-') THEN
            REPEAT
              SetCanceledCheckmark(MasterCanceledCheckmark,RemoveUniqueAttachment);
            UNTIL NEXT = 0
        END;
      END
    END;

    PROCEDURE SetCanceledCheckmark@2(CanceledCheckmark@1003 : Boolean;RemoveUniqueAttachment@1004 : Boolean);
    VAR
      CampaignEntry@1000 : Record 5072;
      LoggedSeg@1001 : Record 5075;
      Attachment@1002 : Record 5062;
    BEGIN
      IF Canceled AND NOT CanceledCheckmark THEN BEGIN
        IF "Logged Segment Entry No." <> 0 THEN BEGIN
          LoggedSeg.GET("Logged Segment Entry No.");
          LoggedSeg.TESTFIELD(Canceled,FALSE);
        END;
        IF "Campaign Entry No." <> 0 THEN BEGIN
          CampaignEntry.GET("Campaign Entry No.");
          CampaignEntry.TESTFIELD(Canceled,FALSE);
        END;
      END;

      IF NOT Canceled AND CanceledCheckmark THEN
        IF UniqueAttachment AND RemoveUniqueAttachment THEN BEGIN
          Attachment.GET("Attachment No.");
          IF Attachment.RemoveAttachment(FALSE) THEN
            "Attachment No." := 0;
        END;

      Canceled := CanceledCheckmark;
      MODIFY;
    END;

    LOCAL PROCEDURE ConfirmToggleCanceledCheckmark@3(NumberOfSelectedLines@1000 : Integer;VAR ErrorTxt@1001 : Text[80]) : Boolean;
    BEGIN
      IF NumberOfSelectedLines = 1 THEN BEGIN
        ErrorTxt := Text009;
        IF Canceled THEN
          EXIT(CONFIRM(
              Text000 +
              Text001,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));

        EXIT(CONFIRM(
            Text002,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      END;
      ErrorTxt := Text010;
      IF Canceled THEN
        EXIT(CONFIRM(
            Text005,TRUE,TABLECAPTION));

      EXIT(CONFIRM(
          Text006,TRUE,TABLECAPTION,FIELDCAPTION(Canceled)));
    END;

    PROCEDURE UniqueAttachment@4() IsUnique@1000 : Boolean;
    VAR
      InteractLogEntry@1001 : Record 5065;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        InteractLogEntry.SETCURRENTKEY("Attachment No.");
        InteractLogEntry.SETRANGE("Attachment No.","Attachment No.");
        InteractLogEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
        IsUnique := NOT InteractLogEntry.FINDFIRST;
      END;
    END;

    LOCAL PROCEDURE FindUniqueAttachment@6() IsUnique@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN
        REPEAT
          IsUnique := UniqueAttachment;
        UNTIL (NEXT = 0) OR IsUnique
    END;

    PROCEDURE ShowDocument@1();
    VAR
      SalesHeader@1000 : Record 36;
      SalesHeaderArchive@1011 : Record 5107;
      SalesInvHeader@1001 : Record 112;
      SalesShptHeader@1002 : Record 110;
      SalesCrMemoHeader@1003 : Record 114;
      IssuedReminderHeader@1004 : Record 297;
      PurchHeader@1005 : Record 38;
      PurchHeaderArchive@1012 : Record 5109;
      PurchInvHeader@1006 : Record 122;
      PurchRcptHeader@1007 : Record 120;
      PurchCrMemoHeader@1008 : Record 124;
      ServHeader@1009 : Record 5900;
      ReturnRcptHeader@1013 : Record 6660;
      IssuedFinChargeMemoHeader@1015 : Record 304;
      ReturnReceiptHeader@1014 : Record 6660;
      ReturnShipmentHeader@1016 : Record 6650;
      ServiceContractHeader@1017 : Record 5965;
    BEGIN
      CASE "Document Type" OF
        "Document Type"::"Sales Qte.":
          IF "Version No." <> 0 THEN BEGIN
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Quote);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Sales Quote Archive",SalesHeaderArchive);
          END ELSE BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Sales Quote",SalesHeader);
          END;
        "Document Type"::"Sales Blnkt. Ord":
          BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::"Blanket Order","Document No.");
            PAGE.RUN(PAGE::"Blanket Sales Order",SalesHeader);
          END;
        "Document Type"::"Sales Ord. Cnfrmn.":
          IF "Version No." <> 0 THEN BEGIN
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Order);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Sales Order Archive",SalesHeaderArchive);
          END ELSE BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Sales Order",SalesHeader);
          END;
        "Document Type"::"Sales Inv.":
          BEGIN
            SalesInvHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Invoice",SalesInvHeader);
          END;
        "Document Type"::"Sales Shpt. Note":
          BEGIN
            SalesShptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Shipment",SalesShptHeader);
          END;
        "Document Type"::"Sales Cr. Memo":
          BEGIN
            SalesCrMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Sales Credit Memo",SalesCrMemoHeader);
          END;
        "Document Type"::"Sales Stmnt.":
          ERROR(Text003);
        "Document Type"::"Sales Rmdr.":
          BEGIN
            IssuedReminderHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Issued Reminder",IssuedReminderHeader);
          END;
        "Document Type"::"Serv. Ord. Create":
          BEGIN
            ServHeader.GET(ServHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Service Order",ServHeader)
          END;
        "Document Type"::"Purch.Qte.":
          IF "Version No." <> 0 THEN BEGIN
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Quote);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Purchase Quote Archive",PurchHeaderArchive);
          END ELSE BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Purchase Quote",PurchHeader);
          END;
        "Document Type"::"Purch. Blnkt. Ord.":
          BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::"Blanket Order","Document No.");
            PAGE.RUN(PAGE::"Blanket Purchase Order",PurchHeader);
          END;
        "Document Type"::"Purch. Ord.":
          IF "Version No." <> 0 THEN BEGIN
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Order);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            PAGE.RUN(PAGE::"Purchase Order Archive",PurchHeaderArchive);
          END ELSE BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::Order,"Document No.");
            PAGE.RUN(PAGE::"Purchase Order",PurchHeader);
          END;
        "Document Type"::"Purch. Inv.":
          BEGIN
            PurchInvHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Invoice",PurchInvHeader);
          END;
        "Document Type"::"Purch. Rcpt.":
          BEGIN
            PurchRcptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Receipt",PurchRcptHeader);
          END;
        "Document Type"::"Purch. Cr. Memo":
          BEGIN
            PurchCrMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Purchase Credit Memo",PurchCrMemoHeader);
          END;
        "Document Type"::"Cover Sheet":
          ERROR(Text004);
        "Document Type"::"Sales Return Order":
          IF SalesHeader.GET(SalesHeader."Document Type"::"Return Order","Document No.") THEN
            PAGE.RUN(PAGE::"Sales Return Order",SalesHeader)
          ELSE BEGIN
            ReturnRcptHeader.SETRANGE("Return Order No.","Document No.");
            PAGE.RUN(PAGE::"Posted Return Receipt",ReturnRcptHeader);
          END;
        "Document Type"::"Sales Finance Charge Memo":
          BEGIN
            IssuedFinChargeMemoHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Issued Finance Charge Memo",IssuedFinChargeMemoHeader);
          END;
        "Document Type"::"Sales Return Receipt":
          BEGIN
            ReturnReceiptHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Return Receipt",ReturnReceiptHeader);
          END;
        "Document Type"::"Purch. Return Shipment":
          BEGIN
            ReturnShipmentHeader.GET("Document No.");
            PAGE.RUN(PAGE::"Posted Return Shipment",ReturnShipmentHeader);
          END;
        "Document Type"::"Purch. Return Ord. Cnfrmn.":
          IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Document No.") THEN
            PAGE.RUN(PAGE::"Purchase Return Order",PurchHeader)
          ELSE BEGIN
            ReturnShipmentHeader.SETRANGE("Return Order No.","Document No.");
            PAGE.RUN(PAGE::"Posted Return Shipment",ReturnShipmentHeader);
          END;
        "Document Type"::"Service Contract":
          BEGIN
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Contract,"Document No.");
            PAGE.RUN(PAGE::"Service Contract",ServiceContractHeader);
          END;
        "Document Type"::"Service Contract Quote":
          BEGIN
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Service Contract Quote",ServiceContractHeader);
          END;
        "Document Type"::"Service Quote":
          BEGIN
            ServHeader.GET(ServHeader."Document Type"::Quote,"Document No.");
            PAGE.RUN(PAGE::"Service Quote",ServHeader);
          END;
      END;
    END;

    PROCEDURE EvaluateInteraction@9();
    VAR
      Selected@1000 : Integer;
    BEGIN
      IF FIND('-') THEN BEGIN
        Selected := DIALOG.STRMENU(Text011);
        IF Selected <> 0 THEN
          REPEAT
            Evaluation := Selected;
            MODIFY;
          UNTIL NEXT = 0
      END;
    END;

    PROCEDURE ResumeInteraction@11();
    VAR
      SegLine@1000 : TEMPORARY Record 5077;
      Cont@1006 : Record 5050;
      Salesperson@1005 : Record 13;
      Campaign@1004 : Record 5071;
      Todo@1003 : Record 5080;
      Opportunity@1002 : Record 5092;
      SegManagement@1001 : Codeunit 5051;
    BEGIN
      SegLine.DELETEALL;
      SegLine.INIT;
      SegManagement.CopyFieldsFromInteractLogEntry(SegLine,Rec);
      SegLine.VALIDATE(Date,WORKDATE);

      IF Todo.GET(GETFILTER("To-do No.")) THEN
        SegLine.SETRANGE("To-do No.",Todo."No.");
      IF Cont.GET(GETFILTER("Contact Company No.")) THEN
        SegLine.SETRANGE("Contact Company No.",Cont."No.");
      IF Cont.GET(GETFILTER("Contact No.")) THEN
        SegLine.SETRANGE("Contact No.",Cont."No.");
      IF Salesperson.GET(GETFILTER("Salesperson Code")) THEN
        SegLine.SETRANGE("Salesperson Code",Salesperson.Code);
      IF Campaign.GET(GETFILTER("Campaign No.")) THEN
        SegLine.SETRANGE("Campaign No.",Campaign."No.");
      IF Opportunity.GET(GETFILTER("Opportunity No.")) THEN
        SegLine.SETRANGE("Opportunity No.",Opportunity."No.");

      SegLine.StartWizard;
    END;

    BEGIN
    END.
  }
}

