OBJECT Table 5640 Main Asset Component
{
  OBJECT-PROPERTIES
  {
    Date=09/07/12;
    Time=12:00:00 PM;
    Version List=NAVW17.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               LockFixedAsset;
               IF "FA No." <> '' THEN BEGIN
                 FA.GET("FA No.");
                 UpdateMainAsset(FA,0);
               END;
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Main Asset Component;
               ESM=Componente activo ppal.;
               FRC=Composante immo. principale;
               ENC=Main Asset Component];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Main Asset No.      ;Code20        ;TableRelation="Fixed Asset";
                                                   CaptionML=[ENU=Main Asset No.;
                                                              ESM=N§ activo ppal.;
                                                              FRC=Nø immo. principale;
                                                              ENC=Main Asset No.];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 3   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF ("FA No." = '') OR ("Main Asset No." = '') THEN
                                                                  EXIT;
                                                                LockFixedAsset;
                                                                FA.GET("FA No.");
                                                                IF "FA No." = "Main Asset No." THEN
                                                                  CreateError("FA No.",1);
                                                                Description := FA.Description;
                                                                MainAssetComp.SETRANGE("Main Asset No.","FA No.");
                                                                IF MainAssetComp.FINDFIRST THEN
                                                                  CreateError("FA No.",1);
                                                                MainAssetComp.SETRANGE("Main Asset No.");
                                                                MainAssetComp.SETCURRENTKEY("FA No.");
                                                                MainAssetComp.SETRANGE("FA No.","FA No.");
                                                                IF MainAssetComp.FINDFIRST THEN
                                                                  CreateError("FA No.",2);
                                                                MainAssetComp.SETRANGE("FA No.","Main Asset No.");
                                                                IF MainAssetComp.FINDFIRST THEN
                                                                  CreateError("Main Asset No.",1);
                                                                UpdateMainAsset(FA,2);
                                                                FA.GET("Main Asset No.");
                                                                IF FA."Main Asset/Component" <> FA."Main Asset/Component"::"Main Asset" THEN BEGIN
                                                                  FA."Main Asset/Component" := FA."Main Asset/Component"::"Main Asset";
                                                                  ERROR(
                                                                    Text001,
                                                                    DepreciationCalc.FAName(FA,''),FA."Main Asset/Component");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=FA No.;
                                                              ESM=A/F N§;
                                                              FRC=Nø immo.;
                                                              ENC=FA No.];
                                                   NotBlank=Yes }
    { 4   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
  }
  KEYS
  {
    {    ;Main Asset No.,FA No.                   ;Clustered=Yes }
    {    ;FA No.                                   }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.;ESM=No se puede cambiar el nombre a %1.;FRC=Vous ne pouvez pas renommer un %1.;ENC=You cannot rename a %1.';
      Text001@1001 : TextConst 'ENU=%1 is not a %2.;ESM=%1 no es un/a %2.;FRC=%1 n''est pas une %2.;ENC=%1 is not a %2.';
      Text002@1002 : TextConst 'ENU=%1 is a %2.;ESM=%1 es un/a %2.;FRC=%1 est un %2.;ENC=%1 is a %2.';
      FA@1003 : Record 5600;
      FADeprBook@1004 : Record 5612;
      MainAssetComp@1005 : Record 5640;
      DepreciationCalc@1006 : Codeunit 5616;
      Text003@1007 : TextConst 'ENU=Main Asset,Component;ESM=Activo principal,Componente;FRC=Immo. principale,Composante;ENC=Main Asset,Component';

    LOCAL PROCEDURE LockFixedAsset@6();
    BEGIN
      FA.LOCKTABLE;
      FADeprBook.LOCKTABLE;
    END;

    LOCAL PROCEDURE UpdateMainAsset@1(VAR FA@1000 : Record 5600;ComponentType@1001 : ' ,Main Asset,Component');
    VAR
      FA2@1002 : Record 5600;
    BEGIN
      IF ComponentType = ComponentType::" " THEN BEGIN
        FA."Main Asset/Component" := FA."Main Asset/Component"::" ";
        FA."Component of Main Asset" := '';
      END;
      IF ComponentType = ComponentType::Component THEN BEGIN
        FA."Component of Main Asset" := "Main Asset No.";
        FA."Main Asset/Component" := FA."Main Asset/Component"::Component;
      END;
      FA.MODIFY(TRUE);
      UpdateFADeprBooks(FA);

      FA.RESET;
      FA.SETCURRENTKEY("Component of Main Asset");
      FA.SETRANGE("Component of Main Asset","Main Asset No.");
      FA.SETRANGE("Main Asset/Component",FA2."Main Asset/Component"::Component);
      FA2.GET("Main Asset No.");
      IF FA.FIND('=><') THEN BEGIN
        IF FA2."Main Asset/Component" <> FA2."Main Asset/Component"::"Main Asset" THEN BEGIN
          FA2."Main Asset/Component" := FA2."Main Asset/Component"::"Main Asset";
          FA2."Component of Main Asset" := FA2."No.";
          FA2.MODIFY(TRUE);
          UpdateFADeprBooks(FA2);
        END;
      END ELSE BEGIN
        FA2."Main Asset/Component" := FA2."Main Asset/Component"::" ";
        FA2."Component of Main Asset" := '';
        FA2.MODIFY(TRUE);
        UpdateFADeprBooks(FA2);
      END;
    END;

    LOCAL PROCEDURE UpdateFADeprBooks@3(VAR FA@1000 : Record 5600);
    BEGIN
      WITH FADeprBook DO BEGIN
        SETCURRENTKEY("FA No.");
        SETRANGE("FA No.",FA."No.");
        IF FIND('-') THEN
          REPEAT
            "Main Asset/Component" := FA."Main Asset/Component";
            "Component of Main Asset" := FA."Component of Main Asset";
            MODIFY(TRUE);
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CreateError@2(FANo@1000 : Code[20];MainAssetComponent@1001 : ' ,Main Asset,Component');
    BEGIN
      FA."No." := FANo;
      ERROR(
        Text002,
        DepreciationCalc.FAName(FA,''),SELECTSTR(MainAssetComponent,Text003));
    END;

    BEGIN
    END.
  }
}

