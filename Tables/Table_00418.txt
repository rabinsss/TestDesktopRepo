OBJECT Table 418 IC Inbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICInboxJnlLine@1000 : Record 419;
               ICInboxPurchHdr@1001 : Record 436;
               ICInboxSalesHdr@1002 : Record 434;
             BEGIN
               CASE "Source Type" OF
                 "Source Type"::Journal:
                   BEGIN
                     ICInboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICInboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxJnlLine.FINDFIRST THEN
                       ICInboxJnlLine.DELETEALL(TRUE);
                   END;
                 "Source Type"::"Sales Document":
                   BEGIN
                     ICInboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxSalesHdr.FINDFIRST THEN
                       ICInboxSalesHdr.DELETE(TRUE);
                   END;
                 "Source Type"::"Purchase Document":
                   BEGIN
                     ICInboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxPurchHdr.FINDFIRST THEN
                       ICInboxPurchHdr.DELETE(TRUE);
                   END;
               END;
             END;

    CaptionML=[ENU=IC Inbox Transaction;
               ESM=Trans. band. ent. IC;
               FRC=Trans. bte r‚cep. IC;
               ENC=IC Inbox Transaction];
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=[ENU=Transaction No.;
                                                              ESM=N§ asiento;
                                                              FRC=Nø de transaction;
                                                              ENC=Transaction No.];
                                                   Editable=No }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=[ENU=IC Partner Code;
                                                              ESM=C¢digo socio IC;
                                                              FRC=Code de partenaire IC;
                                                              ENC=IC Partner Code];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=[ENU=Source Type;
                                                              ESM=Tipo procedencia mov.;
                                                              FRC=Type d'origine;
                                                              ENC=Source Type];
                                                   OptionCaptionML=[ENU=Journal,Sales Document,Purchase Document;
                                                                    ESM=Diario,Documento venta,Documento compra;
                                                                    FRC=Journal,Document de vente,Document d'achat;
                                                                    ENC=Journal,Sales Document,Purchase Document];
                                                   OptionString=Journal,Sales Document,Purchase Document;
                                                   Editable=No }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=[ENU=Document Type;
                                                              ESM=Tipo documento;
                                                              FRC=Type de document;
                                                              ENC=Document Type];
                                                   OptionCaptionML=[ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order";
                                                                    ESM=" ,Pago,Factura,Nota cr‚dito,Reembolso,Pedido,Devoluci¢n";
                                                                    FRC=" ,Paiement,Facture,Note de cr‚dit,Remboursement,Commande,Commande de retour";
                                                                    ENC=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Refund,Order,Return Order];
                                                   Editable=No }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ESM=N§ documento;
                                                              FRC=Nø de document;
                                                              ENC=Document No.];
                                                   Editable=No }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[ENU=Posting Date;
                                                              ESM=Fecha registro;
                                                              FRC=Date de report;
                                                              ENC=Posting Date];
                                                   Editable=No }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=[ENU=Transaction Source;
                                                              ESM=Origen de la transacci¢n;
                                                              FRC=Source de transaction;
                                                              ENC=Transaction Source];
                                                   OptionCaptionML=[ENU=Returned by Partner,Created by Partner;
                                                                    ESM=Devuelta por un socio,Creada por un socio;
                                                                    FRC=Retourn‚ par le partenaire,Cr‚‚ par le partenaire;
                                                                    ENC=Returned by Partner,Created by Partner];
                                                   OptionString=Returned by Partner,Created by Partner;
                                                   Editable=No }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=[ENU=Document Date;
                                                              ESM=Fecha emisi¢n documento;
                                                              FRC=Date de document;
                                                              ENC=Document Date];
                                                   Editable=No }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                IF (("Line Action" = "Line Action"::"Return to IC Partner") OR ("Line Action" = "Line Action"::Accept)) AND
                                                                   ("Transaction Source" = "Transaction Source"::"Returned by Partner")
                                                                THEN
                                                                  ERROR(Text000,"Transaction No.","IC Partner Code");

                                                                IF "Line Action" = "Line Action"::Accept THEN
                                                                  InboxCheckAccept;
                                                              END;

                                                   CaptionML=[ENU=Line Action;
                                                              ESM=Acci¢n de la l¡nea;
                                                              FRC=Action de ligne;
                                                              ENC=Line Action];
                                                   OptionCaptionML=[ENU=No Action,Accept,Return to IC Partner,Cancel;
                                                                    ESM=Ninguna acci¢n,Aceptar,Devolver a IC asociada,Cancelar;
                                                                    FRC=Aucune action,Accepter,Retourner au partenaire IC,Annuler;
                                                                    ENC=No Action,Accept,Return to IC Partner,Cancel];
                                                   OptionString=No Action,Accept,Return to IC Partner,Cancel }
    { 11  ;   ;Original Document No.;Code20       ;CaptionML=[ENU=Original Document No.;
                                                              ESM=N§ del documento original;
                                                              FRC=Nø du document d'origine;
                                                              ENC=Original Document No.] }
    { 12  ;   ;IC Partner G/L Acc. No.;Code20     ;CaptionML=[ENU=IC Partner G/L Acc. No.;
                                                              ESM=N§ cuenta IC asociada;
                                                              FRC=Nø compte G/L du partenaire IC;
                                                              ENC=IC Partner G/L Acc. No.] }
    { 13  ;   ;Source Line No.     ;Integer       ;CaptionML=[ENU=Source Line No.;
                                                              ESM=N§ l¡n. origen;
                                                              FRC=Nø de ligne source;
                                                              ENC=Source Line No.] }
  }
  KEYS
  {
    {    ;Transaction No.,IC Partner Code,Transaction Source,Document Type;
                                                   Clustered=Yes }
    {    ;IC Partner Code                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Transaction No. %1 has been returned by IC Partner %2.\You can only cancel returned transactions.;ESM=La IC asociada %2 ha devuelto la transacci¢n n§ %1.\S¢lo es posible cancelar transacciones devueltas.;FRC=La transaction nø %1 a ‚t‚ retourn‚e par le partenaire IC %2.\Vous ne pouvez annuler que les transactions retourn‚es.;ENC=Transaction No. %1 has been returned by IC Partner %2.\You can only cancel returned transactions.';

    PROCEDURE ShowDetails@1();
    VAR
      ICInBoxJnlLine@1005 : Record 419;
      ICInboxSalesHeader@1004 : Record 434;
      ICInboxPurchHeader@1003 : Record 436;
      FrmICInBoxJnlLine@1002 : Page 616;
      FrmICInboxSalesHeader@1001 : Page 644;
      FrmICInboxPurchHeader@1000 : Page 646;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::Journal:
          BEGIN
            ICInBoxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICInBoxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICInBoxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            FrmICInBoxJnlLine.SETTABLEVIEW(ICInBoxJnlLine);
            FrmICInBoxJnlLine.RUNMODAL;
          END;
        "Source Type"::"Sales Document":
          BEGIN
            ICInboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICInboxSalesHeader.SETTABLEVIEW(ICInboxSalesHeader);
            FrmICInboxSalesHeader.RUNMODAL;
          END;
        "Source Type"::"Purchase Document":
          BEGIN
            ICInboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICInboxPurchHeader.SETTABLEVIEW(ICInboxPurchHeader);
            FrmICInboxPurchHeader.RUNMODAL;
          END;
      END;
    END;

    LOCAL PROCEDURE InboxCheckAccept@40();
    VAR
      ICInboxTransaction2@1001 : Record 418;
      HandledICInboxTrans@1002 : Record 420;
      Text001@1003 : TextConst 'ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Accept.\Do you also want to accept Transaction No. %2?;ESM=N§ asiento %2 es una copia de N§ asiento %1, que se ha establecido en Aceptar.\¨Desea tambi‚n enviar N§ asiento %2?;FRC=La transaction Nø %2 est une copie de la transaction Nø %1, qui a d‚j… ‚t‚ param‚tr‚e sur Accepter.\Voulez-vous ‚galement accepter la transaction Nø %2?;ENC=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Accept.\Do you also want to accept Transaction No. %2?';
      Text002@1000 : TextConst 'ENU=A copy of Transaction No. %1 has already been accepted and is now in the Handled IC Inbox Transactions window.\Do you also want to accept Transaction No. %1?;ESM=Ya se ha aceptado una copia de N§ asiento %1 y ahora est  en la ventana Transacciones bandeja de salida IC procesadas.\¨Desea tambi‚n enviar N§ asiento %1?;FRC=Une copie de la transaction Nø %1 a d‚j… ‚t‚ accept‚e et est maintenant dans la fenˆtre Transactions de boŒte de sortie IC trait‚e.\Voulez-vous ‚galement accepter la transaction Nø %1?;ENC=A copy of Transaction No. %1 has already been accepted and is now in the Handled IC Inbox Transactions window.\Do you also want to accept Transaction No. %1?';
      ICInboxPurchHeader@1004 : Record 436;
      PurchHeader@1005 : Record 38;
      PurchInvHeader@1006 : Record 122;
      Text003@1007 : TextConst 'ENU=A purchase order already exists for transaction %1. If you accept and post this document, you should delete the original purchase order %2 to avoid duplicate postings.;ESM=Ya existe un pedido de compra para la transacci¢n %1. Si acepta y registra este documento, debe eliminar el pedido de compra original %2 para evitar registros duplicados.;FRC=Un bon de commande existe d‚j… pour la transaction %1. Si vous acceptez et reportez ce document, vous devez supprimer le bon de commande original %2 pour ‚viter les reports en double.;ENC=A purchase order already exists for transaction %1. If you accept and post this document, you should delete the original purchase order %2 to avoid duplicate postings.';
      Text004@1008 : TextConst 'ENU=Purchase invoice %1 has already been posted for transaction %2. If you accept and post this document, you will have duplicate postings.\Are you sure you want to accept the transaction?;ESM=La factura de compra %1 ya se ha registrado para la transacci¢n %2. Si acepta y registra este documento, tendr  registros duplicados.\¨Seguro que desea aceptar la transacci¢n?;FRC=La facture achat %1 a d‚j… ‚t‚ report‚e pour la transaction %2. Si vous acceptez et validez ce document, vous aurez des reports en double.\Òtes-vous certain de vouloir accepter la transaction?;ENC=Purchase invoice %1 has already been posted for transaction %2. If you accept and post this document, you will have duplicate postings.\Are you sure you want to accept the transaction?';
    BEGIN
      HandledICInboxTrans.SETRANGE("IC Partner Code","IC Partner Code");
      HandledICInboxTrans.SETRANGE("Document Type","Document Type");
      HandledICInboxTrans.SETRANGE("Source Type","Source Type");
      HandledICInboxTrans.SETRANGE("Document No.","Document No.");
      IF HandledICInboxTrans.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text002,"Transaction No."),FALSE) THEN
          ERROR('');

      ICInboxTransaction2.SETRANGE("IC Partner Code","IC Partner Code");
      ICInboxTransaction2.SETRANGE("Document Type","Document Type");
      ICInboxTransaction2.SETRANGE("Source Type","Source Type");
      ICInboxTransaction2.SETRANGE("Document No.","Document No.");
      ICInboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICInboxTransaction2.SETRANGE("IC Partner G/L Acc. No.","IC Partner G/L Acc. No.");
      ICInboxTransaction2.SETRANGE("Source Line No.","Source Line No.");
      ICInboxTransaction2.SETRANGE("Line Action","Line Action"::Accept);
      IF ICInboxTransaction2.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text001,ICInboxTransaction2."Transaction No.","Transaction No."),FALSE) THEN
          ERROR('');
      IF ("Source Type" = "Source Type"::"Purchase Document") AND
         ("Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"])
      THEN BEGIN
        ICInboxPurchHeader.GET("Transaction No.","IC Partner Code","Transaction Source");
        IF ICInboxPurchHeader."Your Reference" <> '' THEN BEGIN
          PurchHeader.SETRANGE("No.",ICInboxPurchHeader."Your Reference");
          IF PurchHeader.FINDFIRST THEN
            MESSAGE(Text003,ICInboxPurchHeader."IC Transaction No.",ICInboxPurchHeader."Your Reference")
          ELSE BEGIN
            PurchInvHeader.SETCURRENTKEY("Order No.");
            PurchInvHeader.SETRANGE("Order No.",ICInboxPurchHeader."Your Reference");
            IF PurchInvHeader.FINDFIRST THEN
              IF NOT CONFIRM(Text004,TRUE,ICInboxPurchHeader."Your Reference",ICInboxPurchHeader."IC Transaction No.") THEN
                "Line Action" := xRec."Line Action";
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

