OBJECT Table 1228 Payment Jnl. Export Error Text
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00 PM;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Payment Jnl. Export Error Text;
               ESM=Texto de error de exportaci¢n del diario de pagos;
               FRC=Texte erreur exportation feuille paiement;
               ENC=Payment Jnl. Export Error Text];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              ESM=Nombre libro diario;
                                                              FRC=Nom modŠle journal;
                                                              ENC=Journal Template Name] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[ENU=Journal Batch Name;
                                                              ESM=Nombre secci¢n diario;
                                                              FRC=Nom lot de journal;
                                                              ENC=Journal Batch Name] }
    { 3   ;   ;Journal Line No.    ;Integer       ;CaptionML=[ENU=Journal Line No.;
                                                              ESM=N§ l¡nea diario;
                                                              FRC=Nø ligne de journal;
                                                              ENC=Journal Line No.] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=N§ l¡nea;
                                                              FRC=Nø ligne;
                                                              ENC=Line No.] }
    { 5   ;   ;Error Text          ;Text250       ;CaptionML=[ENU=Error Text;
                                                              ESM=Texto error;
                                                              FRC=Texte d'erreur;
                                                              ENC=Error Text] }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              ESM=N§ documento;
                                                              FRC=Nø de document;
                                                              ENC=Document No.] }
    { 7   ;   ;Additional Information;Text250     ;CaptionML=[ENU=Additional Information;
                                                              ESM=Informaci¢n adicional;
                                                              FRC=Informations suppl‚mentaires;
                                                              ENC=Additional Information] }
    { 8   ;   ;Support URL         ;Text250       ;CaptionML=[ENU=Support URL;
                                                              ESM=URL de soporte;
                                                              FRC=URL assistance;
                                                              ENC=Support URL] }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Document No.,Journal Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE CreateNew@3(GenJnlLine@1000 : Record 81;NewText@1001 : Text;NewAddnlInfo@1003 : Text;NewExtSupportInfo@1002 : Text);
    BEGIN
      SetLineFilters(GenJnlLine);
      IF FINDLAST THEN;
      "Journal Template Name" := GenJnlLine."Journal Template Name";
      "Journal Batch Name" := GenJnlLine."Journal Batch Name";
      "Document No." := GenJnlLine."Document No.";
      "Journal Line No." := GenJnlLine."Line No.";
      "Line No." += 1;
      "Error Text" := COPYSTR(NewText,1,MAXSTRLEN("Error Text"));
      "Additional Information" := COPYSTR(NewAddnlInfo,1,MAXSTRLEN("Additional Information"));
      "Support URL" := COPYSTR(NewExtSupportInfo,1,MAXSTRLEN("Support URL"));
      INSERT;
    END;

    PROCEDURE JnlLineHasErrors@1(GenJnlLine@1000 : Record 81) : Boolean;
    BEGIN
      SetLineFilters(GenJnlLine);
      EXIT(NOT ISEMPTY);
    END;

    PROCEDURE JnlBatchHasErrors@5(GenJnlLine@1000 : Record 81) : Boolean;
    BEGIN
      SetBatchFilters(GenJnlLine);
      EXIT(NOT ISEMPTY);
    END;

    PROCEDURE DeleteJnlLineErrors@2(GenJnlLine@1000 : Record 81);
    BEGIN
      IF JnlLineHasErrors(GenJnlLine) THEN
        DELETEALL;
    END;

    PROCEDURE DeleteJnlBatchErrors@4(GenJnlLine@1000 : Record 81);
    BEGIN
      IF JnlBatchHasErrors(GenJnlLine) THEN
        DELETEALL;
    END;

    LOCAL PROCEDURE SetBatchFilters@8(GenJnlLine@1000 : Record 81);
    BEGIN
      SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      IF (GenJnlLine."Journal Template Name" = '') AND (GenJnlLine."Journal Batch Name" = '') THEN
        SETRANGE("Document No.",GenJnlLine."Document No.");
    END;

    LOCAL PROCEDURE SetLineFilters@9(GenJnlLine@1000 : Record 81);
    BEGIN
      SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      SETRANGE("Document No.",GenJnlLine."Document No.");
      SETRANGE("Journal Line No.",GenJnlLine."Line No.");
    END;

    BEGIN
    END.
  }
}

