OBJECT Table 5943 Troubleshooting Header
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ServMgtSetup.GET ;
               IF "No." = '' THEN BEGIN
                 ServMgtSetup.TESTFIELD("Troubleshooting Nos.");
                 NoSeriesMgt.InitSeries(ServMgtSetup."Troubleshooting Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;
             END;

    CaptionML=[ENU=Troubleshooting Header;
               ESM=Cab. detecci¢n errores;
               FRC=En-tˆte d‚pannage;
               ENC=Troubleshooting Header];
    LookupPageID=Page5991;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  ServMgtSetup.GET;
                                                                  NoSeriesMgt.TestManual(ServMgtSetup."Troubleshooting Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              ESM=N§;
                                                              FRC=Nø;
                                                              ENC=No.] }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ESM=Descripci¢n;
                                                              FRC=Description;
                                                              ENC=Description] }
    { 3   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              ESM=Nos. serie;
                                                              FRC=S‚ries de nø;
                                                              ENC=No. Series];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=No %1 was found.;ESM=No se encontr¢ %1.;FRC=Aucun enregistrement %1 n''a ‚t‚ trouv‚.;ENC=No %1 was found.';
      Text001@1001 : TextConst 'ENU=No %1 was found for %2 %3.;ESM=No se encontr¢ %1 para %2 %3.;FRC=Aucun enregistrement %1 n''a ‚t‚ trouv‚ pour l''%2 %3.;ENC=No %1 was found for %2 %3.';
      ServMgtSetup@1002 : Record 5911;
      TblshtgHeader@1003 : Record 5943;
      TblshtgHeader2@1008 : Record 5943;
      TblshtgSetup@1004 : Record 5945;
      NoSeriesMgt@1005 : Codeunit 396;
      Tblshtg@1006 : Page 5990;

    PROCEDURE AssistEdit@1(OldTblshtHeader@1000 : Record 5943) : Boolean;
    BEGIN
      WITH TblshtgHeader DO BEGIN
        TblshtgHeader := Rec;
        ServMgtSetup.GET;
        ServMgtSetup.TESTFIELD("Troubleshooting Nos.");
        IF NoSeriesMgt.SelectSeries(ServMgtSetup."Troubleshooting Nos.",OldTblshtHeader."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := TblshtgHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE ShowForServItemLine@8(ServItemLine@1000 : Record 5901);
    VAR
      TblshtFound@1001 : Boolean;
    BEGIN
      TblshtgSetup.RESET;
      TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::"Service Item");
      TblshtgSetup.SETRANGE("No.",ServItemLine."Service Item No.");
      TblshtFound := TblshtgSetup.FINDFIRST;

      IF NOT TblshtFound THEN BEGIN
        TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::Item);
        TblshtgSetup.SETRANGE("No.",ServItemLine."Item No.");
        TblshtFound := TblshtgSetup.FINDFIRST;
      END;
      IF NOT TblshtFound THEN BEGIN
        TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::"Service Item Group");
        TblshtgSetup.SETRANGE("No.",ServItemLine."Service Item Group Code");
        TblshtFound := TblshtgSetup.FINDFIRST;
      END;
      IF TblshtFound THEN
        RunTroubleshooting
      ELSE
        MESSAGE(
          Text000,TblshtgSetup.TABLECAPTION);
    END;

    PROCEDURE ShowForServItem@3(ServItem@1000 : Record 5940);
    VAR
      TblshtFound@1001 : Boolean;
    BEGIN
      TblshtgSetup.RESET;
      TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::"Service Item");
      TblshtgSetup.SETRANGE("No.",ServItem."No.");
      TblshtFound := TblshtgSetup.FINDFIRST;

      IF NOT TblshtFound THEN BEGIN
        TblshtgSetup.RESET;
        TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::Item);
        TblshtgSetup.SETRANGE("No.",ServItem."Item No.");
        TblshtFound := TblshtgSetup.FINDFIRST;
      END;
      IF NOT TblshtFound THEN BEGIN
        TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::"Service Item Group");
        TblshtgSetup.SETRANGE("No.",ServItem."Service Item Group Code");
        TblshtFound := TblshtgSetup.FINDFIRST;
      END;
      IF TblshtFound THEN
        RunTroubleshooting
      ELSE
        MESSAGE(Text001,TblshtgSetup.TABLECAPTION,ServItem.TABLECAPTION,ServItem."No.");
    END;

    PROCEDURE ShowForItem@4(Item@1000 : Record 27);
    VAR
      TblshtFound@1001 : Boolean;
    BEGIN
      TblshtgSetup.RESET;
      TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::Item);
      TblshtgSetup.SETRANGE("No.",Item."No.");
      TblshtFound := TblshtgSetup.FINDFIRST;
      IF NOT TblshtFound THEN BEGIN
        TblshtgSetup.SETRANGE(Type,TblshtgSetup.Type::"Service Item Group");
        TblshtgSetup.SETRANGE("No.",Item."Service Item Group");
        TblshtFound := TblshtgSetup.FINDFIRST;
      END;
      IF TblshtFound THEN
        RunTroubleshooting
      ELSE
        MESSAGE(Text001,TblshtgSetup.TABLECAPTION,Item.TABLECAPTION,Item."No.");
    END;

    LOCAL PROCEDURE MarkTroubleShtgHeader@2(VAR TblshtgSetup2@1001 : Record 5945);
    BEGIN
      TblshtgSetup2.FIND('-');
      REPEAT
        TblshtgHeader2.GET(TblshtgSetup2."Troubleshooting No.");
        TblshtgHeader2.MARK(TRUE);
      UNTIL TblshtgSetup2.NEXT = 0;
    END;

    LOCAL PROCEDURE RunTroubleshooting@5();
    BEGIN
      TblshtgHeader.GET(TblshtgSetup."Troubleshooting No.");
      MarkTroubleShtgHeader(TblshtgSetup);
      TblshtgHeader2.MARKEDONLY(TRUE);
      CLEAR(Tblshtg);
      Tblshtg.SetCaption(FORMAT(TblshtgSetup.Type),TblshtgSetup."No.");
      Tblshtg.SETRECORD(TblshtgHeader);
      Tblshtg.SETTABLEVIEW(TblshtgHeader2);
      Tblshtg.EDITABLE := FALSE;
      Tblshtg.RUN;
      TblshtgHeader2.RESET;
    END;

    BEGIN
    END.
  }
}

