OBJECT Table 5304 Outlook Synch. Field
{
  OBJECT-PROPERTIES
  {
    Date=09/23/13;
    Time=12:00:00 PM;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    DataCaptionFields=Synch. Entity Code;
    OnInsert=BEGIN
               TESTFIELD("Field No.");

               IF "Table No." <> 0 THEN
                 TESTFIELD("Table Relation");

               CheckDuplicatedRecords;

               IF ISNULLGUID("Record GUID") THEN
                 "Record GUID" := CREATEGUID;
             END;

    OnModify=BEGIN
               TESTFIELD("Field No.");
               CheckDuplicatedRecords;

               IF "Table No." <> 0 THEN
                 TESTFIELD("Table Relation");
             END;

    OnDelete=VAR
               OSynchFilter@1000 : Record 5303;
               OSynchOptionCorrel@1001 : Record 5307;
             BEGIN
               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchOptionCorrel.RESET;
               OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
               OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
               OSynchOptionCorrel.DELETEALL;
             END;

    CaptionML=[ENU=Outlook Synch. Field;
               ESM=Campo sinc. Outlook;
               FRC=Champ de synchro Outlook;
               ENC=Outlook Synch. Field];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                GetMasterInformation;
                                                              END;

                                                   CaptionML=[ENU=Synch. Entity Code;
                                                              ESM=C¢digo entidad sinc.;
                                                              FRC=Code d'entitÇ de synchro;
                                                              ENC=Synch. Entity Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;TableRelation="Outlook Synch. Entity Element"."Element No." WHERE (Synch. Entity Code=FIELD(Synch. Entity Code));
                                                   CaptionML=[ENU=Element No.;
                                                              ESM=Nß elemento;
                                                              FRC=N¯ d'ÇlÇment;
                                                              ENC=Element No.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ESM=Nß l°nea;
                                                              FRC=N¯ ligne;
                                                              ENC=Line No.] }
    { 4   ;   ;Master Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Master Table No.;
                                                              ESM=Nß tabla principal;
                                                              FRC=N¯ de table maåtre;
                                                              ENC=Master Table No.];
                                                   Editable=No }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=[ENU=Outlook Object;
                                                              ESM=Objeto Outlook;
                                                              FRC=Objet Outlook;
                                                              ENC=Outlook Object];
                                                   Editable=No }
    { 6   ;   ;Outlook Property    ;Text80        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;

                                                                IF "Outlook Property" = xRec."Outlook Property" THEN
                                                                  EXIT;

                                                                IF NOT "User-Defined" AND ("Outlook Object" = '') THEN BEGIN
                                                                  IF "Element No." = 0 THEN
                                                                    ERROR(
                                                                      Text009,
                                                                      FIELDCAPTION("Outlook Property"),
                                                                      OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                                END;
                                                                IF SetOSynchOptionCorrelFilter(OSynchOptionCorrel) THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Outlook Property" := xRec."Outlook Property";
                                                                    "User-Defined" := xRec."User-Defined";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                END;

                                                                IF "Outlook Property" <> '' THEN
                                                                  "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              PropertyName@1000 : Text[80];
                                                            BEGIN
                                                              IF "Outlook Object" = '' THEN BEGIN
                                                                IF "Element No." = 0 THEN
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                ERROR(
                                                                  Text009,
                                                                  FIELDCAPTION("Outlook Property"),
                                                                  OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                              END;

                                                              IF "User-Defined" THEN
                                                                ERROR(Text001);

                                                              IF "Element No." = 0 THEN
                                                                PropertyName := OSynchSetupMgt.ShowOItemProperties("Outlook Object")
                                                              ELSE BEGIN
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                PropertyName := OSynchSetupMgt.ShowOCollectionProperties(OSynchEntity."Outlook Item","Outlook Object");
                                                              END;

                                                              IF PropertyName <> '' THEN
                                                                VALIDATE("Outlook Property",PropertyName);
                                                            END;

                                                   CaptionML=[ENU=Outlook Property;
                                                              ESM=Propiedad Outlook;
                                                              FRC=PropriÇtÇ Outlook;
                                                              ENC=Outlook Property] }
    { 7   ;   ;User-Defined        ;Boolean       ;OnValidate=BEGIN
                                                                "Outlook Property" := '';
                                                                IF NOT "User-Defined" THEN
                                                                  EXIT;

                                                                VALIDATE("Outlook Property");
                                                              END;

                                                   CaptionML=[ENU=User-Defined;
                                                              ESM=Definido por el usuario;
                                                              FRC=DÇfini par l'utilisateur;
                                                              ENC=User-Defined] }
    { 8   ;   ;Search Field        ;Boolean       ;CaptionML=[ENU=Search Field;
                                                              ESM=Campo b£squeda;
                                                              FRC=Champ de recherche;
                                                              ENC=Search Field] }
    { 9   ;   ;Condition           ;Text250       ;CaptionML=[ENU=Condition;
                                                              ESM=Condici¢n;
                                                              FRC=êtat;
                                                              ENC=Condition];
                                                   Editable=No }
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Caption");

                                                                IF "Table No." = xRec."Table No." THEN
                                                                  EXIT;

                                                                IF NOT OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") THEN
                                                                  EXIT;

                                                                IF ("Table Relation" <> '') OR SetOSynchOptionCorrelFilter(OSynchOptionCorrel) THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Table No." := xRec."Table No.";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;

                                                                  OSynchFilter.RESET;
                                                                  OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                  OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                                                                  OSynchFilter.DELETEALL;

                                                                  "Table Relation" := '';
                                                                END;

                                                                "Field No." := 0;
                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              IF TableNo <> 0 THEN
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=[ENU=Table No.;
                                                              ESM=Nß tabla;
                                                              FRC=N¯ de la table;
                                                              ENC=Table No.];
                                                   BlankZero=Yes }
    { 11  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              ESM=T°tulo tabla;
                                                              FRC=LÇgende table;
                                                              ENC=Table Caption];
                                                   Editable=No }
    { 12  ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=[ENU=Table Relation;
                                                              ESM=Relaci¢n de tabla;
                                                              FRC=Association de table;
                                                              ENC=Table Relation];
                                                   Editable=No }
    { 13  ;   ;Field No.           ;Integer       ;OnValidate=BEGIN
                                                                TESTFIELD("Field No.");
                                                                CheckReadOnlyStatus;

                                                                IF "Field No." = xRec."Field No." THEN
                                                                  EXIT;

                                                                IF "Table No." = 0 THEN
                                                                  Field.GET("Master Table No.","Field No.")
                                                                ELSE
                                                                  Field.GET("Table No.","Field No.");

                                                                IF Field.Class = Field.Class::FlowFilter THEN
                                                                  ERROR(Text002,Field.Class);

                                                                IF NOT Field.Enabled THEN
                                                                  ERROR(Text012,Field.FieldName);

                                                                IF "User-Defined" THEN
                                                                  VALIDATE("Outlook Property",Field."Field Caption");

                                                                IF SetOSynchOptionCorrelFilter(OSynchOptionCorrel) THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Field No." := xRec."Field No.";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                END;

                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              IF "Table No." = 0 THEN
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.")
                                                              ELSE
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Table No.");

                                                              IF FieldNo <> 0 THEN
                                                                VALIDATE("Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              ESM=Nß campo;
                                                              FRC=N¯ de champ;
                                                              ENC=Field No.];
                                                   BlankZero=Yes }
    { 15  ;   ;Read-Only Status    ;Option        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=[ENU=Read-Only Status;
                                                              ESM=Estado de s¢lo lectura;
                                                              FRC=êtat Lecture seule;
                                                              ENC=Read-Only Status];
                                                   OptionCaptionML=[ENU=" ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook";
                                                                    ESM=" ,S¢lo lectura en Microsoft Dynamics NAV,S¢lo lectura en Outlook";
                                                                    FRC=" ,Lecture seule dans Microsoft Dynamics NAV,Lecture seule dans Outlook";
                                                                    ENC=" ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook"];
                                                   OptionString=[ ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook];
                                                   Editable=No }
    { 16  ;   ;Field Default Value ;Text250       ;OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                                FldRef@1001 : FieldRef;
                                                                BooleanValue@1002 : Boolean;
                                                              BEGIN
                                                                TESTFIELD("Master Table No.");
                                                                TESTFIELD("Field No.");

                                                                IF "Field Default Value" = xRec."Field Default Value" THEN
                                                                  EXIT;

                                                                IF "Outlook Property" <> '' THEN
                                                                  ERROR(Text005,FIELDCAPTION("Field Default Value"),FIELDCAPTION("Outlook Property"));

                                                                CLEAR(RecRef);
                                                                CLEAR(FldRef);

                                                                IF "Table No." = 0 THEN BEGIN
                                                                  Field.GET("Master Table No.","Field No.");
                                                                  RecRef.OPEN("Master Table No.",TRUE);
                                                                END ELSE BEGIN
                                                                  Field.GET("Table No.","Field No.");
                                                                  RecRef.OPEN("Table No.",TRUE);
                                                                END;

                                                                IF Field.Class = Field.Class::FlowField THEN
                                                                  ERROR(Text010,0,Field.Class);

                                                                FldRef := RecRef.FIELD("Field No.");

                                                                IF Field.Type = Field.Type::Option THEN BEGIN
                                                                  IF NOT OSynchTypeConversion.EvaluateOptionField(FldRef,"Field Default Value") THEN
                                                                    ERROR(Text004,"Field Default Value",FldRef.TYPE,FldRef.OPTIONCAPTION);
                                                                  DefaultValueExpression := FORMAT(OSynchTypeConversion.TextToOptionValue("Field Default Value",FldRef.OPTIONCAPTION))
                                                                END ELSE BEGIN
                                                                  IF NOT EVALUATE(FldRef,"Field Default Value") THEN
                                                                    ERROR(Text003,FIELDCAPTION("Field Default Value"),FldRef.TYPE);

                                                                  IF Field.Type = Field.Type::Boolean THEN BEGIN
                                                                    EVALUATE(BooleanValue,"Field Default Value");
                                                                    IF BooleanValue THEN
                                                                      DefaultValueExpression := '1'
                                                                    ELSE
                                                                      DefaultValueExpression := '0';
                                                                  END;
                                                                END;

                                                                RecRef.CLOSE;
                                                              END;

                                                   CaptionML=[ENU=Field Default Value;
                                                              ESM=Valor predeterminado campo;
                                                              FRC=Valeur par dÇfaut de champ;
                                                              ENC=Field Default Value] }
    { 17  ;   ;Record GUID         ;GUID          ;CaptionML=[ENU=Record GUID;
                                                              ESM=GUID registro;
                                                              FRC=GUID d'enregistrement;
                                                              ENC=Record GUID];
                                                   Editable=No }
    { 99  ;   ;DefaultValueExpression;Text250     ;CaptionML=[ENU=DefaultValueExpression;
                                                              ESM=DefaultValueExpression;
                                                              FRC=DefaultValueExpression;
                                                              ENC=DefaultValueExpression] }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.,Line No. ;Clustered=Yes }
    {    ;Record GUID                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Field@1001 : Record 2000000041;
      OSynchEntity@1003 : Record 5300;
      OSynchEntityElement@1004 : Record 5301;
      OSynchFilter@1019 : Record 5303;
      OSynchOptionCorrel@1013 : Record 5307;
      OSynchSetupMgt@1000 : Codeunit 5300;
      OSynchTypeConversion@1008 : Codeunit 5302;
      Text001@1015 : TextConst 'ENU=You cannot choose from a list of Outlook item collections for user-defined fields.;ESM=No puede seleccionar de una lista de colecciones de elementos de Outlook para campos definidos por el usuario.;FRC=Vous ne pouvez pas faire un choix dans une liste d''ensembles d''ÇlÇments Outlook pour les champs dÇfinis par l''utilisateur.;ENC=You cannot choose from a list of Outlook item collections for user-defined fields.';
      Text002@1012 : TextConst 'ENU=You cannot use a %1 field for synchronization.;ESM=No puede utilizar un campo %1 para la sincronizaci¢n.;FRC=Vous ne pouvez pas utiliser un champ %1 pour la synchronisation.;ENC=You cannot use a %1 field for synchronization.';
      Text003@1011 : TextConst 'ENU=The value of the %1 field cannot be converted to the %2 datatype.;ESM=El valor del campo %1 no se puede convertir al tipo de datos %2.;FRC=La valeur du champ %1 ne peut pas àtre convertie au type de donnÇes %2.;ENC=The value of the %1 field cannot be converted to the %2 datatype.';
      Text004@1010 : TextConst 'ENU=The value of the %1 field cannot be converted to the %2 datatype.\The possible option values are: ''%3''.;ESM=El valor del campo %1 no se puede convertir al tipo de datos %2.\Los valores de opci¢n posibles son: ''%3''.;FRC=La valeur du champ %1 ne peut pas àtre convertie au type de donnÇes %2.\Les valeurs d''option possibles sont : ''%3''.;ENC=The value of the %1 field cannot be converted to the %2 datatype.\The possible option values are: ''%3''.';
      Text005@1009 : TextConst 'ENU=The %1 field should be blank when the %2 field is used.;ESM=El campo %1 debe estar vac°o cuando se utilice el campo %2.;FRC=Si vous utilisez le champ %2, le champ %1 devrait rester vide.;ENC=The %1 field should be blank when the %2 field is used.';
      Text006@1006 : TextConst 'ENU=This is not a valid Outlook property name.;ESM=No es un nombre de propiedad de Outlook v†lido.;FRC=Ceci n''est pas un nom de propriÇtÇ Outlook valide.;ENC=This is not a valid Outlook property name.';
      Text007@1005 : TextConst 'ENU=You cannot synchronize the %1 and the %2 fields because they are both write protected.;ESM=No puede sincronizar los campos %1 y %2 porque est†n protegidos contra escritura.;FRC=Vous ne pouvez pas synchroniser les champs %1 et %2, car ils sont tous deux protÇgÇs en Çcriture.;ENC=You cannot synchronize the %1 and the %2 fields because they are both write protected.';
      Text008@1016 : TextConst 'ENU=If you change the value of this field, %1 and %2 records related to this entry will be removed.\Do you want to change this field anyway?;ESM=Si cambia el valor de este campo, se eliminar†n los registros %1 y %2 relacionados con este movimiento.\®Desea cambiar este campo de todos modos?;FRC=Si vous modifiez la valeur de ce champ, les enregistrements %1 et %2 associÇs Ö cette Çcriture seront supprimÇs.\Voulez-vous modifier ce champ quand màme?;ENC=If you change the value of this field, %1 and %2 records related to this entry will be removed.\Do you want to change this field anyway?';
      Text009@1018 : TextConst 'ENU=You cannot change the %1 field if the %2 is not specified for this entity.;ESM=No puede cambiar el campo %1 si %2 no se especific¢ para este objeto.;FRC=Vous ne pouvez pas modifier le champ %1, si %2 n''est pas spÇcifiÇ pour cette entitÇ.;ENC=You cannot change the %1 field if the %2 is not specified for this entity.';
      Text010@1020 : TextConst 'ENU=You cannot use this field for %2 fields.;ESM=No puede utilizar este campo para los campos %2.;FRC=Vous ne pouvez pas utiliser ce champ pour les champs %2.;ENC=You cannot use this field for %2 fields.';
      Text011@1007 : TextConst 'ENU=The %1 table cannot be open, because the %2 or %3 fields are empty.\Fill in these fields with the appropriate values and try again.;ESM=La tabla %1 no se puede abrir porque los campos %2 o %3 est†n vac°os.\Cumplimente estos campos con los valores apropiados y vuelva a intentarlo.;FRC=Impossible d''ouvrir la table %1, car les champs %2 ou %3 sont vides.\Entrez les valeurs appropriÇes dans ces champs, puis rÇessayez.;ENC=The %1 table cannot be open, because the %2 or %3 fields are empty.\Fill in these fields with the appropriate values and try again.';
      Text012@1021 : TextConst 'ENU=You cannot select the %1 field because it is disabled.;ESM=No puede seleccionar el campo %1 porque est† deshabilitado.;FRC=Vous ne pouvez pas sÇlectionner le champ %1, car il est dÇsactivÇ.;ENC=You cannot select the %1 field because it is disabled.';
      Text013@1002 : TextConst 'ENU=You cannot use this value because an Outlook property with this name exists.;ESM=No puede utilizar este valor porque ya existe una propiedad de Outlook con este nombre.;FRC=Vous ne pouvez pas utiliser cette valeur, car il existe dÇjÖ une propriÇtÇ Outlook portant ce nom.;ENC=You cannot use this value because an Outlook property with this name exists.';
      Text014@1022 : TextConst 'ENU=The entry you are trying to create already exists.;ESM=El movimiento que intenta crear ya existe.;FRC=L''Çcriture que vous tentez de crÇer existe dÇjÖ.;ENC=The entry you are trying to create already exists.';

    LOCAL PROCEDURE GetMasterInformation@2();
    BEGIN
      IF "Element No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        "Master Table No." := OSynchEntity."Table No.";
        "Outlook Object" := OSynchEntity."Outlook Item";
      END ELSE BEGIN
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Master Table No." := OSynchEntityElement."Table No.";
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      END;
    END;

    LOCAL PROCEDURE CheckReadOnlyStatus@9();
    VAR
      OSynchProcessLine@1002 : Codeunit 5305;
      IsReadOnlyOutlook@1000 : Boolean;
      IsReadOnlyNavision@1001 : Boolean;
    BEGIN
      IsReadOnlyOutlook := CheckOtlookPropertyName;

      IF ("Outlook Property" <> '') AND ("Field No." <> 0) THEN BEGIN
        IF "Table No." = 0 THEN BEGIN
          Field.GET("Master Table No.","Field No.");
          IF OSynchProcessLine.CheckKeyField("Master Table No.","Field No.") OR (Field.Class = Field.Class::FlowField) THEN
            IsReadOnlyNavision := TRUE;
        END ELSE BEGIN
          OSynchFilter.RESET;
          OSynchFilter.SETRANGE("Record GUID","Record GUID");
          OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
          OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
          IF OSynchFilter.FIND('-') THEN
            REPEAT
              Field.GET(OSynchFilter."Master Table No.",OSynchFilter."Master Table Field No.");
              IF OSynchProcessLine.CheckKeyField("Master Table No.",OSynchFilter."Master Table Field No.") OR
                 (Field.Class = Field.Class::FlowField)
              THEN
                IsReadOnlyNavision := TRUE;
            UNTIL OSynchFilter.NEXT = 0;
        END;

        IF IsReadOnlyOutlook THEN BEGIN
          IF IsReadOnlyNavision THEN
            ERROR(Text007,"Outlook Property",GetFieldCaption);
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook";
        END ELSE BEGIN
          IF IsReadOnlyNavision THEN
            "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV"
          ELSE
            "Read-Only Status" := "Read-Only Status"::" ";
        END;
      END ELSE BEGIN
        IF "Field No." = 0 THEN
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook"
        ELSE
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV";
      END;
    END;

    LOCAL PROCEDURE CheckOtlookPropertyName@3() : Boolean;
    VAR
      IsReadOnly@1000 : Boolean;
    BEGIN
      IF "Outlook Property" = '' THEN
        EXIT(FALSE);

      IF "User-Defined" THEN
        IF "Element No." = 0 THEN BEGIN
          IF OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,TRUE) THEN
            ERROR(Text013);
        END ELSE BEGIN
          OSynchEntity.GET("Synch. Entity Code");
          IF OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               TRUE)
          THEN
            ERROR(Text013);
        END
      ELSE
        IF "Element No." = 0 THEN BEGIN
          IF NOT OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,FALSE) THEN
            ERROR(Text006);
        END ELSE BEGIN
          OSynchEntity.GET("Synch. Entity Code");
          IF NOT
             OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               FALSE)
          THEN
            ERROR(Text006);
        END;

      EXIT(IsReadOnly);
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@4();
    VAR
      OSynchField@1000 : Record 5304;
    BEGIN
      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");
      OSynchField.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchField.SETRANGE("Outlook Property","Outlook Property");
      OSynchField.SETRANGE("Table No.","Table No.");
      OSynchField.SETRANGE("Field No.","Field No.");
      IF NOT OSynchField.ISEMPTY THEN
        ERROR(Text014);
    END;

    PROCEDURE ShowOOptionCorrelForm@1();
    BEGIN
      IF ("Field No." = 0) OR ("Outlook Property" = '') THEN
        ERROR(Text011,
          OSynchOptionCorrel.TABLECAPTION,
          FIELDCAPTION("Field No."),
          FIELDCAPTION("Outlook Property"));
      OSynchSetupMgt.ShowOOptionCorrelForm(Rec);
    END;

    LOCAL PROCEDURE SetOSynchOptionCorrelFilter@5(VAR outlookSynchOptionCorrel@1000 : Record 5307) : Boolean;
    BEGIN
      outlookSynchOptionCorrel.RESET;
      outlookSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      outlookSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      outlookSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
      EXIT(NOT outlookSynchOptionCorrel.ISEMPTY);
    END;

    PROCEDURE GetFieldCaption@6() : Text;
    BEGIN
      IF "Table No." <> 0 THEN BEGIN
        IF Field.GET("Table No.","Field No.") THEN
          EXIT(Field."Field Caption")
      END ELSE
        IF Field.GET("Master Table No.","Field No.") THEN
          EXIT(Field."Field Caption");

      EXIT('');
    END;

    BEGIN
    END.
  }
}

