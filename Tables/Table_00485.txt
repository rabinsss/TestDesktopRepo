OBJECT Table 485 Business Chart Buffer
{
  OBJECT-PROPERTIES
  {
    Date=09/15/15;
    Time=12:00:00 PM;
    Version List=NAVW19.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               ERROR(Text002,TABLECAPTION);
             END;

    CaptionML=[ENU=Business Chart Buffer;
               ESM=Almacenaje gr†fico empresarial;
               FRC=Tampon graphique entreprise;
               ENC=Business Chart Buffer];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;Integer       ;CaptionML=[ENU=ID;
                                                              ESM=Id.;
                                                              FRC=Code;
                                                              ENC=ID] }
    { 2   ;   ;Chart Type          ;Option        ;CaptionML=[ENU=Chart Type;
                                                              ESM=Tipo de gr†fico;
                                                              FRC=Type de graphique;
                                                              ENC=Chart Type];
                                                   OptionCaptionML=[ENU=Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel;
                                                                    ESM=Puntos,,Burbujas,L°neas,,L°neas escalonadas,,,,,Columnas,Columnas apiladas,Columnas apiladas 100,†rea,,µrea apilada,µrea apilada 100,Circular,Anillos,,,Intervalo,,,,Radar,,,,,,,,Embudo;
                                                                    FRC=Point,,Bulles,Ligne,,StepLine,,,,,Colonne,StackedColumn,StackedColumn100,Aires,,StackedArea,StackedArea100,Secteur,Anneau,,,êtendue,,,,Radar,,,,,,,,Entonnoir;
                                                                    ENC=Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel];
                                                   OptionString=Point,,Bubble,Line,,StepLine,,,,,Column,StackedColumn,StackedColumn100,Area,,StackedArea,StackedArea100,Pie,Doughnut,,,Range,,,,Radar,,,,,,,,Funnel }
    { 3   ;   ;Data Type           ;Option        ;CaptionML=[ENU=Data Type;
                                                              ESM=Tipo de datos;
                                                              FRC=Type de donnÇes;
                                                              ENC=Data Type];
                                                   OptionCaptionML=[ENU=String,Integer,Decimal,DateTime;
                                                                    ESM=Cadena,Entero,Decimal,DateTime;
                                                                    FRC=Chaåne,Entier,DÇcimale,DateTime;
                                                                    ENC=String,Integer,Decimal,DateTime];
                                                   OptionString=String,Integer,Decimal,DateTime }
    { 4   ;   ;XML                 ;BLOB          ;CaptionML=[ENU=XML;
                                                              ESM=XML;
                                                              FRC=XML;
                                                              ENC=XML] }
    { 5   ;   ;Drill-Down X Index  ;Integer       ;CaptionML=[ENU=Drill-Down X Index;
                                                              ESM=Desagrupar datos °ndice X;
                                                              FRC=Zoom avant indice X;
                                                              ENC=Drill-Down X Index] }
    { 6   ;   ;Drill-Down Y Value  ;Decimal       ;CaptionML=[ENU=Drill-Down Y Value;
                                                              ESM=Desagrupar datos valor Y;
                                                              FRC=Zoom avant valeur Y;
                                                              ENC=Drill-Down Y Value] }
    { 7   ;   ;Drill-Down Measure Index;Integer   ;CaptionML=[ENU=Drill-Down Measure Index;
                                                              ESM=Desagrupar datos °ndice de medida;
                                                              FRC=Zoom avant indice mesure;
                                                              ENC=Drill-Down Measure Index] }
    { 8   ;   ;Period Length       ;Option        ;CaptionML=[ENU=Period Length;
                                                              ESM=Longitud periodo;
                                                              FRC=DurÇe de la pÇriode;
                                                              ENC=Period Length];
                                                   OptionCaptionML=[ENU=Day,Week,Month,Quarter,Year,Accounting Period,None;
                                                                    ESM=D°a,Semana,Mes,Trimestre,A§o,Periodo contable,Ninguno;
                                                                    FRC=Jour,Semaine,Mois,Trimestre,AnnÇe,PÇriode comptable,Aucun;
                                                                    ENC=Day,Week,Month,Quarter,Year,Accounting Period,None];
                                                   OptionString=Day,Week,Month,Quarter,Year,Accounting Period,None }
    { 9   ;   ;Period Filter Start Date;Date      ;CaptionML=[ENU=Period Filter Start Date;
                                                              ESM=Fecha inicial del filtro de periodo;
                                                              FRC=Date dÇbut filtre pÇriode;
                                                              ENC=Period Filter Start Date] }
    { 10  ;   ;Period Filter End Date;Date        ;CaptionML=[ENU=Period Filter End Date;
                                                              ESM=Fecha final del filtro de periodo;
                                                              FRC=Date fin filtre pÇriode;
                                                              ENC=Period Filter End Date] }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TempBusChartMapToColumn@1001 : TEMPORARY Record 486;
      TempBusChartMapToMeasure@1000 : TEMPORARY Record 486;
      dotNetChartData@1012 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartData";
      dotNetDataTable@1013 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataTable";
      IsInitialized@1014 : Boolean;
      Text001@1002 : TextConst 'ENU=Data Type must be Integer or Decimal for Measure %1.;ESM=El tipo de datos debe ser Integer o Decimal para la medida %1.;FRC=Le type de donnÇes doit àtre dÇfini sur Entier ou DÇcimal pour la mesure %1.;ENC=Data Type must be Integer or Decimal for Measure %1.';
      Text002@1007 : TextConst 'ENU=You cannot insert into table %1.;ESM=No se puede insertar elementos en la tabla %1.;FRC=L''insertion n''est pas possible dans la table %1.;ENC=You cannot insert into table %1.';
      Text003@1005 : TextConst 'ENU=You cannot add more than %1 measures.;ESM=No se puede agregar m†s de %1 mediciones.;FRC=Vous ne pouvez pas ajouter plus de %1 mesures.;ENC=You cannot add more than %1 measures.';

    PROCEDURE Initialize@2();
    VAR
      dotNetCultureInfo@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
    BEGIN
      IF NOT IsInitialized THEN BEGIN
        dotNetDataTable := dotNetDataTable.DataTable('DataTable');
        dotNetCultureInfo := dotNetCultureInfo.CultureInfo(WINDOWSLANGUAGE);
        dotNetDataTable.Locale := dotNetCultureInfo.InvariantCulture;

        dotNetChartData := dotNetChartData.BusinessChartData;
        IsInitialized := TRUE;
      END;
      dotNetDataTable.Clear;
      dotNetDataTable.Columns.Clear;
      dotNetChartData.ClearMeasures;
      ClearMap(TempBusChartMapToColumn);
      ClearMap(TempBusChartMapToMeasure);
    END;

    LOCAL PROCEDURE ClearMap@16(VAR BusChartMap@1000 : Record 486);
    BEGIN
      BusChartMap.RESET;
      BusChartMap.DELETEALL;
    END;

    PROCEDURE SetChartCondensed@30(Condensed@1000 : Boolean);
    BEGIN
      dotNetChartData.ShowChartCondensed := Condensed;
    END;

    PROCEDURE SetXAxis@3(Caption@1000 : Text;Type@1001 : Option);
    BEGIN
      AddDataColumn(Caption,Type);
      dotNetChartData.XDimension := Caption;
    END;

    PROCEDURE SetPeriodXAxis@38();
    VAR
      DataType@1000 : Option;
    BEGIN
      IF "Period Length" = "Period Length"::Day THEN
        DataType := "Data Type"::DateTime
      ELSE
        DataType := "Data Type"::String;
      SetXAxis(FORMAT("Period Length"),DataType);
    END;

    PROCEDURE GetXCaption@35() : Text;
    BEGIN
      EXIT(dotNetChartData.XDimension);
    END;

    PROCEDURE AddMeasure@4(Caption@1000 : Text;Value@1001 : Variant;ValueType@1004 : Option;ChartType@1002 : Integer);
    VAR
      DotNetChartType@1003 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.DataMeasureType";
    BEGIN
      "Data Type" := ValueType;
      IF NOT ("Data Type" IN ["Data Type"::Integer,"Data Type"::Decimal]) THEN
        ERROR(Text001,Caption);
      AddDataColumn(Caption,ValueType);
      DotNetChartType := ChartType;
      dotNetChartData.AddMeasure(Caption,DotNetChartType);
      TempBusChartMapToMeasure.Add(
        COPYSTR(Caption,1,MAXSTRLEN(TempBusChartMapToMeasure.Name)),Value);
    END;

    PROCEDURE GetMaxNumberOfMeasures@40() : Integer;
    VAR
      MaximumNumberOfColoursInChart@1000 : Integer;
    BEGIN
      MaximumNumberOfColoursInChart := 6;
      EXIT(MaximumNumberOfColoursInChart);
    END;

    PROCEDURE RaiseErrorMaxNumberOfMeasuresExceeded@42();
    BEGIN
      ERROR(Text003,GetMaxNumberOfMeasures);
    END;

    PROCEDURE AddColumn@9(Value@1002 : Variant);
    VAR
      Caption@1000 : Text;
    BEGIN
      ConvertDateToDateTime(Value);
      Caption := FORMAT(Value,0,9);
      AddColumnWithCaption(Value,Caption);
    END;

    PROCEDURE AddPeriods@32(FromDate@1000 : Date;ToDate@1001 : Date);
    VAR
      ColumnNo@1002 : Integer;
      NumberOfPeriods@1003 : Integer;
    BEGIN
      NumberOfPeriods := CalcNumberOfPeriods(FromDate,ToDate);
      FOR ColumnNo := 1 TO NumberOfPeriods DO BEGIN
        ToDate := CalcToDate(FromDate);
        AddPeriodColumn(ToDate);
        FromDate := CALCDATE('<1D>',ToDate);
      END;
    END;

    LOCAL PROCEDURE AddPeriodColumn@34(Value@1000 : Variant);
    VAR
      Caption@1001 : Text;
    BEGIN
      ConvertDateToDateTime(Value);
      Caption := GetPeriodCaption(Value);
      IF "Period Length" = "Period Length"::Day THEN
        AddColumnWithCaption(Value,FORMAT(Value,0,9))
      ELSE
        AddColumnWithCaption(Value,Caption);
    END;

    LOCAL PROCEDURE AddColumnWithCaption@37(Value@1000 : Variant;Caption@1001 : Text);
    VAR
      dotNetDataRow@1002 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.NewRow;
      dotNetDataRow.Item(GetXCaption,Caption);
      dotNetDataTable.Rows.Add(dotNetDataRow);
      TempBusChartMapToColumn.Add(Caption,Value);
    END;

    LOCAL PROCEDURE ConvertDateToDateTime@36(VAR Value@1000 : Variant);
    BEGIN
      IF IsXAxisDateTime AND Value.ISDATE THEN
        Value := CREATEDATETIME(Value,120000T);
    END;

    LOCAL PROCEDURE AddDataColumn@5(Caption@1003 : Text;ValueType@1002 : Option);
    VAR
      dotNetDataColumn@1001 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataColumn";
      dotNetSystemType@1000 : DotNet "'mscorlib, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Type";
    BEGIN
      dotNetDataColumn := dotNetDataColumn.DataColumn(Caption);
      dotNetDataColumn.Caption := Caption;
      dotNetDataColumn.ColumnName(Caption);
      dotNetDataColumn.DataType(dotNetSystemType.GetType(GetSystemTypeName(ValueType)));
      dotNetDataTable.Columns.Add(dotNetDataColumn);
    END;

    LOCAL PROCEDURE GetSystemTypeName@6(Type@1000 : Option) : Text;
    BEGIN
      CASE Type OF
        "Data Type"::String:
          EXIT('System.String');
        "Data Type"::Integer:
          EXIT('System.Int32');
        "Data Type"::Decimal:
          EXIT('System.Decimal');
        "Data Type"::DateTime:
          EXIT('System.DateTime');
      END;
    END;

    PROCEDURE SetValue@10(MeasureName@1001 : Text;XAxisIndex@1000 : Integer;Value@1002 : Variant);
    VAR
      dotNetDataRow@1003 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.Rows.Item(XAxisIndex);
      dotNetDataRow.Item(MeasureName,Value);
    END;

    PROCEDURE SetValueByIndex@11(MeasureIndex@1001 : Integer;XAxisIndex@1000 : Integer;Value@1002 : Variant);
    VAR
      dotNetDataRow@1003 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.Rows.Item(XAxisIndex);
      dotNetDataRow.Item(TempBusChartMapToMeasure.GetName(MeasureIndex),Value);
    END;

    PROCEDURE FindFirstMeasure@23(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      TempBusChartMapToMeasure.RESET;
      Result := TempBusChartMapToMeasure.FINDSET;
      BusChartMap := TempBusChartMapToMeasure;
    END;

    PROCEDURE NextMeasure@24(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      Result := TempBusChartMapToMeasure.NEXT <> 0;
      BusChartMap := TempBusChartMapToMeasure;
    END;

    PROCEDURE FindFirstColumn@21(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      TempBusChartMapToColumn.RESET;
      Result := TempBusChartMapToColumn.FINDSET;
      BusChartMap := TempBusChartMapToColumn;
    END;

    PROCEDURE FindMidColumn@29(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    VAR
      MidColumnIndex@1001 : Integer;
    BEGIN
      TempBusChartMapToColumn.RESET;
      TempBusChartMapToColumn.FINDLAST;
      MidColumnIndex := -ROUND(TempBusChartMapToColumn.COUNT DIV 2);
      Result := MidColumnIndex = TempBusChartMapToColumn.NEXT(MidColumnIndex);
      BusChartMap := TempBusChartMapToColumn;
    END;

    PROCEDURE NextColumn@22(VAR BusChartMap@1000 : Record 486) Result : Boolean;
    BEGIN
      Result := TempBusChartMapToColumn.NEXT <> 0;
      BusChartMap := TempBusChartMapToColumn;
    END;

    PROCEDURE GetValue@14(MeasureName@1001 : Text;XAxisIndex@1000 : Integer;VAR Value@1002 : Variant);
    VAR
      dotNetDataRow@1003 : DotNet "'System.Data, Version=2.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
    BEGIN
      dotNetDataRow := dotNetDataTable.Rows.Item(XAxisIndex);
      Value := dotNetDataRow.Item(MeasureName);
    END;

    PROCEDURE GetXValue@7(XAxisIndex@1001 : Integer;VAR Value@1002 : Variant);
    BEGIN
      GetValue(GetXCaption,XAxisIndex,Value);
    END;

    PROCEDURE GetXValueAsDate@19(XIndex@1000 : Integer) : Date;
    VAR
      Value@1001 : Variant;
    BEGIN
      IF IsXAxisDateTime THEN BEGIN
        GetXValue(XIndex,Value);
        EXIT(VARIANT2DATE(Value));
      END;
      TempBusChartMapToColumn.GET(XIndex);
      EXIT(TempBusChartMapToColumn.GetValueAsDate);
    END;

    PROCEDURE GetMeasureValueString@8(MeasureIndex@1000 : Integer) : Text;
    BEGIN
      EXIT(TempBusChartMapToMeasure.GetValueString(MeasureIndex));
    END;

    PROCEDURE GetMeasureName@41(MeasureIndex@1000 : Integer) : Text;
    BEGIN
      EXIT(TempBusChartMapToMeasure.GetName(MeasureIndex));
    END;

    PROCEDURE GetCurrMeasureValueString@12() : Text;
    BEGIN
      EXIT(TempBusChartMapToMeasure.GetValueString("Drill-Down Measure Index"));
    END;

    PROCEDURE Update@15(dotNetChartAddIn@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartAddIn");
    BEGIN
      dotNetChartData.DataTable := dotNetDataTable;
      dotNetChartAddIn.Update(dotNetChartData);
    END;

    PROCEDURE SetDrillDownIndexes@1(dotNetPoint@1000 : DotNet "'Microsoft.Dynamics.Nav.Client.BusinessChart.Model, Version=9.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Client.BusinessChart.BusinessChartDataPoint");
    VAR
      XValueString@1007 : Text;
      MeasureName@1004 : Text;
      YValue@1005 : Decimal;
    BEGIN
      MeasureName := dotNetPoint.Measures.GetValue(0);
      "Drill-Down Measure Index" := TempBusChartMapToMeasure.GetIndex(MeasureName);

      XValueString := dotNetPoint.XValueString;
      IF IsXAxisDateTime THEN
        XValueString := GetDateString(XValueString);
      "Drill-Down X Index" := TempBusChartMapToColumn.GetIndex(XValueString);

      IF EVALUATE(YValue,FORMAT(dotNetPoint.YValues.GetValue(0))) THEN
        "Drill-Down Y Value" := YValue;
    END;

    LOCAL PROCEDURE GetDateString@17(XValueString@1000 : Text) : Text;
    VAR
      Days@1002 : Decimal;
      DateTime@1001 : DateTime;
    BEGIN
      IF EVALUATE(Days,XValueString,9) THEN BEGIN
        DateTime := CREATEDATETIME(DMY2DATE(30,12,1899) + ROUND(Days,1,'<'),120000T);
        EXIT(FORMAT(DateTime,0,9));
      END;
      EXIT(XValueString);
    END;

    PROCEDURE IsXAxisDateTime@26() : Boolean;
    BEGIN
      EXIT(FORMAT(dotNetDataTable.Columns.Item(0).DataType) = 'System.DateTime')
    END;

    PROCEDURE WriteToXML@48(VAR XMLDoc@1000 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlDocument");
    VAR
      XMLElement@1006 : DotNet "'System.Xml, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Xml.XmlElement";
      OutStream@1005 : OutStream;
      InStream@1004 : InStream;
      XMLLine@1003 : Text[80];
      XMLText@1001 : Text;
    BEGIN
      XML.CREATEOUTSTREAM(OutStream);
      dotNetDataTable.WriteXml(OutStream);
      XML.CREATEINSTREAM(InStream);
      WHILE NOT InStream.EOS DO BEGIN
        InStream.READTEXT(XMLLine);
        XMLText := XMLText + XMLLine;
      END;
      XMLElement := XMLDoc.CreateElement('DataTable','test','');
      XMLElement.InnerXml(XMLText);
      XMLDoc.AppendChild(XMLElement);
    END;

    PROCEDURE CalcFromDate@18(Date@1000 : Date) : Date;
    BEGIN
      EXIT(CalcPeriodDate(Date,TRUE));
    END;

    PROCEDURE CalcToDate@13(Date@1000 : Date) : Date;
    BEGIN
      EXIT(CalcPeriodDate(Date,FALSE));
    END;

    LOCAL PROCEDURE CalcPeriodDate@39(Date@1000 : Date;CalcStartDate@1001 : Boolean) : Date;
    VAR
      Modificator@1002 : Text[1];
    BEGIN
      IF Date = 0D THEN
        EXIT(Date);

      CASE "Period Length" OF
        "Period Length"::Day:
          EXIT(Date);
        "Period Length"::Week,
        "Period Length"::Month,
        "Period Length"::Quarter,
        "Period Length"::Year:
          BEGIN
            IF CalcStartDate THEN
              Modificator := '-';
            EXIT(CALCDATE(STRSUBSTNO('<%1C%2>',Modificator,GetPeriodLength),Date));
          END;
      END;
    END;

    PROCEDURE CalcNumberOfPeriods@28(FromDate@1000 : Date;ToDate@1001 : Date) : Integer;
    VAR
      NumberOfPeriods@1002 : Integer;
    BEGIN
      IF FromDate = ToDate THEN
        EXIT(1);
      IF ToDate < FromDate THEN
        SwapDates(FromDate,ToDate);

      CASE "Period Length" OF
        "Period Length"::Day:
          NumberOfPeriods := ToDate - FromDate;
        "Period Length"::Week:
          NumberOfPeriods := (CALCDATE('<-CW>',ToDate) - CALCDATE('<-CW>',FromDate)) DIV 7;
        "Period Length"::Month:
          NumberOfPeriods := DATE2DMY(ToDate,2) - DATE2DMY(FromDate,2) + GetNumberOfYears(FromDate,ToDate) * 12;
        "Period Length"::Quarter:
          NumberOfPeriods := GetQuarterIndex(ToDate) - GetQuarterIndex(FromDate) + GetNumberOfYears(FromDate,ToDate) * 4;
        "Period Length"::Year:
          NumberOfPeriods := GetNumberOfYears(FromDate,ToDate);
      END;
      EXIT(NumberOfPeriods + 1);
    END;

    LOCAL PROCEDURE SwapDates@27(VAR FromDate@1000 : Date;VAR ToDate@1001 : Date);
    VAR
      Date@1002 : Date;
    BEGIN
      Date := FromDate;
      FromDate := ToDate;
      ToDate := Date;
    END;

    LOCAL PROCEDURE GetNumberOfYears@25(FromDate@1000 : Date;ToDate@1001 : Date) : Integer;
    BEGIN
      EXIT(DATE2DMY(ToDate,3) - DATE2DMY(FromDate,3));
    END;

    LOCAL PROCEDURE GetQuarterIndex@20(Date@1000 : Date) : Integer;
    BEGIN
      EXIT((DATE2DMY(Date,2) - 1) DIV 3 + 1);
    END;

    PROCEDURE GetPeriodLength@31() : Text[1];
    BEGIN
      CASE "Period Length" OF
        "Period Length"::Day:
          EXIT('D');
        "Period Length"::Week:
          EXIT('W');
        "Period Length"::Month:
          EXIT('M');
        "Period Length"::Quarter:
          EXIT('Q');
        "Period Length"::Year:
          EXIT('Y');
      END;
    END;

    LOCAL PROCEDURE GetPeriodCaption@33(Date@1000 : Variant) : Text;
    VAR
      PeriodFormManagement@1001 : Codeunit 359;
    BEGIN
      IF Date.ISDATETIME OR Date.ISDATE THEN
        EXIT(PeriodFormManagement.CreatePeriodFormat("Period Length",Date));
      EXIT(FORMAT(Date,0,9));
    END;

    PROCEDURE GetPeriodFromMapColumn@43(ColumnIndex@1002 : Integer;VAR FromDate@1001 : Date;VAR ToDate@1000 : Date);
    BEGIN
      ToDate := GetXValueAsDate(ColumnIndex);
      FromDate := CalcFromDate(ToDate);
    END;

    PROCEDURE InitializePeriodFilter@46(StartDate@1000 : Date;EndDate@1001 : Date);
    BEGIN
      "Period Filter Start Date" := StartDate;
      "Period Filter End Date" := EndDate;
    END;

    PROCEDURE RecalculatePeriodFilter@44(VAR StartDate@1005 : Date;VAR EndDate@1004 : Date;MovePeriod@1000 : ' ,Next,Previous');
    VAR
      Calendar@1003 : Record 2000000007;
      PeriodFormMgt@1002 : Codeunit 359;
      SearchText@1001 : Text[3];
    BEGIN
      IF StartDate <> 0D THEN BEGIN
        Calendar.SETFILTER("Period Start",'%1..%2',StartDate,EndDate);
        IF NOT PeriodFormMgt.FindDate('+',Calendar,"Period Length") THEN
          PeriodFormMgt.FindDate('+',Calendar,Calendar."Period Type"::Date);
        Calendar.SETRANGE("Period Start");
      END;

      CASE MovePeriod OF
        MovePeriod::Next:
          SearchText := '>=';
        MovePeriod::Previous:
          SearchText := '<=';
        ELSE
          SearchText := '';
      END;

      PeriodFormMgt.FindDate(SearchText,Calendar,"Period Length");

      StartDate := Calendar."Period Start";
      EndDate := Calendar."Period End";
    END;

    BEGIN
    END.
  }
}

